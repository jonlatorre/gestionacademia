10 '                       PACAETIQ.BAS
20 '                      --------------
30 '                   PROGRAMA DE ETIQUETAS
40 '         ----------------------------------------
42 '
50 ON ERROR GOTO 9100
52 '
54 CLS
56 GOSUB 33000 'COGER DATOS DE LA EMPRESA.
60 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
61 'LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B"+CHR$(27)+"&l1E"+CHR$(27)+"&l75E";
62 LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B"+CHR$(27)+"&l1E"+CHR$(27)+"&l75E";
64 'LPRINT CHR$(12);
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(20," ")
92 FECHA$=MID$(DATE$,4,2)+"-"+LEFT$(DATE$,2)+"-"+RIGHT$(DATE$,4)
94 FEDIA$=RIGHT$(FECHA$,4)+MID$(FECHA$,4,2)+LEFT$(FECHA$,2)
96 '
100 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
132 '
134 '
140 DEFSTR C
150 '
160 '
170 OPTION BASE 1
180 DIM TABNOM$(16),TABDIR$(16),TABPLA$(16)
190 '
200 '
210 '
220 '
230 '
240 '
250 '
260 GOSUB 15000  'CARGA TABLA DE PROVINCIAS
270 '
280 '
282 '
290 '
300 '**********************************
310 '***PRESENTAMOS MENU EN PANTALLA***
320 '**********************************
330 TIT$="- LISTADO ETIQUETAS DE ALUMNOS -"
340 X%=80-LEN(TIT$)
350 X%=INT(X%/2)
360 GOSUB 8000
370 LOCATE 4,X%:PRINT TIT$
380 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
390 LOCATE 6,23:PRINT "1.- LISTADO DE TODOS LOS ALUMNOS."
400 LOCATE 8,23:PRINT "2.- LISTADO ENTRE ALUMNOS."
405 LOCATE 10,23:PRINT "3.- LISTADO GRUPO A GRUPO."
410 LOCATE 12,23:PRINT "4.- LISTADO UNO A UNO."
415 LOCATE 14,23:PRINT "5.- LISTADO DE BAJAS."
417 LOCATE 16,23:PRINT "6.- FIN DE PROGRAMA."
420 LOCATE 18,28:PRINT "Seleccione Opci¢n : < >"
430 '
432 '
440 '
450 LOCATE 18,49,1,0,6:OPC$=INPUT$(1)
460 IF OPC$<"1" OR OPC$>"6" THEN BEEP:GOTO 450
462 LOCATE 18,49:PRINT OPC$
465 SICA$=" "
470 IF OPC$="6" THEN CLOSE:RUN "PACACLIE"
472 IF OPC$="5" THEN DESDE$="00001":HASTA$="99999":SICA$="1":GOTO 550  'PEDIR CONFORMIDAD A LOS DATOS.
475 IF OPC$="4" THEN 2000
480 IF OPC$="1" THEN DESDE$="00001":HASTA$="99999":GOTO 550  'PEDIR CONFORMIDAD A LOS DATOS.
490 IF OPC$="2" THEN LOCATE 20,19:PRINT "DESDE EL : <     >   HASTA EL : <     >":GOTO 510
500 LOCATE 20,19:PRINT "TECLEA EL NUMERO DE GRUPO ... : <     >":GOTO 532
510 F.C1%=20:C.C1%=31:L.E%=5:BO%=5:ESC%=1:GOSUB 63000
520 IF I.T%<>0 THEN BEEP:GOTO 510
530 DESDE$=D.E$:LOCATE 20,31:PRINT DESDE$
532 F.C1%=20:C.C1%=52:L.E%=5:BO%=5:ESC%=1:GOSUB 63000
534 IF I.T%<>0 THEN BEEP:GOTO 532
536 HASTA$=D.E$:LOCATE 20,52:PRINT HASTA$
538 IF DESDE$>HASTA$ THEN 510
540 '
550 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1-":GOSUB 8500
560 A$=INPUT$(1)
570 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 560
580 IF ASC(A$)=13 THEN 650   'CONTINUAR.
590 LOCATE 14,49:PRINT SPC(1)
600 LOCATE 16,46:PRINT SPC(5)
610 GOSUB 8500
620 GOTO 310
630 '
640 '*****************
650 '***TRATAMIENTO***
660 '*****************
730 GOSUB 8500:MEN$="-- COLOQUE ETIQUETAS Y PULSE UNA TECLA --":GOSUB 8500
740 A$=INPUT$(1)
745 FALUDAT$="FACAALUM.DAT"
750 GOSUB 8500:MEN$="Espere por favor...":GOSUB 8500
760 GOSUB 30010   'apertura del P.C.
770 FF$="":I.T%=0:REG%=1
780 IF OPC$="3" THEN GOSUB 4000:GOTO 800
790 GOSUB 3000    'LECTURA DEL PLAN CONTABLE
800 GOSUB 1160    'BORRAR TABLAS
810 '
820 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 310
830 '
840 WHILE FF$<>"FF"
850   I.T%=I.T%+1
852   IF I.T%>16 THEN GOSUB 1010:I.T%=1   'SACAR LINEA
854   FE1#=VAL(FEDIA$):FE2#=VAL(CFENA):DIF#=FE1#-FE2#
860   IF DIF#>199999! THEN NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOTO 864
862   NOM$="SRES.":APE1$=CAPELL1:APE2$=CAPELL2
864   GOSUB 40000
870   TABNOM$(I.T%)=LEFT$(NOMBRE$,35)
880   TABDIR$(I.T%)=LEFT$(CDIREC,35)
890   CODPRO%=VAL(LEFT$(CCOPOS,2))
900   IF CODPRO%=0 OR CODPRO%>50 THEN TABPLA$(I.T%)=CPOBLA:GOTO 920
910   POBLA$=CCOPOS+" "+CPOBLA:TABPLA$(I.T%)=LEFT$(POBLA$,35)
920   IF OPC$="3" THEN GOSUB 4000:GOTO 940
930   GOSUB 3000    'LECTURA DEL SIGUIENTE
940 '
950 WEND
960 GOSUB 1010  'SACAR LINEA
970 '
980 CLOSE
990 GOTO 310
992 '
1000 '*****************
1010 '***SACAR LINEA***
1020 '*****************
1022 LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B"+CHR$(27)+"&l1E"+CHR$(27)+"&l75E";
1030 LPRINT
1031 LPRINT TAB(3) TABNOM$(1);
1032 LPRINT TAB(43) TABNOM$(2)
1033 '
1034 LPRINT TAB(3) TABDIR$(1);
1035 LPRINT TAB(43) TABDIR$(2)
1036 '
1037 LPRINT TAB(3) TABPLA$(1);
1038 LPRINT TAB(43) TABPLA$(2)
1039 '
1040 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1041 LPRINT TAB(3) TABNOM$(3);
1042 LPRINT TAB(43) TABNOM$(4)
1043 '
1044 LPRINT TAB(3) TABDIR$(3);
1045 LPRINT TAB(43) TABDIR$(4)
1046 '
1047 LPRINT TAB(3) TABPLA$(3);
1048 LPRINT TAB(43) TABPLA$(4)
1049 '
1050 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1051 LPRINT TAB(3) TABNOM$(5);
1052 LPRINT TAB(43) TABNOM$(6)
1053 '
1054 LPRINT TAB(3) TABDIR$(5);
1055 LPRINT TAB(43) TABDIR$(6)
1056 '
1057 LPRINT TAB(3) TABPLA$(5);
1058 LPRINT TAB(43) TABPLA$(6)
1059 '
1060 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1061 LPRINT TAB(3) TABNOM$(7);
1062 LPRINT TAB(43) TABNOM$(8)
1063 '
1064 LPRINT TAB(3) TABDIR$(7);
1065 LPRINT TAB(43) TABDIR$(8)
1066 '
1067 LPRINT TAB(3) TABPLA$(7);
1068 LPRINT TAB(43) TABPLA$(8)
1069 '
1070 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1071 LPRINT TAB(3) TABNOM$(9);
1072 LPRINT TAB(43) TABNOM$(10)
1073 '
1074 LPRINT TAB(3) TABDIR$(9);
1075 LPRINT TAB(43) TABDIR$(10)
1076 '
1077 LPRINT TAB(3) TABPLA$(9);
1078 LPRINT TAB(43) TABPLA$(10)
1079 '
1080 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1081 LPRINT TAB(3) TABNOM$(11);
1082 LPRINT TAB(43) TABNOM$(12)
1083 '
1084 LPRINT TAB(3) TABDIR$(11);
1085 LPRINT TAB(43) TABDIR$(12)
1086 '
1087 LPRINT TAB(3) TABPLA$(11);
1088 LPRINT TAB(43) TABPLA$(12)
1089 '
1090 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1091 LPRINT TAB(3) TABNOM$(13);
1092 LPRINT TAB(43) TABNOM$(14)
1093 '
1094 LPRINT TAB(3) TABDIR$(13);
1095 LPRINT TAB(43) TABDIR$(14)
1096 '
1097 LPRINT TAB(3) TABPLA$(13);
1098 LPRINT TAB(43) TABPLA$(14)
1099 '
1100 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
1101 LPRINT TAB(3) TABNOM$(15);
1102 LPRINT TAB(43) TABNOM$(16)
1103 '
1104 LPRINT TAB(3) TABDIR$(15);
1105 LPRINT TAB(43) TABDIR$(16)
1106 '
1107 LPRINT TAB(3) TABPLA$(15);
1108 LPRINT TAB(43) TABPLA$(16)
1109 '
1110 LPRINT CHR$(12);
1150 '
1160 FOR I%=1 TO 16
1170    TABNOM$(I%)=""
1180    TABDIR$(I%)=""
1190    TABPLA$(I%)=""
1200 NEXT I%
1210 '
1220 RETURN
1230 '
2000 '*****************
2010 '***TRATAMIENTO***
2020 '*****************
2030 GOSUB 8500:MEN$="-- COLOQUE ETIQUETAS Y PULSE UNA TECLA --":GOSUB 8500
2040 A$=INPUT$(1)
2050 FALUDAT$="FACAALUM.DAT"
2060 GOSUB 8500:MEN$="Espere por favor...":GOSUB 8500
2070 GOSUB 30010   'apertura del P.C.
2080 FF$="":N.T%=0:REG%=1
2090 GOSUB 1160    ' BORRAR TABLAS
2095 LOCATE 20,4:PRINT "TECLEA EL NUMERO DE ALUMNO .. : <     >                                    "
2096 F.C1%=20:C.C1%=37:L.E%=5:BO%=5:ESC%=1:GOSUB 63000
2097 IF I.T%<>0 THEN BEEP:GOTO 2095
2098 REG%=VAL(D.E$):LOCATE 20,37:PRINT D.E$
2099 IF REG%<2 OR REG%>ULTIMO% THEN BEEP:GOTO 2095
2100 GET #2,REG%
2102 NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2
2104 GOSUB 40000
2105 LOCATE 20,44:PRINT LEFT$(NOMBRE$,35)
2106 GOSUB 8500:MEN$="ALUMNO CORRECTO "+ENTER$+" , ALUMNO ERRONEO -F1-":GOSUB 8500
2108 A$=INPUT$(1)
2110 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2108
2112 IF ASC(A$)=13 THEN 2160  'CONTINUAR.
2114 GOSUB 8500
2116 GOTO 2095
2120 '
2160   N.T%=N.T%+1
2170   IF N.T%>16 THEN GOSUB 1010:N.T%=1   'SACAR LINEA
2180   FE1#=VAL(FEDIA$):FE2#=VAL(CFENA):DIF#=FE1#-FE2#
2190   IF DIF#>199999! THEN NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOTO 2210
2200   NOM$="SRES.":APE1$=CAPELL1:APE2$=CAPELL2
2210   GOSUB 40000
2220   TABNOM$(N.T%)=LEFT$(NOMBRE$,35)
2230   TABDIR$(N.T%)=LEFT$(CDIREC,35)
2240   CODPRO%=VAL(LEFT$(CCOPOS,2))
2250   IF CODPRO%=0 OR CODPRO%>50 THEN TABPLA$(N.T%)=CPOBLA:GOTO 2270
2260   POBLA$=CCOPOS+" "+CPOBLA:TABPLA$(N.T%)=LEFT$(POBLA$,35)
2270 GOSUB 8500:MEN$="PARA MAS ALUMNOS "+ENTER$+" , FINAL ALUMNOS -F1-":GOSUB 8500
2272 A$=INPUT$(1)
2274 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2272
2276 IF ASC(A$)=13 THEN 2295  'CONTINUAR.
2278 GOSUB 8500
2290 GOTO 2310
2295 GOSUB 8500
2300   GOTO 2095
2310   GOSUB 1010  'SACAR LINEA
2320   '
2330   CLOSE
2340   GOTO 310
2350 '
3000 '********************************
3010 '***LECTURA DE INDICES Y DATOS***
3020 '********************************
3030 REG%=REG%+1
3032 TECLA$=INKEY$:IF TECLA$="" THEN 3040
3034 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
3040 IF REG%>ULTIMO% THEN FF$="FF":RETURN
3050 'GET #2,REG%
3060 IF REG%<VAL(DESDE$) THEN 3030
3070 IF REG%>VAL(HASTA$) THEN FF$="FF":RETURN
3080 'IF DSTATUS$<>"O" THEN 3030
3090 'NORDEN%=CVI(DNORDEN$)
3100 'IF NORDEN%=0 THEN 3030
3110 GET #2,REG%
3115 IF SICA$="1" THEN 3150
3120 IF CSTATUS<>"O" THEN 3030
3130 IF CMARCA="L" THEN 3030
3140 RETURN
3150 IF CSTATUS<>"O" THEN 3030
3160 IF CMARCA<>"L" THEN 3030
3170 RETURN
4000 '********************************
4010 '***LECTURA DE INDICES Y DATOS***
4020 '********************************
4030 REG%=REG%+1
4032 TECLA$=INKEY$:IF TECLA$="" THEN 4040
4034 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
4040 IF REG%>ULTIMO% THEN FF$="FF":RETURN
4110 GET #2,REG%
4120 IF CSTATUS<>"O" THEN 4030
4130 IF CMARCA= "L" THEN 4030
4135 IF CGRUPO<>RIGHT$(HASTA$,3) THEN 4030
4140 RETURN
4150 '
4160 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 11:RETURN
8120 '
8250 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 RUN "PACACLIE"
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=34 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
15000 '***************************************
15010 '***CARGAMOS LAS TABLAS DE PROVINCIAS***
15020 '***************************************
15030 DIM TPRO$(50)
15040 DATA "ALAVA","ALBACETE","ALICANTE","ALMERIA","AVILA","BADAJOZ","BALEARES","BARCELONA","BURGOS","CACERES","CADIZ","CASTELLON","CIUDAD REAL","CORDOBA","LA CORU¥A","CUENCA","GERONA","GRANADA","GUADALAJARA","GUIPUZCOA","HUELVA"
15050 DATA "HUESCA","JAEN","LEON","LERIDA","RIOJA","LUGO","MADRID","MALAGA","MURCIA","NAVARRA","ORENSE","ASTURIAS","PALENCIA","LAS PALMAS","PONTEVEDRA","SALAMANCA","STA. CRUZ TENERIFE","CANTABRIA","SEGOVIA","SEVILLA","SORIA","TARRAGONA"
15060 DATA "TERUEL","TOLEDO","VALENCIA","VALLADOLID","VIZCAYA","ZAMORA","ZARAGOZA"
15070 FOR I%=1 TO 50
15080    READ TPRO$(I%)
15090 NEXT I%
15100 '
15110 RETURN
15120 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,6 AS CFEFAC,3 AS REST2$
30115 GET #2,1
30116 'IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30130 '
30140 RETURN
30150 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
44444 '********************************************
44454 '***RUTINA PARA EDITAR CAMPOS EN CRISTIANO***
44464 '********************************************
44466 IF GUAPRI#>0 AND GUAPRI#<1 THEN GUAPRI$="0":RETURN
44468 IF GUAPRI#<0 AND GUAPRI#>-1 THEN GUAPRI$="0":RETURN
44474 IF GUAPRI#<0 AND GUAPRI#>-1 THEN GUAPRI$=" 0":RETURN
44484 IF GUAPRI#>0 AND GUAPRI#<1 THEN GUAPRI$=" 0":RETURN
44494 GUAPRI$=STR$(GUAPRI#)
44504 SIGNO$=LEFT$(GUAPRI$,1)
44514 GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
44524 'BUSCAMOS SI TIENE DECIMALES.
44534 PUNTO%=0
44544 FOR CRIS%=LEN(GUAPRI$) TO 1 STEP -1
44554  IF MID$(GUAPRI$,CRIS%,1)="." THEN PUNTO%=CRIS%:CRIS%=0
44564 NEXT CRIS%
44574 'DIVIDIMOS EN ENTEROS Y DECIMALES.
44584 IF PUNTO%=0 THEN ENTERO$=GUAPRI$:L.EN%=LEN(ENTERO$):DECIMAL$="":L.DE%=0:GOTO 44624
44594 ENTERO$=LEFT$(GUAPRI$,PUNTO%-1):L.EN%=LEN(ENTERO$):ENTERO#=VAL(ENTERO$)
44604 DECIMAL$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-PUNTO%):L.DE%=LEN(DECIMAL$)
44614 IF LEN(DECIMAL$)<2 THEN DECIMAL$=DECIMAL$+"0":L.DE%=LEN(DECIMAL$)
44624 'REDONDEAMOS LOS DECIMALES
44634 IF DECIMAL$="" THEN 44764  'CONTINUAR
44644 DECA$=LEFT$(DECIMAL$,2)+"."+RIGHT$(DECIMAL$,L.DE%-2)
44654 DECA#=VAL(DECA$)
44664 DECA#=INT(DECA#+.5)
44674 IF DECA#>99 THEN 44734   'SUMAR 1 A ENTERO Y DECIMALES = '00'
44684 DECA$=STR$(DECA#):DECA$=RIGHT$(DECA$,2)
44694 IF LEN(DECA$)<>2 THEN DECA$="0"+DECA$
44704 IF LEFT$(DECA$,1)=" " THEN DECA$="0"+RIGHT$(DECA$,1)
44714 DECIMAL$=DECA$
44724 GOTO 44764  'CONTINUAR
44734 ENTERO#=ENTERO#+1
44744 ENTERO$=STR$(ENTERO#):ENTERO$=RIGHT$(ENTERO$,LEN(ENTERO$)-1):L.EN%=LEN(ENTERO$)
44754 DECIMAL$="00":L.DE%=2
44764 'CONTINUAMOS
44774 CAMPO$="":X%=0
44784 FOR CRIS%=L.EN% TO 1 STEP -1
44794  X%=X%+1
44804  IF X%=4 THEN CAMPO$=CAMPO$+".":X%=1
44814  CAMPO$=CAMPO$+MID$(ENTERO$,CRIS%,1)
44824 NEXT CRIS%
44834 GUAPRI$=""
44844 FOR CRIS%=LEN(CAMPO$) TO 1 STEP -1
44854  GUAPRI$=GUAPRI$+MID$(CAMPO$,CRIS%,1)
44864 NEXT CRIS%
44874 IF GUAPRI$="" THEN GUAPRI$="0"
44884 'GUAPRI$=SIGNO$+GUAPRI$
44894 IF DECIMAL$<>"" THEN GUAPRI$=GUAPRI$+","+LEFT$(DECIMAL$,2)
44904 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
