10 '                       PRESCOBR.BAS
20 '                      --------------
30 '         PREPARAR DATOS PARA HACER LA FACTURACION
40 '         ----------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,G,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),GRU%(20)
170 FOR I%=1 TO 20:GRU%(I%)=0:NEXT I%
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
306 '
308 '
310 GOSUB 8000
312 COLOR 10
314 LOCATE 5,15:PRINT "- PREPARAR DATOS PARA LA FACTURACION DE HOSTELERIA -"
316 LOCATE 6,15:PRINT STRING$(52,205)
318 LOCATE 8,19:PRINT "Precio Trimestre Primer Curso   : <         >"
320 LOCATE 10,19:PRINT "Precio Trimestre Segundo Curso  : <         >"
322 LOCATE 12,19:PRINT "Precio Trimestre Tercer Curso   : <         >"
324 LOCATE 14,19:PRINT "Precio Trimestre Cuarto Curso   : <         >"
326 LOCATE 16,19:PRINT "Precio Trimestre Quinto Curso   : <         >"
510 '
520 FALUDIR$="FRESALUM.ALF":FALUDAT$="FRESALUM.DAT"
600 GOSUB 30052
610 '
612 GOSUB 8500:MEN$="Espere por favor......."
614 GOSUB 8500
618 COLOR 15
888       '***************************
890       '***PRECIO PRIMER CURSO  ***
892       '***************************
894       F.C1%=8:C.C1%=54:L.E%=7:BO%=9:GOSUB 63000
896       IF I.T%=0 THEN PRE1#=VAL(D.E$):GOTO 902
897       GOTO 894
902       GUAPRI#=PRE1#:N.D%=2:GOSUB 64770
904       LOCATE 8,62-L.C%:PRINT GUAPRI$
1000      '***************************
1002      '***PRECIO SEGUNDO CURSO ***
1004      '***************************
1006      F.C1%=10:C.C1%=54:L.E%=7:BO%=9:GOSUB 63000
1008      IF I.T%=0 THEN PRE2#=VAL(D.E$):GOTO 1012
1010      GOTO 1004
1012      GUAPRI#=PRE2#:N.D%=2:GOSUB 64770
1014      LOCATE 10,62-L.C%:PRINT GUAPRI$
1100      '***************************
1102      '***PRECIO TERCER CURSO  ***
1104      '***************************
1106      F.C1%=12:C.C1%=54:L.E%=7:BO%=9:GOSUB 63000
1108      IF I.T%=0 THEN PRE3#=VAL(D.E$):GOTO 1112
1110      GOTO 1104
1112      GUAPRI#=PRE3#:N.D%=2:GOSUB 64770
1114      LOCATE 12,62-L.C%:PRINT GUAPRI$
1200      '***************************
1202      '***PRECIO CUARTO CURSO  ***
1204      '***************************
1206      F.C1%=14:C.C1%=54:L.E%=7:BO%=9:GOSUB 63000
1208      IF I.T%=0 THEN PRE4#=VAL(D.E$):GOTO 1212
1210      GOTO 1204
1212      GUAPRI#=PRE4#:N.D%=2:GOSUB 64770
1214      LOCATE 14,62-L.C%:PRINT GUAPRI$
1300      '***************************
1302      '***PRECIO QUINTO CURSO  ***
1304      '***************************
1306      F.C1%=16:C.C1%=54:L.E%=7:BO%=9:GOSUB 63000
1308      IF I.T%=0 THEN PRE5#=VAL(D.E$):GOTO 1312
1310      GOTO 1304
1312      GUAPRI#=PRE5#:N.D%=2:GOSUB 64770
1314      LOCATE 16,62-L.C%:PRINT GUAPRI$
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 23,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=14 THEN 610
2042   '
2043   FOR NN%=2 TO ULTIMO%
2044   GET #2,NN%
2045   IF CMARCA="L" THEN 2055
2046   IF CCURSO="1" THEN LSET CIMPOR=MKD$(PRE1#):GOTO 2053
2047   IF CCURSO="2" THEN LSET CIMPOR=MKD$(PRE2#):GOTO 2053
2048   IF CCURSO="3" THEN LSET CIMPOR=MKD$(PRE3#):GOTO 2053
2049   IF CCURSO="4" THEN LSET CIMPOR=MKD$(PRE4#):GOTO 2053
2050   IF CCURSO="5" THEN LSET CIMPOR=MKD$(PRE5#):GOTO 2053
2052   GOTO 2055
2053   PUT #2,NN%
2054   '
2055   '
2255   NEXT NN%
2256   GOSUB 8000
2258   COLOR 11:LOCATE 6,18:PRINT "¨ DESEAS CAMBIAR EL IMPORTE DE ALGUN ALUMNO ?"
2259   LOCATE 7,18:PRINT STRING$(45,205)
2260   COLOR 10:LOCATE 9,27:PRINT "Teclear la opci¢n S/N : < >"
2262   COLOR 15:LOCATE 9,52:A$=INPUT$(1)
2264   IF A$<>"S" AND A$<>"N" THEN 2262
2266   LOCATE 9,52:PRINT A$
2268   IF A$="N" THEN CLOSE:GOTO 9000
2555 GOSUB 31000
2560 GOSUB 7800
2565       '*******************************
2570       '***ENTRADA NUMERO DE ALUMNO ***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=12:C.C1%=16:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=4 THEN 9000
2600       GOTO 2585
2635       '
2640       GUAPRI#=VAL(CUENTA$):N.D%=0:GOSUB 64770:LOCATE 12,21-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
2642       N.L%=VAL(CUENTA$)
2645       IF N.L%<2 OR N.L%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2555
2650       GET #2,N.L%
2655       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2555
2728 COLOR 15
2730 NOM$=CNOMBRE
2740 APE1$=CAPELL1
2750 APE2$=CAPELL2
2760 GOSUB 40000
2770 LOCATE 12,25:PRINT CUCUCU$
2780 GUAPRI#=CVD(CIMPOR):N.D%=2:GOSUB 64770
2790 LOCATE 14,24-L.C%:PRINT GUAPRI$
2852      '***************************
2854      '***ENTRADA NUEVO PRECIO ***
2856      '***************************
2858      F.C1%=16:C.C1%=16:L.E%=7:BO%=9:GOSUB 63000
2860      IF I.T%=0 THEN NUE5#=VAL(D.E$):GOTO 2864
2862      GOTO 2856
2864      GUAPRI#=NUE5#:N.D%=2:GOSUB 64770
2866      LOCATE 16,24-L.C%:PRINT GUAPRI$
3000   '*************************************
3002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
3004   '*************************************
3006   GOSUB 8500
3008   COLOR 10:LOCATE 23,15:PRINT "Datos Correctos ";
3010   COLOR 15:PRINT " ENTER ";
3012   COLOR 10:PRINT " Datos Erroneos ";
3014   COLOR 15:PRINT " F1 "
3016   COLOR 10
3018   '
3020   A$=INPUT$(1)
3022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3020
3024   '
3026   IF ASC(A$)=14 THEN 2555
3042   '
3044 LSET CIMPOR=MKD$(NUE5#)
3046 PUT #2,N.L%
3048 GOTO 2555
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 23,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Cambios de Importes "
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : HOSTELERIA ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 22,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 23,1:PRINT CHR$(186):COLOR 11:LOCATE 23,3:PRINT "Mensajes: ":COLOR 10:LOCATE 23,80:PRINT CHR$(186)
8100 LOCATE 24,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 23,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESDISF"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA--":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9132 IF ERL=30220 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero de TARIFAS inesixtente":RET%=1:GOSUB 8500:GOTO 9000
9134 GOTO 9170
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,20 AS CSUCUR,17 AS CCUBAN,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,1 AS CESCUE,9 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FRESALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1):LSET CULTACT=MKI$(1)
30124 'LSET CFICHER="FRESALUM.DAT":LSET CSWG="0":LSET CSWA="0"
30126 'LSET CVIGO=MKI$(0):LSET REST1$="":LSET CFEFAC="0000":LSET REST2$=""
30128 'PUT #2,1
30130 'CLOSE
30132 'STOP
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10
31050 LOCATE 12,3:PRINT "Alumno N§ : <" SPACE$(6) ">" SPACE$(55)
31064 LOCATE 14,3:PRINT "Importe   : <" SPACE$(9) ">"
31068 LOCATE 16,3:PRINT "A Cobrar  : <" SPACE$(9) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ESCUELA DE HOSTELERIA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
