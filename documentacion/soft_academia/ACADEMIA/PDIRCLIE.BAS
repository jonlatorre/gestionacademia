10 '                       PDIRCLIE.BAS
20 '                      --------------
30 '    GESTION FICHERO DE DIRECCIONES PARA ESCRIBIR CARTAS
40 '    ---------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(60," ")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,A,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),GRU%(20),REFE$(99),PRO$(56)
170 FOR I%=1 TO 20:GRU%(I%)=0:NEXT I%
172 DATA "PROVEEDORES","HOTELES","CORRESPONSALES","CLIENTES","MOROSOS","VIP","EMPRESAS","FERIAS"
174 '
176 '
178 '
200 FOR I%=1 TO 99:REFE$(I%)="NO TENEMOS":NEXT I%
201 FOR I%=1 TO 8:READ REFE$(I%):NEXT I%
204 DATA "BIZKAIA        48 94- 37"
205 DATA "VIZCAYA        48 94- 37"
206 DATA "GUIPUZCOA      20 943-46"
207 DATA "ALAVA          01 945-46"
208 DATA "GIPUZKOA       20 943-46"
209 DATA "ARABA          01 945-46"
210 DATA "LA CORU¥A      15 981-46"
211 DATA "LUGO           27 982-46"
212 DATA "ORENSE         32 988-46"
213 DATA "PONTEVEDRA     36 986-46"
214 DATA "ASTURIAS       33 98- 37"
215 DATA "LEON           24 987-46"
216 DATA "ZAMORA         49 980-46"
217 DATA "SALAMANCA      37 923-46"
218 DATA "CANTABRIA      39 942-46"
219 DATA "PALENCIA       34 989-46"
220 DATA "VALLADOLID     47 983-46"
221 DATA "BURGOS         09 947-46"
222 DATA "SEGOVIA        40 921-46"
223 DATA "AVILA          05 920-46"
224 DATA "SORIA          42 975-46"
225 DATA "LA RIOJA       26 941-46"
226 DATA "NAVARRA        31 948-46"
227 DATA "NAFARROA       31 948-46"
228 DATA "ZARAGOZA       50 976-46"
229 DATA "HUESCA         22 974-46"
230 DATA "TERUEL         44 978-46"
231 DATA "BARCELONA      08 93- 37"
232 DATA "TARRAGONA      43 977-46"
233 DATA "LERIDA         25 973-46"
234 DATA "GERONA         17 972-46"
235 DATA "CASTELLON      12 964-46"
236 DATA "VALENCIA       46 96- 37"
237 DATA "ALICANTE       03 96- 37"
238 DATA "BALEARES       07 971-46"
239 DATA "MADRID         28 91- 37"
240 DATA "TOLEDO         45 925-46"
241 DATA "CIUDAD REAL    13 926-46"
242 DATA "CUENCA         16 969-46"
243 DATA "GUADALAJARA    19 949-46"
244 DATA "ALBACETE       02 967-46"
245 DATA "MURCIA         30 968-46"
246 DATA "CACERES        10 927-46"
247 DATA "BADAJOZ        06 924-46"
248 DATA "ALMERIA        04 950-46"
249 DATA "GRANADA        18 958-46"
250 DATA "JAEN           23 953-46"
251 DATA "CORDOBA        14 957-46"
252 DATA "MALAGA         29 95- 37"
253 DATA "SEVILLA        41 95- 37"
254 DATA "CADIZ          11 956-46"
255 DATA "HUELVA         21 959-46"
256 DATA "TENERIFE       38 922-46"
257 DATA "LAS PALMAS     35 928-46"
258 DATA "CEUTA          00 956-46"
259 DATA "MELILLA        00 95- 37"
270 FOR I%=1 TO 56:READ PRO$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
306 '
308 '
310 GOSUB 8000
312 COLOR 11
314 LOCATE 5,10:PRINT "- MENU GENERAL DE MAYORISTAS DE LA AGENCIA DE VIAJES E.I.D.E. -"
316 LOCATE 6,10:PRINT STRING$(63,205)
318 LOCATE 8,9:PRINT " - GESTION DEL FICHERO -"
320 COLOR 10:LOCATE 8,41: PRINT CHR$(186):COLOR 11
322 LOCATE 8,49: PRINT "- L I S T A D O S -"
324 LOCATE 9,9:PRINT STRING$(25,205)
326 COLOR 10:LOCATE 9,41: PRINT CHR$(186):COLOR 11
328 LOCATE 9,49:PRINT STRING$(23,205)
330 COLOR 10:LOCATE 10,9:PRINT "Altas de Mayoristas."
332 LOCATE 10,41:PRINT CHR$(186)
334 LOCATE 10,49:PRINT "Listado num‚rico de Mayoristas."
335 LOCATE 11,41:PRINT CHR$(186)
336 LOCATE 12,9:PRINT "Bajas de Mayoristas."
338 LOCATE 12,41:PRINT CHR$(186)
340 LOCATE 12,49:PRINT "Listado alfab‚tico Mayoristas."
341 LOCATE 13,41:PRINT CHR$(186)
342 LOCATE 14,9:PRINT "Modificaci¢n de Mayoristas."
344 LOCATE 14,41:PRINT CHR$(186)
346 LOCATE 14,49:PRINT "Listado de Cabeceras Cartas."
347 LOCATE 15,41:PRINT CHR$(186)
348 LOCATE 16,9:PRINT "Consulta num‚rica Mayoristas."
350 LOCATE 16,41:PRINT CHR$(186)
352 LOCATE 13,49:PRINT "  "
376 LOCATE 19,21:COLOR 15:PRINT " ESC "+CHR$(205)+CHR$(205)+">":COLOR 10:LOCATE 19,29:PRINT " Fin de tratamiento de Mayoristas.":COLOR 10
378 GOSUB 8500
380 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulse la Tecla ";
382 COLOR 15
384 PRINT "<"+CHR$(196)+CHR$(217)
386 COLOR 10
388 '
390 FILA%=10:COLUMNA%=5:OPCIONES%=4
392 GOSUB 20000
394 IF ATRAS%=1 THEN 390
396 IF I.A%>OPCIONES% THEN 402
398 ON I.A% GOTO 510,2490,2490,2490
400 '
402 FILA%=10:COLUMNA%=45:OPCIONES%=3
404 GOSUB 20000
406 IF ATRAS%=1 THEN 390
408 IF I.A%>OPCIONES% THEN 414
410 ON I.A% GOTO 7300,7310,7320
412 GOTO 300
414 '
416 '
418 GOTO 390
510 '
520 '
600 GOSUB 30000
610 '
612 TIT$="- ALTAS DE MAYORISTAS AGENCIA DE VIAJES E.I.D.E. -"
614 GOSUB 8000:GOSUB 31000
618 '
619 GOSUB 7650
630 '
632       '*******************************
634       '***ENTRADA NUMERO DE ALUMNO ***
636       '*******************************
638       COLOR 15
640       LOCATE 1,1:GET #2,1:N.ORDEN%=ULTIMO%+1
642       N$=STR$(N.ORDEN%):N$=LEFT$(CERO$,6-LEN(N$))+RIGHT$(N$,LEN(N$)-1)
643       GET #2,N.ORDEN%
644       CUENTA$=N$
656       '
658       '
661       GUAPRI#=N.ORDEN%:GOSUB 64770:LOCATE 6,19-L.C%:PRINT GUAPRI$
718       '
720       '********************
722       '***ENTRADA NOMBRE***
724       '********************
726       F.C1%=7:C.C1%=14:L.E%=60:BO%=60:GOSUB 64000
728       IF I.T%=0 THEN NOMBRA$=D.E$:GOTO 742
729       IF I.T%=5 THEN 726
730       IF I.T%=1 AND I.A%=1 THEN 726
732       IF I.T%=1 AND I.A%=3 THEN NOMBRA$=CNOMBRE:GOTO 742
733       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,14:PRINT SPC(60):GOTO 726
734       IF I.T%=3 THEN 726
736       IF I.T%=4 THEN 4000               'Fin Entrada.
738       IF I.T%=2 AND I.A%=1 THEN 742
740       IF I.T%=2 THEN 726
742       IF LEN(NOMBRA$)<60 THEN NOMBRA$=NOMBRA$+LEFT$(BLANCO$,60-LEN(NOMBRA$))
744       LOCATE 7,14:PRINT NOMBRA$
746       '
804       '***********************
806       '***ENTRADA DIRECCION***
808       '***********************
810       F.C1%=8:C.C1%=14:L.E%=60:BO%=60:GOSUB 64000
812       IF I.T%=0 THEN DIREC$=D.E$:GOTO 828
813       IF I.T%=5 THEN 810
814       IF I.T%=1 AND I.A%=1 THEN 810
816       IF I.T%=1 AND I.A%=3 THEN DIREC$=CDIREC:GOTO 828
817       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,14:PRINT SPC(60):GOTO 726
818       IF I.T%=3 THEN 726
820       IF I.T%=4 THEN 4000               'Fin Entrada.
822       IF I.T%=2 AND I.A%=1 THEN 828
824       IF I.T%=2 THEN 806
826       '
828       LOCATE 8,14:PRINT DIREC$
830       '
832       '***********************
834       '***ENTRADA POBLACION***
836       '***********************
838       F.C1%=9:C.C1%=14:L.E%=30:BO%=30:GOSUB 64000
840       IF I.T%=0 THEN POBLA$=D.E$:GOTO 856
841       IF I.T%=5 THEN 838
842       IF I.T%=1 AND I.A%=1 THEN 838
844       IF I.T%=1 AND I.A%=3 THEN POBLA$=CPOBLA:GOTO 856
845       IF I.T%=3 AND I.A%=1 THEN LOCATE 8,14:PRINT SPC(60):GOTO 806
846       IF I.T%=3 THEN 806
848       IF I.T%=4 THEN 4000               'Fin Entrada.
850       IF I.T%=2 AND I.A%=1 THEN 856
852       IF I.T%=2 THEN 834
854       '
856       LOCATE 9,14:PRINT POBLA$
858       '
860       '***********************
862       '***ENTRADA PROVINCIA***
864       '***********************
866       F.C1%=9:C.C1%=59:L.E%=15:BO%=15:GOSUB 64000
868       IF I.T%=0 THEN PROVI$=D.E$:GOTO 882
869       IF I.T%=5 THEN 866
870       IF I.T%=1 AND I.A%=1 THEN 866
872       IF I.T%=1 AND I.A%=3 THEN PROVI$=CPROVI:GOTO 882
873       IF I.T%=3 AND I.A%=1 THEN LOCATE 9,14:PRINT SPC(30):GOTO 834
874       IF I.T%=3 THEN 834
876       IF I.T%=4 THEN 4000               'Fin Entrada.
878       IF I.T%=2 AND I.A%=1 THEN 882
880       IF I.T%=2 THEN 866
882       IF LEN(PROVI$)<>15 THEN PROVI$=PROVI$+LEFT$(BLANCO$,15-LEN(PROVI$))
884       LOCATE 9,59:PRINT PROVI$
886       GOSUB 9500
888       '***************************
890       '***ENTRADA CODIGO POSTAL***
892       '***************************
894       COLOR 12:LOCATE 10,14:PRINT COPO$:COLOR 15
895       F.C1%=10:C.C1%=14+LC%:L.E%=5-LC%:BO%=5-LC%:ESC%=1:GOSUB 63000
896       IF I.T%=0 THEN COPOS$=COPO$+D.E$:GOTO 912
897       IF I.T%=5 THEN 894
898       IF I.T%=1 AND I.A%=1 THEN COPOS$="":GOTO 912
900       IF I.T%=1 AND I.A%=3 THEN COPOS$=CCOPOS:GOTO 912
901       IF I.T%=3 AND I.A%=1 THEN LOCATE 9,59:PRINT SPC(15):GOTO 866
902       IF I.T%=3 THEN 866
904       IF I.T%=4 THEN 4000               'Fin Entrada.
906       IF I.T%=2 AND I.A%=1 THEN 912
908       IF I.T%=2 THEN 894
910       '
912       LOCATE 10,14:PRINT COPOS$
914       '
916       '**********************
918       '***ENTRADA TELEFONO***
920       '**********************
922       COLOR 12:LOCATE 10,59:PRINT TELE$:COLOR 15
923       F.C1%=10:C.C1%=59+LP%:L.E%=10-LP%:BO%=10-LP%:GOSUB 64000
924       IF I.T%=0 THEN TELEFO$=TELE$+D.E$:GOTO 940
925       IF I.T%=5 THEN 922
926       IF I.T%=1 AND I.A%=1 THEN TELEFO$="":GOTO 940
928       IF I.T%=1 AND I.A%=3 THEN TELEFO$=CTELEFO:GOTO 940
929       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,14:PRINT SPC(5):GOTO 894
930       IF I.T%=3 THEN 894
932       IF I.T%=4 THEN 4000               'Fin Entrada.
934       IF I.T%=2 AND I.A%=1 THEN 940
936       IF I.T%=2 THEN 918
938       '
940       '
942       LOCATE 10,59:PRINT TELEFO$
944       '
950       '***********************
952       '***ENTRADA NACION   ***
954       '***********************
956       F.C1%=11:C.C1%=14:L.E%=20:BO%=20:GOSUB 64000
958       IF I.T%=0 THEN NACI$=D.E$:GOTO 978
960       IF I.T%=5 THEN 956
962       IF I.T%=1 AND I.A%=1 THEN 956
964       IF I.T%=1 AND I.A%=3 THEN NACI$=CNACION:GOTO 978
966       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,59:PRINT SPC(10):GOTO 918
968       IF I.T%=3 THEN 918
970       IF I.T%=4 THEN 4000               'Fin Entrada.
972       IF I.T%=2 AND I.A%=1 THEN 978
974       IF I.T%=2 THEN 952
976       '
978       LOCATE 11,14:PRINT NACI$
980       '
982       '***********************
984       '***ENTRADA REFERENCIA**
986       '***********************
988       F.C1%=11:C.C1%=59:L.E%=2:BO%=2:GOSUB 64000
990       IF I.T%=0 THEN REFER$=D.E$:GOTO 1008
992       IF I.T%=5 THEN 988
994       IF I.T%=1 AND I.A%=1 THEN 988
996       IF I.T%=1 AND I.A%=3 THEN REFER$=CREFE:GOTO 1008
998       IF I.T%=3 AND I.A%=1 THEN LOCATE 11,14:PRINT SPC(20):GOTO 952
1000       IF I.T%=3 THEN 952
1002       IF I.T%=4 THEN 4000               'Fin Entrada.
1004       IF I.T%=2 AND I.A%=1 THEN 1008
1006       IF I.T%=2 THEN 988
1008       IF REFER$<"00" OR REFER$>"99" THEN 988
1010       LOCATE 11,59:PRINT REFER$
1012       LOCATE 11,63:PRINT REFE$(VAL(REFER$))
1014       '
1020       '***********************
1022       '***ENTRADA CONTACTO ***
1024       '***********************
1026       F.C1%=12:C.C1%=14:L.E%=30:BO%=30:GOSUB 64000
1028       IF I.T%=0 THEN CONTA$=D.E$:GOTO 1048
1030       IF I.T%=5 THEN 1026
1032       IF I.T%=1 AND I.A%=1 THEN 1026
1034       IF I.T%=1 AND I.A%=3 THEN CONTA$=CCONTA:GOTO 1048
1036       IF I.T%=3 AND I.A%=1 THEN LOCATE 11,59:PRINT SPC(2):LOCATE 11,63:PRINT SPC(17):GOTO 988
1038       IF I.T%=3 THEN 988
1040       IF I.T%=4 THEN 4000               'Fin Entrada.
1042       IF I.T%=2 AND I.A%=1 THEN 1048
1044       IF I.T%=2 THEN 1022
1046       '
1048       LOCATE 12,14:PRINT CONTA$
1050       '
1052       '**********************
1054       '***ENTRADA TELEFONO***
1056       '**********************
1058       COLOR 12:LOCATE 12,59:PRINT TELE$:COLOR 15
1060       F.C1%=12:C.C1%=59+LP%:L.E%=10-LP%:BO%=10-LP%:GOSUB 64000
1062       IF I.T%=0 THEN TELECO$=TELE$+D.E$:GOTO 1082
1064       IF I.T%=5 THEN 1058
1066       IF I.T%=1 AND I.A%=1 THEN TELECO$="":GOTO 1082
1068       IF I.T%=1 AND I.A%=3 THEN TELECO$=CTECON:GOTO 1082
1070       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,14:PRINT SPC(30):GOTO 1026
1072       IF I.T%=3 THEN 1026
1074       IF I.T%=4 THEN 4000               'Fin Entrada.
1076       IF I.T%=2 AND I.A%=1 THEN 1082
1078       IF I.T%=2 THEN 1054
1080       '
1082       '
1084       LOCATE 12,59:PRINT TELECO$
1086       '
1090       '***********************
1092       '***ENTRADA F. PAGO  ***
1094       '***********************
1096       F.C1%=13:C.C1%=14:L.E%=1:BO%=1:GOSUB 64000
1098       IF I.T%=0 THEN FPAGO$=D.E$:GOTO 1115
1100       IF I.T%=5 THEN 1096
1102       IF I.T%=1 AND I.A%=1 THEN 1096
1104       IF I.T%=1 AND I.A%=3 THEN FPAGO$=CPAGO:GOTO 1115
1106       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,59:PRINT SPC(10):GOTO 1054
1108       IF I.T%=3 THEN 1054
1110       IF I.T%=4 THEN 4000               'Fin Entrada.
1112       IF I.T%=2 AND I.A%=1 THEN 1115
1114       IF I.T%=2 THEN 1092
1115       IF FPAGO$<>"P" AND FPAGO$<>"D" THEN 1092
1116       LOCATE 13,14:PRINT FPAGO$
1117       IF FPAGO$="P" THEN LOCATE 13,17:PRINT "PREPAGO":GOTO 1120
1118       LOCATE 13,17:PRINT "DOMICILIACION"
1120       '
1122       '**********************
1124       '***ENTRADA MEDIANTE***
1126       '**********************
1128       '
1130       F.C1%=13:C.C1%=59:L.E%=1:BO%=1:GOSUB 64000
1132       IF I.T%=0 THEN MEDIAN$=D.E$:GOTO 1150
1134       IF I.T%=5 THEN 1128
1136       IF I.T%=1 AND I.A%=1 THEN MEDIAN$=" ":GOTO 1150
1138       IF I.T%=1 AND I.A%=3 THEN MEDIAN$=CMEDIAN:GOTO 1150
1140       IF I.T%=3 AND I.A%=1 THEN LOCATE 13,14:PRINT SPC(1):LOCATE 13,17:PRINT SPC(13):GOTO 1092
1142       IF I.T%=3 THEN 1092
1144       IF I.T%=4 THEN 4000               'Fin Entrada.
1146       IF I.T%=2 AND I.A%=1 THEN 1150
1148       IF I.T%=2 THEN 1124
1150       IF MEDIAN$<>"T" AND MEDIAN$<>"G" AND MEDIAN$<>" " THEN 1130
1152       LOCATE 13,59:PRINT MEDIAN$
1153       IF MEDIAN$="T" THEN LOCATE 13,62:PRINT "TALON":GOTO 1156
1154       IF MEDIAN$="G" THEN LOCATE 13,62:PRINT "GIRO ":GOTO 1156
1156       '
1160       '**********************
1162       '***ENTRADA % INVIERNO*
1164       '**********************
1166       F.C1%=15:C.C1%=37:L.E%=2:BO%=2:GOSUB 63000
1168       IF I.T%=0 THEN NIN%=VAL(D.E$):GOTO 1186
1170       IF I.T%=5 THEN 1160
1172       IF I.T%=1 AND I.A%=1 THEN NIN%=0:GOTO 1186
1174       IF I.T%=1 AND I.A%=3 THEN NIN%=CVI(CNIN):GOTO 1186
1176       IF I.T%=3 AND I.A%=1 THEN LOCATE 13,59:PRINT SPC(1):LOCATE 13,62:PRINT SPC(13):GOTO 1120
1178       IF I.T%=3 THEN 1120
1180       IF I.T%=4 THEN 4000               'Fin Entrada.
1182       IF I.T%=2 AND I.A%=1 THEN 1186
1184       IF I.T%=2 THEN 1162
1186       '
1188       LOCATE 15,37:PRINT USING "##";NIN%
1190       '
1192       '
1194       '
1200       '**********************
1202       '***ENTRADA % VERANO***
1204       '**********************
1206       F.C1%=15:C.C1%=56:L.E%=2:BO%=2:GOSUB 63000
1208       IF I.T%=0 THEN NVE%=VAL(D.E$):GOTO 1226
1210       IF I.T%=5 THEN 1200
1212       IF I.T%=1 AND I.A%=1 THEN NVE%=0:GOTO 1226
1214       IF I.T%=1 AND I.A%=3 THEN NVE%=CVI(CNVE):GOTO 1226
1216       IF I.T%=3 AND I.A%=1 THEN LOCATE 15,37:PRINT SPC(2):GOTO 1160
1218       IF I.T%=3 THEN 1160
1220       IF I.T%=4 THEN 4000               'Fin Entrada.
1222       IF I.T%=2 AND I.A%=1 THEN 1226
1224       IF I.T%=2 THEN 1202
1226       '
1228       LOCATE 15,56:PRINT USING "##";NVE%
1230       '
1232       '
1234       '
1250       '**********************
1252       '***ENTRADA % NAVIDAD**
1254       '**********************
1256       F.C1%=15:C.C1%=77:L.E%=2:BO%=2:GOSUB 63000
1258       IF I.T%=0 THEN NNA%=VAL(D.E$):GOTO 1276
1260       IF I.T%=5 THEN 1250
1262       IF I.T%=1 AND I.A%=1 THEN NNA%=0:GOTO 1276
1264       IF I.T%=1 AND I.A%=3 THEN NNA%=CVI(CNNA):GOTO 1276
1266       IF I.T%=3 AND I.A%=1 THEN LOCATE 15,56:PRINT SPC(2):GOTO 1200
1268       IF I.T%=3 THEN 1200
1270       IF I.T%=4 THEN 4000               'Fin Entrada.
1272       IF I.T%=2 AND I.A%=1 THEN 1276
1274       IF I.T%=2 THEN 1252
1276       '
1278       LOCATE 15,77:PRINT USING "##";NNA%
1280       '
1282       '
1284       '
1300       '**********************
1302       '***ENTRADA % PUENTES**
1304       '**********************
1306       F.C1%=16:C.C1%=37:L.E%=2:BO%=2:GOSUB 63000
1308       IF I.T%=0 THEN NPU%=VAL(D.E$):GOTO 1326
1310       IF I.T%=5 THEN 1300
1312       IF I.T%=1 AND I.A%=1 THEN NPU%=0:GOTO 1326
1314       IF I.T%=1 AND I.A%=3 THEN NPU%=CVI(CNPU):GOTO 1326
1316       IF I.T%=3 AND I.A%=1 THEN LOCATE 15,77:PRINT SPC(2):GOTO 1250
1318       IF I.T%=3 THEN 1250
1320       IF I.T%=4 THEN 4000               'Fin Entrada.
1322       IF I.T%=2 AND I.A%=1 THEN 1326
1324       IF I.T%=2 THEN 1302
1326       '
1328       LOCATE 16,37:PRINT USING "##";NPU%
1330       '
1332       '
1334       '
1350       '**********************
1352       '***ENTRADA % OFERTAS**
1354       '**********************
1356       F.C1%=16:C.C1%=56:L.E%=2:BO%=2:GOSUB 63000
1358       IF I.T%=0 THEN NOF%=VAL(D.E$):GOTO 1376
1360       IF I.T%=5 THEN 1350
1362       IF I.T%=1 AND I.A%=1 THEN NOF%=0:GOTO 1376
1364       IF I.T%=1 AND I.A%=3 THEN NOF%=CVI(CNOF):GOTO 1376
1366       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,37:PRINT SPC(2):GOTO 1300
1368       IF I.T%=3 THEN 1300
1370       IF I.T%=4 THEN 4000               'Fin Entrada.
1372       IF I.T%=2 AND I.A%=1 THEN 1376
1374       IF I.T%=2 THEN 1352
1376       '
1378       LOCATE 16,56:PRINT USING "##";NOF%
1380       '
1382       '
1384       '
1400       '**********************
1402       '***ENTRADA % S.SANTA**
1404       '**********************
1406       F.C1%=16:C.C1%=77:L.E%=2:BO%=2:GOSUB 63000
1408       IF I.T%=0 THEN NSS%=VAL(D.E$):GOTO 1426
1410       IF I.T%=5 THEN 1400
1412       IF I.T%=1 AND I.A%=1 THEN NSS%=0:GOTO 1426
1414       IF I.T%=1 AND I.A%=3 THEN NSS%=CVI(CNSS):GOTO 1426
1416       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,56:PRINT SPC(2):GOTO 1350
1418       IF I.T%=3 THEN 1350
1420       IF I.T%=4 THEN 4000               'Fin Entrada.
1422       IF I.T%=2 AND I.A%=1 THEN 1426
1424       IF I.T%=2 THEN 1402
1426       '
1428       LOCATE 16,77:PRINT USING "##";NSS%
1430       '
1432       '
1434       '
1440       '**********************
1442       '***ENTRADA % INVIERNO*
1444       '**********************
1446       F.C1%=17:C.C1%=37:L.E%=2:BO%=2:GOSUB 63000
1448       IF I.T%=0 THEN IIN%=VAL(D.E$):GOTO 1466
1450       IF I.T%=5 THEN 1440
1452       IF I.T%=1 AND I.A%=1 THEN IIN%=0:GOTO 1466
1454       IF I.T%=1 AND I.A%=3 THEN IIN%=CVI(CIIN):GOTO 1466
1456       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,77:PRINT SPC(2):GOTO 1400
1458       IF I.T%=3 THEN 1400
1460       IF I.T%=4 THEN 4000               'Fin Entrada.
1462       IF I.T%=2 AND I.A%=1 THEN 1466
1464       IF I.T%=2 THEN 1442
1466       '
1468       LOCATE 17,37:PRINT USING "##";IIN%
1470       '
1472       '**********************
1474       '***ENTRADA % VERANO***
1476       '**********************
1478       F.C1%=17:C.C1%=56:L.E%=2:BO%=2:GOSUB 63000
1480       IF I.T%=0 THEN IVE%=VAL(D.E$):GOTO 1498
1482       IF I.T%=5 THEN 1472
1484       IF I.T%=1 AND I.A%=1 THEN IVE%=0:GOTO 1498
1486       IF I.T%=1 AND I.A%=3 THEN IVE%=CVI(CIVE):GOTO 1498
1488       IF I.T%=3 AND I.A%=1 THEN LOCATE 17,37:PRINT SPC(2):GOTO 1440
1490       IF I.T%=3 THEN 1440
1492       IF I.T%=4 THEN 4000               'Fin Entrada.
1494       IF I.T%=2 AND I.A%=1 THEN 1498
1496       IF I.T%=2 THEN 1474
1498       '
1500       LOCATE 17,56:PRINT USING "##";IVE%
1502       '
1504       '**********************
1506       '***ENTRADA % NAVIDAD**
1508       '**********************
1510       F.C1%=17:C.C1%=77:L.E%=2:BO%=2:GOSUB 63000
1512       IF I.T%=0 THEN INA%=VAL(D.E$):GOTO 1530
1514       IF I.T%=5 THEN 1504
1516       IF I.T%=1 AND I.A%=1 THEN INA%=0:GOTO 1530
1518       IF I.T%=1 AND I.A%=3 THEN INA%=CVI(CINA):GOTO 1530
1520       IF I.T%=3 AND I.A%=1 THEN LOCATE 17,56:PRINT SPC(2):GOTO 1472
1522       IF I.T%=3 THEN 1472
1524       IF I.T%=4 THEN 4000               'Fin Entrada.
1526       IF I.T%=2 AND I.A%=1 THEN 1530
1528       IF I.T%=2 THEN 1506
1530       '
1532       LOCATE 17,77:PRINT USING "##";INA%
1534       '
1536       '**********************
1538       '***ENTRADA % PUENTES**
1540       '**********************
1542       F.C1%=18:C.C1%=37:L.E%=2:BO%=2:GOSUB 63000
1544       IF I.T%=0 THEN IPU%=VAL(D.E$):GOTO 1562
1546       IF I.T%=5 THEN 1536
1548       IF I.T%=1 AND I.A%=1 THEN IPU%=0:GOTO 1562
1550       IF I.T%=1 AND I.A%=3 THEN IPU%=CVI(CIPU):GOTO 1562
1552       IF I.T%=3 AND I.A%=1 THEN LOCATE 17,77:PRINT SPC(2):GOTO 1504
1554       IF I.T%=3 THEN 1504
1556       IF I.T%=4 THEN 4000               'Fin Entrada.
1558       IF I.T%=2 AND I.A%=1 THEN 1562
1560       IF I.T%=2 THEN 1538
1562       '
1564       LOCATE 18,37:PRINT USING "##";IPU%
1566       '
1568       '**********************
1570       '***ENTRADA % OFERTAS**
1572       '**********************
1574       F.C1%=18:C.C1%=56:L.E%=2:BO%=2:GOSUB 63000
1576       IF I.T%=0 THEN IOF%=VAL(D.E$):GOTO 1594
1578       IF I.T%=5 THEN 1568
1580       IF I.T%=1 AND I.A%=1 THEN IOF%=0:GOTO 1594
1582       IF I.T%=1 AND I.A%=3 THEN IOF%=CVI(CIOF):GOTO 1594
1584       IF I.T%=3 AND I.A%=1 THEN LOCATE 18,37:PRINT SPC(2):GOTO 1536
1586       IF I.T%=3 THEN 1536
1588       IF I.T%=4 THEN 4000               'Fin Entrada.
1590       IF I.T%=2 AND I.A%=1 THEN 1594
1592       IF I.T%=2 THEN 1570
1594       '
1596       LOCATE 18,56:PRINT USING "##";IOF%
1598       '
1600       '**********************
1602       '***ENTRADA % S.SANTA**
1604       '**********************
1606       F.C1%=18:C.C1%=77:L.E%=2:BO%=2:GOSUB 63000
1608       IF I.T%=0 THEN ISS%=VAL(D.E$):GOTO 1626
1610       IF I.T%=5 THEN 1600
1612       IF I.T%=1 AND I.A%=1 THEN ISS%=0:GOTO 1626
1614       IF I.T%=1 AND I.A%=3 THEN ISS%=CVI(CISS):GOTO 1626
1616       IF I.T%=3 AND I.A%=1 THEN LOCATE 18,56:PRINT SPC(2):GOTO 1568
1618       IF I.T%=3 THEN 1568
1620       IF I.T%=4 THEN 4000               'Fin Entrada.
1622       IF I.T%=2 AND I.A%=1 THEN 1626
1624       IF I.T%=2 THEN 1602
1626       '
1628       LOCATE 18,77:PRINT USING "##";ISS%
1630       '
1644       '***************************
1646       '***ENTRADA OBSERVACIO 1 ***
1648       '***************************
1650       F.C1%=19:C.C1%=14:L.E%=60:BO%=60:GOSUB 64000
1652       IF I.T%=0 THEN OBSE1$=D.E$:GOTO 1670
1654       IF I.T%=5 THEN 1648
1656       IF I.T%=1 AND I.A%=1 THEN OBSE1$="":GOTO 1670
1658       IF I.T%=1 AND I.A%=3 THEN OBSE1$=COBSE1:GOTO 1670
1660       IF I.T%=3 AND I.A%=1 THEN LOCATE 18,77:PRINT SPC(2):GOTO 1600
1662       IF I.T%=3 THEN 1600
1664       IF I.T%=4 THEN 4000               'Fin Entrada.
1666       IF I.T%=2 AND I.A%=1 THEN 1670
1668       IF I.T%=2 THEN 1648
1670       '
1672       LOCATE 19,14:PRINT OBSE1$
1674       '
1744       '***************************
1746       '***ENTRADA OBSERVACIO 2 ***
1748       '***************************
1750       F.C1%=20:C.C1%=14:L.E%=60:BO%=60:GOSUB 64000
1752       IF I.T%=0 THEN OBSE2$=D.E$:GOTO 1770
1754       IF I.T%=5 THEN 1748
1756       IF I.T%=1 AND I.A%=1 THEN OBSE2$="":GOTO 1770
1758       IF I.T%=1 AND I.A%=3 THEN OBSE2$=COBSE2:GOTO 1770
1760       IF I.T%=3 AND I.A%=1 THEN LOCATE 19,14:PRINT SPC(60):GOTO 1648
1762       IF I.T%=3 THEN 1648
1764       IF I.T%=4 THEN 4000               'Fin Entrada.
1766       IF I.T%=2 AND I.A%=1 THEN 1770
1768       IF I.T%=2 THEN 1748
1770       '
1772       LOCATE 20,14:PRINT OBSE2$
1774       '
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=14 AND I.A%=1 THEN 610
2028   IF ASC(A$)=14 AND I.A%=3 THEN 2545
2042   '
2043   LSET CCUENTA=CUENTA$:LSET CNOMBRE=NOMBRA$:LSET CTECON=TELECO$:LSET CNACION=NACI$:LSET CPAGO=FPAGO$:LSET CCONTA=CONTA$:LSET CREFE=REFER$
2044   LSET CDIREC=DIREC$:LSET CPOBLA=POBLA$:LSET CXXX="":LSET CMEDIAN=MEDIAN$
2045   LSET CPROVI=PROVI$:LSET CCOPOS=COPOS$:LSET CTELEFO=TELEFO$
2046   LSET CIIN=MKI$(IIN%):LSET CIVE=MKI$(IVE%):LSET CINA=MKI$(INA%):LSET CIPU=MKI$(IPU%):LSET CIOF=MKI$(IOF%):LSET CISS=MKI$(ISS%)
2047   LSET CNIN=MKI$(NIN%):LSET CNVE=MKI$(NVE%):LSET CNNA=MKI$(NNA%):LSET CNPU=MKI$(NPU%):LSET CNOF=MKI$(NOF%):LSET CNSS=MKI$(NSS%)
2048   LSET CMARCA="A"
2049   LSET CSTATUS="O"
2050   LSET COBSE1=OBSE1$:LSET COBSE2=OBSE2$:LSET CFIL="":
2052   IF I.A%=3 THEN 2100   'REGRABAR EN CASO DE MODIFICACION.
2053   PUT #2,N.ORDEN%:GET #2,1:LSET CULTI=MKI$(N.ORDEN%):PUT #2,1
2054   '
2088   '
2090   '
2092   CLOSE:GOTO 600
2094   '
2096   '
2098   '
2100   'REGRABAR EN CASO DE MODIFICACION.
2102   LSET CMARCA="M":PUT #2,N.L%
2260   '
2420   '
2430   CLOSE:GOTO 2540
2440 '
2450 '
2460 '
2488 '
2490 '**********************************************
2495 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
2500 '**********************************************
2505 '
2510 IF I.A%=2 THEN TIT$="- BAJAS DE MAYORISTAS AGENCIA DE VIAJES E.I.D.E. -"
2515 IF I.A%=4 THEN TIT$="- CONSULTA DE MAYORISTAS AGENCIA DE VIAJES E.I.D.E. -"
2520 IF I.A%=3 THEN TIT$="- MODIFICACION DE MAYORISTAS AGENCIA DE VIAJES E.I.D.E. -"
2525 '
2530 GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
2535 '
2540 GOSUB 30000
2545 GOSUB 8000:GOSUB 31000
2550 IF I.A%=2 THEN GOSUB 7405
2555 IF I.A%=3 THEN GOSUB 7500
2560 IF I.A%=4 THEN GOSUB 7800
2565       '*******************************
2570       '***ENTRADA NUMERO DE ALUMNO ***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=6:C.C1%=14:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=5 THEN 2585
2600       IF I.T%=1 AND I.A%=1 THEN 2585
2605       IF I.T%=1 THEN 2570
2610       IF I.T%=3 THEN 2570                 'Retroceder Campo.
2615       IF I.T%=4 THEN 4000               'Fin Entrada.
2620       IF I.T%=2 AND I.A%=1 THEN 2635
2625       IF I.T%=2 THEN 2570
2630       '
2635       '
2640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 6,19-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
2642       N.L%=VAL(CUENTA$)
2645       IF N.L%<2 OR N.L%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Mayorista no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2550
2650       GET #2,N.L%
2655       IF I.A%<>4 AND CMARCA="L" THEN GOSUB 8500:MEN$="Mayorista dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2550
2660       GOSUB 2700   'PRESENTAR DATOS EN PANTALLA.
2665       IF I.A%=2 THEN 2990
2670       IF I.A%=4 THEN 3240
2675       GOTO 3400
2700 '***********************************
2702 '***PRESENTAMOS DATOS EN PANTALLA***
2704 '***********************************
2728 COLOR 15
2730 LOCATE 7,14:PRINT CNOMBRE
2760 LOCATE 8,14:PRINT CDIREC
2770 LOCATE 9,14:PRINT CPOBLA
2780 LOCATE 9,59:PRINT CPROVI
2790 LOCATE 10,14:PRINT CCOPOS
2800 LOCATE 10,59:PRINT CTELEFO
2802 LOCATE 11,14:PRINT CNACION
2804 LOCATE 11,59:PRINT CREFE
2806 IF CREFE<"01" OR CREFE>"99" THEN LOCATE 11,63:PRINT "NO TENEMOS":GOTO 2820
2816 LOCATE 11,63:PRINT REFE$(VAL(CREFE))
2820 LOCATE 12,14:PRINT CCONTA
2821 LOCATE 12,59:PRINT CTECON
2822       LOCATE 13,14:PRINT CPAGO
2824       IF CPAGO="P" THEN LOCATE 13,17:PRINT "PREPAGO":GOTO 2828
2826       LOCATE 13,17:PRINT "DOMICILIACION"
2828       LOCATE 13,59:PRINT CMEDIAN
2830       IF CMEDIAN="T" THEN LOCATE 13,62:PRINT "TALON":GOTO 2834
2832       IF CMEDIAN="G" THEN LOCATE 13,62:PRINT "GIRO "
2834       '
2836       LOCATE 15,37:PRINT USING "##";CVI(CNIN)
2838       LOCATE 15,56:PRINT USING "##";CVI(CNVE)
2840       LOCATE 15,77:PRINT USING "##";CVI(CNNA)
2842       LOCATE 16,37:PRINT USING "##";CVI(CNPU)
2844       LOCATE 16,56:PRINT USING "##";CVI(CNOF)
2846       LOCATE 16,77:PRINT USING "##";CVI(CNSS)
2856       LOCATE 17,37:PRINT USING "##";CVI(CIIN)
2858       LOCATE 17,56:PRINT USING "##";CVI(CIVE)
2860       LOCATE 17,77:PRINT USING "##";CVI(CINA)
2862       LOCATE 18,37:PRINT USING "##";CVI(CIPU)
2864       LOCATE 18,56:PRINT USING "##";CVI(CIOF)
2866       LOCATE 18,77:PRINT USING "##";CVI(CISS)
2887 LOCATE 19,14:PRINT COBSE1
2888 LOCATE 20,14:PRINT COBSE2
2970 IF CMARCA<>"L" THEN RETURN
2972 COLOR 19:LOCATE 6,23:PRINT "MAYORISTA DADO DE BAJA":COLOR 15
2975 RETURN
2980 '*************************
2990 '***TRATAMIENTO DE BAJA***
3000 '*************************
3010 GOSUB 8500:COLOR 10
3020 LOCATE 22,15:PRINT "DAR DE BAJA ";
3030 COLOR 15:PRINT " ENTER ";
3040 COLOR 10:PRINT " NO DAR DE BAJA ";
3050 COLOR 15:PRINT " F1 "
3055 A$=INPUT$(1)
3057 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3055
3060 IF ASC(A$)=14 THEN 2545
3064 LSET CMARCA="L"
3065 PUT #2,N.L%
3095 '
3104 GOTO 2545
3200 '
3210 '
3220 '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3270 A$=INPUT$(1)      ':GOSUB 7650:LOCATE 8,56:PRINT SPC(20)
3280 GOTO 2545
3290 '
3300 '
3310 '
3400 '*****************************
3410 '***MODIFICACION DE ALUMNOS***
3420 '*****************************
3430 '
3440 GOTO 720     'PASAMOS POR EL ALTA
3450 '
3460 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
7300 RUN "PDIRLANU"  'LISTAR MAYORISTAS ORDEN DE NUMERO
7310 RUN "PDIRLAAL"  'LISTAR MAYORISTAS ORDEN ALFABETICO
7320 RUN "PDIRLICA"  'LISTAR CABECERAS DE LAS CARTAS
7405 '***********************************************************
7410 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION BAJAS         ***
7420 '***********************************************************
7430 GOSUB 8500:LOCATE 22,14
7460 COLOR 10:PRINT " F7 =>";
7470 COLOR 15:PRINT " Fin de Bajas de Mayoristas."
7480 '
7490 RETURN
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7532 'COLOR 10:PRINT "F1 =>";
7534 'COLOR 15:PRINT " Ayuda ";
7540 COLOR 10:PRINT " ENTER =>";
7550 COLOR 15:PRINT " Saltar ";
7560 COLOR 10:PRINT " F7 =>";
7570 COLOR 15:PRINT " Fin Modif. ";
7580 COLOR 10:PRINT " F8 =>";
7590 COLOR 15:PRINT " Campo Atr s"
7600 '
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7672 'COLOR 10:PRINT " F1 =>";
7674 'COLOR 15:PRINT " Ayuda ";
7680 COLOR 10:PRINT " ENTER =>";
7690 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Altas ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 22,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Consultas de Mayoristas."
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,37:PRINT "Aplicaci¢n : MAYORISTAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PDIRINIC"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FDIRCLIE.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FDIRCLIE.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
9500 '***************************************
9502 '***RUTINA PARA C.POSTALES Y PREFIJOS***
9504 '***************************************
9506 '
9508 ' COPOS$=C.P.   LC%=LONGITUD C.P.
9510 ' TELEFO$=PREFIJO   LP%=LONGI-PREFIJO LT%=LONGI-TELEFONO
9512 '
9514 FOR N%=1 TO 56
9516 IF PROVI$<>LEFT$(PRO$(N%),15) THEN 9530
9518 COPO$=MID$(PRO$(N%),16,2)
9520 LP%=VAL(MID$(PRO$(N%),23,1))
9522 LT%=VAL(MID$(PRO$(N%),24,1))
9524 TELE$=MID$(PRO$(N%),19,LP%)
9526 LC%=2
9528 RETURN
9530 NEXT N%
9532 COPO$="":TELE$="":LC%=0:LP%=0:LT%=0
9534 RETURN
9536 '
20000 '********************************************
20010 '*** RATONCITO PARA SELECCIONAR OPCIONES  ***
20020 '********************************************
20030 '***                                      ***
20040 '*** FILA%=FILA DONDE APARECERA EL RATON  ***
20050 '***                                      ***
20060 '*** COLUMNA%=COLUMNA DONDE APARECERA EL  ***
20070 '***          RATON                       ***
20080 '***                                      ***
20090 '*** OPCIONES%=NUMERO DE OPCIONES         ***
20100 '***                                      ***
20110 '*** CON ESC SALE A LA SENTENCIA 9000     ***
20120 '***                                      ***
20130 '********************************************
20140 '
20150 R$=CHR$(0):ATRAS%=0
20160 FOR I.A%=1 TO OPCIONES%
20170 COLOR 15:LOCATE FILA%,COLUMNA%:PRINT CHR$(205)+CHR$(205)+">"
20180 COLOR 10:R$=INKEY$:IF R$="" THEN 20180
20190 IF ASC(R$)=27 THEN 9000
20200 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
20210 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
20220 IF R$="2" THEN R$=CHR$(31)
20230 IF R$="8" THEN R$=CHR$(30)
20240 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 20250  ELSE 20180
20250 IF ASC(R$)=13 THEN RETURN
20260 LOCATE FILA%,COLUMNA%:PRINT "   "
20270 IF ASC(R$)=30 AND I.A%=1 THEN ATRAS%=1:RETURN
20280 IF ASC(R$)=30 THEN FILA%=FILA%-2:I.A%=I.A%-1:GOTO 20170 'AL NEXT
20290 FILA%=FILA%+2
20300 NEXT I.A%
20310 RETURN
20320 'ESTO SE ACABA
20330 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30525 '
30590 ' Apertura del fichero de datos   FDIRCLIE.DAT
30600 OPEN "R",#2,"FDIRCLIE.DAT",420
30610 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,60 AS CNOMBRE,60 AS CDIREC,30 AS CPOBLA,15 AS CPROVI,10 AS CTECON,5 AS CCOPOS,10 AS CTELEFO,20 AS CNACION
30612 FIELD #2,216 AS TODO1$,1 AS CPAGO,30 AS CCONTA,2 AS CNIN,2 AS CNVE,2 AS CNNA,2 AS CNPU,2 AS CNOF,2 AS CNSS,8 AS CXXX,60 AS COBSE1,60 AS COBSE2,1 AS CMARCA,2 AS CREFE
30613 FIELD #2,250 AS TODO2$,140 AS TODO3$,2 AS CIIN,2 AS CIVE,2 AS CINA,2 AS CIPU,2 AS CIOF,2 AS CISS,1 AS CMEDIAN,17 AS CFIL
30614 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,12 AS CFICHER,250 AS CEST1$,150 AS CEST2$
30615  GET #2,1
30616  IF CFICHER<>"FDIRCLIE.DAT" THEN 9144
30617  ULTIMO%=CVI(CULTI)
30630 'FOR A%=2 TO ULTIMO%
30632 'GET #2,A%
30636 'LSET CNIN=MKI$(0):LSET CNVE=MKI$(0):LSET CNNA=MKI$(0):LSET CNPU=MKI$(0):LSET CNOF=MKI$(0):LSET CNSS=MKI$(0)
30638 'LSET CIIN=MKI$(0):LSET CIVE=MKI$(0):LSET CINA=MKI$(0):LSET CIPU=MKI$(0):LSET CIOF=MKI$(0):LSET CISS=MKI$(0)
30640 'PUT #2,A%
30650 'NEXT A%
30652 'CLOSE:STOP
30940 RETURN
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 4,X%:PRINT TIT$
31042 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 6,1:PRINT "N£mero .. : <" SPACE$(6) ">"
31054 LOCATE 7,1:PRINT "Nombre .. : <" SPACE$(60) ">"
31058 LOCATE 8,1:PRINT "Direcci¢n : <" SPACE$(60) ">"
31060 LOCATE 9,1:PRINT "Poblaci¢n : <" SPACE$(30) ">"
31062 LOCATE 9,46:PRINT "Provincia : <" SPACE$(15) ">"
31064 LOCATE 10,1:PRINT "CD.Postal : <" SPACE$(5) ">"
31066 LOCATE 10,46:PRINT "Tel‚fono  : <" SPACE$(10) ">"
31068 LOCATE 11,1:PRINT "Naci¢n .. : <" SPACE$(20) ">"
31070 LOCATE 11,46:PRINT "Referencia: <" SPACE$(2) ">"SPACE$(18)
31072 LOCATE 12,1:PRINT "Contacto. : <" SPACE$(30) ">"
31074 LOCATE 12,46:PRINT "Tel‚fono  : <" SPACE$(10) ">"
31076 LOCATE 13,1:PRINT "Forma Pago: < >"
31078 LOCATE 13,46:PRINT "Mediante  : < >"
31086 LOCATE 15,1:PRINT "Comisiones Nacionales : Invierno : <  >     Verano  : <  >     Navidades : <  >"
31088 LOCATE 16,1:PRINT "                        Puentes  : <  >     Ofertas : <  >     S. Santa  : <  >"
31090 LOCATE 17,1:PRINT "Comisiones Internaci. : Invierno : <  >     Verano  : <  >     Navidades : <  >"
31092 LOCATE 18,1:PRINT "                        Puentes  : <  >     Ofertas : <  >     S. Santa  : <  >"
31201 LOCATE 19,1:PRINT "Observ. 1 : <" SPACE$(60) ">"
31202 LOCATE 20,1:PRINT "        2 : <" SPACE$(60) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** AGENCIA DE VIAJES EIDE ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
