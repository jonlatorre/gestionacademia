10 '                       PCAMBIAF.BAS
20 '                      --------------
30 '          CAMBIAR FECHAS CON MOTIVO DEL A¥O 2.000
40 '          ---------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 '
130 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,4)
134 '
140 DEFSTR C,B,G,F,X,O
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
306 '
308 EMPRESA$="***PRIETO-ACADEMIA***"
310 GOSUB 8000
312 COLOR 10
314 LOCATE 10,19:PRINT "- PREPARAR FECHAS PARA EL PROXIMO MILENIO -"
316 LOCATE 11,19:PRINT STRING$(43,205)
510 '
520 GOSUB 8500:MEN$="Espere por favor.......":GOSUB 8500
600 GOSUB 30000:GOSUB 32000
610 '
1000 GET #2,1
1002 GET #3,1
1004 LSET OCUEN1=CCUEN1:LSET OSTAT=CSTAT:LSET OULTI=CULTI
1006 LSET OULTACT=CULTACT:LSET OFICHER=CFICHER:LSET OSWG=CSWG
1008 LSET OSWA=CSWA:LSET OVIGO=CVIGO:LSET RESO1$=""
1010 LSET OFEFAC="19"+CFEFAC:LSET RESO2$=""
1012 PUT #3,1
1014 '
1016 FOR AA%=2 TO ULTIMO%
1018 GET #2,AA%
1020 GET #3,AA%
1022 LSET OCUENTA=CCUENTA:LSET OSTATUS=CSTATUS:LSET ONOMBRE=CNOMBRE
1024 LSET OAPELL1=CAPELL1:LSET OAPELL2=CAPELL2:LSET ODIREC=CDIREC
1026 LSET OPOBLA=CPOBLA:LSET OPROVI=CPROVI:LSET OCOPOS=CCOPOS
1028 LSET OTELEFO=CTELEFO:LSET OTELEFA=CTELEFA:LSET ODNI=CDNI
1030 LSET OGRUPO=CGRUPO:LSET OFENA="19"+CFENA:LSET OBANCO=CBANCO
1032 LSET OSUCUR=CSUCUR:LSET ODIGI=CDIGI:LSET OCUBAN=CCUBAN
1034 LSET OFALTA="19"+CFALTA:LSET OFBAJA="19"+CFBAJA:LSET OFILL=""
1036 LSET OCUESP=CCUESP:LSET OIMPOR=CIMPOR:LSET OMARCA=CMARCA
1038 LSET OCONFI=CCONFI:LSET OOBSER=COBSER:LSET OFACT="19"+CFACT
1040 PUT #3,AA%
1042 NEXT AA%
1044 CLOSE
1046 GOTO 9000
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,"FACAALUM.DAT",270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,21 AS CKAKA,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
32000 '************************************
32010 '***APERTURA FICHERO ALUMNOS NUEVO***
32020 '************************************
32025 '
32090 ' Apertura del fichero de datos   FACAALUM.NUE
32100 OPEN "R",#3,"FACAALUM.NUE",270
32110 FIELD #3,5 AS OCUENTA,1 AS OSTATUS,20 AS ONOMBRE,20 AS OAPELL1,20 AS OAPELL2,40 AS ODIREC,20 AS OPOBLA,15 AS OPROVI,5 AS OCOPOS,7 AS OTELEFO,7 AS OTELEFA,13 AS ODNI,3 AS OGRUPO
32112 FIELD #3,176 AS TODA1$,8 AS OFENA,4 AS OBANCO,4 AS OSUCUR,2 AS ODIGI,10 AS OCUBAN,8 AS OFALTA,8 AS OFBAJA,1 AS OCUESP,4 AS OIMPOR,1 AS OMARCA,1 AS OCONFI,20 AS OOBSER,6 AS OFACT,17 AS OFILL
32114 FIELD #3,5 AS OCUEN1,1 AS OSTAT,2 AS OULTI,2 AS OULTACT,12 AS OFICHER,1 AS OSWG,1 AS OSWA,2 AS OVIGO,235 AS RESO1$,6 AS OFEFAC,3 AS RESO2$
32140 RETURN
32150 '
