10 '                       PINFCOAL.BAS
20 '                      --------------
30 '       CONSULTA ALFABETICA DE ALUMNOS DE INFORMATICA
40 '       ---------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,G,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 GOTO 4500
2700 '***********************************
2702 '***PRESENTAMOS DATOS EN PANTALLA***
2704 '***********************************
2728 COLOR 15
2730 LOCATE 8,56:PRINT CNOMBRE
2740 LOCATE 9,19:PRINT CAPELL1
2750 LOCATE 9,56:PRINT CAPELL2
2760 LOCATE 10,19:PRINT CDIREC
2770 LOCATE 11,19:PRINT CPOBLA
2780 LOCATE 11,56:PRINT CPROVI
2790 LOCATE 12,19:PRINT CCOPOS
2800 LOCATE 12,56:PRINT CTELEFO+" "+CTELEFA
2810 LOCATE 13,19:PRINT CDNI
2820 LOCATE 13,56:PRINT CCURSO:LOCATE 13,59
2821 IF CCURSO="1" THEN PRINT "PRIMERO"
2822 IF CCURSO="2" THEN PRINT "SEGUNDO"
2823 IF CCURSO="3" THEN PRINT "TERCERO"
2824 IF CCURSO="4" THEN PRINT "CUARTO "
2825 IF CCURSO="5" THEN PRINT "QUINTO "
2840 LOCATE 14,19:PRINT RIGHT$(CFENA,2)"-"MID$(CFENA,3,2)"-"LEFT$(CFENA,2)
2845 LOCATE 14,56:PRINT RIGHT$(CFALTA,2)"-"MID$(CFALTA,3,2)"-"LEFT$(CFALTA,2)
2860 LOCATE 15,19:PRINT CCUESP
2862 GUAPRI#=CVS(CIMPOR):N.D%=2:GOSUB 64770
2865 LOCATE 15,64-L.C%:PRINT GUAPRI$
2866 'IF I.A%=3 THEN 2870
2867 LOCATE 16,19:PRINT CCONFI
2870 LOCATE 16,56:PRINT COBSER
2871 BUSCA$=CBANCO:GOSUB 34260
2872 IF SW1%=0 THEN BA$=LEFT$(BNOM,19):BANC$=CBANCO:GOTO 2874
2873 BA$="NO EXISTE":BANC$=""
2874 LOCATE 17,19:PRINT BANC$
2875 LOCATE 17,24:PRINT BA$
2876 'IF CBANCO<>"2095" THEN 2881
2877 'CUPRI$=LEFT$(CCUBAN,10):EDI$="1":GOSUB 37000
2878 'IF DI%=1 THEN 2881
2879 '
2880 'LOCATE 17,56:PRINT CUPRIE$:GOTO 2882
2881 LOCATE 17,56:PRINT CCUBAN
2882 LOCATE 18,19:PRINT CSUCUR "-" CDIGI
2883 COLOR 11
2885 LOCATE 20,29:PRINT "Alumnos en vigor : "
2890 GUAPRI#=VIVAS%:N.D%=0:GOSUB 64770
2892 COLOR 15
2895 LOCATE 20,52-L.C%:PRINT GUAPRI$
2963 IF CMARCA<>"L" THEN RETURN
2965 LOCATE 18,56:PRINT RIGHT$(CFBAJA,2)"-"MID$(CFBAJA,3,2)"-"LEFT$(CFBAJA,2)
2970 COLOR 19:LOCATE 6,56:PRINT " - ALUMNO DADO DE BAJA -":COLOR 15
2975 RETURN
4500 '**************************
4502 '***CONSULTA ALFABETICA ***
4506 '**************************
4508 FALUDIR$="FINFALUM.ALF":FALUDAT$="FINFALUM.DAT"
4510 'GOSUB 8000:GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
4520 GOSUB 30010        'Apertura Fichero ALUMNOS
4530 IF CSWA="0" THEN CLOSE:GOTO 4550
4532 GET #2,1
4534 LSET CSWA="0"
4535 LSET CULTACT=MKI$(ULTIMO%)
4536 PUT #2,1
4538 CLOSE
4540 CLS
4542 'SHELL "ACANUME.BAT"
4545 SHELL "INFALFA.BAT"
4550 'GOSUB 8000
4551 FALUDIR$="FINFALUM.ALF":FALUDAT$="FINFALUM.DAT"
4552 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
4554 GOSUB 30010        'Apertura Fichero ALUMNOS
4555 GOSUB 34010        'Apertura Fichero BANCOS
4560 TIT$="- CONSULTA ALFABETICA DE ALUMNOS DE INFORMATICA -"
4570 '
4580 GOSUB 8000:GOSUB 36000
4590 GOSUB 7800
4600 '
4610       '********************
4620       '***ENTRADA NOMBRE***
4630       '********************
4635       N%=0
4640       F.C1%=8:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
4650       IF I.T%=0 THEN NOMBRE$=D.E$:N%=N%+1:GOTO 4720
4660       IF I.T%=1 THEN 4760
4690       IF I.T%=4 THEN 9000               'Fin Entrada.
4710       GOTO 4640
4720       IF LEN(D.E$)<20 THEN NOMBRE$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
4730       LOCATE 8,37:PRINT NOMBRE$
4740       '
4750       '***********************
4760       '***ENTRADA APELLIDO1***
4770       '***********************
4780       F.C1%=9:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
4790       IF I.T%=0 THEN APELL1$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$)):GOTO 4860
4800       IF I.T%=3 THEN 4620
4840       IF I.T%=4 THEN 9000
4850       GOTO 4760
4860       IF LEN(D.E$)<20 THEN APELL1$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
4870       LOCATE 9,37:PRINT APELL1$
4880       '
4890       '***********************
4900       '***ENTRADA APELLIDO2***
4910       '***********************
4920       F.C1%=10:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
4930       IF I.T%=0 THEN APELL2$=D.E$:N%=N%+2:GOTO 5000
4960       IF I.T%=3 THEN 4760
4970       IF I.T%=4 THEN 9000               'Fin Entrada.
4980       IF I.T%=1 THEN 5030               'Fin Entrada.
5000       IF LEN(D.E$)<20 THEN APELL2$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
5010       LOCATE 10,37:PRINT APELL2$
5020       '
5030   '*************************************
5035   '***PEDIMOS CONFORMIDAD A LOS DATOS***
5040   '*************************************
5042   GOSUB 8500
5044   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
5046   COLOR 15:PRINT " ENTER ";
5048   COLOR 10:PRINT " Datos Erroneos ";
5050   COLOR 15:PRINT " F1 "
5052   COLOR 10
5054   '
5056   A$=INPUT$(1)
5060   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 5056
5062   '
5064   IF ASC(A$)=14 THEN 4580
5066   SIHAY%=0
5100        IF N%=3 THEN BURRA$=APELL1$+APELL2$+NOMBRE$:GOTO 5109
5102        IF N%=1 THEN BURRA$=APELL1$+NOMBRE$:GOTO 5109
5103        IF N%=2 THEN BURRA$=APELL1$+APELL2$:GOTO 5109
5104        BURRA$=APELL1$
5109        GOSUB 6000
5110        IF SWESC%=1 AND ULTIMO%=ULTACT% THEN GOSUB 8500:MEN$=" Este Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
5112        IF SWESC%=1 THEN 5300
5115        GET #2,CVI(FULTI)
5120        GOSUB 8000:GOSUB 31000:LOCATE 8,19:PRINT USING"##,###";VAL(CCUENTA)
5121        GOSUB 2700
5122        R.A%=R.A%+1:GET #1,R.A%
5124        IF N%=3 THEN BUSCO$=FAPE1+FAPE2+FCALLA:GOTO 5134
5125        IF N%=1 THEN BUSCO$=FAPE1+FLLAMA:GOTO 5134
5126        IF N%=2 THEN BUSCO$=FAPE1+FAPE2:GOTO 5134
5128        BUSCO$=FAPE1
5134        IF BURRA$<>BUSCO$ THEN 5220
5135        GOSUB 8500:MEN$="Hay m s Alumnos iguales, para verlos ENTER , si n¢ F1":GOSUB 8500
5136        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5136
5137        IF ASC(A$)=14 THEN 5240 ELSE 5115
5170        '
5180        '
5220        IF ULTIMO%=ULTACT% THEN 5240
5230        GOSUB 8500:MEN$="Para ver si hay m s Alumnos iguales, pulsa ENTER , si n¢ F1":GOSUB 8500
5231        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5231
5232        IF ASC(A$)=14 THEN 5240
5233        GOTO 5300
5240        GOSUB 8500:MEN$="  Pulse una tecla para nueva consulta ":GOSUB 8500
5250        A$=INPUT$(1)
5260        GOTO 4580
5270        '
5280        '
5300        FOR R.A%=ULTACT% TO ULTIAL%
5302        GET #1,R.A%
5304        IF N%=3 THEN BUSCO$=FAPE1+FAPE2+FCALLA:GOTO 5312
5306        IF N%=1 THEN BUSCO$=FAPE1+FLLAMA:GOTO 5312
5308        IF N%=2 THEN BUSCO$=FAPE1+FAPE2:GOTO 5312
5310        BUSCO$=FAPE1
5312        IF BURRA$<>BUSCO$ THEN 5400
5313        GET #2,CVI(FULTI)
5314        GOSUB 8000:GOSUB 31000:LOCATE 8,19:PRINT USING"##,###";VAL(CCUENTA)
5316        GOSUB 2700:SIHAY%=1
5318        GOSUB 8500:MEN$="Para ver si hay m s Alumnos iguales, pulsa ENTER , si n¢ F1":GOSUB 8500
5320        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5320
5322        IF ASC(A$)=14 THEN 5240
5400        NEXT R.A%
5402        IF SIHAY%=1 THEN 5240
5406        GOSUB 8500:MEN$=" Este Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
6000 '*************************
6010 '***BUSQUEDA DICOTOMICA***
6020 '*************************
6024   '
6030 'El argumento de busqueda sobre 'BURRA$'
6040 'El numero de registros sobre 'N.R%'
6050 R.A%=0           'Donde devuelve el nro. de registro.
6060 SWESC%=0         'SW indicador de la busqueda.
6070 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
6080 '
6090 R.I%=INT((N.R%/2)+.5)
6100 R.C%=R.I%
6110 IF R.I%<1 THEN SWESC%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 7000
6115 IF R.I%>N.R% THEN SWESC%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 7000
6120 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
6122 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 6130
6124 IF N%=1 THEN RPROV$=FAPE1+FLLAMA:GOTO 6130
6126 IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 6130
6128 RPROV$=FAPE1
6130 IF RPROV$=BURRA$ THEN 6220      'Registro Buscado.
6140 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
6150 IF RPROV$>BURRA$ THEN R.I%=R.I%-R.C%:GOTO 6170
6160 R.I%=R.I%+R.C%
6170 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 6110
6180 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 6110
6190 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 6110
6200 REG2%=R.I%:SW100%=0
6210 GOTO 6110
6220 'Registro Buscado.
6230 FOR JMG%=R.I% TO 1 STEP -1
6240     GET #1,JMG%
6242 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 6250
6244 IF N%=1 THEN RPROV$=FAPE1+FLLAMA:GOTO 6250
6246 IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 6250
6248 RPROV$=FAPE1
6250     IF RPROV$<>BURRA$ THEN R.A%=JMG%+1:JMG%=-5
6260 NEXT JMG%
6270 IF JMG%=0 THEN R.B%=1
6280 IF JMG%=0 THEN R.B%=1
6290 IF SWESC%=0 THEN GET #1,R.A%
6295 RETURN
7000 FOR R.I%=DES% TO HAS%
7010     GET #1,R.I%
7020 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 7055
7030 IF N%=1 THEN RPROV$=FAPE1+FLLAMA:GOTO 7055
7040 IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 7055
7050 RPROV$=FAPE1
7055     IF RPROV$=BURRA$ THEN SWESC%=0:GOTO 6220
7060 NEXT R.I%
7070 RETURN
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 22,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Consultas de Alumnos "
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PINFCLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO,2 AS CMIERDA
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,21 AS CKAKA,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FINFALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 5,X%:PRINT TIT$
31042 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 8,6:PRINT "Alumno N§ : <" SPACE$(6) ">"
31052 LOCATE 8,43:PRINT "Nombre    : <" SPACE$(20) ">"
31054 LOCATE 9,6:PRINT "Apellido 1: <" SPACE$(20) ">"
31056 LOCATE 9,43:PRINT "Apellido 2: <" SPACE$(20) ">"
31058 LOCATE 10,6:PRINT "Direcci¢n : <" SPACE$(40) ">"
31060 LOCATE 11,6:PRINT "Poblaci¢n : <" SPACE$(20) ">"
31062 LOCATE 11,43:PRINT "Provincia : <" SPACE$(15) ">"
31064 LOCATE 12,6:PRINT "CD.Postal : <" SPACE$(5) ">"
31066 LOCATE 12,43:PRINT "Tel‚fonos : <" SPACE$(15) ">"
31068 LOCATE 13,6:PRINT "D.N.I.    : <" SPACE$(13) ">"
31070 LOCATE 13,43:PRINT "Curso     : <" SPACE$(1) ">"
31092 LOCATE 14,6:PRINT "F. Nacim. : <"SPACE$(8) ">"
31094 LOCATE 14,43:PRINT "F. Alta.  : <        >"
31103 LOCATE 15,6:PRINT "Cuota Esp.: < >"
31105 LOCATE 15,43:PRINT "Importe.  : <" SPACE$(9) ">"
31123 LOCATE 16,6:PRINT "Confirma. : < >"
31125 LOCATE 16,43:PRINT "Observac. : <" SPACE$(20) ">"
31196 LOCATE 17,6:PRINT "Banco     : <" SPACE$(4) ">"
31198 LOCATE 17,43:PRINT "N.Cuenta  : <" SPACE$(10) ">"
31200 LOCATE 18,6:PRINT "Sucur.Dig.: <" SPACE$(7) ">"
31228 IF CMARCA<>"L" THEN RETURN
31230 LOCATE 18,43:PRINT "Fec. Baja : <" SPACE$(8) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ACADEMIA INFORMATICA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*****************************
34010 '***APERTURA FICHERO BANCOS***
34020 '*****************************
34030 FICHE1$="FACABANC.DAT"
34040 '
34050 'APERTURA FICHERO DE DATOS.
34060 OPEN "R",#3,FICHE1$,125
34070 'REGISTRO 1.
34080 FIELD #3,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
34090 'REGISTRO DE DATOS.
34100 FIELD #3,4 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,5 AS BFIL001,1 AS BSTATUS
34110 GET #3,1
34120 IF BETIQUETA="FACABANC.DAT" THEN 34160  'CONTINUAR
34130 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
34140 A$=INPUT$(1)
34150 GOTO 9000
34160 'CONTINUAR.
34170 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
34180 ULTBAN%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
34190 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
34200 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
34210 N.B%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
34220 '
34230 RETURN
34240 '
34250 '*************************
34260 '***LECTURA AZAR BANCOS***
34270 '*************************
34280 'El argumento de busqueda sobre 'BUSCA$'
34290 'El numero de registros sobre 'N.B%'
34300 R.B%=0           'Donde devuelve el nro. de registro.
34310 R.W%=0           'Donde devuelve el nro. de registro del DIR.
34320 SW1%=0         'SW indicador de la busqueda.
34330 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34340 '
34350 R.I%=INT((N.B%/2)+.5)
34360 R.C%=R.I%
34370 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34490
34380 IF R.I%>N.B% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.B%:GOTO 34490
34390 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34400 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 34660      'MIRAR STATUS
34410 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34420 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34440
34430 R.I%=R.I%+R.C%
34440 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34370
34450 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34370
34460 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34370
34470 REG2%=R.I%:SW100%=0
34480 GOTO 34370
34490 'REGISTRO NO BUSCADO.
34500 XX%=0
34510 FOR A%=DES% TO HAS%
34520   GET #3,A%
34530   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
34540 NEXT A%
34550 IF XX%=1 THEN 34660
34560 '
34570 '
34580 'Registro NO BUSCADO
34590 XX%=0
34600 IF N.B%=0 THEN N.B%=1
34610 FOR JMG%=N.B% TO ULTBAN%
34620     GET #3,JMG%
34630     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
34640 NEXT JMG%
34650 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
34660 'MIRAMOS STATUS.
34670 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
34680 WSTATUS$=""
34690 RETURN
34700 '
36000 '**********************************************
36010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
36020 '**********************************************
36030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
36040 LOCATE 5,X%:PRINT TIT$
36042 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
36052 LOCATE 8,24:PRINT "Nombre    : <" SPACE$(20) ">"
36054 LOCATE 9,24:PRINT "Apellido 1: <" SPACE$(20) ">"
36056 LOCATE 10,24:PRINT "Apellido 2: <" SPACE$(20) ">"
36058 RETURN
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
