10 '                       PACACONU.BAS
20 '                      --------------
30 '        TRATAMIENTO DE LAS FACTURAS PARA CONTABILIDAD
40 '        ---------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(100,"Ä")
100 '
130 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,5,2)+"-"+LEFT$(FEDIA$,4)
134 '
140 DEFSTR C,A,F,J,N,G
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
162 DIM PRE!(5),FAL%(12),PERI$(12)
198 '
200 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SETIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 GOSUB 10000
500 TIT$="- HACER FACTURAS PARA CONTABILIDAD -":GOTO 510
505 '
510 '
600  SHELL "ACACONU.BAT"
602 GOSUB 8000
606 LOCATE 6,23:PRINT TIT$
608 LOCATE 7,23:PRINT STRING$(LEN(TIT$),205)
610 OPEN "I",#3,"RECIBOS.FAC"
612 IF EOF(3) THEN 622
614 LINE INPUT #3,LINEA$
616 IF LEFT$(LINEA$,4)="5180" THEN FE$=MID$(LINEA$,17,6):GOTO 612
618 IF LEFT$(LINEA$,4)="5980" THEN IM$=MID$(LINEA$,89,10):GOTO 612
620 GOTO 612
622 LOCATE 14,22:PRINT "LA FECHA DE FACTURACION ES : "LEFT$(FE$,2)"-"MID$(FE$,3,2)"-"RIGHT$(FE$,2)
623 GUAPRI#=VAL(IM$)/100:N.D%=2:GOSUB 64760
624 LOCATE 16,22:PRINT "EL IMPORTE FACTURADO FUE   :"
625 LOCATE 16,58-L.C%:PRINT GUAPRI$
626 GOSUB 8500:MEN$="Si los datos son correctos pulsa - ENTER -, si err¢neos - F1 -":GOSUB 8500
627 'SUMAT#=GUAPRI#
628 A$=INPUT$(1)
630 '
631 IF ASC(A$)<13 OR ASC(A$)>14 THEN 628
632 IF ASC(A$)=14 THEN 9000
641 GOSUB 8000
642 COLOR 10
643 LOCATE 6,23:PRINT TIT$
646 LOCATE 7,23:PRINT STRING$(LEN(TIT$),205)
700 '
702 '
704 '
708 CLOSE
2304 '
2306 TOTAL#=0:TODO#=0
2308 OPEN "I",#3,"RECIBOS.CLA"
2310 GOSUB 36000
2314 GOSUB 7650
2316       '***************************
2318       '***ENTRADA FECHA FACTURA***
2320       '***************************
2322       F.C1%=14:C.C1%=44:L.E%=8:BO%=10:GOSUB 63000
2324       IF I.T%=0 THEN NACI$=D.E$:GOTO 2338
2326       IF I.T%=5 THEN 2318
2328       IF I.T%=1 THEN 2318
2330       IF I.T%=3 THEN LOCATE 14,44:PRINT SPC(10):GOTO 2318
2332       IF I.T%=4 THEN 9000             'Fin Entrada.
2334       GOTO 2318
2336       '
2338       FECSUB$=NACI$:GOSUB 60000
2340       IF ERF%<>1 THEN 2346
2342       BEEP:GOSUB 8500:MEN$="Fecha de Factura Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 14,44:PRINT SPC(10)
2344       GOSUB 7650:GOTO 2318
2346       LOCATE 14,44:PRINT FDMA$:FENA$=FAMD$:MEME$=MID$(FENA$,5,2)
2348       '
2355       '
2406   '*************************************
2408   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2410   '*************************************
2412   GOSUB 8500
2414   COLOR 10:LOCATE 22,15:PRINT "Fecha Correcta ";
2416   COLOR 15:PRINT " ENTER ";
2418   COLOR 10:PRINT " Fecha Err¢nea ";
2420   COLOR 15:PRINT " F1 "
2422   COLOR 10
2424   '
2426   A$=INPUT$(1)
2428   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2426
2430   '
2432   IF ASC(A$)=13 THEN 2436
2434   CLOSE:GOTO 2310
2436   '
4000 '
4005 TIT$="- LISTAR FACTURAS DE LA ESCUELA DE IDIOMAS -"
4011 GOSUB 8000
4012 COLOR 10      ':GOSUB 10000
4014 LOCATE 5,18:PRINT TIT$
4016 LOCATE 6,18:PRINT STRING$(LEN(TIT$),205)
4018 COLOR 11:LOCATE 10,16:PRINT "N§ de Factura inicial : <      >
4019 '
4020       '**********************
4022       '***ENTRADA DEL GRUPO**
4024       '**********************
4026       COLOR 15:F.C1%=10:C.C1%=41:L.E%=5:BO%=6:GOSUB 63000
4028       IF I.T%=0 THEN FAC!=VAL(D.E$):GOTO 4032
4030       BEEP:GOTO 4026
4032       GUAPRI#=FAC!:GOSUB 64770
4033       LOCATE 10,41:PRINT SPC(6):LOCATE 10,46-L.C%:PRINT GUAPRI$
4034       '
4056   '*************************************
4058   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4060   '*************************************
4062   GOSUB 8500
4064   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4066   COLOR 15:PRINT " ENTER ";
4068   COLOR 10:PRINT " Datos Erroneos ";
4070   COLOR 15:PRINT " F1 "
4072   COLOR 10
4074   '
4076   A$=INPUT$(1)
4078   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4076
4080   '
4082   IF ASC(A$)=13 THEN 4090
4084   LOCATE 10,2:PRINT SPC(78)
4086   LOCATE 12,2:PRINT SPC(78)
4088   CLOSE:GOTO 4000
4090   '
4095   N.FAC!=FAC!-1:LIN%=90:PAG%=0
4100 '
4102   WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4103  'LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(15)
4105   LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
4110   '
4112 IF EOF(3) THEN 4293
4114 LINE INPUT #3,LINEA$
4116 ALU$=MID$(LINEA$,24,5)
4118 NOM$=MID$(LINEA$,29,40)
4120 EURO$=MID$(LINEA$,89,8)
4121 PESE$=MID$(LINEA$,89,10)
4122 DECI$=MID$(LINEA$,97,2)
4124 AA%=VAL(ALU$)
4126 JUAN!=VAL(EURO$)
4128 IF VAL(PESE$)=0 THEN 4112
4130   '
4155   '
4160   '
4175   IF LIN%<57 THEN 4224
4180   IF LIN%=90 THEN 4190
4185   LPRINT CHR$(12)
4190   LIN%=0:PAG%=PAG%+1
4195   LPRINT TAB(4) "*** E. I. D. E.    CIF: B48474647 ***"
4196   LPRINT TAB(4) "*** GENARO ORAA 6   TFNO: 4619555 ***"
4197   LPRINT TAB(4) "*** S  A  N  T  U  R  C  E  48980 ***";
4198   LPRINT TAB(57) "RELACION DE FACTURAS EMITIDAS EL DIA "FDMA$;
4200   LPRINT TAB(119) USING "HOJA : ##";PAG%
4205   LPRINT TAB(4) STRING$(37,"-");
4207   LPRINT TAB(57) STRING$(47,"-");
4210   LPRINT TAB(119) STRING$(9,"-"):LPRINT
4215  LPRINT TAB(4) " FACTURA                   APELLIDOS Y NOMBRE                                     I  M  P  O  R  T  E  S                   "
4217  LPRINT TAB(4) "----------  --------------------------------------------------  ------------------------------------------------------------"
4219 'LPRINT
4222  LIN%=6
4224  N.FAC!=N.FAC!+1:
4225  GUAPRI#=N.FAC!:N.D%=0:GOSUB 64770
4226  LPRINT TAB(11-L.C%) GUAPRI$+"-R";
4227  LPRINT TAB(16) NOM$;
4236  GUAPRI#=VAL(PESE$)/100:N.D%=2:GOSUB 64770
4237  LPRINT TAB(76-L.C%) GUAPRI$;
4289  LIN%=LIN%+1
4290  TODO#=TODO#+GUAPRI#
4291  IMPOR#=VAL(EURO$):GOSUB 4500
4292  GOTO 4112
4293  '
4294  LPRINT
4295  LPRINT TAB(33) "TOTAL FACTURADO EL DIA "FDMA$;
4296  GUAPRI#=TODO#:N.D%=2:GOSUB 64770
4298  LPRINT TAB(76-L.C%) GUAPRI$;
4299  IMPOR#=TODO#:GOSUB 5500
4300  LPRINT CHR$(12)
4302  CLOSE
4304  GOTO 9000
4306  '
4308  '
4500 'TODO$=STR$(IMPOR#):L.P%=0:TODO$=RIGHT$(TODO$,LEN(TODO$)-1)
4501 'FOR I%=1 TO LEN(TODO$)
4502 'IF MID$(TODO$,I%,1)="." THEN L.P%=I%:I%=LEN(TODO$)
4503 'NEXT I%
4505 IMPOR$=STR$(IMPOR#)
4507 'IF L.P%=0 THEN CENTI$=". ":GOTO 4520
4510 CENTI$=" Y "+DECI$+" %."
4520 GOSUB 10120
4605 'IMPOR$=STR$(IMPOR#)
4610 'IMPOR$=RIGHT$(IMPOR$,LEN(IMPOR$)-1)
4620 'GOSUB 10120
5000 'IMPOR#=GUAPRI#:GOSUB 10120
5002 LPRINT TAB(80) LIT1$
5003 IF LEFT$(LIT2$,3)="***" THEN 5008
5004 LPRINT TAB(80) LIT2$:LIN%=LIN%+1
5005 IF LEFT$(LIT3$,3)="***" THEN 5008
5006 LPRINT TAB(80) LIT3$:LIN%=LIN%+1
5008 LPRINT TAB(4) STRING$(123,"."):LIN%=LIN%+1
5010 RETURN
5012 '
5500 TODO$=STR$(IMPOR#):L.P%=0:TODO$=RIGHT$(TODO$,LEN(TODO$)-1)
5501 FOR I%=1 TO LEN(TODO$)
5502  IF MID$(TODO$,I%,1)="." THEN L.P%=I%:I%=LEN(TODO$)
5503 NEXT I%
5505 IMPOR$=LEFT$(TODO$,L.P%-1)
5507 IF L.P%=0 THEN CENTI$=". ":GOTO 5520
5510 CENTI$=" Y "+MID$(TODO$,L.P%+1,2)+" %."
5520 GOSUB 10120
5605 'IMPOR$=STR$(IMPOR#)
5610 'IMPOR$=RIGHT$(IMPOR$,LEN(IMPOR$)-1)
5620 'GOSUB 10120
6000 'IMPOR#=GUAPRI#:GOSUB 10120
6002 LPRINT TAB(80) LIT1$
6003 IF LEFT$(LIT2$,3)="***" THEN 5008
6004 LPRINT TAB(80) LIT2$:LIN%=LIN%+1
6005 IF LEFT$(LIT3$,3)="***" THEN 5008
6006 LPRINT TAB(80) LIT3$:LIN%=LIN%+1
6008 LPRINT TAB(4) STRING$(123,"."):LIN%=LIN%+1
6010 RETURN
6012 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,13
7672 COLOR 10:PRINT "ENTER =>";
7674 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Opci¢n.";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s "
7740 'COLOR 10:PRINT " F10 =>";
7750 'COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,26:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 CLS      'RUN "PACAFANO"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FTURALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FTURALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
10000 '*******************************************
10010 '***CARGAMOS LAS TABLAS PARA LOS IMPORTES***
10020 '*******************************************
10030 DEFSTR T:DIM TA(31),TB(9),TC(9):IMPMAN$="000000000"
10040 DATA "UNO ","DOS ","TRES ","CUATRO ","CINCO ","SEIS ","SIETE ","OCHO ","NUEVE ","DIEZ ","ONCE ","DOCE ","TRECE ","CATORCE ","QUINCE ","DIECISEIS "
10050 DATA "DIECISIETE ","DIECIOCHO ","DIECINUEVE ","VEINTE ","VEINTIUNA ","VEINTIDOS ","VEINTITRES ","VEINTICUATRO ","VEINTICINCO ","VEINTISEIS ","VEINTISIETE ","VEINTIOCHO ","VEINTINUEVE ","TREINTA ","TREINTA Y UNO "
10060 FOR I%=1 TO 31:READ TA(I%):NEXT I%
10070 DATA "DIEZ ","CIENTO ","VEINTE ",DOSCIENT,"TREINTA ",TRESCIENT,"CUARENTA ",CUATROCIENT,"CINCUENTA ",QUINIENT,"SESENTA ",SEISCIENT,"SETENTA ",SETECIENT,"OCHENTA ",OCHOCIENT,"NOVENTA ",NOVECIENT
10080 FOR I%=1 TO 9:READ TB(I%),TC(I%):NEXT I%
10090 RETURN
10100 '
10110 '********************************************************************
10120 '***RUTINA PESETAS.ENTRADA IMPOR$ E IMPOR#(IMPORTE A TRANSFORMAR).***
10130 '********************************************************************
10140 LIT1$="":LIT2$="":LIT3$="":IND33%=0:IF IMPOR#=0 THEN LIT1$=LIT1$+"CERO.":GOTO 10220
10150 'Desglose del importe
10160 IMPOR$=LEFT$(IMPMAN$,9-LEN(IMPOR$))+IMPOR$
10170 PESETA$=RIGHT$(IMPOR$,3):PESETA%=VAL(PESETA$):MYM$=LEFT$(IMPOR$,6):MYM%=VAL(MYM$):DECPTA$=RIGHT$(PESETA$,2):DECPTA%=VAL(DECPTA$):DIG7%=VAL(LEFT$(PESETA$,1)):DIG8%=VAL(LEFT$(DECPTA$,1)):DIG9%=VAL(RIGHT$(DECPTA$,1))
10180 MILLON$=LEFT$(MYM$,3):MILLON%=VAL(MILLON$):MILES$=RIGHT$(MYM$,3):MILES%=VAL(MILES$):DECMIL$=RIGHT$(MILES$,2):DECMIL%=VAL(DECMIL$):DIG4%=VAL(LEFT$(MILES$,1)):DIG5%=VAL(LEFT$(DECMIL$,1)):DIG6%=VAL(RIGHT$(DECMIL$,1))
10190 DECMLL$=RIGHT$(MILLON$,2):DECMLL%=VAL(DECMLL$):DIG1%=VAL(LEFT$(MILLON$,1)):DIG2%=VAL(LEFT$(DECMLL$,1)):DIG3%=VAL(RIGHT$(DECMLL$,1))
10200 'Tratamiento particular
10210 GOSUB 10270:GOSUB 10350:GOSUB 10430 'Tratamiento millones,miles y pesetas
10220 'FIN RUTINA
10230 IF LEN(LIT1$)<48 THEN LIT1$=LIT1$+STRING$(48-LEN(LIT1$),"*")
10240 IF LEN(LIT2$)<48 THEN LIT2$=LIT2$+STRING$(48-LEN(LIT2$),"*")
10250 IF LEN(LIT3$)<48 THEN LIT3$=LIT3$+STRING$(48-LEN(LIT3$),"*")
10260 RETURN
10270 'Tratamiento millones
10280 IF MILLON%=0 THEN 10340
10290 IF MILLON%=1 THEN LIT1$="UN MILLON ":GOTO 10340
10300 IF MILLON%=100 THEN LIT1$="CIEN MILLONES ":GOTO 10340
10310 DECENA%=DECMLL%:DIGA%=DIG1%:DIGB%=DIG2%:DIGC%=DIG3%:IND90%=1
10320 GOSUB 10590 'Tratamiento comun
10330 IND90%=0:MAN$="MILLONES ":GOSUB 10520 'Salida a literales
10340 RETURN
10350 'Tratamiento miles
10360 IF MILES%=0 THEN 10420
10370 IF MILES%=1 THEN 10410
10380 IF MILES%=100 THEN MAN$="CIEN ":GOSUB 10520:GOTO 10410
10390 DECENA%=DECMIL%:DIGA%=DIG4%:DIGB%=DIG5%:DIGC%=DIG6%
10400 GOSUB 10590 'Tratamiento comun
10410 MAN$="MIL ":GOSUB 10520 'Salida a literales
10420 RETURN
10430 'Tratamiento pesetas
10440 IF PESETA%=0 THEN 10500
10450 'IF PESETA%=1 AND MYM>0 THEN MAN$="Y ":GOSUB 1700 'Salida a literales
10460 IF PESETA%=1 THEN MAN$="UN ":GOSUB 10520:GOTO 10500
10470 IF PESETA%=100 THEN MAN$="CIEN ":GOSUB 10520:GOTO 10500
10480 DECENA%=DECPTA%:DIGA%=DIG7%:DIGB%=DIG8%:DIGC%=DIG9%
10490 GOSUB 10590 'Tratmiento comun
10500 IF LIT2$="" THEN 10502 ELSE LIT2$=LEFT$(LIT2$,LEN(LIT2$)-1)+" EUROS"+CENTI$:RETURN
10502 SS%=LEN(LIT1$):IF (48-SS%)<11 THEN LIT2$="EUROS"+CENTI$:RETURN
10504 LIT1$=LEFT$(LIT1$,LEN(LIT1$)-1)+" EUROS"+CENTI$
10510 RETURN
10520 'Salida a literales
10530 IF IND33%=2 THEN 10570
10540 IF IND33%=1 THEN 10560
10550 IF (48-LEN(LIT1$))>=LEN(MAN$)THEN LIT1$=LIT1$+MAN$:RETURN ELSE LIT2$=LIT2$+MAN$:IND33%=1:RETURN
10560 IF (48-LEN(LIT2$))>=LEN(MAN$)THEN LIT2$=LIT2$+MAN$:RETURN ELSE LIT3$=LIT3$+MAN$:IND33%=2:RETURN
10570 LIT3$=LIT3$+MAN$
10580 RETURN
10590 'Tratamiento comun
10600 IF DIGA%=O THEN 10640 ELSE MAN$=TC(DIGA%)
10610 IF DIGA%=1 THEN 10630
10620 IF IND90%=1 THEN MAN$=MAN$+"OS " ELSE MAN$=MAN$+"AS "
10630 GOSUB 10520 'Salida a literales
10640 IF DIGB%=0 THEN 10680
10650 IF DECENA%>29 THEN MAN$=TB(DIGB%):GOSUB 10520:GOTO 10680
10660 IF IND90%=1 AND DECENA%=21 THEN MAN$="VEINTIUN ":GOSUB 10520:IND60%=1:GOTO 10680
10670 MAN$=TA(DECENA%):GOSUB 10520:IND60%=1
10680 IF IND60%=1 OR DIGC%=0 THEN 10730
10690 IF DIGB%<>0 THEN MAN$="Y ":GOSUB 10520
10700 IF IND90%=1 AND DIGC%=1 THEN MAN$="UN ":GOSUB 10520:GOTO 10730
10710 IF DIGC%=1 THEN MAN$="UNA ":GOSUB 10520:GOTO 10730
10720 MAN$=TA(DIGC%):GOSUB 10520
10730 IND60%=0
10740 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="E. I. D. E."
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
36000 '*********************************
36002 '*** PANTALLA DE DATOS A METER ***
36004 '*********************************
36005 COLOR 11
36006 LOCATE 14,27:PRINT "Fecha Factura : <          >"
36008 'LOCATE 16,27:PRINT "Hay Turismo.. : < >"
36010 'LOCATE 18,27:PRINT "Precio Turismo: <       >"
36024 '
36026 RETURN
36028 '
40000 '
40010 CUCUCU$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(XOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(XOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(XPE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(XPE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(XPE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(XPE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(XOM$,LNOMBRE%)+" "+LEFT$(XPE1$,LAPELL1%)+" "+LEFT$(XPE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAAAA sobre FECSUB$
60040 '   -  Devuelve sobre FDMA$ la fecha en formato DD-MM-AAAA
60045 '   -  Devuelve sobre FAMD$ la fecha en formato AAAAMMDD
60050 '   -  Utiliza ERF% de Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,4):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 '
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1900  OR ANO% > 2999 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
