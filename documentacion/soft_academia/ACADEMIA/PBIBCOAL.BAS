10 '                       PBIBCOAL.BAS
20 '                      --------------
30 '          CONSULTA ALFABETICA DE LIBROS POR TEMAS
40 '          ---------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 EMPRESA$="*** ESCUELA DE IDIOMAS ***"
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,D,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 GOTO 4500
2700 '***********************************
2710 '***PRESENTAMOS DATOS EN PANTALLA***
2720 '***********************************
2730 COLOR 15
2740 LOCATE 8,71:PRINT CTEMAS
2750 LOCATE 10,19:PRINT CNOMBRE
2760 LOCATE 12,19:PRINT CAUTOR
2770 LOCATE 14,19:PRINT CEDITOR
2780 LOCATE 16,19:PRINT COBSER
2785 IF CSITUA="0" THEN 2820
2790 LOCATE 18,19:PRINT CALUMNO
2800 LOCATE 20,19:PRINT CTELEFO
2810 LOCATE 20,71:PRINT MID$(CFEDE,5,2)"-"MID$(CFEDE,3,2)"-"LEFT$(CFEDE,2)
2820 IF CMARCA="L" THEN LOCATE 8,30:COLOR 28:PRINT "*** BAJA ***"
2830 COLOR 15
2840 RETURN
4500 '**************************
4502 '***CONSULTA ALFABETICA ***
4506 '**************************
4508 FALUDIR$="FBIBLIOT.TEM":FALUDAT$="FBIBLIOT.DAT"
4510 '
4520 GOSUB 30010        'Apertura Fichero ALUMNOS
4530 IF CSWA="0" THEN CLOSE:GOTO 4550
4532 GET #2,1
4534 LSET CSWA="0"
4535 LSET CULTACT=MKI$(ULTIMO%)
4536 PUT #2,1
4538 CLOSE
4540 CLS
4542 '
4545 SHELL "BIBTEMA.BAT"
4550 '
4551 FALUDIR$="FBIBLIOT.TEM":FALUDAT$="FBIBLIOT.DAT"
4552 '
4554 GOSUB 30010        'Apertura Fichero ALUMNOS
4560 TIT$="- CONSULTA ALFABETICA DE LIBROS POR TEMAS -"
4570 '
4580 GOSUB 8000:GOSUB 36000
4590 GOSUB 7800
4600 '
4610       '********************
4620       '***ENTRADA TEMAS ***
4630       '********************
4635       '
4640       F.C1%=10:C.C1%=46:L.E%=3:BO%=3:GOSUB 64000
4650       IF I.T%=0 THEN TEMA$=D.E$:GOTO 4720
4660       IF I.T%=1 THEN 4640
4690       IF I.T%=4 THEN 9000               'Fin Entrada.
4710       GOTO 4640
4720       IF LEN(D.E$)<3 THEN TEMA$=D.E$+LEFT$(BLANCO$,3-LEN(D.E$))
4730       LOCATE 10,46:PRINT TEMA$
4740       '
5020       '
5030   '*************************************
5035   '***PEDIMOS CONFORMIDAD A LOS DATOS***
5040   '*************************************
5042   GOSUB 8500
5044   COLOR 10:LOCATE 22,15:PRINT "Tema Correcto ";
5046   COLOR 15:PRINT " ENTER ";
5048   COLOR 10:PRINT " Tema Err¢neo ";
5050   COLOR 15:PRINT " F1 "
5052   COLOR 10
5054   '
5056   A$=INPUT$(1)
5060   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 5056
5062   '
5064   IF ASC(A$)=14 THEN 4580
5066   SIHAY%=0
5104        BURRA$=TEMA$
5109        GOSUB 6000
5110        IF SWESC%=1 AND ULTIMO%=ULTACT% THEN GOSUB 8500:MEN$=" Este Tema no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
5112        IF SWESC%=1 THEN 5300
5115        GET #2,CVI(FULTI)
5120        GOSUB 8000:GOSUB 31000:LOCATE 8,19:PRINT USING"##,###";VAL(CCUENTA)
5121        GOSUB 2700
5122        R.A%=R.A%+1:GET #1,R.A%
5128        BUSCO$=FTEMA
5134        IF BURRA$<>BUSCO$ THEN 5220
5135        GOSUB 8500:MEN$="Hay m s Libros del Tema, para verlos ENTER , si n¢ F1":GOSUB 8500
5136        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5136
5137        IF ASC(A$)=14 THEN 5240 ELSE 5115
5170        '
5180        '
5220        IF ULTIMO%=ULTACT% THEN 5240
5230        GOSUB 8500:MEN$="Para ver si hay m s Libros del Tema, pulsa ENTER , si n¢ F1":GOSUB 8500
5231        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5231
5232        IF ASC(A$)=14 THEN 5240
5233        GOTO 5300
5240        GOSUB 8500:MEN$="  Pulse una tecla para nueva consulta ":GOSUB 8500
5250        A$=INPUT$(1)
5260        GOTO 4580
5270        '
5280        '
5300        FOR R.A%=ULTACT% TO ULTIAL%
5302        GET #1,R.A%
5310        BUSCO$=FTEMA
5312        IF BURRA$<>BUSCO$ THEN 5400
5313        GET #2,CVI(FULTI)
5314        GOSUB 8000:GOSUB 31000:LOCATE 8,19:PRINT USING"##,###";VAL(CCUENTA)
5316        GOSUB 2700:SIHAY%=1
5318        GOSUB 8500:MEN$="Para ver si hay m s Libros del Tema, pulsa ENTER , si n¢ F1":GOSUB 8500
5320        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5320
5322        IF ASC(A$)=14 THEN 5240
5400        NEXT R.A%
5402        IF SIHAY%=1 THEN 5240
5406        GOSUB 8500:MEN$=" Este Tema no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
6000 '*************************
6010 '***BUSQUEDA DICOTOMICA***
6020 '*************************
6024   '
6030 'El argumento de busqueda sobre 'BURRA$'
6040 'El numero de registros sobre 'N.R%'
6050 R.A%=0           'Donde devuelve el nro. de registro.
6060 SWESC%=0         'SW indicador de la busqueda.
6070 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
6080 '
6090 R.I%=INT((N.R%/2)+.5)
6100 R.C%=R.I%
6110 IF R.I%<1 THEN SWESC%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 7000
6115 IF R.I%>N.R% THEN SWESC%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 7000
6120 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
6128 RPROV$=FTEMA
6130 IF RPROV$=BURRA$ THEN 6220      'Registro Buscado.
6140 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
6150 IF RPROV$>BURRA$ THEN R.I%=R.I%-R.C%:GOTO 6170
6160 R.I%=R.I%+R.C%
6170 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 6110
6180 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 6110
6190 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 6110
6200 REG2%=R.I%:SW100%=0
6210 GOTO 6110
6220 'Registro Buscado.
6230 FOR JMG%=R.I% TO 1 STEP -1
6240     GET #1,JMG%
6248 RPROV$=FTEMA
6250     IF RPROV$<>BURRA$ THEN R.A%=JMG%+1:JMG%=-5
6260 NEXT JMG%
6270 IF JMG%=0 THEN R.B%=1
6280 IF JMG%=0 THEN R.B%=1
6290 IF SWESC%=0 THEN GET #1,R.A%
6295 RETURN
7000 FOR R.I%=DES% TO HAS%
7010     GET #1,R.I%
7050 RPROV$=FTEMA
7055     IF RPROV$=BURRA$ THEN SWESC%=0:GOTO 6220
7060 NEXT R.I%
7070 RETURN
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 22,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Consultas de Libros por Temas."
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : BIBLIOTECA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PBIBLIOT"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,50
30054 FIELD #1,2 AS FULTI,3 AS FTEMA,40 AS FLLAMA,4 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30060 'LSET FULTI=MKI$(1):LSET FTEMA="":LSET FLLAMA=""
30061 'LSET FILER="":LSET FSTA="A"
30062 'PUT #1,1
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,40 AS CNOMBRE,40 AS CAUTOR,40 AS CEDITOR,3 AS CTEMAS,6 AS CFEAL,7 AS CTELEFO,40 AS CALUMNO,6 AS CFESA,6 AS CFEDE
30112 FIELD #2,194 AS TODO1$,1 AS CSITUA,6 AS CFEBA,4 AS CVECES,1 AS CMARCA,40 AS COBSER,24 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,1 AS CSWA,250 AS REST1$,9 AS REST2$
30115 GET #2,1
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1)
30124 'LSET CULTACT=MKI$(1):LSET CSWA="0":LSET REST1$="":LSET REST2$=""
30126 'PUT #2,1
30128 '
30138 '
30150 '
30500 OPEN "R",#4,"FBIBQUIE.DAT",70
30510 FIELD #4,5 AS DCUENTA,1 AS DSTATUS,40 AS DNOMBRE,7 AS DTELEFO,6 AS DDESDE,6 AS DHASTA,5 AS DLIBRE
30514 FIELD #4,5 AS DCUEN1,1 AS DSTAT,2 AS DULTI,62 AS DRESTO
30515 GET #4,1
30517  DULTI%=CVI(DULTI)
30522 'LSET DCUEN1="":LSET DSTAT="A":LSET DULTI=MKI$(1)
30524 'LSET DRESTO=""
30526 'PUT #4,1
30528 'CLOSE
30538 'STOP
30540 RETURN
30550 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 5,X%:PRINT TIT$
31042 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 8,6:PRINT "Libro N§. : <" SPACE$(6) ">"
31052 LOCATE 8,63:PRINT "Tema : <" SPACE$(3) ">"
31058 LOCATE 10,6:PRINT "T¡tulo .. : <" SPACE$(40) ">"
31064 LOCATE 12,6:PRINT "Autor ... : <" SPACE$(40) ">"
31092 LOCATE 14,6:PRINT "Editor .. : <"SPACE$(40) ">"
31123 LOCATE 16,6:PRINT "Observac. : <" SPACE$(40) ">"
31130 'IF I.A%<>4 THEN 31260
31200 LOCATE 18,6:PRINT "Lo tiene  : <" SPACE$(40) ">"
31205 LOCATE 20,6:PRINT "Tel‚fono  : <" SPACE$(7) ">"
31230 LOCATE 20,55:PRINT "Hasta el d¡a : <" SPACE$(8) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
36000 '**********************************************
36010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
36020 '**********************************************
36030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
36040 LOCATE 7,X%:PRINT TIT$
36042 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
36056 LOCATE 10,32:PRINT "Tema ..... : <   >"
36058 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
