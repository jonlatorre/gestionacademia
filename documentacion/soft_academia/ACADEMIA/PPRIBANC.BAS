10 '                         PACABANC.BAS
20 '                       ---------------
30 '                 GESTION FICHERO DE BANCOS
40 '               -------------------------------
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0"):GUION$=STRING$(15,"-")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"-"+MID$(FDIA$,3,2)+"-"+LEFT$(FDIA$,2)
94 '
96 DEFSTR B
98 '
100 '
110 '
120 '
130 '
140 '
490 '*********************************
500 '***PRESENTAMOS EL MENU GENERAL***
510 '*********************************
520 TIT$="- MENU FICHERO DE BANCOS -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 4,X%:PRINT TIT$
560 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
570 LOCATE 7,26:PRINT "1.- INTRODUCIR BANCOS."
580 LOCATE 8,26:PRINT "2.- BAJAS DE BANCOS."
590 LOCATE 9,26:PRINT "3.- MODIFICACION DE BANCOS."
600 LOCATE 10,26:PRINT "4.- CONSULTAS DE BANCOS."
610 LOCATE 11,26:PRINT "5.- LISTADO POR NUMERO."
620 LOCATE 12,26:PRINT "6.- No se usa."
630 LOCATE 13,26:PRINT "7.- No se usa."
640 LOCATE 14,26:PRINT "8.- No se usa."
650 LOCATE 15,26:PRINT "9.- No se usa."
660 LOCATE 16,25:PRINT "10.- No se usa."
662 LOCATE 17,25:PRINT "11.- No se usa."
670 LOCATE 19,24:COLOR 12:PRINT " ESC ";
680 COLOR 15:PRINT " Finalizar ";
690 COLOR 11:PRINT " OPCION : <  > "
700 '
710 'PEDIMOS OPCION.
720 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
730 IF I.T%=4 THEN COLOR 15:GOTO 9000
740 IF I.T%<>0 THEN 720
750 COLOR 15
760 OPC%=VAL(D.E$)
770 IF OPC%<1 OR OPC%>11 THEN 670
780 ON OPC% GOTO 1010,1010,1010,1010,5070,500,500,500,500,500,500
790 '
800 '
810 '
1000 '*****************************************************
1010 '***TRATAMIENTO COMUN DE ALTA,BAJA.MODI. Y CONSULTA***
1020 '*****************************************************
1030 IF OPC%=1 THEN TITA$="- INTRODUCIR BANCOS -":GOTO 1080
1040 IF OPC%=2 THEN TITA$="- BAJAS DE BANCOS -":GOTO 1080
1050 IF OPC%=3 THEN TITA$="- MODIFICACION DE BANCOS -":GOTO 1080
1060 TITA$="- CONSULTAS DE BANCOS -"
1070 '
1080 GOSUB 31000  'PRESENTAR PANTALLA ENTRADA DE DATOS.
1090 '
1100 '
1110 '
1112 GOSUB 30010  'APERTURA FICHERO DE BANCOS
1114 '
1116 '
1118 '
1119 GOSUB 7650
1120 '*****************************
1130 '***PEDIMOS NUMERO DE BANCO***
1140 '*****************************
1150 F.C1%=7:C.C1%=32:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
1160 IF I.T%=0 THEN NBAN$=D.E$:GOTO 1240
1170 IF I.T%=1 THEN 1150
1180 '
1190 IF I.T%=2 THEN 1150
1210 IF I.T%=3 THEN 1150
1220 IF I.T%=4 THEN 6800   'FIN DE ENTRADA
1230 GOTO 1150
1240 'HACER LECTURA DEL FICHERO.
1250 LOCATE 7,32:PRINT NBAN$
1260 BUSCA$=NBAN$
1270 GOSUB 30410    'LECTURA AL AZAR.
1271 COLOR 15
1272 '
1280 IF SW1%=1 AND OPC%=1 THEN 1690   'CONTINUAR ALTA Y CLAVE NO BUSCADA.
1290 IF SW1%=0 AND OPC%=1 THEN GOSUB 8500:MEN$="-YA EXISTE, PULSE UNA TECLA-":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 1130
1292 IF SW1%=1 AND OPC%<>1 THEN GOSUB 8500:MEN$="-NO EXISTE, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 1130
1300 '
1310 'PRESENTAMOS EL REGISTRO EN PANTALLA.
1320 LOCATE 9,32:PRINT BNOM
1322 LOCATE 11,32:PRINT BDIREC
1324 LOCATE 13,32:PRINT BCPOS
1326 LOCATE 15,32:PRINT BPOBLA
1328 LOCATE 17,32:PRINT BPROV
1330 '
1640 IF OPC%=2 THEN 4400    'TRATAR BAJA.
1650 IF OPC%=4 THEN 4320    'TRATAR CONSULTA.
1660 '
1670 '
1680 '******************************
1690 '***ENTRADA NOMBRE DEL BANCO***
1700 '******************************
1710 F.C1%=9:C.C1%=32:L.E%=30:BO%=30:GOSUB 64000
1720 IF I.T%=0 THEN NOM$=D.E$:GOTO 1810
1730 IF I.T%=1 AND OPC%=3 THEN NOM$=BNOM:GOTO 1810
1740 IF I.T%=1 THEN NOM$=STRING$(30," "):GOTO 1810
1750 IF I.T%=2 AND OPC%=1 THEN 1810
1760 IF I.T%=2 THEN 1710
1770 IF I.T%=3 AND OPC%=1 THEN LOCATE 9,32:PRINT SPC(30):GOTO 1130
1780 IF I.T%=3 THEN LOCATE 9,32:PRINT BNOM:GOTO 1130
1790 IF I.T%=4 THEN 6800  'FIN DE DATOS.
1800 GOTO 1690
1810 LOCATE 9,32:PRINT NOM$
1820 '
1830 '*****************************
1840 '***ENTRADA DIRECCION BANCO***
1850 '*****************************
1860 F.C1%=11:C.C1%=32:L.E%=30:BO%=30:GOSUB 64000
1870 IF I.T%=0 THEN DIREC$=D.E$:GOTO 1960
1880 IF I.T%=1 AND OPC%=3 THEN DIREC$=BDIREC:GOTO 1960
1890 IF I.T%=1 THEN DIREC$=STRING$(30," "):GOTO 1960
1900 IF I.T%=2 AND OPC%=1 THEN 1960
1910 IF I.T%=2 THEN 1860
1920 IF I.T%=3 AND OPC%=1 THEN LOCATE 11,32:PRINT SPC(30):GOTO 1690
1930 IF I.T%=3 THEN LOCATE 11,32:PRINT BDIREC:GOTO 1690
1940 IF I.T%=4 THEN 6800  'FIN DE DATOS.
1950 GOTO 1860
1960 LOCATE 11,32:PRINT DIREC$
1970 '
1980 '***************************
1990 '***ENTRADA CODIGO POSTAL***
2000 '***************************
2010 F.C1%=13:C.C1%=32:L.E%=5:BO%=5:NEG%=0:L.D%=0:GOSUB 63000
2020 IF I.T%=0 THEN CPOS$=D.E$:GOTO 2110
2030 IF I.T%=1 AND OPC%=3 THEN CPOS$=BCPOS:GOTO 2110
2040 IF I.T%=1 THEN CPOS$=STRING$(5," "):GOTO 2110
2050 IF I.T%=2 AND OPC%=1 THEN 2110
2060 IF I.T%=2 THEN 2010
2070 IF I.T%=3 AND OPC%=1 THEN LOCATE 13,32:PRINT SPC(5):GOTO 1840
2080 IF I.T%=3 THEN LOCATE 13,32:PRINT BCPOS:GOTO 1840
2090 IF I.T%=4 THEN 6800  'FIN DE DATOS.
2100 GOTO 2010
2110 LOCATE 13,32:PRINT CPOS$
2120 '
2130 '***********************
2140 '***ENTRADA POBLACION***
2150 '***********************
2160 F.C1%=15:C.C1%=32:L.E%=30:BO%=30:GOSUB 64000
2170 IF I.T%=0 THEN POBLA$=D.E$:GOTO 2260
2180 IF I.T%=1 AND OPC%=3 THEN POBLA$=BPOBLA:GOTO 2260
2190 IF I.T%=1 THEN POBLA$=STRING$(30," "):GOTO 2260
2200 IF I.T%=2 AND OPC%=1 THEN 2260
2210 IF I.T%=2 THEN 2160
2220 IF I.T%=3 AND OPC%=1 THEN LOCATE 15,32:PRINT SPC(30):GOTO 1990
2230 IF I.T%=3 THEN LOCATE 15,32:PRINT BPOBLA:GOTO 1990
2240 IF I.T%=4 THEN 6800  'FIN DE DATOS.
2250 GOTO 2160
2260 LOCATE 15,32:PRINT POBLA$
2270 '
2280 '***********************
2290 '***ENTRADA PROVINCIA***
2300 '***********************
2310 F.C1%=17:C.C1%=32:L.E%=20:BO%=20:GOSUB 64000
2320 IF I.T%=0 THEN PROVI$=D.E$:GOTO 2420
2330 IF I.T%=1 AND OPC%=3 THEN PROVI$=BPROV:GOTO 2420
2340 IF I.T%=1 THEN PROVI$=STRING$(20," "):GOTO 2420
2360 IF I.T%=2 AND OPC%=1 THEN 2420
2370 IF I.T%=2 THEN 2310
2380 IF I.T%=3 AND OPC%=1 THEN LOCATE 17,32:PRINT SPC(20):GOTO 2140
2390 IF I.T%=3 THEN LOCATE 15,32:PRINT BPROV:GOTO 2140
2400 IF I.T%=4 THEN 6800  'FIN DE DATOS.
2410 GOTO 2310
2420 LOCATE 17,32:PRINT PROVI$
2430 '
3590 '*************************************
3600 '***PEDIMOS CONFORMIDAD A LOS DATOS***
3610 '*************************************
3620 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" ,DATOS ERRONEOS -F1-":GOSUB 8500
3630 A$=INPUT$(1)
3640 IF ASC(A$)<13 OR ASC(A$)>14 THEN 3630
3650 IF ASC(A$)=14 THEN GOSUB 7650:GOTO 1690
3652 '
3654 IF OPC%=3 OR WSTATUS$="L" THEN GB1%=R.B%:GOTO 3700
3660 '
3670 '
3680 'GRABAR DATOS.
3690 ULTDIR%=ULTDIR%+1:GB1%=ULTDIR%
3700 GET #1,GB1%
3710 LSET BANCO=NBAN$
3712 LSET BNOM=NOM$
3714 LSET BDIREC=DIREC$
3716 LSET BCPOS=CPOS$
3718 LSET BPOBLA=POBLA$
3719 LSET BPROV=PROVI$
3720 LSET BFIL001=STRING$(5,0)
3730 LSET BSTATUS="O"
3740 '
3750 PUT #1,GB1%
3760 '
4040 'REGRABAMOS REGISTRO DE DATOS.
4042 IF OPC%=3 THEN 4100
4050 GET #1,1
4060 ALTDIR%=ALTDIR%+1
4070 LSET BULTIMO=MKI$(ULTDIR%)
4080 LSET BALTAS=MKI$(ALTDIR%)
4090 PUT #1,1
4100 CLOSE
4110 '
4112 GOSUB 4610    'BORRAR PANTALLA
4114 GOSUB 30010   'APERTURA FICHERO
4116 '
4118 GOTO 1119     'OTRO.
4120 '
4310 '*****************************
4320 '***TRATAMIENTO DE CONSULTA***
4330 '*****************************
4340 GOSUB 8500:MEN$="PULSE UNA TECLA PARA NUEVA CONSULTA":GOSUB 8500
4350 A$=INPUT$(1)
4360 GOTO 4100
4362 '
4370 '
4380 '
4390 '*************************
4400 '***TRATAMIENTO DE BAJA***
4410 '*************************
4420 GOSUB 8500:MEN$="DAR DE BAJA "+ENTER$+" , NO DAR DE BAJA -F1-":GOSUB 8500
4430 A$=INPUT$(1)
4440 IF ASC(A$)<13 OR ASC(A$)>14 THEN 4430
4450 IF ASC(A$)=14 THEN 4100   'NO DAR DE BAJA
4460 'DAR DE BAJA.
4470 LSET BSTATUS="L"
4480 PUT #1,R.B%
4490 'REGRABAMOS REGISTRO 1
4492 GET #1,1
4500 ALTDIR%=ALTDIR%-1
4510 LSET BALTAS=MKI$(ALTDIR%)
4520 PUT #1,1
4530 '
4540 GOTO 4100
4550 '
4600 '**********************************
4610 '***BORRADO DATOS DE LA PANTALLA***
4620 '**********************************
4630 LOCATE 7,32:PRINT SPC(4)
4640 LOCATE 9,32:PRINT SPC(30)
4650 LOCATE 11,32:PRINT SPC(30)
4660 LOCATE 13,32:PRINT SPC(5)
4670 LOCATE 15,32:PRINT SPC(30)
4680 LOCATE 17,32:PRINT SPC(20)
4690 '
4700 RETURN
4710 '
5060 '**********************
5070 '***LISTADO POR NRO.***
5080 '**********************
5090 'MIRAMOS SI ESTA EL FICHERO ACTUALIZADO.
5100 GOSUB 30010   'APERTURA FICHERO DE PLAN CONTABLE.
5110 CLOSE
5120 IF ULTDIR%=ACTDIR% THEN 5210  'CONTINUAR CON EL PROGRAMA.
5130 '
5140 FICBAT$="ACABANCO.BAT;"+DIRE$+";"+PROGRAMA$
5150 '
5160 CLS
5170 SHELL FICBAT$
5180 CLS
5190 CLOSE
5192 GOSUB 30010
5194 GET #1,1
5196 ULTIMO%=ALTDIR%+1
5198 LSET BULTIMO=MKI$(ULTIMO%)
5200 LSET BULTACT=MKI$(ULTIMO%)
5202 PUT #1,1
5204 CLOSE
5210 '
5220 '
5230 '
5240 '
5250 '
5500 '***********************************************************
5502 '***LISTADO NUMERICO FICHERO GRUPOS Y SUBGRUPOS CONTABLES***
5504 '***********************************************************
5506 TIT$="- LISTADO FICHERO BANCOS -"
5508 '
5510 '
5512 X%=80-LEN(TIT$)
5514 X%=INT(X%/2)
5516 GOSUB 8000
5518 LOCATE 6,X%:PRINT TIT$
5520 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
5522 LOCATE 11,12:PRINT "DESDE EL BANCO : <    >     HASTA EL BANCO : <    >"
5524 LOCATE 14,29:COLOR 0,7,0:PRINT " ESC ";:COLOR 7,0,0:PRINT " Terminar Listado"
5526 'PEDIMOS DESDE.
5530 F.C1%=11:C.C1%=30:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
5532 IF I.T%=0 THEN DESDE$=D.E$:GOTO 5538
5534 IF I.T%=4 THEN CLOSE:GOTO 500
5536 GOTO 5530
5538 LOCATE 11,30:PRINT DESDE$
5540 'PEDIMOS HASTA.
5542 F.C1%=11:C.C1%=58:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
5544 IF I.T%=0 THEN HASTA$=D.E$:GOTO 5550
5546 IF I.T%=4 THEN CLOSE:GOTO 500
5548 GOTO 5552
5550 LOCATE 11,58:PRINT HASTA$
5552 IF DESDE$>HASTA$ THEN PLAY "A":GOTO 5530
5554 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1-":GOSUB 8500
5556 A$=INPUT$(1)
5558 IF ASC(A$)<13 OR ASC(A$)>14 THEN 5556
5560 IF ASC(A$)=14 THEN GOSUB 8500:GOTO 5530
5562 '
5590 'ABRIR FICHEROS.
5592 WIDTH "LPT1:",140:OPEN "LPT1:" FOR OUTPUT AS #9
5594 LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(15)
5596 '
5600 GOSUB 30010  'ABRIR INDICE Y DATOS.
5602 ULTIMO%=ACTDIR%
5604 '
5614 '
5616 '
5618 REG%=1:PAG%=0:LIN%=90:FF$="":NCLI%=0
5620 '
5622 GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5624 '
5626 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 5502
5628 '
5630 WHILE FF$<>"FF"
5632   '
5634   'LINEA DE DETALLE
5636   IF LIN%>59 THEN GOSUB 5746  'HACER CABECERAS.
5638   LPRINT TAB(7) BANCO;
5640   LPRINT TAB(17) BNOM;
5642   LPRINT TAB(52) BDIREC;
5644   LPRINT TAB(87) BCPOS;
5646   LPRINT TAB(96) BPOBLA
5666   LIN%=LIN%+1:NCLI%=NCLI%+1
5716   '
5718   '
5720   GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5722   '
5724   '
5726   '
5728 WEND
5730 '
5732 LPRINT
5734 LPRINT TAB(5):LPRINT USING "TOTAL BANCOS LISTADOS : #####";NCLI%
5736 LPRINT CHR$(12)
5738 CLOSE
5740 GOTO 500
5742 '
5744 '***************************
5746 '***CABECERAS DEL LISTADO***
5748 '***************************
5749 TIT2$="LISTADO FICHERO DE BANCOS AL "+FECHA$
5750 '
5751 IF LIN%<>90 THEN LPRINT CHR$(12)
5752 PAG%=PAG%+1
5754 LPRINT TAB(1) EMPRESA$;
5756 LPRINT TAB(51) TIT2$;
5758 LPRINT TAB(118):LPRINT USING "PAGINA : ####";PAG%
5760 LPRINT TAB(1) STRING$(LEN(EMPRESA$),"=");
5762 LPRINT TAB(51) STRING$(LEN(TIT2$),"=");
5764 LPRINT TAB(118) STRING$(13,"=")
5765 '
5766 LPRINT
5780 LPRINT TAB(6) "BANCO";
5782 LPRINT TAB(26) "N O M B R E";
5784 LPRINT TAB(58) "D I R E C C I O N";
5786 LPRINT TAB(87) "C.PO.";
5788 LPRINT TAB(103) "P O B L A C I O N"
5790 '
5792 LPRINT TAB(5) STRING$(8,"=");
5794 LPRINT TAB(17) STRING$(30,"=");
5796 LPRINT TAB(52) STRING$(30,"=");
5798 LPRINT TAB(87) STRING$(5,"=");
5800 LPRINT TAB(96) STRING$(30,"=")
5802 LPRINT
5804 '
5890 '
5892 LIN%=8
5893 RETURN
5894 '********************************
5896 '***LECTURA DE INDICES Y DATOS***
5898 '********************************
5900 REG%=REG%+1
5901 TECLA$=INKEY$:IF TECLA$="" THEN 5903
5902 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
5903 IF REG%>ULTIMO% THEN FF$="FF":RETURN
5904 GET #1,REG%
5918 IF BSTATUS<>"O" THEN 5900
5920 IF BANCO<DESDE$ THEN 5900
5922 IF BANCO>HASTA$ THEN FF$="FF":RETURN
5924 '
5926 RETURN
6000 '
6800 '**************************
6810 '***FIN ENTRADA DE DATOS***
6820 '**************************
6830 CLOSE:COLOR 15
6840 GOTO 500
6850 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Opci¢n. ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s. ";
7732 COLOR 10:PRINT " F10 =>";
7734 COLOR 15:PRINT " Duplicar Campo. "
7740 '
7750 RETURN
7770 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 RUN "PACAINIC"
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '*****************************
30010 '***APERTURA FICHERO BANCOS***
30020 '*****************************
30022 FICHE1$="\"+DIRE$+"\FACABANC.DAT"
30024 '
30030 'APERTURA FICHERO DE DATOS.
30040 OPEN "R",#1,FICHE1$,125
30042 'REGISTRO 1.
30050 FIELD #1,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
30052 'REGISTRO DE DATOS.
30054 FIELD #1,4 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,5 AS BFIL001,1 AS BSTATUS
30060 GET #1,1
30070 IF BETIQUETA="FACABANC.DAT" THEN 30110  'CONTINUAR
30080 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
30090 A$=INPUT$(1)
30100 GOTO 9000
30110 'CONTINUAR.
30120 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30122 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30130 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
30140 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30142 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30150 '
30160 RETURN
30170 '
30400 '*************************
30410 '***LECTURA AZAR BANCOS***
30420 '*************************
30430 'El argumento de busqueda sobre 'BUSCA$'
30440 'El numero de registros sobre 'N.R%'
30450 R.B%=0           'Donde devuelve el nro. de registro.
30452 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30460 SW1%=0         'SW indicador de la busqueda.
30470 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30480 '
30490 R.I%=INT((N.R%/2)+.5)
30500 R.C%=R.I%
30510 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30630
30520 IF R.I%>N.R% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 30630
30530 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
30540 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 30800      'MIRAR STATUS
30550 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30560 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 30580
30570 R.I%=R.I%+R.C%
30580 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30510
30590 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30510
30600 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30510
30610 REG2%=R.I%:SW100%=0
30620 GOTO 30510
30630 'REGISTRO NO BUSCADO.
30640 XX%=0
30650 FOR A%=DES% TO HAS%
30660   GET #1,A%
30670   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
30680 NEXT A%
30690 IF XX%=1 THEN 30800
30700 '
30710 '
30720 'Registro NO BUSCADO
30730 XX%=0
30740 IF N.R%=0 THEN N.R%=1
30750 FOR JMG%=N.R% TO ULTIMO%
30760     GET #1,JMG%
30770     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
30780 NEXT JMG%
30790 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30800 'MIRAMOS STATUS.
30810 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30820 WSTATUS$=""
30830 RETURN
30840 '
31000 '********************************
31010 '***PRESENTAR ENTRADA DE DATOS***
31020 '********************************
31030 X%=80-LEN(TITA$):X%=X%/2
31040 GOSUB 8000
31042 LOCATE 4,X%:PRINT TITA$
31044 LOCATE 5,X%:PRINT STRING$(LEN(TITA$),205)
31046 LOCATE 7,18:PRINT "NRO. BANCO : <" SPACE$(4) ">"
31048 LOCATE 9,18:PRINT "BANCO      : <" SPACE$(30) ">"
31050 LOCATE 11,18:PRINT "DIRECCION  : <" SPACE$(30) ">"
31052 LOCATE 13,18:PRINT "C.POSTAL   : <" SPACE$(5) ">"
31054 LOCATE 15,18:PRINT "POBLACION  : <" SPACE$(30) ">"
31056 LOCATE 17,18:PRINT "PROVINCIA  : <" SPACE$(20) ">"
31060 '
31070 RETURN
31080 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63152  IF ASC(D.R$)=14 THEN I.T%=5:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64122  IF ASC(D.R$)=14 THEN JMG%=L.E%:I.T%=5:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¤" OR D.R$="¥" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
