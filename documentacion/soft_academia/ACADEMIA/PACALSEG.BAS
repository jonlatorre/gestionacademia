10 '                       PACALSEG.BAS
20 '                      --------------
30 '         LISTADO ALFABETICO DE ALUMNOS PARA SEGUROS
40 '         ------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(20," ")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,4)
134 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,B,F
150 OPTION BASE 1
200 DIM TABENT$(30)
520 '
522 '
530 '
550 '
570 '
590 '
600 GOTO 4490
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 'IF NOSAL%=0 THEN 3760
3757 LPRINT
3760 PAG%=PAG%+1:LIN%=1
3770 LPRINT TAB(6) EMPRESA$;
3780 LPRINT TAB(53) "RELACION ALFABETICA DE ALUMNOS AL " FECHA$ ;
3790 LPRINT TAB(112) "HOJA NUMERO :";:GUAPRI#=PAG%:GOSUB 64770:LPRINT TAB(130-L.C%) GUAPRI$
3800 LPRINT TAB(6) STRING$(LEN(EMPRESA$),"-");
3810 LPRINT TAB(53) STRING$(44,"-");
3820 LPRINT TAB(112) STRING$(19,"-")
3840 'LPRINT
3850 LPRINT TAB(6) "NUMERO";
3860 LPRINT TAB(14) "       A P E L L I D O S   Y   N O M B R E";
3870 LPRINT TAB(73) "NUMERO";
3880 LPRINT TAB(81) "       A P E L L I D O S   Y   N O M B R E"
3900 '
3910 LPRINT TAB(6) STRING$(6,"-");
3912 LPRINT TAB(14) STRING$(50,"-");
3914 LPRINT TAB(73) STRING$(6,"-");
3916 LPRINT TAB(81) STRING$(50,"-");
3930 RETURN
3940 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4491 '*************************
4492 'GOSUB 8000:LOCATE 6,23:PRINT "-- RELACION ALFABETICA DE SOCIOS --"
4493 'LOCATE 7,23:PRINT STRING$(35,205)
4494 'GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4495 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
4496 GOSUB 30000
4498 IF CSWA="0" THEN CLOSE:GOTO 4501
4499 GET #2,1:LSET CSWA="0":PUT #2,1:CLOSE
4500 SHELL "ACAALFA.BAT"
4501 GOSUB 8000
4502  'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4503  'LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B"+CHR$(27)+"&l7D";
4504 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4506 '
4510 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
4520 COLOR 10:LOCATE 6,23:PRINT "- LISTADO DE ALUMNOS PARA SEGUROS -"
4530 LOCATE 7,23:PRINT STRING$(35,205)
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4820 GOSUB 34000:N.ALU%=0
4830 FOR REG%=2 TO ULTIMO%
4832   GET #1,REG%:N.L%=CVI(FULTI)
4834   IF N.L%<2 OR N.L%>ULTIMO% THEN 4900
4840   GET #2,N.L%
4842   IF CSTATUS<>"O" THEN 4900 'AL NEXT
4844   IF CMARCA="L" THEN 4900
4850   NOM$=CAPELL1:APE1$=CAPELL2:APE2$=CNOMBRE:GOSUB 40000
4852   LSET BNUMERO=CCUENTA:LSET BNOMBRE=CUCUCU$
4854   N.ALU%=N.ALU%+1:PUT #3,N.ALU%
4855   EDAD#=VAL(FEDIA$)-VAL(CFENA)
4856   IF EDAD#<150000# THEN MENOS%=MENOS%+1:GOTO 4860
4858   MAS14%=MAS14%+1
4860   TOALU%=TOALU%+1
4900 NEXT REG%
4910 CLOSE #3
4915 NOSAL%=0
4920 GOSUB 34000
4922   WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4924   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B"+CHR$(27)+"&l7D";
4930 FOR XXX%=1 TO N.ALU% STEP 114
4935     GOSUB 3740
4937     NOSAL%=1
4940     FOR YYY%=XXX% TO XXX%+56
4950         IF YYY%>N.ALU% THEN 5020
4955         GET #3,YYY%
4960         GUAPRI#=VAL(BNUMERO):GOSUB 64770
4965         LPRINT TAB(11-L.C%) GUAPRI$;
4970         LPRINT TAB(14) BNOMBRE;
4975         ZZZ%=YYY%+57
4980         IF ZZZ%>N.ALU% THEN LPRINT:GOTO 5005
4985         GET #3,ZZZ%
4990         GUAPRI#=VAL(BNUMERO):GOSUB 64770
4995         LPRINT TAB(78-L.C%) GUAPRI$;
5000         LPRINT TAB(81) BNOMBRE
5005     NEXT YYY%
5007     LPRINT CHR$(12)
5010 NEXT XXX%
5020 '
5025 '
5030 LPRINT
5035 LPRINT TAB(6) "Alumnos hasta 14 a¤os .......... ";
5040 GUAPRI#=MENOS%:GOSUB 64770:LPRINT TAB(43-L.C%) GUAPRI$;
5045 LPRINT TAB(48) "Alumnos mayores de 14 a¤os ..... ";
5050 GUAPRI#=MAS14%:GOSUB 64770:LPRINT TAB(85-L.C%) GUAPRI$;
5055 LPRINT TAB(91) "Total de Alumnos ............... ";
5060 GUAPRI#=TOALU%:GOSUB 64770:LPRINT TAB(128-L.C%) GUAPRI$
5070 LPRINT CHR$(12)
5080 GOTO 9000
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 10:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9035 KILL"FACASEGU.DAT"
9040 RUN "PACACLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,6 AS CFEFAC,3 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30130 '
30140 RETURN
30150 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*****************************
34010 '***APERTURA FICHERO SEGUROS**
34020 '*****************************
34030 FICHE1$="FACASEGU.DAT"
34040 '
34050 'APERTURA FICHERO DE DATOS.
34060 OPEN "R",#3,FICHE1$,55
34070 'REGISTRO 1.
34080 FIELD #3,5 AS BNUMERO,50 AS BNOMBRE
34230 RETURN
34240 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
