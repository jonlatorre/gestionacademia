10 '                       PBUIHOBE.BAS
20 '                      --------------
30 '          LISTADO DE LAS PAPELAS PARA LOS BANCOS
40 '          --------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
135 DIALIS$=RIGHT$(FECHA$,2)+MID$(FECHA$,4,2)+LEFT$(FECHA$,2)
140 DEFSTR D,F,K,L,A,J,C,T
150 OPTION BASE 1
165 DIM TA(31),TB(9),TC(9),LITMES$(12),TABENT$(30),ORI$(4)
170 DIM TABRUT$(8),TODI%(24),DIAS%(12):KEY OFF:IMPMAN$="000000000"
175 DATA "UNO ","DOS ","TRES ","CUATRO ","CINCO ","SEIS ","SIETE ","OCHO ","NUEVE ","DIEZ ","ONCE ","DOCE ","TRECE ","CATORCE ","QUINCE ","DIECISEIS "
180 DATA "DIECISIETE ","DIECIOCHO ","DIECINUEVE ","VEINTE ","VEINTIUNA ","VEINTIDOS ","VEINTITRES ","VEINTICUATRO ","VEINTICINCO ","VEINTISEIS ","VEINTISIETE ","VEINTIOCHO ","VEINTINUEVE ","TREINTA ","TREINTA Y UNO "
185 FOR I%=1 TO 31:READ TA(I%):NEXT I%
190 DATA "DIEZ ","CIENTO ","VEINTE ",DOSCIENT,"TREINTA ",TRESCIENT,"CUARENTA ",CUATROCIENT,"CINCUENTA ",QUINIENT,"SESENTA ",SEISCIENT,"SETENTA ",SETECIENT,"OCHENTA ",OCHOCIENT,"NOVENTA ",NOVECIENT
195 FOR I%=1 TO 9:READ TB(I%),TC(I%):NEXT I%
200 DATA ENERO,FEBRERO,MARZO,ABRIL,MAYO,JUNIO,JULIO,AGOSTO,SEPTIEMBRE,OCTUBRE,NOVIEMBRE,DICIEMBRE
205 FOR I%=1 TO 12:READ LITMES$(I%):NEXT I%
210 DATA -Originala bezeroarentzako / Original para el cliente,-Bulegorako Kopia / Copia para la Oficina
215 DATA -Aurrezki-Sailerako Kopia / Copia para el Servicio de Ahorro,-Kontratazio-Zentrurako Kopia / Copia para el Centro de Contrataci¢n
220 FOR I%=1 TO 4 :READ ORI$(I%):NEXT I%
225 DATA 31,28,31,30,31,30,31,31,30,31,30,31
230 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
235 DATA 0,31,59,90,120,151,181,212,243,273,304,334
237 DATA 0,31,60,91,121,152,182,213,244,274,305,335
240 FOR I%=1 TO 24:READ TODI%(I%):NEXT I%
300 '*************************************
302 '***COMIENZO DEL PROGRAMA DE LISTAR***
304 '*************************************
306 '
310 GOSUB 31000
400 IF KULTIAL%=KULTIAC% AND LSWA="0" THEN CLOSE:GOTO 480
410 GET #4,1
420 LSET KULAC=MKI$(KULTIAL%)
430 PUT #4,1
432 GET #5,1
434 LSET LSWA="0"
436 PUT #5,1
440 CLOSE
450 CLS
460 SHELL "BUILDIR.BAT"
465 SHELL "BUILALF.BAT"
470 '
480 GOSUB 30000
490 IF JULTIAL%=JULTIAC% THEN CLOSE:GOTO 570
500 GET #6,1
510 LSET JULAC=MKI$(JULTIAL%)
520 PUT #6,1
522 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 CLOSE
540 CLS
550 SHELL "BUIAUTO.BAT"
560 '
570 '
590 '
600 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
605 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
610 GOSUB 8000
620 GOSUB 30000:GOSUB 31000
630 TIT$="- LISTAR RESGUARDOS DE INGRESO -"
640 GOSUB 32000
720       '********************
722       '***ENTRADA PUEBLO***
724       '********************
726       F.C1%=7:C.C1%=13:L.E%=2:BO%=2:GOSUB 64000
728       IF I.T%=0 THEN PUEBLO$=D.E$:GOTO 742
729       IF I.T%=5 THEN 726
730       IF I.T%=1 THEN 726
732       IF I.T%=1 AND I.A%=3 THEN PUEBLO$=LEFT$(LCLAV,2):GOTO 742
733       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,13:PRINT SPC(2):GOTO 726
734       IF I.T%=3 THEN 726
736       IF I.T%=4 THEN 9000               'Fin Entrada.
738       IF I.T%=2 AND I.A%=1 THEN 742
740       IF I.T%=2 THEN 726
742       '
744       LOCATE 7,13:PRINT PUEBLO$
746       '
748       '***********************
750       '***ENTRADA JUZGADO  ***
752       '***********************
754       F.C1%=7:C.C1%=29:L.E%=3:BO%=3:GOSUB 63000
756       IF I.T%=0 THEN JUZGADO$=D.E$:GOTO 770
757       IF I.T%=5 THEN 754
758       IF I.T%=1 THEN 754
760       IF I.T%=1 AND I.A%=3 THEN JUZGADO$=MID$(LCLAV,3,3):GOTO 770
761       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,13:PRINT SPC(2):GOTO 726
762       IF I.T%=3 THEN 726
764       IF I.T%=4 THEN 9000               'Fin Entrada.
766       IF I.T%=2 AND I.A%=1 THEN 770
768       IF I.T%=2 THEN 750
770       '
772       LOCATE 7,29:PRINT JUZGADO$
774       '
776       '***********************
778       '***ENTRADA N. AUTO  ***
780       '***********************
782       F.C1%=7:C.C1%=43:L.E%=5:BO%=5:GOSUB 63000
784       IF I.T%=0 THEN NUAUTO$=D.E$:GOTO 798
785       IF I.T%=5 THEN 782
786       IF I.T%=1 THEN 782
788       IF I.T%=1 AND I.A%=3 THEN NUAUTO$=MID$(LCLAV,8,5):GOTO 798
789       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,29:PRINT SPC(3):GOTO 750
790       IF I.T%=3 THEN 750
792       IF I.T%=4 THEN 9000               'Fin Entrada.
794       IF I.T%=2 AND I.A%=1 THEN 798
796       IF I.T%=2 THEN 778
798       '
800       LOCATE 7,43:PRINT NUAUTO$
802       '
804       '***********************
806       '***ENTRADA A¥O AUTO ***
808       '***********************
810       F.C1%=7:C.C1%=63:L.E%=2:BO%=2:GOSUB 63000
812       IF I.T%=0 THEN FEAUTO$=D.E$:GOTO 828
813       IF I.T%=5 THEN 810
814       IF I.T%=1 THEN 810
816       IF I.T%=1 AND I.A%=3 THEN FEAUTO$=MID$(LCLAV,6,2):GOTO 828
817       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,43:PRINT SPC(5):GOTO 778
818       IF I.T%=3 THEN 778
820       IF I.T%=4 THEN 9000               'Fin Entrada.
822       IF I.T%=2 AND I.A%=1 THEN 828
824       IF I.T%=2 THEN 806
826       '
828       LOCATE 7,63:PRINT FEAUTO$
830       '
832       '***********************
834       '***ENTRADA NUME.LOTE***
836       '***********************
838       F.C1%=7:C.C1%=76:L.E%=2:BO%=2:GOSUB 63000
840       IF I.T%=0 THEN LOTE$=D.E$:GOTO 856
841       IF I.T%=5 THEN 838
842       IF I.T%=1 THEN 838
844       IF I.T%=1 AND I.A%=3 THEN LOTE$=RIGHT$(LCLAV,2):GOTO 856
845       IF I.T%=3 AND I.A%=1 THEN LOCATE 7,63:PRINT SPC(2):GOTO 806
846       IF I.T%=3 THEN 806
848       IF I.T%=4 THEN 9000               'Fin Entrada.
850       IF I.T%=2 AND I.A%=1 THEN 856
852       IF I.T%=2 THEN 834
854       '
856       LOCATE 7,76:PRINT LOTE$
858       '
860       TONTO$=PUEBLO$+JUZGADO$+FEAUTO$+NUAUTO$+LOTE$:CLA$=TONTO$
862       GOSUB 31240
864        IF SWL%=0 THEN 890
868        GOSUB 8500:MEN$=" Este Lote no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1)
870       CLOSE
880       GOTO 600
882       '
890       GET #5,CVI(KULTI)
892       IF LMARCA="B" THEN GOSUB 8500:MEN$="Este Lote est  dado de Baja, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 870
916        '
917        '
918        '
919        '
920        '
1000       '*******************************
1002       '***ENTRADA NUMERO DE SUBASTA***
1004       '*******************************
1008       F.C1%=10:C.C1%=51:L.E%=1:BO%=1:ESC%=0:GOSUB 63000
1010       IF I.T%=0 THEN SUBA$=D.E$:GOTO 1028 'Entrada OK
1024       IF I.T%=4 THEN 9000
1026       GOTO 1008
1028       IF SUBA$<"1" OR SUBA$>"3" THEN BEEP:GOTO 1008
1038       '
1040       LOCATE 10,51:PRINT SUBA$
1045       IF SUBA$="1" THEN LOCATE 10,54:PRINT "PRIMERA SUBASTA":GOTO 1060
1050       IF SUBA$="2" THEN LOCATE 10,54:PRINT "SEGUNDA SUBASTA":GOTO 1060
1056       LOCATE 10,54:PRINT "TERCERA SUBASTA"
1060       '
1100       '*******************************
1102       '***ENTRADA NOMBRE DEL TITULAR**
1104       '*******************************
1108       F.C1%=12:C.C1%=51:L.E%=1:BO%=1:ESC%=0:GOSUB 63000
1110       IF I.T%=0 THEN NOMB$=D.E$:GOTO 1128 'Entrada OK
1120       IF I.T%=3 THEN LOCATE 10,51:PRINT SPC(1):LOCATE 10,54:PRINT SPC(15):GOTO 1008
1122       IF I.T%=4 THEN 9000               'Fin Entrada.
1124       GOTO 1108
1128       IF NOMB$<"1" OR NOMB$>"2" THEN BEEP:GOTO 1108
1130       LOCATE 12,51:PRINT NOMB$
1132       IF NOMB$="1" THEN LOCATE 12,54:PRINT "GERAR":GOTO 1136
1134       LOCATE 12,54:PRINT "GILDO":GOTO 1136
1136       '
1138       '
1140       '
1200       '*******************************
1202       '***ENTRADA FECHA DEL 600    ***
1204       '*******************************
1208       F.C1%=14:C.C1%=51:L.E%=1:BO%=1:ESC%=0:GOSUB 63000
1210       IF I.T%=0 THEN TIPO$=D.E$:GOTO 1228 'Entrada OK
1220       IF I.T%=3 THEN LOCATE 12,51:PRINT SPC(1):LOCATE 12,54:PRINT SPC(10):GOTO 1108
1222       IF I.T%=4 THEN 9000               'Fin Entrada.
1224       GOTO 1208
1228       IF TIPO$<"1" OR TIPO$>"2" THEN BEEP:GOTO 1208
1230       LOCATE 14,51:PRINT TIPO$
1232       IF TIPO$="1" THEN LOCATE 14,54:PRINT "CONSIGNACION":GOTO 1236
1234       LOCATE 14,54:PRINT "   REMATE   "
1236       '
1240       '
1300       '*******************************
1302       '***ENTRADA NUMERO DE CUENTA ***
1304       '*******************************
1308       F.C1%=16:C.C1%=51:L.E%=12:BO%=12:ESC%=0:GOSUB 63000
1310       IF I.T%=0 THEN CU$=D.E$:GOTO 1328 'Entrada OK
1320       IF I.T%=3 THEN LOCATE 14,51:PRINT SPC(1):LOCATE 14,54:PRINT SPC(12):GOTO 1208
1322       IF I.T%=4 THEN 9000               'Fin Entrada.
1324       GOTO 1308
1328       '
1330       LOCATE 16,51:PRINT CU$
1332       '
1334       '
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=14 THEN CLOSE:GOTO 600
2028   '
2030   '
2040  GOSUB 8500:MEN$="COLOCA HOJAS DEL TIPO - B - Y PULSA UNA TECLA"
2050  GOSUB 8500
2060 A$=INPUT$(1)
2080  'GET #4,NL%:GET #5,CVI(KULTI)
2090   TONTA$=LEFT$(CLA$,12):GOSUB 30240
2100   IF SWA%=1 THEN GOSUB 8500:MEN$="Este auto no existe, pulsar una tecla:GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 600
2110   IF ATIPO="1" THEN CC$=" 1 8":GOTO 2120
2111   IF ATIPO="2" THEN CC$=" 1 7":GOTO 2120
2112   IF ATIPO="3" THEN CC$=" 1 4":GOTO 2120
2114   IF ATIPO="4" THEN XX$=" 1 5":GOTO 2120
2115   CC$=" 1 6"
2120   '
2130   JJ$=" "    '+MID$(CU$,1,1)+" "+MID$(CU$,2,1)+" "+MID$(CU$,3,1)+" "+MID$(CU$,4,1)
2140   EE$=" "+MID$(CLA$,9,1)+" "+MID$(CLA$,10,1)+" "+MID$(CLA$,11,1)+" "+MID$(CLA$,12,1)
2145   AA$=" "+MID$(CLA$,6,1)+" "+MID$(CLA$,7,1)
2150   IF NOMB$="1" THEN TITU$="GERARDO EGUIA GASTACA":DNI$="14.571.000":GOTO 2170
2160   TITU$="HERMENEGILDO EGUIA FERRI":DNI$="16.192.304":GOTO 2170
2170   IF TIPO$="1" THEN CONCE1$="CONSIGNACION":CONCE2$="   SUBASTA  ":GOTO 2180
2175   CONCE1$="   REMATE   ":CONCE2$="   SUBASTA  "
2180   '
2185   SUBA1#=CVD(LSUBA1):CONSI1%=CVI(LCONS1):CONSIG1#=INT((SUBA1#*CONSI1%/100)+.5):REMATE1#=SUBA1#-CONSIG1#
2190   SUBA2#=CVD(LSUBA2):CONSI2%=CVI(LCONS2):CONSIG2#=INT((SUBA2#*CONSI2%/100)+.5):REMATE2#=SUBA2#-CONSIG2#
2195   SUBA3#=CVD(LSUBA3):CONSI3%=CVI(LCONS3):CONSIG3#=INT((SUBA3#*CONSI3%/100)+.5):REMATE3#=SUBA3#-CONSIG3#
2200   IF SUBA$="1" AND TIPO$="1" THEN IMPOR#=CONSIG1#:GOTO 2210
2201   IF SUBA$="2" AND TIPO$="1" THEN IMPOR#=CONSIG2#:GOTO 2210
2202   IF SUBA$="3" AND TIPO$="1" THEN IMPOR#=CONSIG3#:GOTO 2210
2204   IF SUBA$="1" THEN IMPOR#=REMATE1#:GOTO 2210
2205   IF SUBA$="2" THEN IMPOR#=REMATE2#:GOTO 2210
2206   IMPOR#=REMATE3#
2210   '
2220   GOSUB 64600
2230   FOR I%=1 TO 12:LPRINT:NEXT I%
2240   LPRINT TAB(3) "PRIMERA INSTANCIA N§ "MID$(CLA$,3,3);
2242   IF LEFT$(CLA$,2)="BI" THEN LPRINT " DE BILBAO";:GOTO 2260
2243   IF LEFT$(CLA$,2)="VI" THEN LPRINT " DE VITORIA";:GOTO 2260
2244   IF LEFT$(CLA$,2)="BA" THEN LPRINT " DE BARACALDO";:GOTO 2260
2245   IF LEFT$(CLA$,2)="PM" THEN LPRINT " DE MALLORCA";:GOTO 2260
2246   IF LEFT$(CLA$,2)="B " THEN LPRINT " DE BARCELONA";:GOTO 2260
2247   IF LEFT$(CLA$,2)="M " THEN LPRINT " DE MADRID";:GOTO 2260
2248   IF LEFT$(CLA$,2)="GE" THEN LPRINT " DE GERNIKA";:GOTO 2260
2260   '
2262   LPRINT TAB(70) JJ$;
2264   LPRINT TAB(84) CC$;
2266   LPRINT TAB(89) EE$;
2268   LPRINT TAB(98) AA$
2270  'LPRINT TAB(110) CU$
2272   LPRINT
2274   LPRINT TAB(45) CU$
2290   '
2292   LPRINT
2294   LPRINT TAB(3) TITU$
2296   LPRINT TAB(113) CONCE1$
2298   LPRINT TAB(15) DNI$;
2300   LPRINT TAB(113) CONCE2$
2310   LPRINT    ':LPRINT:LPRINT
2312   LPRINT TAB(40) LIT1$
2314   LPRINT TAB(29) LIT2$:LPRINT
2316   GUAPRI#=IMPOR#:GOSUB 64770
2318   LPRINT TAB(80-L.C%) GUAPRI$
2320   LPRINT CHR$(12)
2340   CLOSE:GOTO 600
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : EDICTOS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PBUIINIC"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO DE AUTOS***
30020 '*******************************
30030 '
30040 OPEN "R",#6,"FBUIAUTO.DIR",20
30050 FIELD #6,2 AS JULTI,12 AS JCLAVE,2 AS JULAC,3 AS JLIB,1 AS JSTA
30060 GET #6,1
30070 JULTIAL%=CVI(JULTI)
30080 JULTIAC%=CVI(JULAC):N.J%=JULTIAC%
30090 ' Apertura del fichero de datos   FBUIAUTO.DAT
30100 OPEN "R",#7,"FBUIAUTO.DAT",240
30110 FIELD #7,12 AS ACLAV,1 AS ATIPO,3 AS AMEDIO,6 AS AFEC1,1 AS AESF1,6 AS AFEC2,1 AS AESF2,6 AS AFEC3,1 AS AESF3,20 AS ADEMAN,8 AS AIMPOR,40 AS APRODE,40 AS ANADA1,40 AS ANADA2
30120 FIELD #7,185 AS AODO1$,2 AS ACLI01,2 AS ACLI02,2 AS ACLI03,2 AS ACLI04,2 AS ACLI05,2 AS ACLI06,2 AS ACLI07
30130 FIELD #7,199 AS AODO2$,2 AS ACLI08,2 AS ACLI09,2 AS ACLI10,2 AS ACLI11,2 AS ACLI12,2 AS ACLI13,2 AS ACLI14,2 AS ACLI15,4 AS AHORA1,4 AS AHORA2,4 AS AHORA3,6 AS AFEME,5 AS AODO3$,1 AS AMARCA,1 AS ASTAT
30140 FIELD #7,235 AS ARES1,2 AS AULTI,1 AS ASWA,1 AS AMARC1,1 AS ASTA1
30150 GET #7,1
30160 '
30170 AULTIMO%=CVI(AULTI)
30180 '
30190 '
30200 RETURN
30210 '
30220 '
30230 '**********************************
30240 '***BUSQUEDA DICOTOMICA DE AUTOS***
30250 '**********************************
30260   '
30270 'El argumento de busqueda sobre 'TONTA$'
30280 'El numero de registros sobre 'N.K%'
30290 N.L%=0           'Donde devuelve el nro. de registro.
30300 SWA%=0         'SW indicador de la busqueda.
30310 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30320 '
30330 R.I%=INT((N.J%/2)+.5)
30340 R.C%=R.I%
30350 IF R.I%<1 THEN SWA%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30550
30360 IF R.I%>N.J% THEN SWA%=1:DES%=R.I%-R.C%:HAS%=N.J%:GOTO 30550
30370 IF R.I%=1 THEN R.I%=2:GET #6,R.I% ELSE GET #6,R.I%
30380 IF JCLAVE=TONTA$ THEN 30470      'Registro Buscado.
30390 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30400 IF JCLAVE>TONTA$ THEN R.I%=R.I%-R.C%:GOTO 30420
30410 R.I%=R.I%+R.C%
30420 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30350
30430 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30350
30440 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30350
30450 REG2%=R.I%:SW100%=0
30460 GOTO 30350
30470 'Registro Buscado.
30480 FOR JMG%=R.I% TO 1 STEP -1
30490     GET #6,JMG%
30500     IF JCLAVE<>TONTA$ THEN R.B%=JMG%+1:JMG%=-5
30510 NEXT JMG%
30520 IF JMG%=0 THEN R.B%=1
30530 IF SWA%=0 THEN GET #6,R.B%:N.L%=CVI(JULTI):GET #7,N.L%
30540 RETURN
30550 FOR R.I%=DES% TO HAS%
30560     GET #6,R.I%
30570     IF JCLAVE=TONTA$ THEN SWA%=0:GOTO 30470
30580 NEXT R.I%
30590 FOR R.I%=JULTIAC% TO JULTIAL%
30600     GET #6,R.I%
30610     IF JCLAVE=TONTA$ THEN SWA%=0:GOTO 30470
30620 NEXT R.I%
30630 RETURN
31000 '*******************************
31010 '***APERTURA FICHERO DE LOTES***
31020 '*******************************
31030 '
31040 OPEN "R",#4,"FBUILOTE.DIR",40
31050 FIELD #4,2 AS KULTI,14 AS KCLAVE,20 AS KCALLE,2 AS KULAC,1 AS KLIB,1 AS KSTA
31060 GET #4,1
31070 KULTIAL%=CVI(KULTI)
31080 KULTIAC%=CVI(KULAC):N.K%=KULTIAC%
31090 ' Apertura del fichero de datos   FBUILOTE.DAT
31100 OPEN "R",#5,"FBUILOTE.DAT",300
31110 FIELD #5,14 AS LCLAV,62 AS LDESC,20 AS LCALLE,22 AS LPISO,20 AS LPOBLA,15 AS LPROVI,1 AS LESTAD,20 AS LAQUIEN,8 AS LSUBA1,2 AS LCONS1,8 AS LSUBA2,2 AS LCONS2,8 AS LSUBA3,2 AS LCONS3
31120 FIELD #5,204 AS LODO1$,2 AS LCLI01,2 AS LCLI02,2 AS LCLI03,2 AS LCLI04,2 AS LCLI05,2 AS LCLI06,2 AS LCLI07
31130 FIELD #5,218 AS LODO2$,2 AS LCLI08,2 AS LCLI09,2 AS LCLI10,2 AS LCLI11,2 AS LCLI12,2 AS LCLI13,2 AS LCLI14,2 AS LCLI15,62 AS LNOTIF,2 AS LODO3$,1 AS LMARCA,1 AS LSTAT
31140 FIELD #5,235 AS LRES1,60 AS LRES2,2 AS LULTI,1 AS LSWA,1 AS LMARC1,1 AS LSTA1
31150 GET #5,1
31160 '
31170 LULTIMO%=CVI(LULTI)
31180 '
31190 '
31200 RETURN
31210 '
31220 '
31230 '**********************************
31240 '***BUSQUEDA DICOTOMICA DE LOTES***
31250 '**********************************
31260   '
31270 'El argumento de busqueda sobre 'TONTO$'
31280 'El numero de registros sobre 'N.K%'
31290 N.L%=0           'Donde devuelve el nro. de registro.
31300 SWL%=0         'SW indicador de la busqueda.
31310 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
31320 '
31330 R.I%=INT((N.K%/2)+.5)
31340 R.C%=R.I%
31350 IF R.I%<1 THEN SWL%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 31550
31360 IF R.I%>N.K% THEN SWL%=1:DES%=R.I%-R.C%:HAS%=N.K%:GOTO 31550
31370 IF R.I%=1 THEN R.I%=2:GET #4,R.I% ELSE GET #4,R.I%
31380 IF KCLAVE=TONTO$ THEN 31470      'Registro Buscado.
31390 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
31400 IF KCLAVE>TONTO$ THEN R.I%=R.I%-R.C%:GOTO 31420
31410 R.I%=R.I%+R.C%
31420 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 31350
31430 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 31350
31440 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 31350
31450 REG2%=R.I%:SW100%=0
31460 GOTO 31350
31470 'Registro Buscado.
31480 FOR JMG%=R.I% TO 1 STEP -1
31490     GET #4,JMG%
31500     IF KCLAVE<>TONTO$ THEN R.B%=JMG%+1:JMG%=-5
31510 NEXT JMG%
31520 IF JMG%=0 THEN R.B%=1
31530 IF SWL%=0 THEN GET #4,R.B%:NL%=CVI(KULTI)
31540 RETURN
31550 FOR R.I%=DES% TO HAS%
31560     GET #4,R.I%
31570     IF KCLAVE=TONTO$ THEN SWL%=0:GOTO 31470
31580 NEXT R.I%
31590 FOR R.I%=KULTIAC% TO KULTIAL%
31600     GET #4,R.I%
31610     IF KCLAVE=TONTO$ THEN SWL%=0:GOTO 31470
31620 NEXT R.I%
31630 RETURN
32000 '**********************************************
32010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
32020 '**********************************************
32030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
32040 LOCATE 4,X%:PRINT TIT$
32050 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
32060 LOCATE 6,1:PRINT CHR$(218) STRING$(78,196) CHR$(191)
32070 LOCATE 6,33:PRINT ">  C L A V E  <"
32080 LOCATE 7,1:PRINT CHR$(179) STRING$(78," ") CHR$(179)
32100 LOCATE 8,1:PRINT CHR$(192) STRING$(78,196) CHR$(217)
32110 COLOR 11
32120 LOCATE 7,3:PRINT "Pueblo : <  >  Juzgado : <   >  Auto : <     >  A¤o Auto : <  >  Lote : <  >"
32130 LOCATE 10,21:PRINT "Subasta N£mero ........... : < >"
32135 LOCATE 12,21:PRINT "Nombre 1 -Gerar  2 -Gildo. : < >"
32137 LOCATE 14,21:PRINT "Consignaci¢n 1  Remate 2 . : < >"
32140 LOCATE 16,21:PRINT "N£mero de Cuenta ......... : <            >"
32145 LOCATE 19,27:PRINT "Final de listar hojas ";:COLOR 15:PRINT "- F7 -"
32208 COLOR 15:RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64500 '********************************
64502 '*** CONVERTIR FECHAS A LETRA ***
64504 '********************************
64506 '
64508 IMPOR#=VAL(RIGHT$(FECH$,4))
64510 IND90%=1:GOSUB 64600 :IND90%=0
64512 DD%=VAL(MID$(FECH$,4,2)):MM%=VAL(LEFT$(FECH$,2))
64514 IF DD%=21 THEN DIAFEC$="VEINTIUNO " ELSE DIAFEC$=TA(DD%)
64516 FDIA$=" AL "+DIAFEC$+"DE "+LITMES$(MM%)+" DE " +LIT1$
64518 RETURN
64520 '
64600  '********************
64602 '***RUTINA PESETAS***
64604 '********************
64606 LIT$="":LIT1$="":LIT2$="":IND33%=0:IF IMPOR#=0 THEN LIT1$=LIT1$+"CERO.":LIT$=LIT1$:GOTO 64638
64608 'Desglose del importe
64610 IMPOR$=STR$(IMPOR#):IMPOR$=RIGHT$(IMPOR$,LEN(IMPOR$)-1)
64612 IMPOR$=LEFT$(IMPMAN$,11-LEN(IMPOR$))+IMPOR$
64614 IMPOR2$=LEFT$(IMPOR$,2)
64616 IMPOR$=RIGHT$(IMPOR$,9)
64618 GUAJE%=VAL(IMPOR2$)
64620 GUA1%=VAL(LEFT$(IMPOR2$,1))
64622 GUA2%=VAL(RIGHT$(IMPOR2$,1))
64624 '
64626 '
64628 PESETA$=RIGHT$(IMPOR$,3):PESETA%=VAL(PESETA$):MYM$=LEFT$(IMPOR$,6):MYM!=VAL(MYM$):DECPTA$=RIGHT$(PESETA$,2):DECPTA%=VAL(DECPTA$):DIG7%=VAL(LEFT$(PESETA$,1)):DIG8%=VAL(LEFT$(DECPTA$,1)):DIG9%=VAL(RIGHT$(DECPTA$,1))
64630 MILLON$=LEFT$(MYM$,3):MILLON%=VAL(MILLON$):MILES$=RIGHT$(MYM$,3):MILES%=VAL(MILES$):DECMIL$=RIGHT$(MILES$,2):DECMIL%=VAL(DECMIL$):DIG4%=VAL(LEFT$(MILES$,1)):DIG5%=VAL(LEFT$(DECMIL$,1)):DIG6%=VAL(RIGHT$(DECMIL$,1))
64632 DECMLL$=RIGHT$(MILLON$,2):DECMLL%=VAL(DECMLL$):DIG1%=VAL(LEFT$(MILLON$,1)):DIG2%=VAL(LEFT$(DECMLL$,1)):DIG3%=VAL(RIGHT$(DECMLL$,1))
64634 'Tratamiento particular
64636 GOSUB 64740:GOSUB 64646:GOSUB 64662:GOSUB 64678
64638 'FIN RUTINA
64640 LIT1$=LIT1$+STRING$(45-LEN(LIT1$),CHR$(247))
64642 LIT2$=LIT2$+STRING$(56-LEN(LIT2$),CHR$(247))
64644 RETURN
64646 'Tratamiento millones
64648 IF MILLON%=0 THEN 64660
64650 IF MILLON%=1 THEN MAN$="UN MILLON ":GOSUB 64698:GOTO 64660
64652 IF MILLON%=100 THEN MAN$="CIEN MILLONES ":GOSUB 64698:GOTO 64660
64654 DECENA%=DECMLL%:DIGA%=DIG1%:DIGB%=DIG2%:DIGC%=DIG3%:IND90%=1
64656 GOSUB 64708 'Tratamiento comun
64658 IND90%=0:MAN$="MILLONES ":GOSUB 64698 'Salida a literales
64660 RETURN
64662 'Tratamiento miles
64664 IF MILES%=0 THEN 64676
64666 IF MILES%=1 THEN 64674
64668 IF MILES%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64674
64670 DECENA%=DECMIL%:DIGA%=DIG4%:DIGB%=DIG5%:DIGC%=DIG6%
64672 GOSUB 64708 'Tratamiento comun
64674 MAN$="MIL ":GOSUB 64698 'Salida a literales
64676 RETURN
64678 'Tratamiento pesetas
64680 IF PESETA%=0 THEN 64692
64682 'IF PESETA%=1 AND MYM>0 THEN MAN$="Y ":GOSUB 1700 'Salida a literales
64684 IF PESETA%=1 THEN MAN$="UNA ":GOSUB 64698:GOTO 64692
64686 IF PESETA%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64692
64688 DECENA%=DECPTA%:DIGA%=DIG7%:DIGB%=DIG8%:DIGC%=DIG9%
64690 GOSUB 64708 'Tratmiento comun
64692 IF LIT2$="" THEN LIT1$=LEFT$(LIT1$,LEN(LIT1$)-1)+"." ELSE LIT2$=LEFT$(LIT2$,LEN(LIT2$)-1)+"."
64694 LIT$=LEFT$(LIT$,LEN(LIT$)-1)+"."
64696 RETURN
64698 'Salida a literales
64700 IF IND33%=1 THEN LIT2$=LIT2$+MAN$:GOTO 64704
64702 IF (45-LEN(LIT1$))>=LEN(MAN$)THEN LIT1$=LIT1$+MAN$ ELSE LIT2$=LIT2$+MAN$:IND33%=1
64704 LIT$=LIT$+MAN$
64706 RETURN
64708 'Tratamiento comun
64710 IF DIGA%=0 THEN 64718 ELSE MAN$=TC(DIGA%)
64712 IF DIGA%=1 THEN 64716
64714 IF IND90%=1 THEN MAN$=MAN$+"OS " ELSE MAN$=MAN$+"AS "
64716 GOSUB 64698 'Salida a literales
64718 IF DIGB%=0 THEN 64726
64720 IF DECENA%>29 THEN MAN$=TB(DIGB%):GOSUB 64698:GOTO 64726
64722 IF IND90%=1 AND DECENA%=21 THEN MAN$="VEINTIUN ":GOSUB 64698:IND60%=1:GOTO 64726
64724 MAN$=TA(DECENA%):GOSUB 64698:IND60%=1
64726 IF IND60%=1 OR DIGC%=0 THEN 64736
64728 IF DIGB%<>0 THEN MAN$="Y ":GOSUB 64698
64730 IF IND90%=1 AND DIGC%=1 THEN MAN$="UN ":GOSUB 64698:GOTO 64736
64732 IF DIGC%=1 THEN MAN$="UNA ":GOSUB 64698:GOTO 64736
64734 MAN$=TA(DIGC%):GOSUB 64698
64736 IND60%=0
64738 RETURN
64740 'Tratamiento miles de millones.
64742 IF GUAJE%=0 THEN 64756
64744 IF GUAJE%=1 THEN 64752
64746 IF GUAJE%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64752
64748 DECENA%=GUAJE%:DIGA%=0:DIGB%=GUA1%:DIGC%=GUA2%
64750 GOSUB 64708 'Tratamiento comun
64752 MAN$="MIL ":GOSUB 64698 'Salida a literales
64754 IF MILLON%=0 THEN MAN$="MILLONES ":GOSUB 64698 'Salida a literales.
64756 RETURN
64758 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
