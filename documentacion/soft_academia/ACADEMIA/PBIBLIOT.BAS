10 '                       PBIBLIOT.BAS
20 '                      --------------
30 '          GESTION FICHERO BIBLIOTECA DE ACADEMIAS
40 '          ---------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 EMPRESA$="*** ESCUELA DE IDIOMAS ***
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,D,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
306 '
308 '
310 GOSUB 8000
312 COLOR 11
314 LOCATE 5,21:PRINT "- MENU GENERAL DE GESTION DE LIBROS -"
316 LOCATE 6,21:PRINT STRING$(39,205)
318 LOCATE 8,9:PRINT " - GESTION DEL FICHERO -"
320 COLOR 10:LOCATE 8,41: PRINT CHR$(186):COLOR 11
322 LOCATE 8,49: PRINT "- L I S T A D O S -"
324 LOCATE 9,9:PRINT STRING$(25,205)
326 COLOR 10:LOCATE 9,41: PRINT CHR$(186)
328 COLOR 11:LOCATE 9,49:PRINT STRING$(23,205)
330 COLOR 10:LOCATE 10,9:PRINT "Altas de Libros."
332 LOCATE 10,41:PRINT CHR$(186)
334 LOCATE 10,49:PRINT "Listado num‚rico de Libros."
336 LOCATE 11,9:PRINT "Bajas de Libros."
338 LOCATE 11,41:PRINT CHR$(186)
340 LOCATE 11,49:PRINT "Listado alfab‚tico de Libros."
342 LOCATE 12,9:PRINT "Modificaci¢n de Libros."
344 LOCATE 12,41:PRINT CHR$(186)
346 LOCATE 12,49:PRINT "Listado de Libros por Temas."
348 LOCATE 13,9:PRINT "Consulta num‚rica de Libros."
350 LOCATE 13,41:PRINT CHR$(186)
352 LOCATE 13,49:PRINT "Listar Libros a devolver HOY."
354 LOCATE 14,9:PRINT "Consulta alfab‚tica por Temas."
356 LOCATE 14,41:PRINT CHR$(186)
358 LOCATE 14,49:PRINT "     "
360 LOCATE 15,9:PRINT "Salida Libros de Biblioteca."
361 LOCATE 15,41:PRINT CHR$(186)
362 LOCATE 15,49:PRINT "   "
363 LOCATE 16,9:PRINT "Devolucci¢n Libros a Biblioteca."
364 LOCATE 16,41:PRINT CHR$(186)
365 LOCATE 16,49:PRINT "     "
366 LOCATE 17,9:PRINT "¨Quien ha tenido el Libro?"
367 LOCATE 17,41:PRINT CHR$(186)
368 LOCATE 17,49:PRINT "          "
369 LOCATE 18,9:PRINT "    "
370 LOCATE 18,41:PRINT CHR$(186)
371 LOCATE 18,49:PRINT "          "
376 LOCATE 20,21:COLOR 12:PRINT " ESC "+CHR$(205)+CHR$(205)+">":COLOR 15:LOCATE 20,29:PRINT " Fin de tratamiento de Biblioteca":COLOR 10
378 GOSUB 8500
380 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulse la Tecla ";
382 COLOR 15
384 PRINT "<"+CHR$(196)+CHR$(217)
386 COLOR 10
388 '
390 FILA%=10:COLUMNA%=5:OPCIONES%=8
392 GOSUB 20000
394 IF ATRAS%=1 THEN 390
396 IF I.A%>OPCIONES% THEN 402
398 ON I.A% GOTO 510,2490,2490,2490,7402,7340,7350,7360
400 '
402 FILA%=10:COLUMNA%=45:OPCIONES%=4
404 GOSUB 20000
406 IF ATRAS%=1 THEN 390
408 IF I.A%>OPCIONES% THEN 414
410 ON I.A% GOTO 7300,7310,7330,7380
412 '
414 '
416 '
418 GOTO 390
510 '
520 FALUDIR$="FBIBLIOT.ALF":FALUDAT$="FBIBLIOT.DAT"
600 GOSUB 30052
610 '
612 TIT$="- A L T A S   D E   L I B R O S -"
614 GOSUB 8000:GOSUB 31000
618 '
619 GOSUB 7650
630 '
632       '*******************************
634       '***ENTRADA NUMERO DE ALUMNO ***
636       '*******************************
638       COLOR 15
640       LOCATE 1,1:GET #2,1:N.ORDEN%=ULTIMO%+1
642       N$=STR$(N.ORDEN%):N$=LEFT$(CERO$,6-LEN(N$))+RIGHT$(N$,LEN(N$)-1)
643       GET #2,N.ORDEN%
644       CUENTA$=N$
656       '
658       '
661       GUAPRI#=N.ORDEN%:GOSUB 64770:LOCATE 8,24-L.C%:PRINT GUAPRI$
718       '
720       '********************
722       '***ENTRADA TEMA  ***
724       '********************
726       F.C1%=8:C.C1%=71:L.E%=3:BO%=3:GOSUB 64000
728       IF I.T%=0 THEN TEMA$=D.E$:SM%=1:GOTO 742
729       IF I.T%=5 THEN 726
730       IF I.T%=1 AND I.A%=1 THEN TEMA$="":GOTO 742
732       IF I.T%=1 AND I.A%=3 THEN TEMA$=CTEMAS:GOTO 742
733       IF I.T%=3 AND I.A%=1 THEN LOCATE 8,71:PRINT SPC(3):GOTO 726
734       IF I.T%=3 THEN 726
736       IF I.T%=4 THEN 4000               'Fin Entrada.
738       IF I.T%=2 AND I.A%=1 THEN 742
740       IF I.T%=2 THEN 726
742       IF LEN(TEMA$)<3 THEN TEMA$=TEMA$+LEFT$(BLANCO$,3-LEN(TEMA$))
744       LOCATE 8,71:PRINT TEMA$
746       '
748       '***********************
750       '***ENTRADA DEL TITULO**
752       '***********************
754       F.C1%=10:C.C1%=19:L.E%=40:BO%=40:GOSUB 64000
756       IF I.T%=0 THEN TITULO$=D.E$:SM%=1:GOTO 770
757       IF I.T%=5 THEN 754
758       IF I.T%=1 AND I.A%=1 THEN 754
760       IF I.T%=1 AND I.A%=3 THEN TITULO$=CNOMBRE:GOTO 770
761       IF I.T%=3 AND I.A%=1 THEN LOCATE 8,71:PRINT SPC(3):GOTO 726
762       IF I.T%=3 THEN 726
764       IF I.T%=4 THEN 4000               'Fin Entrada.
766       IF I.T%=2 AND I.A%=1 THEN 770
768       IF I.T%=2 THEN 750
770       '
771       IF LEN(TITULO$)<40 THEN TITULO$=TITULO$+LEFT$(BLANCO$,40-LEN(TITULO$))
772       LOCATE 10,19:PRINT TITULO$
774       '
776       '***********************
778       '***ENTRADA AUTOR    ***
780       '***********************
782       F.C1%=12:C.C1%=19:L.E%=40:BO%=40:GOSUB 64000
784       IF I.T%=0 THEN AUTOR$=D.E$:GOTO 798
785       IF I.T%=5 THEN 782
786       IF I.T%=1 AND I.A%=1 THEN AUTOR$=" ":GOTO 798
788       IF I.T%=1 AND I.A%=3 THEN AUTOR$=CAUTOR:GOTO 798
789       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,19:PRINT SPC(40):GOTO 750
790       IF I.T%=3 THEN 750
792       IF I.T%=4 THEN 4000               'Fin Entrada.
794       IF I.T%=2 AND I.A%=1 THEN 798
796       IF I.T%=2 THEN 778
798       '
800       LOCATE 12,19:PRINT AUTOR$
802       '
804       '***********************
806       '***ENTRADA EDITOR   ***
808       '***********************
810       F.C1%=14:C.C1%=19:L.E%=40:BO%=40:GOSUB 64000
812       IF I.T%=0 THEN EDITOR$=D.E$:GOTO 828
813       IF I.T%=5 THEN 810
814       IF I.T%=1 AND I.A%=1 THEN EDITOR$="":GOTO 828
816       IF I.T%=1 AND I.A%=3 THEN EDITOR$=CEDITOR:GOTO 828
817       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,19:PRINT SPC(40):GOTO 778
818       IF I.T%=3 THEN 778
820       IF I.T%=4 THEN 4000               'Fin Entrada.
822       IF I.T%=2 AND I.A%=1 THEN 828
824       IF I.T%=2 THEN 806
826       '
828       LOCATE 14,19:PRINT EDITOR$
830       '
832       '***********************
834       '***ENTRADA OBSERVAC.***
836       '***********************
838       F.C1%=16:C.C1%=19:L.E%=40:BO%=40:GOSUB 64000
840       IF I.T%=0 THEN OBSER$=D.E$:GOTO 856
841       IF I.T%=5 THEN 838
842       IF I.T%=1 AND I.A%=1 THEN OBSER$="":GOTO 856
844       IF I.T%=1 AND I.A%=3 THEN OBSER$=COBSER:GOTO 856
845       IF I.T%=3 AND I.A%=1 THEN LOCATE 14,19:PRINT SPC(40):GOTO 806
846       IF I.T%=3 THEN 806
848       IF I.T%=4 THEN 4000               'Fin Entrada.
850       IF I.T%=2 AND I.A%=1 THEN 856
852       IF I.T%=2 THEN 834
854       '
856       LOCATE 16,19:PRINT OBSER$
858       '
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=14 AND I.A%=1 THEN 610
2028   IF ASC(A$)=14 AND I.A%=3 THEN 2545
2042   '
2043   LSET CCUENTA=CUENTA$:LSET CNOMBRE=TITULO$:LSET CAUTOR=AUTOR$
2044   LSET CEDITOR=EDITOR$:LSET CTEMAS=TEMA$:LSET COBSER=OBSER$
2048   LSET CMARCA="A"
2049   LSET CSTATUS="O"
2050   LSET CFEAL=FEDIA$:LSET CFILL="":
2052   IF I.A%=3 THEN 2100   'REGRABAR EN CASO DE MODIFICACION.
2053   LSET CVECES="0000":LSET CSITUA="0":PUT #2,N.ORDEN%:GET #2,1:LSET CULTI=MKI$(N.ORDEN%):LSET CSWA="1":PUT #2,1
2054   'GRABAMOS DIR.
2055   ULTIAL%=ULTIAL%+1
2056   GET #1,ULTIAL%
2058   LSET FULTI=MKI$(N.ORDEN%)
2059   LSET FTEMA=TEMA$:LSET FLLAMA=TITULO$
2060   LSET FILER="  "
2062   LSET FSTA="O"
2064   PUT #1,ULTIAL%
2066   'REGRABAMOS 1 DIR.
2068   GET #1,1
2070   LSET FULTI=MKI$(ULTIAL%)
2080   PUT #1,1
2092   CLOSE:GOTO 600
2094   '
2096   '
2098   '
2100   'REGRABAR EN CASO DE MODIFICACION.
2102   LSET CMARCA="M":PUT #2,N.L%
2104   IF SM%=0 THEN 2420
2108   SM%=0
2110   GET #2,1:LSET CSWA="1":PUT #2,1
2120  FOR NUM%=2 TO ULTIAL%
2130  GET #1,NUM%
2140  IF CVI(FULTI)<>N.L% THEN 2200
2150   LSET FLLAMA=TITULO$:LSET FTEMA=TEMA$
2160   PUT #1,NUM%
2170   GOTO 2420
2200   NEXT NUM%
2260   '
2420   '
2430   CLOSE:GOTO 2540
2440 '
2450 '
2460 '
2488 '
2490 '**********************************************
2495 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
2500 '**********************************************
2505 '
2510 IF I.A%=2 THEN TIT$="- B A J A S   D E   L I B R O S -"
2515 IF I.A%=4 THEN TIT$="- CONSULTA NUMERICA DE LIBROS -"
2520 IF I.A%=3 THEN TIT$="- MODIFICACION DE LIBROS -"
2525 '
2530 GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
2535 FALUDIR$="FBIBLIOT.ALF":FALUDAT$="FBIBLIOT.DAT"
2540 GOSUB 30052
2545 GOSUB 8000:GOSUB 31000
2550 IF I.A%=2 THEN GOSUB 7405
2555 IF I.A%=3 THEN GOSUB 7500
2560 IF I.A%=4 THEN GOSUB 7800
2565       '*******************************
2570       '***ENTRADA NUMERO DE ALUMNO ***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=8:C.C1%=19:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=5 THEN 2585
2600       IF I.T%=1 AND I.A%=1 THEN 2585
2605       IF I.T%=1 THEN 2570
2610       IF I.T%=3 THEN 2570                 'Retroceder Campo.
2615       IF I.T%=4 THEN 4000               'Fin Entrada.
2620       IF I.T%=2 AND I.A%=1 THEN 2635
2625       IF I.T%=2 THEN 2570
2630       '
2635       '
2640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 8,24-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
2642       N.L%=VAL(CUENTA$)
2645       IF N.L%<2 OR N.L%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Libro no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2550
2650       GET #2,N.L%
2652       IF I.A%=4 THEN 2660
2655       IF CMARCA="L" THEN GOSUB 8500:MEN$="Libro dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2550
2657       IF CSITUA="1" THEN GOSUB 8500:MEN$="Este Libro est  en USO, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2550
2660       GOSUB 2700   'PRESENTAR DATOS EN PANTALLA.
2665       IF I.A%=2 THEN 2990
2670       IF I.A%=4 THEN 3240
2675       GOTO 3400
2700 '***********************************
2702 '***PRESENTAMOS DATOS EN PANTALLA***
2704 '***********************************
2728 COLOR 15
2730 LOCATE 8,71:PRINT CTEMAS
2760 LOCATE 10,19:PRINT CNOMBRE
2790 LOCATE 12,19:PRINT CAUTOR
2800 LOCATE 14,19:PRINT CEDITOR
2810 LOCATE 16,19:PRINT COBSER
2820 IF I.A%<>4 THEN RETURN
2840 LOCATE 18,19:PRINT CSITUA
2845 IF CSITUA="0" THEN LOCATE 18,22:PRINT "EN BIBLIOTECA":GOTO 2970
2850 LOCATE 18,22:PRINT "EN USO       "
2860 LOCATE 18,71:PRINT RIGHT$(CFEDE,2)"-"MID$(CFEDE,3,2)"-"LEFT$(CFEDE,2)
2867 LOCATE 20,19:PRINT CALUMNO
2870 LOCATE 20,71:PRINT CTELEFO
2970 IF CMARCA<>"L" THEN RETURN
2972 COLOR 28:LOCATE 8,27:PRINT "- ALUMNO DADO DE BAJA -":COLOR 15
2975 RETURN
2980 '*************************
2990 '***TRATAMIENTO DE BAJA***
3000 '*************************
3010 GOSUB 8500:COLOR 10
3020 LOCATE 22,15:PRINT "DAR DE BAJA ";
3030 COLOR 15:PRINT " ENTER ";
3040 COLOR 10:PRINT " NO DAR DE BAJA ";
3050 COLOR 15:PRINT " F1 "
3055 A$=INPUT$(1)
3057 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3055
3060 IF ASC(A$)=14 THEN 2545
3062 LSET CMARCA="L"
3064 LSET CFEBA=FEDIA$
3065 PUT #2,N.L%
3104 GOTO 2545
3200 '
3210 '
3220 '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3270 A$=INPUT$(1)      ':GOSUB 7650:LOCATE 8,56:PRINT SPC(20)
3280 GOTO 2545
3290 '
3300 '
3310 '
3400 '*****************************
3410 '***MODIFICACION DE ALUMNOS***
3420 '*****************************
3430 '
3440 GOTO 720     'PASAMOS POR EL ALTA
3450 '
3460 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
7300 RUN "PBIBLANU"  'LISTAR LIBROS ORDEN DE NUMERO
7310 RUN "PBIBLAAL"  'LISTAR LIBROS ORDEN ALFABETICO
7320 '
7330 RUN "PBIBLAGR"  'LISTAR LIBROS POR TEMAS
7340 RUN "PBIBSACA"  'SACAR LIBROS DE LA BIBLIOTECA
7350 RUN "PBIBDEVO"  'DEVOLVER LIBROS A LA BIBLIOTECA
7360 RUN "PBIBQUIE"  'QUIEN HA TENIDO EL LIBRO
7380 RUN "PBIBENUS"  'LISTAR LIBROS EN USO
7390 '
7402 RUN "PBIBCOAL"  'CONSULTA ALFABETICA POR TEMAS
7405 '***********************************************************
7410 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION BAJAS         ***
7420 '***********************************************************
7430 GOSUB 8500:LOCATE 22,14
7460 COLOR 10:PRINT " F7 =>";
7470 COLOR 15:PRINT " Fin de Bajas de Libros. "
7480 '
7490 RETURN
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7532 'COLOR 10:PRINT "F1 =>";
7534 'COLOR 15:PRINT " Ayuda ";
7540 COLOR 10:PRINT " ENTER =>";
7550 COLOR 15:PRINT " Saltar ";
7560 COLOR 10:PRINT " F7 =>";
7570 COLOR 15:PRINT " Fin Modif. ";
7580 COLOR 10:PRINT " F8 =>";
7590 COLOR 15:PRINT " Campo Atr s"
7600 '
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7672 'COLOR 10:PRINT " F1 =>";
7674 'COLOR 15:PRINT " Ayuda ";
7680 COLOR 10:PRINT " ENTER =>";
7690 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Altas ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 22,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Consultas de Libros. "
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : BIBLIOTECA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 SHELL "BIBFINA.BAT"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
20000 '********************************************
20010 '*** RATONCITO PARA SELECCIONAR OPCIONES  ***
20020 '********************************************
20030 '***                                      ***
20040 '*** FILA%=FILA DONDE APARECERA EL RATON  ***
20050 '***                                      ***
20060 '*** COLUMNA%=COLUMNA DONDE APARECERA EL  ***
20070 '***          RATON                       ***
20080 '***                                      ***
20090 '*** OPCIONES%=NUMERO DE OPCIONES         ***
20100 '***                                      ***
20110 '*** CON ESC SALE A LA SENTENCIA 9000     ***
20120 '***                                      ***
20130 '********************************************
20140 '
20150 R$=CHR$(0):ATRAS%=0
20160 FOR I.A%=1 TO OPCIONES%
20170 COLOR 15:LOCATE FILA%,COLUMNA%:PRINT CHR$(205)+CHR$(205)+">"
20180 COLOR 10:R$=INKEY$:IF R$="" THEN 20180
20190 IF ASC(R$)=27 THEN 9000
20200 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
20210 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
20220 IF R$="2" THEN R$=CHR$(31)
20230 IF R$="8" THEN R$=CHR$(30)
20240 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 20250  ELSE 20180
20250 IF ASC(R$)=13 THEN RETURN
20260 LOCATE FILA%,COLUMNA%:PRINT "   "
20270 IF ASC(R$)=30 AND I.A%=1 THEN ATRAS%=1:RETURN
20280 IF ASC(R$)=30 THEN FILA%=FILA%-1:I.A%=I.A%-1:GOTO 20170 'AL NEXT
20290 FILA%=FILA%+1
20300 NEXT I.A%
20310 RETURN
20320 'ESTO SE ACABA
20330 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,50
30054 FIELD #1,2 AS FULTI,3 AS FTEMA,40 AS FLLAMA,4 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30060 'LSET FULTI=MKI$(1):LSET FTEMA="":LSET FLLAMA=""
30061 'LSET FILER="":LSET FSTA="A"
30062 'PUT #1,1
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,40 AS CNOMBRE,40 AS CAUTOR,40 AS CEDITOR,3 AS CTEMAS,6 AS CFEAL,7 AS CTELEFO,40 AS CALUMNO,6 AS CFESA,6 AS CFEDE
30112 FIELD #2,194 AS TODO1$,1 AS CSITUA,6 AS CFEBA,4 AS CVECES,1 AS CMARCA,40 AS COBSER,24 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,1 AS CSWA,250 AS REST1$,9 AS REST2$
30115 GET #2,1
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1)
30124 'LSET CULTACT=MKI$(1):LSET CSWA="0":LSET REST1$="":LSET REST2$=""
30126 'PUT #2,1
30128 '
30138 '
30150 '
30500 OPEN "R",#4,"FBIBQUIE.DAT",70
30510 FIELD #4,5 AS DCUENTA,1 AS DSTATUS,40 AS DNOMBRE,7 AS DTELEFO,6 AS DDESDE,6 AS DHASTA,5 AS DLIBRE
30514 FIELD #4,5 AS DCUEN1,1 AS DSTAT,2 AS DULTI,62 AS DRESTO
30515 GET #4,1
30517  DULTI%=CVI(DULTI)
30522 'LSET DCUEN1="":LSET DSTAT="A":LSET DULTI=MKI$(1)
30524 'LSET DRESTO=""
30526 'PUT #4,1
30528 'CLOSE
30538 'STOP
30540 RETURN
30550 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 5,X%:PRINT TIT$
31042 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 8,6:PRINT "Libro N§. : <" SPACE$(6) ">"
31052 LOCATE 8,63:PRINT "Tema : <" SPACE$(3) ">"
31058 LOCATE 10,6:PRINT "T¡tulo .. : <" SPACE$(40) ">"
31064 LOCATE 12,6:PRINT "Autor ... : <" SPACE$(40) ">"
31092 LOCATE 14,6:PRINT "Editor .. : <"SPACE$(40) ">"
31123 LOCATE 16,6:PRINT "Observac. : <" SPACE$(40) ">"
31130 IF I.A%<>4 THEN 31260
31200 LOCATE 18,6:PRINT "Situaci¢n : <" SPACE$(1) ">"
31205 LOCATE 18,63:PRINT "Hasta: <" SPACE$(8) ">"
31230 LOCATE 20,6:PRINT "Poseedor  : <" SPACE$(40) ">"
31235 LOCATE 20,63:PRINT "Tfno.: <" SPACE$(7) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
38760 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
