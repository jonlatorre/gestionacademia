10 '                         PFACTURA.BAS
20 '                       ---------------
30 '               CONFECCION DE FACTURAS EN OFICIAL
31 '               ---------------------------------
32 '
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"/"+MID$(FDIA$,3,2)+"/"+LEFT$(FDIA$,2)
94 '
96 DEFSTR D,P,A,E,X,M,R,T,J
97 '
98 '
99 '
100 '
102 GOSUB 30502   'APERTURA FICHERO DE ALBARANES EN OFICIAL.
104 CLOSE:IF ULTPED!<>1 THEN 108    'CONTINUAR.
106 GOTO 9000      'FACTURAR EN BLANCO
107 '
108 OPTION BASE 1
110 DIM DIAS%(12),TPRO$(50),FECVTO$(3),IMPVTO#(3),BIMPO#(6)
120 DATA 31,28,31,30,31,30,31,31,30,31,30,31
130 FOR I%=1 TO 12
140   READ DIAS%(I%)
150 NEXT I%
160 '----CARGAMOS LAS TABLAS DE PROVINCIAS------
170 DATA "ALAVA","ALBACETE","ALICANTE","ALMERIA","AVILA","BADAJOZ","BALEARES","BARCELONA","BURGOS","CACERES","CADIZ","CASTELLON","CIUDAD REAL","CORDOBA","LA CORU¥A","CUENCA","GERONA","GRANADA","GUADALAJARA","GUIPUZCOA","HUELVA"
180 DATA "HUESCA","JAEN","LEON","LERIDA","RIOJA","LUGO","MADRID","MALAGA","MURCIA","NAVARRA","ORENSE","ASTURIAS","PALENCIA","LAS PALMAS","PONTEVEDRA","SALAMANCA","STA. CRUZ TENERIFE","CANTABRIA","SEGOVIA","SEVILLA","SORIA","TARRAGONA"
190 DATA "TERUEL","TOLEDO","VALENCIA","VALLADOLID","VIZCAYA","ZAMORA","ZARAGOZA"
200 FOR I%=1 TO 50
210    READ TPRO$(I%)
220 NEXT I%
230 '
240 '
250 '
252 '
254 '
256 '
258 '
260 '
478 'APERTURA DE IMPRESORA.
480  WIDTH "LPT1:",145:OPEN "LPT1:" FOR OUTPUT AS #9
482  LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s10h0B";
484 '
485 FACTURA#=1111:RECIBO#=2222:DESCLI$="4300001":HASCLI$="4300001":FECFAC$="960308":WFECVAL$=FECFAC$
486 GOTO 1000
490 '**************************************
500 '***PRESENTAMOS EL MENU DEL PROGRAMA***
510 '**************************************
520 TIT$="- FACTURACION OFICIAL -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 5,X%:PRINT TIT$
560 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
570 COLOR 11:LOCATE 8,22:PRINT "NRO. DE FACTURA INICIAL : <       >"
580 LOCATE 10,22:PRINT "NRO. DE RECIBO INICIAL  : <       >"
590 LOCATE 13,7:PRINT "FACTURAR DESDE EL CLIENTE : <43     > HASTA EL CLIENTE : <43     >"
600 LOCATE 15,26:PRINT "FECHA DE FACTURA : <DDMMAA>"
610 LOCATE 18,32:COLOR 12:PRINT " ESC ";:COLOR 10
620 PRINT " Finalizar."
622 GOSUB 8500
624 LOCATE 22,20:COLOR 15:PRINT " F8 ";:COLOR 10
626 PRINT " RETRO":COLOR 15
630 '
640 '
650 '
660 '*************************************
662 '***PEDIMOS NRO. DE FACTURA INICIAL***
664 '*************************************
666 F.C1%=8:C.C1%=49:L.E%=6:L.D%=0:NEG%=0:BO%=7:GOSUB 63000
668 IF I.T%=0 THEN FACTURA#=VAL(D.E$):GOTO 676
670 IF I.T%=3 THEN 666
672 IF I.T%=4 THEN 9000
674 GOTO 666
676 GUAPRI#=FACTURA#:GOSUB 44444:GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
678 X%=56-LEN(GUAPRI$):LOCATE 8,49:PRINT SPC(7):LOCATE 8,X%:PRINT GUAPRI$
680 '
682 '************************************
684 '***PEDIMOS NRO. DE RECIBO INICIAL***
686 '************************************
688 F.C1%=10:C.C1%=49:L.E%=6:L.D%=0:NEG%=0:BO%=7:GOSUB 63000
690 IF I.T%=0 THEN RECIBO#=VAL(D.E$):GOTO 698
692 IF I.T%=3 THEN 662
694 IF I.T%=4 THEN 9000
696 GOTO 688
698 GUAPRI#=RECIBO#:GOSUB 44444:GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
700 X%=56-LEN(GUAPRI$):LOCATE 10,49:PRINT SPC(7):LOCATE 10,X%:PRINT GUAPRI$
702 '
704 '***************************
706 '***PEDIMOS DESDE CLIENTE***
708 '***************************
710 F.C1%=13:C.C1%=38:L.E%=5:L.D%=0:NEG%=0:BO%=5:GOSUB 63000
712 IF I.T%=0 THEN DESCLI$=D.E$:GOTO 720
714 IF I.T%=3 THEN 684
716 IF I.T%=4 THEN 9000
717 IF I.T%=1 THEN DESCLI$="4300001":HASCLI$="4399999":LOCATE 13,36:PRINT DESCLI$:LOCATE 13,65:PRINT HASCLI$:GOTO 746     'PEDIR FECHA FACTURA.
718 GOTO 710
720 DESCLI$="43"+DESCLI$:LOCATE 13,36:PRINT DESCLI$
722 '
724 '***************************
726 '***PEDIMOS HASTA CLIENTE***
728 '***************************
730 F.C1%=13:C.C1%=67:L.E%=5:L.D%=0:NEG%=0:BO%=5:GOSUB 63000
732 IF I.T%=0 THEN HASCLI$=D.E$:GOTO 740
734 IF I.T%=3 THEN 706
736 IF I.T%=4 THEN 9000
738 GOTO 730
740 HASCLI$="43"+HASCLI$:LOCATE 13,65:PRINT HASCLI$
742 '
744 '***************************
746 '***PEDIMOS FECHA FACTURA***
748 '***************************
750 F.C1%=15:C.C1%=46:L.E%=6:L.D%=0:NEG%=0:BO%=6:GOSUB 63000
752 IF I.T%=0 THEN FECFAC$=D.E$:GOTO 760
754 IF I.T%=3 THEN 726
756 IF I.T%=4 THEN 9000
758 GOTO 750
760 FECSUB$=FECFAC$:GOSUB 40000
762 IF ERF%=1 THEN PLAY "A":GOTO 750
764 FECFAC$=FAMD$
766 WFECVAL$=FECFAC$
768 '
990 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1":GOSUB 8500
992 A$=INPUT$(1)
994 IF ASC(A$)<13 OR ASC(A$)>14 THEN 992
996 IF ASC(A$)=14 THEN GOSUB 8500:GOTO 570
998 '
1000                     '******************************
1010                     '***TRATAMIENTO DEL PROGRAMA***
1020                     '******************************
1030 GOSUB 8500
1040 GOSUB 30002   'APERTURA FICHERO DE PLAN CONTABLE.
1050 '
1060 GOSUB 30502   'APERTURA FICHERO DE ALBARANES CLASIFICADO OFICIAL.
1070 GOSUB 30702   'APERTURA FICHERO RESUMEN DE FACTURACION.
1080 GOSUB 30602   'APERTURA FICHERO MOVIMIENTOS CONTABLES FACTURACION.
1082 GOSUB 31000   'APERTURA DE PRODUCTOS
1084 GOSUB 31130   'APERTURA DE RUTAS
1086 '
1088 '
1090 '
1100 '
1110 '
1120 GOSUB 8500:MEN$="COLOQUE PAPEL DE FACTURAS OFICIAL Y PULSE UNA TECLA":GOSUB 8500
1130 A$=INPUT$(1)
1140 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
1150 REG!=1     'PARA LECTURA DE ALBARANES
1160 FF$=""
1170 LIN%=90
1180 PAG%=0
1190 GVEN#=0         'TOTAL GENERAL VENTAS
1200 GPOR#=0         'TOTAL GENERAL PORTES
1202 '''''REGRES%=1       'FICHERO REGISTRO RESUMEN.
1204 '''''REGCON!=1       'REGISTRO MOVIMIENTO CONTABLE
1206 REGCOM!=1       'PARA LECTURA REGISTRO DE COMENTARIOS.
1210 LUIS%=0         'PARA QUE SE PARE CUANDO HAGA LA PRIMERA FACTURA
1220 '
1230 GOSUB 3010      'LECTURA SECUENCIAL DE ALBARANES.
1240 '
1250 WHILE FF$<>"FF"
1252   WSN$=SN$
1260   WCLI$=XCLRU:PAG%=0
1270   BUSCA$=XCLI:GOSUB 30084  'LECTURA AZAR PLAN CONTABLE.
1280   IF SW1%=1 THEN 1462      'NO EXISTE EL CLIENTE.
1290   GET #2,R.B%
1300   NOMCLI$=PNOMBRE
1302   CONTACTO$=PCONTACTO
1310   DIRCLI$=PDIREC
1320   CPCLI$=PCPOS
1330   PLACLI$=PPOBLA
1340   DIA1P$=PDIA1
1350   DIA2P$=PDIA2
1360   DIA3P$=PDIA3
1370   FPAGO$=PFPOFI      'OJO FORMA DE PAGO OFICIAL.
1380   DPP!=CVS(PDCTOFI)  'OJO DESCUENTO EN OFICIAL.
1390   DNI$=PNIF
1400   PROVE$=PPROVE
1410   ACEPTA$=PACEPTA    'OJO SOLO EN OFICIAL.
1412   AGRUPA$=PAGRUPA
1420   GFI!=CVS(PGFI)
1430   IVA$=PIVA
1440   REQ$=PRECARGO
1452   WVENDE$=PR1
1460   GOTO 1650     'PONER ACUMULADORES A CERO.
1462   'NO EXISTE EL CLIENTE.
1480   NOMCLI$="­NO ESTA ABIERTO!"
1482   CONTACTO$=""
1490   DIRCLI$=" "
1500   CPCLI$="00000"
1510   PLACLI$=" "
1520   DIA1P$=" "
1530   DIA2P$=" "
1540   DIA3P$=" "
1550   FPAGO$=" "         'OJO FORMA DE PAGO OFICIAL.
1560   DPP!=0             'OJO DESCUENTO EN OFICIAL.
1570   DNI$=" "
1580   PROVE$=" "
1590   ACEPTA$="0"        'OJO SOLO EN OFICIAL.
1600   GFI!=0
1610   IVA$="0":I.IVA1%=6:I.IVA2%=0
1620   REQ$="  "
1622   WVENDE$=STRING$(7," "):WCOMIS!=0
1630   AGRUPA$="N"
1640   '
1650   'PONER ACUMULADORES A CERO.
1660   TOTIMP#=0              'TOTAL SUMA DE IMPORTES.
1662   VENTA#=0               'TOTAL VENTA+GFI-DPP+PORTES
1670   TOTAUT#=0              'TOTAL AUTOPISTA
1680   TOTDPP#=0              'TOTAL DESCUENTO PRONTO PAGO
1690   TOTGFI#=0              'TOTAL GASTOS FINANCIACION.
1725   BIMPO#=0               'BASE IMPONIBLE
1730   PESOS#=0               'TONELADAS
1735   CANTI#=0               'TONELADAS
1770   '
1780  WHILE FF$<>"FF" AND WCLI$=XCLRU
1782   RUT$=XRUT
1784   WHILE FF$<>"FF" AND WCLI$=XCLRU AND RUT$=XRUT
1786     PRODU$=XNPRO
1788     WHILE FF$<>"FF" AND WCLI$=XCLRU AND RUT$=XRUT AND PRODU$=XNPRO
1840     'ACUMULAMOS EN LOS DIFERENTES ACUMULADORES.
1850     IMPLIN#=CVD(XPRECI)
1852     CANTI#=CANTI#+IMPLIN#
1855     PESOS#=PESOS#+CVD(XCANTI)
1860     VALDTO#=IMPLIN#*(DCTO!/100)
1862     VALDTO#=INT(VALDTO#+.5)
1864     TOTAUT#=TOTAUT#+CVD(XAUTOP)
1870     IMPLIN#=IMPLIN#-VALDTO#:VENTA#=IMPLIN#:GVEN#=GVEN#+IMPLIN#
1872     TOTIMP#=TOTIMP#+IMPLIN#+CVD(XAUTOP)
1880     '
1890     'DESCUENTO PRONTO PAGO.
1900     VALDPP#=VENTA#*(DPP!/100)
1902     VALDPP#=INT(VALDPP#+.5)
1910     TOTDPP#=TOTDPP#+VALDPP#
1920     'VENTA#=VENTA#-VALDPP#:GVEN#=GVEN#-VALDPP#
1925     GVEN#=GVEN#-VALDPP#
2000     '
2010     'CONTINUAR.
2020     '
2025    BIMPO#=BIMPO#+VENTA#
2040     '
2050     '
2070     IF AGRUPA$="N" THEN GOSUB 3320    'LINEA DE DETALLE.
2080     '
2090     '
2100     GOSUB 3010    'LECTURA SECUENCIAL FICHERO DE ALBARANES.
2110     '
2112    WEND
2114    'ROTURA POR PRODUCTO
2116    IF LIN%>55 THEN GOSUB 3510
2118    GET #3,VAL(PRODU$)
2120    JESUS$=RUT$:GOSUB 31230
2122    LPRINT TAB(11) USING "TRANSPORTE DE ######,### T.M. DE ";PESOS#;:LPRINT ANOAR
2124    LPRINT TAB(11) "DE " RNOAR;
2126    LPRINT USING "A #,### PTS. T.M.";CVD(RTONE);
2128    GUAPRI#=INT(CANTI#/1000):GOSUB 44444
2130    LPRINT TAB(76-LEN(GUAPRI$)) GUAPRI$
2132    PESOS#=0:CANTI#=0:LIN%=LIN%+1
2134    IF TOTAUT#=0 THEN 2144
2136    LPRINT TAB(11) "POR IMPORTES DE AUTOPISTA TRANSPORTE ANTERIOR ";
2138    GUAPRI#=TOTAUT#:GOSUB 44444
2140    LPRINT TAB(76-LEN(GUAPRI$)) GUAPRI$
2142    TOTAUT#=0:LIN%=LIN%+1
2144    LPRINT
2146    LIN%=LIN%+1
2150   WEND
2152   'ROTURA POR RUTA
2154   '
2156   '
2170  WEND
2172   'ROTURA POR CLIENTE.
2174   FF$=WFF$
2176   '
2180   GOSUB 7000    'DAR LINEA DE TOTALES.
2190   GOSUB 6002    'ESCRIBIR REGISTRO CONTABLE CLIENTE.
2200   GOSUB 6102    'ESCRIBIR REGISTRO CONTABLE I.V.A.
2210   GOSUB 6502    'ESCRIBIR REGISTRO RESUMEN DE FACTURACION.
2220   '
2230   FACTURA#=FACTURA#+1
2240   IF PAG%<>0 THEN LIN%=85
2250   '
2260 WEND
2270 'ROTURA POR FIN DE FICHERO.
2280 '
2290 GOSUB 6302   'ESCRIBIR REGISTRO CONTABLE PORTES
2300 GOSUB 6202   'ESCRIBIR REGISTRO CONTABLE VENTAS
2310 '
2320 IF PAG%<>0 THEN LPRINT CHR$(12)
2330 '
2340 '
2350 '
2360 CLOSE
2370 CLEAR
2380 RUN "PFACTURA"
2390 '
2400 END
2410 '
3000 '**********************************
3010 '***LECTURA FICHERO DE ALBARANES***
3020 '**********************************
3030 REG!=REG!+1
3040 IF REG!>ULTPED! THEN FF$="FF":RETURN
3050 GET #6,REG!
3060 IF XSTATUS<>"O" THEN 3030
3062 IF XCLI="4399999" THEN 3030
3070 'PRECIO#=CVD(XPRE)
3080 'DCTO!=CVS(XDCTO)
3090 'CANTI#=CVD(XCANTI)
3100 '
3260 IF XCLI>=DESCLI$ AND XCLI<=HASCLI$ THEN SN$="S":RETURN
3270 SN$="N"
3280 '
3290 RETURN
3300 '
3310 '*************************************
3320 '***L I N E A   D E   D E T A L L E***
3330 '*************************************
3340 IF LIN%>55 THEN GOSUB 3510   'HACER CABECERAS.
3350 'IF CANTI#<0 THEN LPRINT TAB(2) "ABONO";:GOTO 3356
3352 IF XARTI="99999999" THEN 3356
3354 LPRINT TAB(3) XARTI;
3355 LPRINT TAB(13) XRARO;
3356 LPRINT TAB(20) XDESCRI;
3358 IF CANTI#=0 THEN 3372
3360 GUAPRI#=CANTI#:GOSUB 44444
3370 X%=59-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$;
3372 IF PRECIO#=0 THEN 3376
3374 LPRINT TAB(61):LPRINT USING "######.##";PRECIO#;
3376 'IF DCTO!=0 THEN 3380
3378 'LPRINT TAB(61):LPRINT USING "##.##";DCTO!;
3380 '
3390 '
3400 IF IMPLIN#=0 THEN LPRINT:GOTO 3430
3410 GUAPRI#=IMPLIN#:GOSUB 44444
3420 X%=81-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$
3430 LIN%=LIN%+1
3440 '
3450 RETURN
3460 '
3470 '
3500 '*****************************************
3510 '***C A B E C E R A S   P R I M E R A S***
3520 '*****************************************
3530 IF LIN%<>90 THEN LPRINT CHR$(12)
3532 LUIS%=LUIS%+1
3534 IF LUIS%<>2 THEN 3540
3536 'PLAY "AB":GOSUB 8500:MEN$="PULSE UNA TECLA PARA SEGUIR LISTANDO":GOSUB 8500
3537 GOSUB 8500:MEN$="PULSE UNA TECLA PARA SEGUIR LISTANDO":GOSUB 8500
3538 A$=INPUT$(1)
3540 '
3550 FOR I%=1 TO 8
3560   LPRINT
3570 NEXT I%
3580 '
3590 LPRINT TAB(41) NOMCLI$
3592 '
3600 LPRINT TAB(41) DIRCLI$
3610 LPRINT TAB(41) CPCLI$ " ";
3620 LPRINT PLACLI$
3630 CP%=VAL(LEFT$(CPCLI$,2))
3640 IF CP%<1 OR CP%>50 THEN LPRINT:GOTO 3652
3650 LPRINT TAB(41) TPRO$(CP%)
3652 FOR I%=1 TO 6
3654   LPRINT
3656 NEXT I%
3658 'IF VAL(PROVE$)=0 THEN 3662
3660 'LPRINT TAB(35) "NRO. PROV. : " PROVE$;
3662 PAG%=PAG%+1
3664 'IF PAG%>1 THEN LPRINT TAB(64):LPRINT USING "PAG.:##";PAG% ELSE LPRINT
3666 'FOR I%=1 TO 4
3668 '  LPRINT
3670 'NEXT I%
3672 GUAPRI#=FACTURA#:GOSUB 44444
3674 X%=26-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$;
3676 LPRINT TAB(30) RIGHT$(FECFAC$,2) "-" MID$(FECFAC$,3,2) "-" LEFT$(FECFAC$,2);
3678  LPRINT TAB(45) DNI$;
3680 'LPRINT TAB(53) WCLI$;
3682 'LPRINT TAB(37) "INDICADA ABAJO";
3684 LPRINT TAB(68):LPRINT USING "HOJA ###";PAG%
3690 '
3700 '
3710 FOR I%=1 TO 4
3720   LPRINT
3730 NEXT I%
3732 IF AGRUPA$="S" THEN 3750
3734 'IF WCLI$="4300001" THEN 3750
3736 'LPRINT TAB(21) "PEDIDO N§ " XRPED
3738 'LPRINT
3740 'LIN%=24:RETURN
3750 LIN%=24
3760 '
3780 RETURN
3790 '
6000 '*******************************
6002 '***REGISTRO CONTABLE CLIENTE***
6004 '*******************************
6005 IF TOTFAC#=0 THEN RETURN
6006 FECASI$=FECFAC$
6008 CUENTA$=WCLI$
6010 CONTRA$=STRING$(7," ")
6012 IF ACEPTA$="1" THEN CONC0$="92" ELSE CONC0$="02"
6014 IF TOTFAC#<0 THEN CONC1$="ABONO":DOH$="H":TOTFAC#=TOTFAC#*(-1):CONC0$="52":GOTO 6018
6016 CONC1$="N/FRA.":DOH$="D"
6018 CONC$=CONC0$+CONC1$+STR$(FACTURA#)
6020 IMPORTE#=TOTFAC#
6022 NVTO$=STR$(N.V%):NVTO$=RIGHT$(NVTO$,1)
6024 FECVTO$=FECVTO$(1)
6026 PAGO1$="S":PAGO2$="S":PAGO3$="S"
6027 BIMP#=0
6028 GOSUB 6402   'ESCRIBIR REGISTRO.
6030 '
6032 RETURN
6034 '
6040 '
6050 '
6060 '
6070 '
6100 '***************************
6102 '***REGISTRO CONTABLE IVA***
6104 '***************************
6105 '
6106 FECASI$=FECFAC$
6108 CUENTA$="4780000"
6110 CONTRA$=STRING$(7," ")
6112 CONC0$="02":TOTIVA#=IVA#+REQ#
6114 IF TOTIVA#<0 THEN DOH$="D":TOTIVA#=TOTIVA#*(-1):CONC0$="52":GOTO 6118
6116 DOH$="H"
6118 CONC$=CONC0$+WCLI$+"/"+IVA$+"/"+REQ$+"/"+STR$(FACTURA#)
6120 IMPORTE#=TOTIVA#
6122 NVTO$=" "
6124 FECVTO$=STRING$(6," ")
6126 PAGO1$="S":PAGO2$="S":PAGO3$="S"
6127 BIMP#=BASIMP#
6128 GOSUB 6402   'ESCRIBIR REGISTRO.
6130 '
6132 RETURN
6134 '
6136 '
6138 '
6140 '
6142 '
6200 '******************************
6202 '***REGISTRO CONTABLE VENTAS***
6204 '******************************
6205 IF GVEN#=0 THEN RETURN
6206 '
6207 FECASI$=FECFAC$
6208 CUENTA$="7000000"
6210 CONTRA$=STRING$(7," ")
6212 CONC0$="02"
6214 IF GVEN#<0 THEN DOH$="D":GVEN#=GVEN#*(-1):CONC0$="52":GOTO 6218
6216 DOH$="H"
6218 CONC$=CONC0$+"VENTAS"
6220 IMPORTE#=GVEN#
6222 NVTO$=" "
6224 FECVTO$=STRING$(6," ")
6226 PAGO1$="S":PAGO2$="S":PAGO3$="S"
6227 BIMP#=0
6228 GOSUB 6402   'ESCRIBIR REGISTRO.
6230 '
6232 RETURN
6234 '
6236 '
6238 '
6240 '
6242 '
6300 '******************************
6302 '***REGISTRO CONTABLE PORTES***
6304 '******************************
6305 IF GPOR#=0 THEN RETURN
6306 FECASI$=FECFAC$
6308 CUENTA$="6500000"
6310 CONTRA$=STRING$(7," ")
6312 CONC0$="02"
6314 IF GPOR#<0 THEN DOH$="D":GPOR#=GPOR#*(-1):CONC0$="52":GOTO 6318
6316 DOH$="H"
6318 CONC$=CONC0$+"PORTES"
6320 IMPORTE#=GPOR#
6322 NVTO$=" "
6324 FECVTO$=STRING$(6," ")
6326 PAGO1$="S":PAGO2$="S":PAGO3$="S"
6327 BIMP#=0
6328 GOSUB 6402   'ESCRIBIR REGISTRO.
6330 '
6332 RETURN
6334 '
6336 '
6338 '
6340 '
6342 '
6400 '********************************
6402 '***ESCRIBIR REGISTRO CONTABLE***
6404 '********************************
6406 '
6408 REGCON!=REGCON!+1
6410 IF REGCON!>99999! THEN GOSUB 8500:MEN$="FICHERO DE CONTABILIDAD LLENO, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
6412 GET #5,REGCON!
6414 NORDEN$=STR$(REGCON!)
6416 NORDEN$=RIGHT$(NORDEN$,LEN(NORDEN$)-1)
6418 IF LEN(NORDEN$)<>5 THEN NORDEN$=LEFT$(CERO$,5-LEN(NORDEN$))+NORDEN$
6420 LSET MNORDEN=NORDEN$
6422 LSET MFECASI=FECASI$
6424 LSET MCUENTA=CUENTA$
6426 LSET MCONTRA=CONTRA$
6428 LSET MCONCEP=CONC$
6430 LSET MDEHA=DOH$
6432 LSET MIMPORTE=MKD$(IMPORTE#)
6434 LSET MNVTO=NVTO$
6436 LSET MFECVTO=FECVTO$
6438 LSET MPAGO1=PAGO1$
6440 LSET MPAGO2=PAGO2$
6442 LSET MPAGO3=PAGO3$
6443 LSET MBIMPO=MKD$(BIMP#)
6444 LSET MFI2=STRING$(17,0)
6446 LSET MSTATUS="O"
6448 PUT #5,REGCON!
6450 'REGRABAMOS REGISTRO 1
6452 GET #5,1
6456 LSET MULTIMO=MKS$(REGCON!)
6466 PUT #5,1
6468 '
6470 RETURN
6472 '
6500 '*******************************
6502 '***ESCRIBIR REGISTRO RESUMEN***
6504 '*******************************
6506 FOR I%=1 TO N.V%
6507   IF IMPVTO#(I%)=0 THEN 6610 'AL NEXT
6508   REGRES%=REGRES%+1
6510   GET #7,REGRES%
6512   LSET RORDEN=MKI$(REGRES%)
6514   LSET RNCLI=WCLI$
6516   NFAC$=STR$(FACTURA#)
6518   NFAC$=RIGHT$(NFAC$,LEN(NFAC$)-1)
6520   IF LEN(NFAC$)<>6 THEN NFAC$=LEFT$(CERO$,6-LEN(NFAC$))+NFAC$
6522   LSET RNFAC=NFAC$
6524   NREC$=STR$(RECIBO#):RECIBO#=RECIBO#+1
6526   NREC$=RIGHT$(NREC$,LEN(NREC$)-1)
6528   IF LEN(NREC$)<>6 THEN NREC$=LEFT$(CERO$,6-LEN(NREC$))+NREC$
6530   LSET RNREC=NREC$
6532   LSET RTFAC="O"    'OFICIAL
6534   LSET RFFAC=FECFAC$
6536   LSET RFVTO=FECVTO$(I%)
6538   IMPO$=STR$(IMPVTO#(I%))
6540   IMPO$=RIGHT$(IMPO$,LEN(IMPO$)-1)
6542   IF LEN(IMPO$)<>10 THEN IMPO$=LEFT$(CERO$,10-LEN(IMPO$))+IMPO$
6544   LSET RIMPO=IMPO$
6546   IF IMPVTO#(I%)<0 THEN COA$="A" ELSE COA$="C"
6548   LSET RCOA=COA$
6550   LSET RTEMPO="   "
6552   LSET RVENDE=RIGHT$(WVENDE$,2)
6554   LSET RCOMIS=MKS$(0)
6556   LSET RBASIMP=MKD$(BASIMP#)
6560   '
6570   LSET RFILL01=STRING$(37,0)
6580   LSET RSTATUS="O"
6590   PUT #7,REGRES%
6600   '
6610 NEXT I%
6620 'REGRABAR REGISTRO 1
6622 GET #7,1
6626 LSET RULTIMO=MKI$(REGRES%)
6642 PUT #7,1
6644 '
6646 RETURN
6648 '
7000 '*************************************
7005 '***L I N E A   D E   T O T A L E S***
7010 '*************************************
7012 IF TOTDPP#=0 THEN 7025
7015 BIMPO#=BIMPO#-TOTDPP#
7025 IVA#=BIMPO#*(VAL(IVA$)/100):IVA#=INT(IVA#+.5)
7030 REQ#=BIMPO#*(VAL(REQ$)/100):REQ#=INT(REQ#+.5):BASIMP#=BIMPO#(1)
7032 IF WSN$="S" THEN 7060    'CONTINUAR
7035 GOTO 7305   'TOTAL FACTURA
7040 IF BIMPO#(2)=0 THEN BASIMP#=BIMPO#(6):IVA#=0:REQ#=0:GOTO 7305  'TOT.FACTU.
7055 GOTO 7305   'TOTAL FACTURA
7060 'SALTO%=59-LIN%
7065 'FOR I%=1 TO SALTO%
7070 ' LPRINT
7075 'NEXT I%
7080 '
7100 GUAPRI#=INT(TOTIMP#/1000):GOSUB 44444
7102 LPRINT:LPRINT TAB(50) "SUMA PESETAS..";
7105 X%=76-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$
7110 '
7115 LPRINT:LIN%=LIN%+3
7117 IF TOTDPP#=0 THEN LPRINT:GOTO 7130
7120 '
7122 LPRINT TAB(50) USING "D. P. P.  ##";DPP!;
7124 GUAPRI#=INT(TOTDPP#/1000):GOSUB 44444
7125 '
7126 X%=76-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$:LPRINT:LIN%=LIN%+2
7130 IF TOTPOR#=0 THEN 7150
7135 GUAPRI#=INT(TOTPOR#/1000):GOSUB 44444
7137 LPRINT TAB(50) "TOTAL PORTES .";
7140 X%=76-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$:LPRINT:LIN%=LIN%+2
7145 '
7150 '
7195 'COLOCAR BASE IMPONIBLE
7200 '
7205 'GUAPRI#=BIMPO#:GOSUB 44444
7210 'X%=34-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$;
7215 '
7220 IF IVA#=0 THEN 7240
7225 LPRINT TAB(50) USING "I. V. A.  ##";VAL(IVA$);:LPRINT "%";
7230 GUAPRI#=INT(IVA#/1000):GOSUB 44444
7235 X%=76-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$:LPRINT:LIN%=LIN%+2
7240 '
7245 'IF REQ#=0 THEN 7265
7250 'LPRINT TAB(53) REQ$;
7255 'GUAPRI#=REQ#:GOSUB 44444
7260 'X%=66-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$;
7265 '
7300 '
7305 TOTFAC#=TOTIMP#-TOTDPP#+TOTPOR#+IVA#+REQ#
7310 IF WSN$="N" THEN 7335
7315 GUAPRI#=INT(TOTFAC#/1000):GOSUB 44444
7317 LPRINT TAB(50) "TOTAL FACTURA..";
7320 X%=76-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$:LIN%=LIN%+1
7325 '
7330 '
7335 SALTO%=59-LIN%
7337 FOR I%=1 TO SALTO%
7340  LPRINT
7342 NEXT I%
7345 '
7350 '*******************************************************************
7355 '***L I N E A    D E   F O R M A   D E   P A G O   Y   V C T O S.***
7360 '*******************************************************************
7365 IF FPAGO$="0" THEN LITVCTO$="CONTADO":N.V%=1:SUMA%=0:GOTO 7470
7370 IF FPAGO$="7" THEN LITVCTO$="REPOSICION":N.V%=1:SUMA%=0:GOTO 7470
7375 IF FPAGO$="1" AND ACEPTA$="1" THEN LITVCTO$="GIRO 30 DIAS ACEPTADO":N.V%=1:SUMA%=30:GOTO 7470
7380 IF FPAGO$="1" THEN LITVCTO$="GIRO 30 DIAS":N.V%=1:SUMA%=30:GOTO 7470
7385 IF FPAGO$="2" AND ACEPTA$="1" THEN LITVCTO$="GIRO 60 DIAS ACEPTADO":N.V%=1:SUMA%=60:GOTO 7470
7390 IF FPAGO$="2" THEN LITVCTO$="GIRO 60 DIAS":N.V%=1:SUMA%=60:GOTO 7470
7395 IF FPAGO$="3" AND ACEPTA$="1" THEN LITVCTO$="GIRO 90 DIAS ACEPTADO":N.V%=1:SUMA%=90:GOTO 7470
7400 IF FPAGO$="3" THEN LITVCTO$="GIRO 90 DIAS":N.V%=1:SUMA%=90:GOTO 7470
7405 IF FPAGO$="4" AND ACEPTA$="1" THEN LITVCTO$="GIRO 30 Y 60 DIAS ACEPTADO":N.V%=2:SUMA%=30:GOTO 7470
7410 IF FPAGO$="4" THEN LITVCTO$="GIRO 30 Y 60 DIAS":N.V%=2:SUMA%=30:GOTO 7470
7415 IF FPAGO$="5" AND ACEPTA$="1" THEN LITVCTO$="GIRO 90 Y 120 DIAS ACEPTADO":N.V%=2:SUMA%=90:GOTO 7470
7420 IF FPAGO$="5" THEN LITVCTO$="GIRO 90 Y 120 DIAS":N.V%=2:SUMA%=90:GOTO 7470
7425 IF FPAGO$="6" AND ACEPTA$="1" THEN LITVCTO$="GIRO 60,90 Y 120 DIAS ACEPTADO":N.V%=3:SUMA%=60:GOTO 7470
7430 IF FPAGO$="6" THEN LITVCTO$="GIRO 60,90 Y 120 DIAS":N.V%=3:SUMA%=60:GOTO 7470
7435 IF FPAGO$="8" AND ACEPTA$="1" THEN LITVCTO$="RECIBO":N.V%=1:SUMA%=120:GOTO 7470
7440 IF FPAGO$="8" THEN LITVCTO$="RECIBO":N.V%=1:SUMA%=0:GOTO 7470
7445 IF FPAGO$="9" AND ACEPTA$="1" THEN LITVCTO$="GIRO 60 Y 90 DIAS ACEPTADO":N.V%=2:SUMA%=60:GOTO 7470
7450 IF FPAGO$="A" AND ACEPTA$="1" THEN LITVCTO$="180 DIAS ACEPTADO":N.V%=1:SUMA%=180:GOTO 7470
7455 IF FPAGO$="A" THEN LITVCTO$="180 DIAS":N.V%=1:SUMA%=180:GOTO 7470
7460 IF FPAGO$="T" THEN LITVCTO$="TRANSFERENCIA A : ":N.V%=1:SUMA%=0:GOTO 7470
7465 SUMA%=0:N.V%=1:LITVCTO$=""
7470 '
7475 '------------------------------------------
7480 '---CALCULO DE LAS FECHAS DE VENCIMIENTO---
7485 '------------------------------------------
7490 FOR I%=1 TO 3
7495   FECVTO$(I%)=""
7500 NEXT I%
7505 '
7510 DD$=RIGHT$(WFECVAL$,2):DD%=VAL(DD$)
7515 MM$=MID$(WFECVAL$,3,2):MM%=VAL(MM$)
7520 AA$=LEFT$(WFECVAL$,2):AA%=VAL(AA$)
7525 '
7530 FOR I%=1 TO N.V%
7535    DD%=DD%+SUMA%
7540    IF DD%>30 THEN DD%=DD%-30:MM%=MM%+1
7545    IF MM%>12 THEN MM%=1:AA%=AA%+1
7550    IF DD%>30 THEN 7540
7555    '
7560    AA$=STR$(AA%):AA$=RIGHT$(AA$,LEN(AA$)-1)
7565    IF LEN(AA$)<>2 THEN AA$="0"+AA$
7570    AA$=STR$(AA%):AA$=RIGHT$(AA$,LEN(AA$)-1)
7575    IF LEN(AA$)<>2 THEN AA$="0"+AA$
7580    MM$=STR$(MM%):MM$=RIGHT$(MM$,LEN(MM$)-1)
7585    IF LEN(MM$)<>2 THEN MM$="0"+MM$
7590    DD$=STR$(DD%):DD$=RIGHT$(DD$,LEN(DD$)-1)
7595    IF LEN(DD$)<>2 THEN DD$="0"+DD$
7597 IF MM$="08" THEN MM$="09":DD$="05"
7600    FECVTO$(I%)=AA$+MM$+DD$
7605    SUMA%=30
7610 NEXT I%
7615 '
7616  'FOR I%=1 TO N.V%
7618  'IF MID$(FECVTO$(I%),3,2)="08" THEN RIGHT$(FECVTO$(I%),4)="0905"
7620  'NEXT I%
7622  'FOR I%=1 TO N.V%
7624  'IF VAL(DIA1P$)<>0 THEN RIGHT$(FECVTO$(I%),2)=DIA1P$
7625  'NEXT I%
7630 '-----------------------------
7635 '---CALCULO DEL DIA DE PAGO---
7640 '-----------------------------
7645 FOR I%=1 TO N.V%
7650   DD$=RIGHT$(FECVTO$(I%),2):DD%=VAL(DD$)
7655   MM$=MID$(FECVTO$(I%),3,2):MM%=VAL(MM$)
7660   AA$=LEFT$(FECVTO$(I%),2):AA%=VAL(AA$)
7665   DIA1%=VAL(DIA1P$)
7670   DIA2%=VAL(DIA2P$)
7675   DIA3%=VAL(DIA3P$)
7680   '
7685   IF DIA1%=0 AND DIA2%=0 AND DIA3%=0 THEN 7750
7690   IF DIA1%=DD% OR DIA2%=DD% OR DIA3%=DD% THEN 7750
7695   IF DD%<DIA1% THEN DD%=DIA1%:GOTO 7750
7700   IF DD%<DIA2% THEN DD%=DIA2%:GOTO 7750
7705   IF DD%<DIA3% THEN DD%=DIA3%:GOTO 7750
7710   DW%=99
7715   IF DIA1%<DW% AND DIA1%<>0 THEN DW%=DIA1%
7720   IF DIA2%<DW% AND DIA2%<>0 THEN DW%=DIA2%
7725   IF DIA3%<DW% AND DIA3%<>0 THEN DW%=DIA3%
7730   MM%=MM%+1
7735   IF MM%>12 THEN MM%=1:AA%=AA%+1
7740   DD%=DW%
7745   '
7750   IF (AA% MOD 4)=0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
7755   IF DD%>DIAS%(MM%) THEN DD%=DIAS%(MM%)
7760   AA$=STR$(AA%):AA$=RIGHT$(AA$,LEN(AA$)-1)
7765   IF LEN(AA$)<>2 THEN AA$="0"+AA$
7770   MM$=STR$(MM%):MM$=RIGHT$(MM$,LEN(MM$)-1)
7775   IF LEN(MM$)<>2 THEN MM$="0"+MM$
7780   DD$=STR$(DD%):DD$=RIGHT$(DD$,LEN(DD$)-1)
7785   IF LEN(DD$)<>2 THEN DD$="0"+DD$
7790   FECVTO$(I%)=AA$+MM$+DD$
7795   '
7800 NEXT I%
7805 '
7810 '---------------------------------------------
7815 '---CALCULO DEL IMPORTE DE LOS VENCIMIENTOS---
7820 '---------------------------------------------
7825 FOR I%=1 TO 3
7830   IMPVTO#(I%)=0
7835 NEXT I%
7840 IMPTOT#=0
7845 IF TOTFAC#<0 THEN IMPVTO#(1)=TOTFAC#:GOTO 7890
7850 IMPA#=TOTFAC#/N.V%
7855 IMPA#=INT(IMPA#)
7860 FOR I%=1 TO N.V%
7865   IMPVTO#(I%)=IMPA#
7870   IMPTOT#=IMPTOT#+IMPA#
7875 NEXT I%
7880 IF IMPTOT#<>TOTFAC# THEN IMPVTO#(N.V%)=IMPVTO#(N.V%)+(TOTFAC#-IMPTOT#)
7885 '
7890 '-----------------------------
7895 '---ESCRIBIR LA LINEA FINAL---
7900 '-----------------------------
7905 IF WSN$="N" THEN RETURN
7910 FOR I%=1 TO 2
7915   LPRINT
7920 NEXT I%
7925 IF TOTFAC#<0 THEN LITVCTO$="- A B O N O -"
7930 LPRINT TAB(3) LITVCTO$;
7935 IF LITVCTO$="COMPENSACION" THEN RETURN
7940 IF TOTFAC#<0 THEN RETURN
7945 IF LEFT$(LITVCTO$,4)="TRAN" THEN LPRINT TAB(21) CUANTO$:RETURN
7947   LPRINT TAB(30) "FECHA VCTO. :  ";:X%=47
7950 FOR I%=1 TO N.V%
7960   LPRINT TAB(X%) RIGHT$(FECVTO$(I%),2) "-" MID$(FECVTO$(I%),3,2) "-" LEFT$(FECVTO$(I%),2);:X%=X%+13:NEXT I%
7965   LPRINT TAB(30) "IMPORTE.    :";:Y%=55
7967 FOR I%=1 TO N.V%
7970   GUAPRI#=IMPVTO#(I%):GOSUB 44444
7975   X%=Y%-LEN(GUAPRI$):LPRINT TAB(X%) GUAPRI$;:Y%=Y%+13
7980 NEXT I%
7985 '
7990 RETURN
7995 '
8000 '**************************
8010 '***CARATULA DE PANTALLA***
8020 '**************************
8030 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8040 LOCATE 2,1:PRINT CHR$(186)
8050 COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10
8060 LOCATE 2,34:PRINT "Aplicaci¢n : FACTURACION"
8070 LOCATE 2,63:COLOR 10:PRINT "FECHA : ";:COLOR 11:PRINT FECHA$:COLOR 10
8080 LOCATE 2,80:PRINT CHR$(186)
8090 LOCATE 3,1:PRINT CHR$(200) STRING$(78,205) CHR$(188)
8140 LOCATE 21,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8150 LOCATE 22,1:PRINT CHR$(186)
8160 LOCATE 22,3:COLOR 11:PRINT "Mensajes :":COLOR 10
8170 LOCATE 22,80:PRINT CHR$(186)
8180 LOCATE 23,1:PRINT CHR$(200) STRING$(78,205) CHR$(188);
8190 RETURN
8200 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 RUN "PFACMENU"
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 IF ERL=30608 OR ERL=30708 THEN RESUME NEXT
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '***************************************
30002 '***APERTURA FICHERO DE PLAN CONTABLE***
30004 '***************************************
30006 FICHE1$="FCONPLAC.DIR"
30008 FICHE2$="FCONPLAC.DAT"
30010 'APERTURA FICHERO DE INDICES.
30012 OPEN "R",#1,FICHE1$,19
30014 'REGISTRO 1.
30016 FIELD #1,2 AS DULTIMO,2 AS DALTAS,2 AS DULTACT,12 AS DETIQUETA,1 AS DSTATUS
30018 'REGISTRO DE DATOS.
30020 FIELD #1,7 AS DCUENTA,2 AS DNREG,9 AS DFILLER,1 AS DSTATUS
30022 GET #1,1
30024 IF DETIQUETA="FCONPLAC.DIR" THEN 30032  'CONTINUAR
30026 GOSUB 8500:MEN$="NO EXISTE FICHERO INDICES PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30028 A$=INPUT$(1)
30030 GOTO 9000
30032 'CONTINUAR.
30034 ULTDIR%=CVI(DULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30036 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30038 ALTDIR%=CVI(DALTAS)     'NUMERO DE ALTAS EN DIR.
30040 ACTDIR%=CVI(DULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30042 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30044 '
30046 'APERTURA FICHERO DE DATOS.
30048 OPEN "R",#2,FICHE2$,390
30050 'REGISTRO 1.
30052 FIELD #2,2 AS PULTIMO,2 AS PALTAS,12 AS PETIQUETA,200 AS PFIL1,133 AS PFIL2,1 AS PSTATUS,40 AS PFIL1000
30054 'REGISTRO DE DATOS CLIENTES.
30056 FIELD #2,7 AS PCUENTA,40 AS PNOMBRE,40 AS PDIREC,5 AS PCPOS,30 AS PPOBLA,40 AS PDIPAG,5 AS PCPPAG,30 AS PPOPAG,14 AS PNIF,11 AS PTELE1,11 AS PTELE2,8 AS PBANCO,15 AS PCTA,11 AS PPROVE,1 AS PACEPTA,6 AS PRIESGO,4 AS PGFI,2 AS PIVA,110 AS PRESTO
30058 FIELD #2,250 AS PANTE1,30 AS PANTE2,2 AS PRECARGO,1 AS PFPOFI,4 AS PDCTOFI,1 AS PFPBLA,4 AS PDCTBLA,2 AS PDIA1,2 AS PDIA2,2 AS PDIA3,7 AS PR1,7 AS PR2,3 AS PZONA,1 AS PAGRUPA,4 AS PCOMIS,28 AS PFIL3,1 AS PACTIVO,1 AS PSTATUS,40 AS PCONTACTO
30060 GET #2,1
30062 IF PETIQUETA="FCONPLAC.DAT" THEN 30070 'CONTINUAR.
30064 GOSUB 8500:MEN$="NO EXISTE EL FICHERO DE PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30066 A$=INPUT$(1)
30068 GOTO 9000
30070 'CONTINUAR.
30072 ULTDAT%=CVI(PULTIMO)    'ULTIMO DE DAT.
30074 ALTDAT%=CVI(PALTAS)     'ALTAS DE DAT.
30076 '
30078 RETURN
30080 '
30082 '************************************
30084 '***LECTURA AZAR DEL PLAN CONTABLE***
30086 '************************************
30088 'El argumento de busqueda sobre 'BUSCA$'
30090 'El numero de registros sobre 'N.R%'
30092 R.B%=0           'Donde devuelve el nro. de registro.
30094 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30096 SW1%=0         'SW indicador de la busqueda.
30098 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30100 WSTATUS$=""
30102 R.I%=INT((N.R%/2)+.5)
30104 R.C%=R.I%
30106 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30130
30108 IF R.I%>N.R% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 30130
30110 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
30112 IF DCUENTA=BUSCA$ THEN R.W%=R.I%:R.B%=CVI(DNREG):GOTO 30164      'MIRAR STATUS
30114 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30116 IF DCUENTA>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 30120
30118 R.I%=R.I%+R.C%
30120 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30106
30122 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30106
30124 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30106
30126 REG2%=R.I%:SW100%=0
30128 GOTO 30106
30130 'REGISTRO NO BUSCADO.
30132 XX%=0
30134 FOR A%=DES% TO HAS%
30136   GET #1,A%
30138   IF DCUENTA=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=CVI(DNREG):XX%=1:A%=HAS%
30140 NEXT A%
30142 IF XX%=1 THEN 30164
30144 '
30146 '
30148 'Registro NO BUSCADO
30150 XX%=0
30152 IF N.R%=0 THEN N.R%=1
30154 FOR JMG%=N.R% TO ULTIMO%
30156     GET #1,JMG%
30158     IF DCUENTA=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=CVI(DNREG):XX%=1:JMG%=32000
30160 NEXT JMG%
30162 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30164 'MIRAMOS STATUS.
30166 IF DSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30168 WSTATUS$=""
30170 RETURN
30172 '
30500 '********************************
30502 '***APERTURA FICHERO ALBARANES***
30504 '********************************
30506 FICHE6$="FFACALBA.CLA"
30508 OPEN "R",#6,FICHE6$,155
30510 'REGISTRO 1
30512 FIELD #6,1 AS XSTATU1,1 AS XMARC1,4 AS XULTIMO,12 AS XETIQUETA,4 AS XULTALB,4 AS XULTACT,129 AS XFIL0
30514 'REGISTRO DE DATOS.
30516 FIELD #6,1 AS XSTATUS,1 AS XMARCA,6 AS XORDEN,1 AS XCABO,1 AS XOOB,7 AS XCLI,6 AS XALBA,20 AS XRPED,8 AS XTRAC,8 AS XCIST,6 AS XFECHA,6 AS XFEDES,2 AS XPRO
30518 FIELD #6,73 AS XRELLE,11 AS XCLRU,5 AS XNPRO,10 AS XZZZ,2 AS XXXX,8 AS XCANTI,1 AS XFCO,8 AS XPRECI,4 AS XRUT,8 AS XAUTOP,1 AS XFACT,24 AS XRESTO
30520 'LEEMOS EL REGISTRO 0.
30522 GET #6,1
30524  IF XETIQUETA="FFACALBA.DAT" THEN 30532 'CONTINUAR.
30526  GOSUB 8500:MEN$="NO EXISTE FICHERO ALBARANES, PULSE UNA TECLA":GOSUB 8500
30528  A$=INPUT$(1)
30530  GOTO 9000
30532  'CONTINUAR
30534  ULTPED!=CVS(XULTIMO)    'ULTIMO MOVIMIENTOS INTRODUCIDO
30536  ULTALB!=CVS(XULTALB)    'ULTIMO NUMERO DE ALBARAN.
30538  ULTACT!=CVS(XULTACT)
30540 '
30542 'LSET XSTATU1="A":LSET XMARC1="A":LSET XULTIMO=MKS$(1)
30544 'LSET XETIQUETA="FFACALBA.DAT":LSET XULTALB=MKS$(1)
30546 'LSET XULTACT=MKS$(1):LSET XFIL0=""
30548 'PUT #7,1
30550 'CLOSE:STOP
30552  RETURN
30554  '
30600 '************************************************************
30602 '***APERTURA FICHERO DE MOVIMIENTOS CONTABLES(FACTURACION)***
30604 '************************************************************
30606 FICHE5$="FFACCONT.DAT"
30608 KILL FICHE5$
30610 OPEN "R",#5,FICHE5$,100
30612 'REGISTRO 0 DE DATOS.
30614 FIELD #5,5 AS MNORDEN,4 AS MULTIMO,4 AS MALTAS,12 AS METIQUETA,1 AS MSW,6 AS MFDIA,67 AS MFI1,1 AS MSTATUS
30616 'REGISTRO DE DATOS.
30618 FIELD #5,5 AS MNORDEN,6 AS MFECASI,7 AS MCUENTA,7 AS MCONTRA,30 AS MCONCEP,1 AS MDEHA,8 AS MIMPORTE,1 AS MNVTO,6 AS MFECVTO,1 AS MPAGO1,1 AS MPAGO2,1 AS MPAGO3,8 AS MBIMPO,17 AS MFI2,1 AS MSTATUS
30620 'GENERAMOS REGISTRO 1.
30622 GET #5,1
30624 LSET MNORDEN="00001"
30626 LSET MULTIMO=MKS$(1)
30628 LSET MALTAS=MKS$(1)
30630 LSET METIQUETA="FFACCONT.DAT"
30632 LSET MSW="0"
30634 LSET MFDIA=STRING$(6,0)
30636 LSET MFI1=STRING$(67,0)
30638 LSET MSTATUS="A"
30640 PUT #5,1
30642 REGCON!=CVS(MULTIMO)
30644 '
30646 RETURN
30648 '
30700 '*********************************************
30702 '***APERTURA FICHERO RESUMEN DE FACTURACION***
30704 '*********************************************
30706 FICHE7$="FFACRESU.DAT"
30708 KILL FICHE7$
30710 OPEN "R",#7,FICHE7$,100
30712 'REGISTRO 0 DE DATOS.
30714 FIELD #7,2 AS RORDEN,2 AS RULTIMO,12 AS RETIQUETA,83 AS RFILL00,1 AS RSTATUS
30716 'REGISTRO DE DATOS.
30718 FIELD #7,2 AS RORDEN,7 AS RNCLI,6 AS RNFAC,6 AS RNREC,1 AS RTFAC,6 AS RFFAC,6 AS RFVTO,10 AS RIMPO,1 AS RCOA,3 AS RTEMPO,2 AS RVENDE,4 AS RCOMIS,8 AS RBASIMP,37 AS RFILL01,1 AS RSTATUS
30720 'GENERAMOS REGISTRO 1.
30722 GET #7,1
30724 LSET RORDEN=MKI$(1)
30726 LSET RULTIMO=MKI$(1)
30728 LSET RETIQUETA="FFACRESU.DAT"
30730 LSET RFIL00=STRING$(83,0)
30732 LSET RSTATUS="A"
30734 PUT #7,1
30736 REGRES%=CVI(RULTIMO)
30738 '
30740 RETURN
30742 '
31000 '***********************************
31005 '***APERTURA FICHERO DE MERCANCIAS**
31010 '***********************************
31015 '
31020 FICHE3$="FFACARTI.DAT"
31025 'APERTURA FICHERO DE INDICES.
31030 OPEN "R",#3,FICHE3$,60
31035 'REGISTRO 1.
31040 FIELD #3,2 AS AULTI,12 AS AETIQ,44 AS ALIBRE,1 AS AMARC1,1 AS ASTA1
31045 'REGISTRO DE DATOS.
31050 FIELD #3,2 AS ANUAR,30 AS ANOAR,9 AS APELI,2 AS ADENS,15 AS AREST,1 AS AMARCA,1 AS ASTAT
31055 GET #3,1
31060  IF AETIQ="FFACARTI.DAT" THEN 31080  'CONTINUAR
31065  GOSUB 8500:MEN$="NO EXISTE FICHERO DE MERCANCIAS, PULSE UNA TECLA":GOSUB 8500
31070  A$=INPUT$(1)
31075  GOTO 9000
31080 'CONTINUAR.
31085  ULTDIRA%=CVI(AULTI)    'ULTIMO INTRODUCIDO EN DIR.
31090 '
31095 'LSET AULTI=MKI$(1):LSET AETIQ="FFACARTI.DAT"
31100 'LSET ALIBRE="":LSET AMARC1="A":LSET ASTA1="A"
31105 'PUT #3,1
31110 'CLOSE:STOP
31115  '
31120  RETURN
31125 '***********************************
31130 '***APERTURA FICHERO DE MERCANCIAS**
31135 '***********************************
31140 '
31145 FICHE4$="FFACRUTA.DAT"
31150 'APERTURA FICHERO DE INDICES.
31155 OPEN "R",#4,FICHE4$,70
31160 'REGISTRO 1.
31165 FIELD #4,2 AS RULTI,12 AS RETIQ,2 AS RACTU,52 AS RLIBRE,1 AS RMARC1,1 AS RSTA1
31170 'REGISTRO DE DATOS.
31175 FIELD #4,4 AS RNUAR,30 AS RNOAR,3 AS RCLAV,7 AS RCLIE,8 AS RTONE,8 AS RVIAJ,8 AS RLIBR,1 AS RMARCA,1 AS RSTAT
31180 GET #4,1
31185  IF RETIQ="FFACRUTA.DAT" THEN 31205  'CONTINUAR
31190  GOSUB 8500:MEN$="NO EXISTE FICHERO DE RUTAS, PULSE UNA TECLA":GOSUB 8500
31195  A$=INPUT$(1)
31200  GOTO 9000
31205 'CONTINUAR.
31210  ULTRUTA%=CVI(RULTI):ULTACTU%=CVI(RACTU)
31215  N.O%=ULTACTU%
31220  '
31225  RETURN
31230 '*************************
31235 '***LECTURA AZAR BANCOS***
31240 '*************************
31245 'El argumento de busqueda sobre 'BUSCA$'
31250 'El numero de registros sobre 'N.O%'
31255 R.B%=0           'Donde devuelve el nro. de registro.
31260 R.W%=0           'Donde devuelve el nro. de registro del DIR.
31265 SW1%=0         'SW indicador de la busqueda.
31270 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
31275 '
31280 R.I%=INT((N.O%/2)+.5)
31285 R.C%=R.I%
31290 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 31350
31295 IF R.I%>N.O% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.O%:GOTO 31350
31300 IF R.I%=1 THEN R.I%=2:GET #4,R.I% ELSE GET #4,R.I%
31305 IF RNUAR=JESUS$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 31435      'MIRAR STATUS
31310 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
31315 IF RNUAR>JESUS$ THEN R.I%=R.I%-R.C%:GOTO 31325
31320 R.I%=R.I%+R.C%
31325 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 31290
31330 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 31290
31335 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 31290
31340 REG2%=R.I%:SW100%=0
31345 GOTO 31290
31350 'REGISTRO NO BUSCADO.
31355 XX%=0
31360 FOR A%=DES% TO HAS%
31365   GET #4,A%
31370   IF RNUAR=JESUS$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
31375 NEXT A%
31380 IF XX%=1 THEN 31435
31385 '
31390 '
31395 'Registro NO BUSCADO
31400 XX%=0
31405 IF N.O%=0 THEN N.O%=1
31410 FOR JMG%=N.O% TO ULTRUTA%
31415     GET #4,JMG%
31420     IF RNUAR=JESUS$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
31425 NEXT JMG%
31430 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
31435 'MIRAMOS STATUS.
31440 IF RSTAT<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
31445 WSTATUS$=""
31450 RETURN
31455 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
44444 '********************************************
44454 '***RUTINA PARA EDITAR CAMPOS EN CRISTIANO***
44464 '********************************************
44494 GUAPRI$=STR$(GUAPRI#)
44504 SIGNO$=LEFT$(GUAPRI$,1)
44514 GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
44524 'BUSCAMOS SI TIENE DECIMALES.
44534 PUNTO%=0
44544 FOR CRIS%=LEN(GUAPRI$) TO 1 STEP -1
44554  IF MID$(GUAPRI$,CRIS%,1)="." THEN PUNTO%=CRIS%:CRIS%=0
44564 NEXT CRIS%
44574 'DIVIDIMOS EN ENTEROS Y DECIMALES.
44584 IF PUNTO%=0 THEN ENTERO$=GUAPRI$:L.EN%=LEN(ENTERO$):DECIMAL$="":L.DE%=0:GOTO 44624
44594 ENTERO$=LEFT$(GUAPRI$,PUNTO%-1):L.EN%=LEN(ENTERO$):ENTERO#=VAL(ENTERO$)
44604 DECIMAL$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-PUNTO%):L.DE%=LEN(DECIMAL$)
44614 IF LEN(DECIMAL$)<2 THEN DECIMAL$=DECIMAL$+"0":L.DE%=LEN(DECIMAL$)
44624 'REDONDEAMOS LOS DECIMALES
44634 IF DECIMAL$="" THEN 44764  'CONTINUAR
44644 DECA$=LEFT$(DECIMAL$,2)+"."+RIGHT$(DECIMAL$,L.DE%-2)
44654 DECA#=VAL(DECA$)
44664 DECA#=INT(DECA#+.5)
44674 IF DECA#>99 THEN 44734   'SUMAR 1 A ENTERO Y DECIMALES = '00'
44684 DECA$=STR$(DECA#):DECA$=RIGHT$(DECA$,2)
44694 IF LEN(DECA$)<>2 THEN DECA$="0"+DECA$
44704 IF LEFT$(DECA$,1)=" " THEN DECA$="0"+RIGHT$(DECA$,1)
44714 DECIMAL$=DECA$
44724 GOTO 44764  'CONTINUAR
44734 ENTERO#=ENTERO#+1
44744 ENTERO$=STR$(ENTERO#):ENTERO$=RIGHT$(ENTERO$,LEN(ENTERO$)-1):L.EN%=LEN(ENTERO$)
44754 DECIMAL$="00":L.DE%=2
44764 'CONTINUAMOS
44774 CAMPO$="":X%=0
44784 FOR CRIS%=L.EN% TO 1 STEP -1
44794  X%=X%+1
44804  IF X%=4 THEN CAMPO$=CAMPO$+".":X%=1
44814  CAMPO$=CAMPO$+MID$(ENTERO$,CRIS%,1)
44824 NEXT CRIS%
44834 GUAPRI$=""
44844 FOR CRIS%=LEN(CAMPO$) TO 1 STEP -1
44854  GUAPRI$=GUAPRI$+MID$(CAMPO$,CRIS%,1)
44864 NEXT CRIS%
44874 IF GUAPRI$="" THEN GUAPRI$="0"
44884 GUAPRI$=SIGNO$+GUAPRI$
44894 IF DECIMAL$<>"" THEN GUAPRI$=GUAPRI$+","+LEFT$(DECIMAL$,2)
44904 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 'LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63152  IF ASC(D.R$)=14 THEN I.T%=5:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¤" OR D.R$="¥" OR D.R$="§" OR D.R$="¦" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
