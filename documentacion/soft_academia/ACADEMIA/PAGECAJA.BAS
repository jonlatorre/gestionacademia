10 '                       PAGECAJA.BAS
20 '                      --------------
30 '      GESTION DE LA CAJA DE LA ESCUELA DE HOSTELERIA
40 '   ----------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(178):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
102 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
104 FEDIA$=RIGHT$(FECHA$,2)+MID$(FECHA$,4,2)+LEFT$(FECHA$,2)
106 ME%=VAL(MID$(FEDIA$,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(FEDIA$,2))
108 NDIA%=ME%*31+DI%
110 GOSUB 33000
112 DEFSTR H,C
114 OPTION BASE 1
116 DIM TABRUT$(6),DIAS%(12),TABENT$(30)
118 DATA 31,28,31,30,31,30,31,31,30,31,30,31
120 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
122 GOSUB 8000
176 COLOR 10:LOCATE 9,26:PRINT CHR$(201)+STRING$(28,205)+CHR$(187)
177 FOR J%=10 TO 15 :LOCATE J%,26:PRINT CHR$(186)+STRING$(28," ")+CHR$(186):NEXT
178 LOCATE 16,26:PRINT CHR$(200)+STRING$(28,205)+CHR$(188);
180 LOCATE 12,30:PRINT "Pulsar Clave de Acceso"
181 LOCATE 13,30:PRINT STRING$(22,205)
182 LOCATE 15,38:PRINT "<    >"
183 LOCATE 15,39,1,0,7:C$=INPUT$(1):PRINT " ";:C$=C$+INPUT$(1)
184 PRINT " ";:C$=C$+INPUT$(1)
185 PRINT " ";:C$=C$+INPUT$(1)
186 PRINT " "
187 IF C$="A91Z" THEN 193
188 IF C$="AI23" THEN 193
189 IF C$="F311" THEN 193
190 '
191 IF C$="JMPL" THEN 193
192 BEEP:GOTO 183
193 OPEN "O",#2,"FAGECLA.DIA":PRINT #2,C$:CLOSE
194 FICHERO$="FAGECAJA.A"+RIGHT$(FECHA$,2)
199 GOSUB 30000
200 GET #1,NDIA%
202 IF HSTA="O" THEN CLOSE:GOTO 300
204 LOCATE 12,30:PRINT "Pulsar Importe Inicial"
206 LOCATE 13,30:PRINT STRING$(22,205)
208 LOCATE 15,36:PRINT "<        >"
210       '*******************************
212       '***ENTRADA IMPORTE INICIAL  ***
214       '*******************************
216       COLOR 15
218       F.C1%=15:C.C1%=37:L.E%=6:BO%=8:ESC%=0:GOSUB 63000
220       IF I.T%=0 THEN IM1#=VAL(D.E$):GOTO 224  'Entrada OK
222       BEEP:GOTO 216
224       GUAPRI#=IM1#:GOSUB 64770
226       LOCATE 15,37:PRINT SPC(8)
228       LOCATE 15,44-L.C%:PRINT GUAPRI$
230       '
232 LOCATE 12,30:PRINT "Repite Importe Inicial"
234 LOCATE 13,30:PRINT STRING$(22,205)
236 LOCATE 15,36:PRINT "<        >"
238       '*******************************
240       '***ENTRADA IMPORTE INICIAL  ***
242       '*******************************
244       COLOR 15
246       F.C1%=15:C.C1%=37:L.E%=6:BO%=8:ESC%=0:GOSUB 63000
248       IF I.T%=0 THEN IM2#=VAL(D.E$):GOTO 252  'Entrada OK
250       BEEP:GOTO 244
252       GUAPRI#=IM2#:GOSUB 64770
254       LOCATE 15,37:PRINT SPC(8)
256       LOCATE 15,44-L.C%:PRINT GUAPRI$
258       '
260       IF IM1#<>IM2# THEN BEEP:GOTO 204
262       LSET HINI=MKD$(IM1#)
264       LSET HFIN=MKD$(IM1#)
266       LSET HSTA="O"
268       PUT #1,NDIA%
270       CLOSE
300 DATA "     OPERACIONES DE CAJA     "
310 DATA "      CONSULTA POR DIAS      "
320 DATA "   LISTADO DIARIO REDUCIDO   "
330 DATA "     LISTADOS GENERALES      "
335 DATA "     FINAL DEL PROGRAMA      "
340 DATA "                             "
350 FOR I%=1 TO 6
360    READ TABRUT$(I%)
370 NEXT I%
380 '
390 '******************************
400 '***TRATAMIENTO DEL PROGRAMA***
410 '******************************
420 'Presentamos el Menu del Programa.
430 GOSUB 8000
440 TIT$="- GESTION FICHERO GENERAL DE LA CAJA DE LA AGENCIA -"
450 X%=80-LEN(TIT$):X%=INT(X%/2)
460 COLOR 11:LOCATE 7,X%:PRINT TIT$
470 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
480 FOR I%=10 TO 15
490    LOCATE I%,24:PRINT TABRUT$(I%-9)
500 NEXT I%
510 '
520 GOSUB 8500:COLOR 11
530 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulsando la Tecla ";
540 COLOR 15:PRINT " ENTER "
550 'COLOR 7,0,0
560 '
570 C.F%=10:C%=24:L.F%=15:N.L%=5
580 GOSUB 50000
590 '
600 ON I.A% GOTO 6000,610,610,6100,6300
610 '
612 GOSUB 8000
613 COLOR 11
614 IF I.A%=2 THEN TI$="CONSULTA RESUMIDA POR DIAS":GOTO 620
616 TI$="LISTADO RESUMIDO DE UN DIA"
620 LOCATE 6,28:PRINT TI$
622 LOCATE 7,28:PRINT STRING$(26,205)
630 LOCATE 12,21:PRINT "Teclea la Fecha que deseas : <        >":COLOR 15
640 '
650 GOSUB 7650
700       '***********************
702       '***ENTRADA DE FECHA ***
704       '***********************
706       F.C1%=12:C.C1%=51:L.E%=6:BO%=8:GOSUB 63000
708       IF I.T%=0 THEN NACI$=D.E$:GOTO 726
710       IF I.T%=4 THEN 4000
712       GOTO 700
726       FECSUB$=NACI$:GOSUB 60000
728       IF ERF%<>1 THEN 736
730       BEEP:GOSUB 8500:MEN$="Fecha solicitada Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,51:PRINT SPC(8)
732       GOTO 702
736       IF RIGHT$(NACI$,2)<"94" THEN 730
738       LOCATE 12,51:PRINT FDMA$:FELI$=FDMA$:FENA$=FAMD$
800   '*************************************
802   '***PEDIMOS CONFORMIDAD A LOS DATOS***
804   '*************************************
806   GOSUB 8500
808   LOCATE 22,15:COLOR 10:PRINT "Fecha Correcta ";
810   COLOR 15:PRINT "  ENTER  ";
812   COLOR 10:PRINT " Fecha Err¢nea ";
814   COLOR 15:PRINT "  F1 "
816   '
818   '
820   A$=INPUT$(1)
822   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 820
824   '
826   IF ASC(A$)=13 THEN 830
827   LOCATE 12,51:PRINT SPC(8):GOTO 702
830   '
832 ME%=VAL(MID$(FENA$,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(FENA$,2))
834 XDIA%=ME%*31+DI%
836 FICHERO$="FAGECAJA.A"+LEFT$(FENA$,2)
838 GOSUB 30000
839 GET #1,XDIA%
840 IF I.A%=2 THEN 1000
842 GOTO 4490
1000 '
1010 GOSUB 31000
1105 GUAPRI#=CVD(HINI):GOSUB 64770
1110 LOCATE 8,58-L.C%:PRINT GUAPRI$
1112 'GUAPRI#=CVS(HINI):GOSUB 64770
1113 'LOCATE 8,72-L.C%:PRINT GUAPRI$
1115 GUAPRI#=CVD(HCOB):GOSUB 64770
1120 LOCATE 10,58-L.C%:PRINT GUAPRI$
1125 GUAPRI#=CVD(HCTA):GOSUB 64770
1130 LOCATE 11,58-L.C%:PRINT GUAPRI$
1135 GUAPRI#=CVD(HCPL):GOSUB 64770
1140 LOCATE 12,58-L.C%:PRINT GUAPRI$
1142 GUAPRI#=CVD(HCOT):GOSUB 64770
1143 LOCATE 13,58-L.C%:PRINT GUAPRI$
1145 GUAPRI#=CVD(HCOB)+CVD(HCTA)+CVD(HCPL)+CVD(HCOT):GOSUB 64770
1150 LOCATE 13,72-L.C%:PRINT GUAPRI$
1155 GUAPRI#=CVD(HPAG):GOSUB 64770
1160 LOCATE 15,58-L.C%:PRINT GUAPRI$
1165 GUAPRI#=CVD(HPTA):GOSUB 64770
1170 LOCATE 16,58-L.C%:PRINT GUAPRI$
1175 GUAPRI#=CVD(HPPL):GOSUB 64770
1180 LOCATE 17,58-L.C%:PRINT GUAPRI$
1182 GUAPRI#=CVD(HPOT):GOSUB 64770
1183 LOCATE 18,58-L.C%:PRINT GUAPRI$
1185 GUAPRI#=CVD(HPAG)+CVD(HPTA)+CVD(HPPL)+CVD(HPOT):GOSUB 64770
1190 LOCATE 18,72-L.C%:PRINT GUAPRI$
1195 GUAPRI#=CVD(HFIN):GOSUB 64770
1200 LOCATE 20,72-L.C%:PRINT GUAPRI$
1205 CLOSE
1210 GOSUB 8500:MEN$="Pulse una tecla para nueva consulta ":GOSUB 8500
1215 A$=INPUT$(1):GOTO 610
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 390      'Volvemos al Menu.
4060 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4496 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4498 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
4500 LPRINT
4502 LPRINT TAB(24) "*** AGENCIA DE VIAJES E.I.D.E. ***"
4504 LPRINT TAB(24) STRING$(34,205)
4506 LPRINT
4508 LPRINT TAB(21) "LISTADO RESUMEN DE CAJA DEL DIA "FELI$
4510 LPRINT TAB(21) STRING$(40,205)
4512 LPRINT:LPRINT
4514 LPRINT TAB(11) "     D  E  S  C  R  I  P  C  I  O  N          I M P O R T E"
4516 LPRINT TAB(11) STRING$(40,205);
4518 LPRINT TAB(56) STRING$(15,205)
4520 LPRINT
4522 LPRINT TAB(11) "Importe inicial en met lico CAJA .......";
4524 GUAPRI#=CVD(HINI):GOSUB 64770
4526 LPRINT TAB(70-L.C%) GUAPRI$
4528 LPRINT:LPRINT
4532 LPRINT TAB(11) "Cobros en met lico .....................";
4534 GUAPRI#=CVD(HCOB):GOSUB 64770
4536 LPRINT TAB(70-L.C%) GUAPRI$
4542 LPRINT TAB(11) "Cobros en talones ......................";
4544 GUAPRI#=CVD(HCTA):GOSUB 64770
4546 LPRINT TAB(70-L.C%) GUAPRI$
4552 LPRINT TAB(11) "Cobros con tarjetas ....................";
4554 GUAPRI#=CVD(HCPL):GOSUB 64770
4556 LPRINT TAB(70-L.C%) GUAPRI$
4562 LPRINT TAB(11) "Otros cobros ...........................";
4564 GUAPRI#=CVD(HCOT):GOSUB 64770
4566 LPRINT TAB(70-L.C%) GUAPRI$
4568 LPRINT:LPRINT
4570 TOCO#=CVD(HCOB)+CVD(HCTA)+CVD(HCPL)+CVD(HCOT):GUAPRI#=TOCO#:GOSUB 64770
4572 LPRINT TAB(11) "T O T A L   C O B R O S ................";
4574 LPRINT TAB(70-L.C%) GUAPRI$
4576 LPRINT:LPRINT
4632 LPRINT TAB(11) "Pagos en met lico ......................";
4634 GUAPRI#=CVD(HPAG):GOSUB 64770
4636 LPRINT TAB(70-L.C%) GUAPRI$
4642 LPRINT TAB(11) "Pagos en talones .......................";
4644 GUAPRI#=CVD(HPTA):GOSUB 64770
4646 LPRINT TAB(70-L.C%) GUAPRI$
4652 LPRINT TAB(11) "Pagos con tarjetas .....................";
4654 GUAPRI#=CVD(HPPL):GOSUB 64770
4656 LPRINT TAB(70-L.C%) GUAPRI$
4662 LPRINT TAB(11) "Otros pagos ............................";
4664 GUAPRI#=CVD(HPOT):GOSUB 64770
4666 LPRINT TAB(70-L.C%) GUAPRI$
4668 LPRINT:LPRINT
4670 TOPA#=CVD(HPAG)+CVD(HPTA)+CVD(HPPL)+CVD(HPOT):GUAPRI#=TOPA#:GOSUB 64770
4672 LPRINT TAB(11) "T O T A L   P A G O S ..................";
4674 LPRINT TAB(70-L.C%) GUAPRI$
4676 LPRINT:LPRINT
4678 GUAPRI#=CVD(HINI)+CVD(HCOB)-CVD(HPAG):GOSUB 64770
4680 LPRINT TAB(11) "Importe final en met lico CAJA .........";
4682 LPRINT TAB(70-L.C%) GUAPRI$
4684 LPRINT:LPRINT
4688 GUAPRI#=TOCO#-TOPA#:GOSUB 64770
4690 LPRINT TAB(11) "T O T A L   G E N E R A L   D I A ......";
4692 LPRINT TAB(70-L.C%) GUAPRI$
4694 LPRINT CHR$(12)
4696 CLOSE
4698 GOTO 400
6000 RUN "PAGEDIST"
6100 RUN "PAGELIST"
6300 RUN "PAGEINIC"
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7680 '
7690 '
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin de la Opci¢n."
7720 'COLOR 0,7,0:PRINT " F8 =>";
7730 'COLOR 7,0,0:PRINT " M.Izda. ";
7740 'COLOR 0,7,0:PRINT " F10 =>";
7750 'COLOR 7,0,0:PRINT " Dup. "
7760 COLOR 15
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,36:PRINT "Aplicaci¢n : C A J A    ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PAGEINIC"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FAGECALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FAGECALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO DIARIO  ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30090 ' Apertura del fichero de datos   FRESCAJA.DIA
30095 '
30100 OPEN "R",#1,FICHERO$,90
30105 '
30110 FIELD #1,1 AS HSTA,8 AS HINI,8 AS HCOB,8 AS HPAG,8 AS HCTA,8 AS HPTA,8 AS HCPL,8 AS HPPL,8 AS HCOT,8 AS HPOT,8 AS HFIN,9 AS HRES
30114 '
30116 'FOR A%=1 TO 372
30117 'GET #1,A%
30118 'LSET HSTA="C":LSET HRES="":LSET HCOT=MKD$(0):LSET HPOT=MKD$(0)
30119 'LSET HINI=MKD$(0):LSET HCOB=MKD$(0):LSET HPAG=MKD$(0):LSET HCTA=MKD$(0)
30120 'LSET HPTA=MKD$(0):LSET HCPL=MKD$(0):LSET HPPL=MKD$(0):LSET HFIN=MKD$(0)
30121 'PUT #1,A%
30122 'NEXT A%
30123 'CLOSE:STOP
30128 RETURN
30130 '
30140 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 GOSUB 8000:COLOR 11
31040 LOCATE 4,24:PRINT "CONSULTA DE CAJA DEL DIA " RIGHT$(FENA$,2)"-"MID$(FENA$,3,2)"-"LEFT$(FENA$,2)
31042 LOCATE 5,24:PRINT STRING$(33,205)
31050 LOCATE 6,9:PRINT "    D  E  S  C  I  P  C  I  O  N          IMPORTE       TOTALES"
31054 LOCATE 7,9:PRINT "------------------------------------     ---------     ---------"
31056 LOCATE 8,9:PRINT "Importe inicial en Met lico CAJA ..."
31058 LOCATE 10,9:PRINT "Cobros en Met lico ................."
31060 LOCATE 11,9:PRINT "Cobros en Talones .................."
31062 LOCATE 12,9:PRINT "Cobros con Tarjetas ................"
31063 LOCATE 13,9:PRINT "Otros cobros ......................."
31064 LOCATE 15,9:PRINT "Pagos en Met lico .................."
31066 LOCATE 16,9:PRINT "Pagos en Talones ..................."
31068 LOCATE 17,9:PRINT "Pagos con Tarjetas ................."
31069 LOCATE 18,9:PRINT "Otros pagos ........................"
31070 LOCATE 20,9:PRINT "Importe final en Met lico CAJA ....."
31072 COLOR 15
31084 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** AGENCIA DE VIAJES EIDE ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 12:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 15
50300 RETURN
50340 '
50510 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
