10 '                       PRESLAGR.BAS
20 '                      --------------
30 '        LISTADO NUMERICO DE ALUMNOS POR ASIGNATURAS
40 '        -------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,H,F,A,N
150 OPTION BASE 1
200 DIM TABENT$(30),MES$(12)
210 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SEPTIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
220 FOR A%=1 TO 12:READ MES$(A%):NEXT A%
520 GOTO 4490
522 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 'GOSUB 30010        'Apertura Fichero ALUNMOS
550 'GOSUB 34000         'APERTURA FICHERO DE CALENDARIO
552 IF LEFT$(GLUNESA,1)<>" " OR LEFT$(GLUNESB,1)<>" " THEN DI1$="LU"
554 IF LEFT$(GMARTEA,1)<>" " OR LEFT$(GMARTEB,1)<>" " THEN DI2$="MA"
556 IF LEFT$(GMIERCA,1)<>" " OR LEFT$(GMIERCB,1)<>" " THEN DI3$="MI"
558 IF LEFT$(GJUEVEA,1)<>" " OR LEFT$(GJUEVEB,1)<>" " THEN DI4$="JU"
560 IF LEFT$(GVIERNA,1)<>" " OR LEFT$(GVIERNB,1)<>" " THEN DI5$="VI"
570 '
590 '
600 BUSCA$=FEDIA$+"01":GOSUB 34230
605 N.L%=N.L%-1
610 L1$=CHR$(218):L2$=CHR$(179):L3$=CHR$(179):L4$=CHR$(197):L5$=CHR$(179):L6$=CHR$(193)
625 N.L%=N.L%+1
630 GET #4,N.L%
632 IF MID$(BUSCA$,3,2)<>MID$(HDIA,3,2) THEN 800
635 IF HSBIL="F" THEN 625
636 IF LEFT$(HDES,2)=DI1$ THEN 644
637 IF LEFT$(HDES,2)=DI2$ THEN 644
638 IF LEFT$(HDES,2)=DI3$ THEN 644
639 IF LEFT$(HDES,2)=DI4$ THEN 644
640 IF LEFT$(HDES,2)=DI5$ THEN 644
642 GOTO 625
644 L1$=L1$+CHR$(196)+CHR$(196)+CHR$(194)
645 L2$=L2$+LEFT$(HDES,2)+CHR$(179)
650 L3$=L3$+RIGHT$(HDIA,2)+CHR$(179)
655 L4$=L4$+CHR$(196)+CHR$(196)+CHR$(197)
660 L5$=L5$+"  "+CHR$(179)
661 L6$=L6$+CHR$(196)+CHR$(196)+CHR$(193)
665 GOTO 625
800 L1$=LEFT$(L1$,LEN(L1$)-1)+CHR$(191)
801 L4$=LEFT$(L4$,LEN(L4$)-1)+CHR$(180)
802 L6$=LEFT$(L6$,LEN(L6$)-1)+CHR$(217)
804 DI1$="  ":DI2$="  ":DI3$="  ":DI4$="  ":DI5$="  "
3310 RETURN
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 IF LIN%=90 THEN GOSUB 3740   '--HACER CABECERAS--
3532 NOM$=CAPELL1:APE1$=CAPELL2:APE2$=CNOMBRE
3540 GOSUB 40000:NOMBRE$=LEFT$(NOMBRE$,30) 'e en una linea.
3542 FENA$=RIGHT$(CFENA,2)+"-"+MID$(CFENA,3,2)+"-"+LEFT$(CFENA,2)
3544 IF CCONFI<>"S" THEN CONF$="N" ELSE CONF$=CCONFI
3550 GUAPRI#=VAL(CCUENTA):GOSUB 64770
3568 LPRINT TAB(1) CHR$(179);:LPRINT TAB(8-L.C%) GUAPRI$;
3570 LPRINT TAB(10) CHR$(179)+" "+FENA$;
3572 LPRINT TAB(21) CHR$(179);
3574 LPRINT TAB(25) CHR$(179)+" "+NOMBRE$;
3576 LPRINT TAB(58) L5$
3582 LPRINT TAB(1) CHR$(195)+STRING$(8,196)+CHR$(197);
3584 LPRINT TAB(11) STRING$(10,196)+CHR$(197);
3586 LPRINT TAB(22) STRING$(3,196)+CHR$(197);
3588 LPRINT TAB(26) STRING$(32,196);
3590 LPRINT TAB(58) L4$
3592 'LPRINT TAB(84) STRING$(14,196)+CHR$(197);
3594 'LPRINT TAB(99) STRING$(14,196)+CHR$(197);
3596 'LPRINT TAB(114) STRING$(15,196)+CHR$(180)
3642 LIN%=LIN%+2
3650 '
3660 RETURN
3670 '
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 IF LIN%= 90 THEN 3769
3756 LPRINT TAB(1) CHR$(192)+STRING$(8,196)+CHR$(193);
3757 LPRINT TAB(11) STRING$(10,196)+CHR$(193);
3758 LPRINT TAB(22) STRING$(3,196)+CHR$(193);
3759 LPRINT TAB(26) STRING$(32,196);
3760 LPRINT TAB(58) L6$
3761 'LPRINT TAB(84) STRING$(14,196)+CHR$(193);
3762 'LPRINT TAB(99) STRING$(14,196)+CHR$(193);
3763 'LPRINT TAB(114) STRING$(15,196)+CHR$(217)
3768 LPRINT CHR$(12)
3769 PAG%=PAG%+1:LIN%=0
3770 GOSUB 550
3772 LPRINT TAB(1) EMPRESA$;
3775 TITE$="RELACION DE ALUMNOS POR ASIGNATURAS PARA EL MES DE "+MES$(VAL(RIGHT$(FEDIA$,2)))+" DE 1.9"+LEFT$(FEDIA$,2):XX%=INT(LEN(TITE$)/2)
3780 LPRINT TAB(70-XX%) TITE$;
3790 LPRINT TAB(111) USING "HOJA NUMERO : #,###";PAG%
3800 LPRINT TAB(1) STRING$(LEN(EMPRESA$),"-");
3810 LPRINT TAB(70-XX%) STRING$(LEN(TITE$),"-");
3820 LPRINT TAB(111) STRING$(19,"-")
3840 LPRINT:LPRINT
3842 'GET #6,VAL(CGRUPO)
3844 LPRINT TAB(1) "Asignatura . "GGRUPO;
3846 LPRINT TAB(24) "Curso ...... "GCURSO;
3848 LPRINT TAB(44) "Texto ...... "GTEXTO;
3856 LPRINT TAB(116) "Grupo ...... "MID$(ACLAVE,4,1):LPRINT
3950 LPRINT:LPRINT
4000 LPRINT TAB(58) L1$
4002 LPRINT TAB(58) L2$
4050 LPRINT TAB(3) "NUMERO";
4055 LPRINT TAB(12) "F.NACIM.";
4057 LPRINT TAB(22) "   ";
4060 LPRINT TAB(33) "NOMBRE Y APELLIDOS";
4062 LPRINT TAB(58) L3$
4100 LPRINT TAB(1) CHR$(218)+STRING$(8,196)+CHR$(194);
4102 LPRINT TAB(11) STRING$(10,196)+CHR$(194);
4104 LPRINT TAB(22) STRING$(3,196)+CHR$(194);
4106 LPRINT TAB(26) STRING$(32,196);
4108 LPRINT TAB(58) L4$
4110 'LPRINT TAB(84) STRING$(14,196)+CHR$(194);
4112 'LPRINT TAB(99) STRING$(14,196)+CHR$(194);
4114 'LPRINT TAB(114) STRING$(15,196)+CHR$(191)
4128 LIN%=LIN%+7
4129 ANTEG$=CGRUPO
4130 RETURN
4140 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 '
4496 GOSUB 35000
4497 IF ASWA="0" THEN CLOSE:GOTO 4501
4498 GET #7,1:LSET ASWA="0":PUT #7,1:CLOSE
4499 SHELL "RESASIG.BAT"
4500 '
4501 GOSUB 8000
4502 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4503 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4504 '
4520 COLOR 10:LOCATE 6,17:PRINT "- LISTADO DE ALUMNOS POR ASIGNATURAS Y GRUPOS -"
4525 LOCATE 7,17:PRINT STRING$(47,205)
4526 COLOR 11:LOCATE 12,13:PRINT "TECLEA MES Y A¥O PARA EL CUAL QUIERES EL LISTADO: <    >"
4528 COLOR 15:LOCATE 16,25:PRINT " F10";
4530 COLOR 11:PRINT " Volver al Men£ Anterior"
4532 COLOR 15
4533 F.C1%=12:C.C1%=64:L.E%=4:BO%=4:ESC%=0:GOSUB 63000
4534 IF I.T%=0 THEN LOCATE 12,64:PRINT D.E$:GOTO 4537
4535 IF I.T%=2 THEN CLOSE:GOTO 9000
4536 CLOSE:BEEP:LOCATE 12,64:PRINT SPC(4):GOTO 4533
4537 FEDIA$=RIGHT$(D.E$,2)+LEFT$(D.E$,2)
4538 IF VAL(LEFT$(FEDIA$,2))<89 OR VAL(LEFT$(FEDIA$,2))>99 THEN 4536
4539 IF VAL(RIGHT$(FEDIA$,2))<1 OR VAL(RIGHT$(FEDIA$,2))>12 THEN 4536
4540 'GOSUB 550
4550 COLOR 11:LOCATE 14,13:PRINT "DESDE EL LA ASIGNATURA N§. : <   >      HASTA LA : <   >"
4552 COLOR 15:LOCATE 16,25:PRINT " F10";
4554 COLOR 11:PRINT " Volver al Men£ Anterior"
4560 COLOR 15
4590 'Pedimos numero de Orden DESDE.
4600 F.C1%=14:C.C1%=43:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
4610 IF I.T%=0 THEN LOCATE 14,43:PRINT D.E$:GOTO 4640
4620 IF I.T%=2 THEN CLOSE:GOTO 9000
4630 GOTO 4600
4640 DESDE$=D.E$
4645 '
4650 '
4660 '
4670 '
4680 GOSUB 8500:MEN$="- HASTA=999, hasta el Fin de Fichero -":GOSUB 8500
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=14:C.C1%=65:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
4710 IF I.T%=0 THEN LOCATE 14,65:PRINT D.E$:GOTO 4740
4720 IF I.T%=2 THEN CLOSE:GOTO 9000
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 HASTA$=D.E$
4780 '
4790 'Comprobamos DESDE con HASTA.
4800 IF DESDE$>HASTA$ THEN BEEP:GOSUB 8500:MEN$="N£mero. -DESDE- mayor que -HASTA-, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4801 PAG%=0:LIN%=90
4802   GOSUB 8500
4803   COLOR 10:LOCATE 22,15:PRINT " Datos Correctos ";
4804   COLOR 15:PRINT " ENTER ";
4805   COLOR 10:PRINT " Datos Erroneos ";
4806   COLOR 15:PRINT " F1 "
4807   COLOR 10
4814 A$=INPUT$(1)
4816 IF ASC(A$)=14 THEN 4550
4817 FALUDIR$="FRESALUM.ALF":FALUDAT$="FRESALUM.DAT"
4818 GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4819 GOSUB 30000:GOSUB 35000
4820 GOSUB 32000:GOSUB 34000:REG%=1:LIN%=90
4830 GOSUB 7000
4832 IF FF$="FF" THEN GOSUB 8500:MEN$="No tenemos los datos solicitados, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4834 WHILE FF$<>"FF"
4836       GRU$=LEFT$(ACLAVE,4)
4838       WHILE FF$<>"FF" AND GRU$=LEFT$(ACLAVE,4)
4840          ASI%=VAL(LEFT$(ACLAVE,3)):ALU%=VAL(RIGHT$(ACLAVE,5))
4842          GET #2,ALU%:GET #6,ASI%
4843          IF CMARCA="L" THEN 4846
4844          GOSUB 3500      'ESCRIBIR LINEA DETALLE
4846          GOSUB 7000      'LEER OTRO REGISTRO
4848             '
4850       WEND
4904       LPRINT TAB(1) CHR$(192)+STRING$(8,196)+CHR$(193);
4906       LPRINT TAB(11) STRING$(10,196)+CHR$(193);
4908       LPRINT TAB(22) STRING$(3,196)+CHR$(193);
4910       LPRINT TAB(26) STRING$(32,196);
4912       LPRINT TAB(58) L6$
4916       LIN%=90:LPRINT CHR$(12)
4962 WEND
4963 LIN%=90
4964 CLOSE
4966 GOTO 4501
4968 '
4969 '
4970 '
7000 '***************************************
7005 '***LECTURA DEL FICHERO DE NOTAS-ASIG***
7010 '***************************************
7015 '
7020 REG%=REG%+1
7025 GET #7,REG%
7030 IF REG%>AULTIMO% THEN FF$="FF":RETURN
7035 IF LEFT$(ACLAVE,3)<DESDE$ THEN 7020
7040 IF LEFT$(ACLAVE,3)>HASTA$ THEN REG%=AULTIMO%:GOTO 7020
7045 '
7050 RETURN
7055 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : HOSTELERIA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR HH%=1 TO 7500:NEXT HH%:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESCLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FRESALUM.ALF inexistente, pusar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FRESALUM.DAT inexistente, pusar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,14 AS CKAKA,10 AS CCUBAN,7 AS CKAK1,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,10 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FRESALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
32000 '**********************************
32010 '***APERTURA FICHERO ASIGNATURAS***
32020 '**********************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FRESASIG.DAT",70
32060 FIELD #6,1 AS GSTATUS,1 AS GMARCA,3 AS GGRUPO,1 AS GCICLO,2 AS GCURSO,50 AS GTEXTO,2 AS GALTAS,10 AS GLIBRE
32070 FIELD #6,1 AS GSTAT,1 AS GMAR,2 AS GULTIMO,12 AS GETIQUETA,54 AS GLIB1
32110 GET #6,1
32120  IF GETIQUETA="FRESASIG.DAT" THEN 32140
32130  GOSUB 8500:MEN$="- NO TENEMOS FICHERO DE ASIGNATURAS, PULSAR UNA TECLA -":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140  ULTGRU%=CVI(GULTIMO)
32150  RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ESCUELA DE HOSTELERIA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*******************************
34005 '***APERTURA FICHERO HIBRIDO ***
34010 '*******************************
34020 'Comprobamos la existencia del fichero.
34030 'NAME "FACTCALE.DAT" AS "FACTCALE.GUA"
34040 'NAME "FACTCALE.GUA" AS "FACTCALE.DAT"
34050 OPEN "R",#3,"FACACALE.DIR",12
34060 FIELD #3,6 AS HDIADIR,2 AS HNORDEN,1 AS HSTATUS,3 AS HRESTO
34070 FIELD #3,2 AS HULTIMO,10 AS HRESTO1
34080 GET #3,1
34090 HIBDIR%=CVI(HULTIMO):N.E%=HIBDIR%
34100 ' Apertura del fichero de datos   FACTCALE.DAT
34110 OPEN "R",#4,"FACACALE.DAT",20
34120 FIELD #4,6 AS HDIA,9 AS HDES,1 AS HSTAT,1 AS HSBIL,3 AS HFIL
34130 FIELD #4,2 AS HULTI,2 AS HULTACT,16 AS RESTO$
34140 GET #4,1
34150 HIBIMO%=CVI(HULTI)
34160 HIBACT%=HIBIMO%
34170 '
34180 RETURN
34190 '
34200 '
34210 '*************************
34220 '*************************
34230 '***BUSQUEDA DICOTOMICA***
34240 '*************************
34250 'El argumento de busqueda sobre 'BUSCA$'
34260 'El numero de registros sobre 'N.R%'
34270 'El REGISTRO ENCONTRADO SOBRE  N.L%
34280 N.L%=0:DES%=0:HAS%=0
34290 SWBUSCA%=0         'SW indicador de la busqueda.
34300 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34310 '
34320 R.I%=INT((N.E%/2)+.5)
34330 R.C%=R.I%
34340 IF R.I%<1 THEN SWBUSCA%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34460
34350 IF R.I%>N.E% THEN SWBUSCA%=1:DES%=R.I%-R.C%:HAS%=N.E%:GOTO 34460
34360 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34370 IF HDIADIR=BUSCA$ THEN N.L%=CVI(HNORDEN):GET #4,N.L%:RETURN     'Registro Buscado.
34380 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34390 IF HDIADIR>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34410
34400 R.I%=R.I%+R.C%
34410 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34340
34420 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34340
34430 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34340
34440 REG2%=R.I%:SW100%=0
34450 GOTO 34340
34460 'BUSQUEDA DE LOS ULTIMOS METIDOS.
34470 'BUSQUEDA DE LOS ULTIMOS METIDOS.
34480 SWBUSCA%=1
34490 FOR A% = DES% TO HAS%
34500 GET #1,A%
34510 IF HDIADIR=BUSCA$ THEN SWBUSCA%=0:N.L%=CVI(HNORDEN):GET #4,N.L%:RETURN     'Registro Buscado.
34520 NEXT A%
34530 FOR N.L%=HIBACT%+1 TO HIBIMO%
34540    GET #4,N.L%
34550    IF BUSCA$=HDIA THEN SWBUSCA%=0:WN.L%=N.L%:N.L%=ULTIMO%:GOTO 34560
34560 NEXT N.L%
34570 N.L%=WN.L%
34580 RETURN
34590 '
35000 '***********************************
35005 '***APERTURA FICHERO NOTAS-FALTAS***
35010 '***********************************
35015 '
35020 OPEN "R",#7,"FRESAUNO.DIR",20
35025 FIELD #7,2 AS AULTI,2 AS AULTA,9 AS ACLAVE,4 AS ALIBRE,1 AS ASWA,1 AS AMARCA,1 AS ASTA
35030 GET #7,1
35035  AULTIMO%=CVI(AULTI)
35040  AULACTU%=CVI(AULTA):N.R%=AULACTU%
35060 ' Apertura del fichero de datos   FRESAUNO.DAT
35065 OPEN "R",#8,"FRESAUNO.DAT",50
35070 FIELD #8,9 AS NCLAVE,2 AS NNO01,2 AS NSO01,1 AS NRE01,2 AS NFA01,2 AS NJU01
35075 FIELD #8,18 AS NFI01,2 AS NNO02,2 AS NSO02,1 AS NRE02,2 AS NFA02,2 AS NJU02
35080 FIELD #8,27 AS NFI02,2 AS NNO03,2 AS NSO03,1 AS NRE03,2 AS NFA03,2 AS NJU03,2 AS NNO04,2 AS NSO04,8 AS NFIL1,1 AS NMARCA,1 AS NSTAT
35085 FIELD #8,2 AS NULTI,2 AS NULTA,1 AS NSWA,43 AS NFI03,1 AS NMAR1,1 AS NSTA1
35090 GET #8,1
35095  NULTI%=CVI(NULTI)
35100  NULTA%=CVI(NULTA)
35130 RETURN
35135 '
35140 '*************************
35145 '***BUSQUEDA DICOTOMICA***
35150 '*************************
35155   '
35160 'El argumento de busqueda sobre 'BURRA$'
35165 'El numero de registros sobre 'N.R%'
35170 R.A%=0           'Donde devuelve el nro. de registro.
35175 SWESC%=0         'SW indicador de la busqueda.
35180 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
35185 '
35190 R.I%=INT((N.R%/2)+.5)
35195 R.C%=R.I%
35200 IF R.I%<1 THEN SWESC%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 35300
35205 IF R.I%>N.R% THEN SWESC%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 35300
35210 IF R.I%=1 THEN R.I%=2:GET #7,R.I% ELSE GET #7,R.I%
35215 RPROV$=ACLAVE
35220 IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
35225 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
35230 IF RPROV$>BURRA$ THEN R.I%=R.I%-R.C%:GOTO 35240
35235 R.I%=R.I%+R.C%
35240 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 35200
35245 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 35200
35250 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 35200
35255 REG2%=R.I%:SW100%=0
35260 GOTO 35200
35265 'Registro Buscado.
35270 FOR JMG%=N.R% TO AULTIMO%
35275     GET #7,JMG%
35280 RPROV$=ACLAVE
35285     IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
35290 NEXT JMG%
35295 RETURN
35300 FOR R.I%=DES% TO HAS%
35305     GET #7,R.I%
35310 RPROV$=ACLAVE
35315     IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
35320 NEXT R.I%
35325 GOTO 35265
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
