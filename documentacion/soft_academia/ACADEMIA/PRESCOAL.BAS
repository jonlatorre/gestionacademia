10 '                       PRESCOAL.BAS
20 '                      --------------
30 '       CONSULTA ALFABETICA DE ALUMNOS DE HOSTELERIA
40 '       --------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,G,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 GOTO 4500
2700 '***********************************
2702 '***PRESENTAMOS DATOS EN PANTALLA***
2704 '***********************************
2728 COLOR 15
2730 LOCATE 6,59:PRINT CNOMBRE
2740 LOCATE 7,14:PRINT CAPELL1
2750 LOCATE 7,59:PRINT CAPELL2
2760 LOCATE 8,14:PRINT CDIREC
2770 LOCATE 9,14:PRINT CPOBLA
2780 LOCATE 9,59:PRINT CPROVI
2790 LOCATE 10,14:PRINT CCOPOS
2800 LOCATE 10,59:PRINT CTELEFO+" "+CTELEFA
2810 LOCATE 11,14:PRINT CDNI
2820 LOCATE 11,59:PRINT CCURSO
2825 IF CCURSO="1" THEN LOCATE 11,62:PRINT "PRIMERO":GOTO 2840
2826 IF CCURSO="2" THEN LOCATE 11,62:PRINT "SEGUNDO":GOTO 2840
2828 IF CCURSO="3" THEN LOCATE 11,62:PRINT "TERCERO":GOTO 2840
2830 IF CCURSO="4" THEN LOCATE 11,62:PRINT "CUARTO":GOTO 2840
2832 LOCATE 11,62:PRINT "QUINTO":GOTO 2840
2840 LOCATE 12,14:PRINT RIGHT$(CFENA,2)"-"MID$(CFENA,3,2)"-"LEFT$(CFENA,2)
2845 LOCATE 12,59:PRINT RIGHT$(CFALTA,2)"-"MID$(CFALTA,3,2)"-"LEFT$(CFALTA,2)
2871 BUSCA$=CBANCO:GOSUB 34260
2872 IF SW1%=0 THEN BA$=LEFT$(BNOM,25):BANC$=CBANCO:GOTO 2874
2873 BA$="NO EXISTE":BANC$=""
2874 LOCATE 13,14:PRINT BANC$
2875 LOCATE 13,19:PRINT BA$
2876 IF CBANCO<>"2095" THEN 2881
2877 CUPRI$=LEFT$(CCUBAN,10):EDI$="1":GOSUB 37000
2878 IF DI%=1 THEN 2881
2879 '
2880 LOCATE 13,59:PRINT CUPRIE$:GOTO 2882
2881 LOCATE 13,59:PRINT CCUBAN
2882 LOCATE 14,14:PRINT CSUCUR"-"CDIGI
2884 GUAPRI#=CVD(CIMPOR):GOSUB 64770
2886 LOCATE 14,66-L.C%:PRINT GUAPRI$
2887 LOCATE 16,23:PRINT COBSE1
2888 LOCATE 17,23:PRINT COBSE2
2889 'LOCATE 18,23:PRINT COBSE3
2890 'GOTO 2970    'IF I.A%<>4 THEN 2970
2962 GUAPRI#=VIVAS%:GOSUB 64770:COLOR 11
2964 LOCATE 21,29:PRINT "Alumnos en vigor : ":COLOR 15
2966 LOCATE 21,52-L.C%:PRINT GUAPRI$
2970 IF CMARCA<>"L" THEN RETURN
2972 COLOR 19:LOCATE 4,61:PRINT "ALUMNO DADO DE BAJA":COLOR 15
2975 RETURN
4500 '**************************
4502 '***CONSULTA ALFABETICA ***
4506 '**************************
4508 FALUDIR$="FRESALUM.ALF":FALUDAT$="FRESALUM.DAT"
4510 'GOSUB 8000:GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
4520 GOSUB 30010        'Apertura Fichero ALUMNOS
4530 IF CSWA="0" THEN CLOSE:GOTO 4550
4532 GET #2,1
4534 LSET CSWA="0"
4535 LSET CULTACT=MKI$(ULTIMO%)
4536 PUT #2,1
4538 CLOSE
4540 CLS
4545 SHELL "RESALFA.BAT"
4550 '
4551 FALUDIR$="FRESALUM.ALF":FALUDAT$="FRESALUM.DAT"
4552 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
4554 GOSUB 30010        'Apertura Fichero ALUMNOS
4555 GOSUB 34010        'Apertura Fichero BANCOS
4556 'GOSUB 32000         'APERTURA FICHERO DE GRUPOSS
4560 TIT$="- CONSULTA ALFABETICA DE ALUMNOS HOSTELERIA -"
4570 '
4580 GOSUB 8000:GOSUB 36000
4590 GOSUB 7800
4600 '
4750       '***********************
4760       '***ENTRADA APELLIDO1***
4770       '***********************
4775       N%=0
4780       F.C1%=8:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
4790       IF I.T%=0 THEN APELL1$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$)):GOTO 4860
4800       IF I.T%=3 THEN 4760
4840       IF I.T%=4 THEN 9000
4850       GOTO 4760
4860       IF LEN(D.E$)<20 THEN APELL1$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
4870       LOCATE 8,37:PRINT APELL1$
4880       '
4890       '***********************
4900       '***ENTRADA APELLIDO2***
4910       '***********************
4920       F.C1%=9:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
4930       IF I.T%=0 THEN APELL2$=D.E$:N%=N%+1:GOTO 5000
4960       IF I.T%=3 THEN 4760
4970       IF I.T%=4 THEN 9000               'Fin Entrada.
4980       IF I.T%=1 THEN 5030               'Fin Entrada.
5000       IF LEN(D.E$)<20 THEN APELL2$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
5010       LOCATE 9,37:PRINT APELL2$
5015       '********************
5016       '***ENTRADA NOMBRE***
5017       '********************
5018       'N%=0
5019       F.C1%=10:C.C1%=37:L.E%=20:BO%=20:GOSUB 64000
5020       IF I.T%=0 THEN NOMBRE$=D.E$:N%=N%+2:GOTO 5024
5021       IF I.T%=1 THEN 5026
5022       IF I.T%=4 THEN 9000               'Fin Entrada.
5023       GOTO 5016
5024       IF LEN(D.E$)<20 THEN NOMBRE$=D.E$+LEFT$(BLANCO$,20-LEN(D.E$))
5025       LOCATE 10,37:PRINT NOMBRE$
5026       '
5030   '*************************************
5035   '***PEDIMOS CONFORMIDAD A LOS DATOS***
5040   '*************************************
5042   GOSUB 8500
5044   COLOR 10:LOCATE 23,15:PRINT "Datos Correctos ";
5046   COLOR 15:PRINT " ENTER ";
5048   COLOR 10:PRINT " Datos Erroneos ";
5050   COLOR 15:PRINT " F1 "
5052   COLOR 10
5054   '
5056   A$=INPUT$(1)
5060   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 5056
5062   '
5064   IF ASC(A$)=14 THEN 4580
5066   SIHAY%=0
5100        IF N%=3 THEN BURRA$=APELL1$+APELL2$+NOMBRE$:GOTO 5109
5102        IF N%=1 THEN BURRA$=APELL1$+APELL2$:GOTO 5109
5103      ' IF N%=2 THEN BURRA$=APELL1$+APELL2$:GOTO 5109
5104        BURRA$=APELL1$
5109        GOSUB 6000
5110        IF SWESC%=1 AND ULTIMO%=ULTACT% THEN GOSUB 8500:MEN$=" Este Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
5112        IF SWESC%=1 THEN 5300
5115        GET #2,CVI(FULTI)
5120        GOSUB 8000:GOSUB 31000:LOCATE 6,14:PRINT USING"##,###";VAL(CCUENTA)
5121        GOSUB 2700
5122        R.A%=R.A%+1:GET #1,R.A%
5124        IF N%=3 THEN BUSCO$=FAPE1+FAPE2+FCALLA:GOTO 5134
5125        IF N%=1 THEN BUSCO$=FAPE1+FAPE2:GOTO 5134
5126      ' IF N%=2 THEN BUSCO$=FAPE1+FAPE2:GOTO 5134
5128        BUSCO$=FAPE1
5134        IF BURRA$<>BUSCO$ THEN 5220
5135        GOSUB 8500:MEN$="Hay m s Alumnos iguales, para verlos ENTER , si n¢ F1":GOSUB 8500
5136        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5136
5137        IF ASC(A$)=14 THEN 5240 ELSE 5115
5170        '
5180        '
5220        IF ULTIMO%=ULTACT% THEN 5240
5230        GOSUB 8500:MEN$="Para ver si hay m s Alumnos iguales, pulsa ENTER , si n¢ F1":GOSUB 8500
5231        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5231
5232        IF ASC(A$)=14 THEN 5240
5233        GOTO 5300
5240        GOSUB 8500:MEN$="  Pulse una tecla para nueva consulta ":GOSUB 8500
5250        A$=INPUT$(1)
5260        GOTO 4580
5270        '
5280        '
5300        FOR R.A%=ULTACT% TO ULTIAL%
5302        GET #1,R.A%
5304        IF N%=3 THEN BUSCO$=FAPE1+FAPE2+FCALLA:GOTO 5312
5306        IF N%=1 THEN BUSCO$=FAPE1+FAPE2:GOTO 5312
5308      ' IF N%=2 THEN BUSCO$=FAPE1+FAPE2:GOTO 5312
5310        BUSCO$=FAPE1
5312        IF BURRA$<>BUSCO$ THEN 5400
5313        GET #2,CVI(FULTI)
5314        GOSUB 8000:GOSUB 31000:LOCATE 6,14:PRINT USING"##,###";VAL(CCUENTA)
5316        GOSUB 2700:SIHAY%=1
5318        GOSUB 8500:MEN$="Para ver si hay m s Alumnos iguales, pulsa ENTER , si n¢ F1":GOSUB 8500
5320        A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5320
5322        IF ASC(A$)=14 THEN 5240
5400        NEXT R.A%
5402        IF SIHAY%=1 THEN 5240
5406        GOSUB 8500:MEN$=" Este Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4580
6000 '*************************
6010 '***BUSQUEDA DICOTOMICA***
6020 '*************************
6024   '
6030 'El argumento de busqueda sobre 'BURRA$'
6040 'El numero de registros sobre 'N.R%'
6050 R.A%=0           'Donde devuelve el nro. de registro.
6060 SWESC%=0         'SW indicador de la busqueda.
6070 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
6080 '
6090 R.I%=INT((N.R%/2)+.5)
6100 R.C%=R.I%
6110 IF R.I%<1 THEN SWESC%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 7000
6115 IF R.I%>N.R% THEN SWESC%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 7000
6120 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
6122 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 6130
6124 IF N%=1 THEN RPROV$=FAPE1+FAPE2:GOTO 6130
6126 'IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 6130
6128 RPROV$=FAPE1
6130 IF RPROV$=BURRA$ THEN 6220      'Registro Buscado.
6140 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
6150 IF RPROV$>BURRA$ THEN R.I%=R.I%-R.C%:GOTO 6170
6160 R.I%=R.I%+R.C%
6170 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 6110
6180 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 6110
6190 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 6110
6200 REG2%=R.I%:SW100%=0
6210 GOTO 6110
6220 'Registro Buscado.
6230 FOR JMG%=R.I% TO 1 STEP -1
6240     GET #1,JMG%
6242 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 6250
6244 IF N%=1 THEN RPROV$=FAPE1+FAPE2:GOTO 6250
6246 'IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 6250
6248 RPROV$=FAPE1
6250     IF RPROV$<>BURRA$ THEN R.A%=JMG%+1:JMG%=-5
6260 NEXT JMG%
6270 IF JMG%=0 THEN R.B%=1
6280 IF JMG%=0 THEN R.B%=1
6290 IF SWESC%=0 THEN GET #1,R.A%
6295 RETURN
7000 FOR R.I%=DES% TO HAS%
7010     GET #1,R.I%
7020 IF N%=3 THEN RPROV$=FAPE1+FAPE2+FLLAMA:GOTO 7055
7030 IF N%=1 THEN RPROV$=FAPE1+FAPE2:GOTO 7055
7040 'IF N%=2 THEN RPROV$=FAPE1+FAPE2:GOTO 7055
7050 RPROV$=FAPE1
7055     IF RPROV$=BURRA$ THEN SWESC%=0:GOTO 6220
7060 NEXT R.I%
7070 RETURN
7800 '***********************************************************
7810 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION CONSULTAS     ***
7820 '***********************************************************
7830 GOSUB 8500:LOCATE 23,14
7860 COLOR 10:PRINT " F7 =>";
7870 COLOR 15:PRINT " Fin de Consultas de Alumnos "
7880 '
7890 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : HOSTELERIA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 22,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 23,1:PRINT CHR$(186):COLOR 11:LOCATE 23,3:PRINT "Mensajes: ":COLOR 10:LOCATE 23,80:PRINT CHR$(186)
8100 LOCATE 24,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 23,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESCLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FRESALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FRESALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,14 AS CKAKA,10 AS CCUBAN,7 AS CKAK1,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,10 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FRESALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1):LSET CULTACT=MKI$(1)
30124 'LSET CFICHER="FRESALUM.DAT":LSET CSWG="0":LSET CSWA="0"
30126 'LSET CVIGO=MKI$(0):LSET REST1$="":LSET CFEFAC="0000":LSET REST2$=""
30128 'PUT #2,1
30130 'CLOSE
30132 'STOP
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 4,X%:PRINT TIT$
31042 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 6,1:PRINT "Alumno N§ : <" SPACE$(6) ">"
31052 LOCATE 6,46:PRINT "Nombre    : <" SPACE$(20) ">"
31054 LOCATE 7,1:PRINT "Apellido 1: <" SPACE$(20) ">"
31056 LOCATE 7,46:PRINT "Apellido 2: <" SPACE$(20) ">"
31058 LOCATE 8,1:PRINT "Direcci¢n : <" SPACE$(40) ">"
31060 LOCATE 9,1:PRINT "Poblaci¢n : <" SPACE$(20) ">"
31062 LOCATE 9,46:PRINT "Provincia : <" SPACE$(15) ">"
31064 LOCATE 10,1:PRINT "CD.Postal : <" SPACE$(5) ">"
31066 LOCATE 10,46:PRINT "Tel‚fonos : <" SPACE$(15) ">"
31068 LOCATE 11,1:PRINT "D.N.I.    : <" SPACE$(13) ">"
31070 LOCATE 11,46:PRINT "Curso     : <" SPACE$(1) ">"
31092 LOCATE 12,1:PRINT "F. Nacim. : <"SPACE$(8) ">"
31094 LOCATE 12,46:PRINT "F. Alta.  : <        >"
31196 LOCATE 13,1:PRINT "Banco     : <" SPACE$(4) ">"
31198 LOCATE 13,46:PRINT "N.Cuenta  : <" SPACE$(10) ">"
31199 LOCATE 14,1:PRINT "Ofi.-Digi.: <" SPACE$(7) ">"
31200 LOCATE 14,46:PRINT "Importe   : <" SPACE$(8) ">"
31201 LOCATE 16,6:PRINT "Observaciones 1 <" SPACE$(50) ">"
31202 LOCATE 17,6:PRINT "              2 <" SPACE$(50) ">"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ESCUELA DE HOSTELERIA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*****************************
34010 '***APERTURA FICHERO BANCOS***
34020 '*****************************
34030 FICHE1$="FACABANC.DAT"
34040 '
34050 'APERTURA FICHERO DE DATOS.
34060 OPEN "R",#3,FICHE1$,125
34070 'REGISTRO 1.
34080 FIELD #3,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
34090 'REGISTRO DE DATOS.
34100 FIELD #3,4 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,5 AS BFIL001,1 AS BSTATUS
34110 GET #3,1
34120 IF BETIQUETA="FACABANC.DAT" THEN 34160  'CONTINUAR
34130 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
34140 A$=INPUT$(1)
34150 GOTO 9000
34160 'CONTINUAR.
34170 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
34180 ULTBAN%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
34190 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
34200 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
34210 N.B%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
34220 '
34230 RETURN
34240 '
34250 '*************************
34260 '***LECTURA AZAR BANCOS***
34270 '*************************
34280 'El argumento de busqueda sobre 'BUSCA$'
34290 'El numero de registros sobre 'N.B%'
34300 R.B%=0           'Donde devuelve el nro. de registro.
34310 R.W%=0           'Donde devuelve el nro. de registro del DIR.
34320 SW1%=0         'SW indicador de la busqueda.
34330 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34340 '
34350 R.I%=INT((N.B%/2)+.5)
34360 R.C%=R.I%
34370 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34490
34380 IF R.I%>N.B% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.B%:GOTO 34490
34390 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34400 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 34660      'MIRAR STATUS
34410 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34420 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34440
34430 R.I%=R.I%+R.C%
34440 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34370
34450 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34370
34460 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34370
34470 REG2%=R.I%:SW100%=0
34480 GOTO 34370
34490 'REGISTRO NO BUSCADO.
34500 XX%=0
34510 FOR A%=DES% TO HAS%
34520   GET #3,A%
34530   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
34540 NEXT A%
34550 IF XX%=1 THEN 34660
34560 '
34570 '
34580 'Registro NO BUSCADO
34590 XX%=0
34600 IF N.B%=0 THEN N.B%=1
34610 FOR JMG%=N.B% TO ULTBAN%
34620     GET #3,JMG%
34630     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
34640 NEXT JMG%
34650 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
34660 'MIRAMOS STATUS.
34670 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
34680 WSTATUS$=""
34690 RETURN
34700 '
36000 '**********************************************
36010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
36020 '**********************************************
36030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
36040 LOCATE 5,X%:PRINT TIT$
36042 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
36052 LOCATE 8,24:PRINT "Apellido 1: <" SPACE$(20) ">"
36054 LOCATE 9,24:PRINT "Apellido 2: <" SPACE$(20) ">"
36056 LOCATE 10,24:PRINT "Nombre    : <" SPACE$(20) ">"
36058 RETURN
37000 '*******************************************************************
37010 '*                                                                 *
37020 '*  RUTINA PARA VERIFICAR NUMEROS DE CUENTA Y SUS DIGITOS          *
37030 '*                                                                 *
37040 '*  ENTRADA NUMERO CUENTA EN -- CUPRI$ --                          *
37050 '*                                                                 *
37060 '*  SALIDA BUENA   DI%     = 0                                     *
37070 '*                 CUPRIF$ = CUENTA FORMATEADA PARA FICHEROS       *
37080 '*                 CUPRIE$ = CUENTA EDITADA PARA LISTADOS          *
37090 '*                                                                 *
37100 '*  SALIDA MALA    DI% = 1                                         *
37110 '*                                                                 *
37120 '*******************************************************************
37130 DIG%=0:N1%=0:N2%=0:N3%=0:N4%=0:N5%=0:N6%=0:N7%=0:N8%=0:N9%=0:DI%=0:N10%=0
37140 IF MID$(CUPRI$,1,1)>"2" THEN 38000
37145 IF MID$(CUPRI$,1,2) = "27" THEN 37210
37150 IF MID$(CUPRI$,1,1) <> "2" THEN 37210
37155 IF LEN(CUPRI$)<6 THEN 38730
37160 LN%=LEN(CUPRI$)-4:LC%=10-LEN(CUPRI$)
37170 CUPRIF$=LEFT$(CUPRI$,4)+LEFT$(CERO$,LC%)+RIGHT$(CUPRI$,LN%)
37180 CUPRIE$=CUPRIF$
37190 IF EDI$="1" THEN EDI$="0":RETURN
37200 D.E$="0"+MID$(CUPRIF$,2,9):GOTO 37380
37210 '
37285  IF MID$(CUPRI$,1,2) = "10" OR MID$(CUPRI$,1,2)="15" THEN 37300
37290 GOTO 38730
37300 IF LEN(CUPRI$)<4 THEN 38730
37305 LN%=LEN(CUPRI$)-2:LC%=10-LEN(CUPRI$)
37310 CUPRIF$=MID$(CUPRI$,1,2)+LEFT$(CERO$,LC%)+MID$(CUPRI$,3,LN%)
37320 CUPRIE$=CUPRIF$
37330 IF EDI$="1" THEN EDI$="0":RETURN
37340 D.E$=MID$(CUPRIF$,1,2)+LEFT$(CERO$,LC%)+RIGHT$(CUPRIF$,LN%)
37350 ' IF MID$(D.E$,1,2) = "30" THEN 37370
37360 IF MID$(D.E$,3,2)<"11" THEN MID$(D.E$,1,2)="00"
37365 GOTO 37380
37370 '
37380 '
37390 '
37400 N1$=STR$(VAL(MID$(D.E$,9,1))*2)
37410 IF LEN(N1$)=2 THEN 37430
37420 N1%=N1%+VAL(MID$(N1$,3,1))
37430 N1%=N1%+VAL(MID$(N1$,2,1))
37440 N2%=VAL(MID$(D.E$,8,1))
37450 N3$=STR$(VAL(MID$(D.E$,7,1))*2)
37460 IF LEN(N3$)=2 THEN 37480
37470 N3%=N3%+VAL(MID$(N3$,3,1))
37480 N3%=N3%+VAL(MID$(N3$,2,1))
37490 N4%=VAL(MID$(D.E$,6,1))
37500 N5$=STR$(VAL(MID$(D.E$,5,1))*2)
37510 IF LEN(N5$)=2 THEN 37530
37520 N5%=N5%+VAL(MID$(N5$,3,1))
37530 N5%=N5%+VAL(MID$(N5$,2,1))
37540 N6%=VAL(MID$(D.E$,4,1))
37550 N7$=STR$(VAL(MID$(D.E$,3,1))*2)
37560 IF LEN(N7$)=2 THEN 37580
37570 N7%=N7%+VAL(MID$(N7$,3,1))
37580 N7%=N7%+VAL(MID$(N7$,2,1))
37590 N8%=VAL(MID$(D.E$,2,1))
37600 N9$=STR$(VAL(MID$(D.E$,1,1))*2)
37610 IF LEN(N9$)=2 THEN 37630
37620 N9%=N9%+VAL(MID$(N9$,3,1))
37630 N9%=N9%+VAL(MID$(N9$,2,1))
37640 'N10%=VAL(MID$(D.E$,1,1))
37650 DIG%=N1%+N2%+N3%+N4%+N5%+N6%+N7%+N8%+N9%    '+N10%
37660 DIG%=100-DIG%
37670 GOTO 38670
38000 IF MID$(CUPRI$,1,2)="38" THEN 38300
38010 IF MID$(CUPRI$,1,2)="75" THEN 38300
38020 IF MID$(CUPRI$,1,2)="75" THEN 38300
38030 IF MID$(CUPRI$,1,2)="80" THEN 38300
38040 IF MID$(CUPRI$,1,2)="90" THEN 38300
38300 IF LEN(CUPRI$)<4 THEN 38730
38305 LN%=LEN(CUPRI$)-2:LC%=10-LEN(CUPRI$)
38310 CUPRIF$=MID$(CUPRI$,1,2)+LEFT$(CERO$,LC%)+MID$(CUPRI$,3,LN%)
38320 CUPRIE$=CUPRIF$
38330 IF EDI$="1" THEN EDI$="0":RETURN
38340 D.E$=MID$(CUPRIF$,1,2)+LEFT$(CERO$,LC%)+RIGHT$(CUPRIF$,LN%)
38350 N1%=VAL(MID$(D.E$,9,1))*2
38360 N2%=VAL(MID$(D.E$,8,1))*3
38370 N3%=VAL(MID$(D.E$,7,1))*4
38380 N4%=VAL(MID$(D.E$,6,1))*5
38390 N5%=VAL(MID$(D.E$,5,1))*6
38400 N6%=VAL(MID$(D.E$,4,1))*7
38410 N7%=VAL(MID$(D.E$,3,1))*8
38420 N10%=N1%+N2%+N3%+N4%+N5%+N6%+N7%
38430 N11%=INT(N10%/11)
38440 DIG%=N10%-(N11%*11)
38442 IF DIG%=1 THEN D$="0":GOTO 38720
38444 IF DIG%=2 THEN D$="9":GOTO 38720
38446 IF DIG%=3 THEN D$="8":GOTO 38720
38448 IF DIG%=4 THEN D$="7":GOTO 38720
38450 IF DIG%=5 THEN D$="6":GOTO 38720
38452 IF DIG%=6 THEN D$="5":GOTO 38720
38454 IF DIG%=7 THEN D$="4":GOTO 38720
38456 IF DIG%=8 THEN D$="3":GOTO 38720
38458 IF DIG%=9 THEN D$="2":GOTO 38720
38460 IF DIG%=10 THEN D$="1":GOTO 38720
38462 IF DIG%=0 THEN D$="1":GOTO 38720
38670 D$=STR$(DIG%)
38720 IF RIGHT$(D$,1)=RIGHT$(D.E$,1) THEN DI%=0:RETURN
38730 DI%=1:RETURN
38740 '
38750 ' ***** FINAL DE LA RUTINA DE NUMERO DE CUENTA *****
38760 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
