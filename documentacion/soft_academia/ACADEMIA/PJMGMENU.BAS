10 '                         PJMGMENU.BAS
20 '                       ---------------
30 '                MENU GENERAL DE APLICACIONES
40 '
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
81 FOR I%=1 TO 10:KEY I%,CHR$(0):NEXT I%
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"/"+MID$(FDIA$,3,2)+"/"+LEFT$(FDIA$,2)
94 '
100 '*****************************************************************
110 '***COMPROBAMOS QUE LOS FICHEROS PRINCIPALES ESTAN CLASIFICADOS***
120 '*****************************************************************
130 '
140 '***PLAN CONTABLE****
150 DEFSTR D,P:GOSUB 30010   'APERTURA FICHERO DE PLAN CONTABLE.
160 CLOSE
170 IF ULTDIR%=ACTDIR% THEN 320  'CONTINUAR CON EL PROGRAMA.
180 '
190 FICBAT$="ACOPLANC.BAT;"+DIRE$+";"+PROGRAMA$
200 '
210 CLS
220 SHELL FICBAT$
230 CLS
240 CLOSE
250 GOSUB 30010
260 GET #1,1
270 ULTIMO%=ALTDIR%+1
280 LSET DULTIMO=MKI$(ULTIMO%)
290 LSET DULTACT=MKI$(ULTIMO%)
300 PUT #1,1
310 CLOSE
320 '
330 '***GRUPOS Y SUBGRUPOS CONTABLES***
340 'MIRAMOS SI ESTA EL FICHERO ACTUALIZADO.
342 DEFSTR G:GOSUB 31010   'APERTURA FICHERO DE PLAN CONTABLE.
344 CLOSE
346 IF ULTDIR%=ACTDIR% THEN 376  'CONTINUAR CON EL PROGRAMA.
348 '
350 FICBAT$="ACOCONCE.BAT;"+DIRE$+";"+PROGRAMA$
352 '
354 CLS
356 SHELL FICBAT$
358 CLS
360 CLOSE
362 GOSUB 31010
364 GET #1,1
366 ULTIMO%=ALTDIR%+1
368 LSET GULTIMO=MKI$(ULTIMO%)
370 LSET GULTACT=MKI$(ULTIMO%)
372 PUT #1,1
374 CLOSE
376 '
378 '***ARTICULOS****
379 GOTO 414
380 DEFSTR A:GOSUB 31254   'APERTURA FICHERO DE ARTICULOS.
382 CLOSE
384 IF ULTDIRA%=ACTDIRA% THEN 414  'CONTINUAR CON EL PROGRAMA.
386 '
388 FICBAT$="AALCNART.BAT;"+DIRE$+";"+PROGRAMA$
390 '
392 CLS
394 SHELL FICBAT$
396 CLS
398 CLOSE
400 GOSUB 31254
402 GET #3,1
404 ULTIMO%=ALTDIRA%+1
406 LSET ADULTIMO=MKI$(ULTIMO%)
408 LSET ADULTACT=MKI$(ULTIMO%)
410 PUT #3,1
412 CLOSE
414 '****BANCOS****
416 DEFSTR B:GOSUB 31402   'APERTURA FICHERO DE PLAN CONTABLE.
418 CLOSE
420 IF ULTDIR%=ACTDIR% THEN 446   'CONTINUAR CON EL PROGRAMA.
422 FICBAT$="AFABANCO.BAT;"+DIRE$+";"+PROGRAMA$
424 CLS
426 SHELL FICBAT$
428 CLS
430 CLOSE
432 GOSUB 31402
434 GET #1,1
436 ULTIMO%=ALTDIR%+1
438 LSET BULTIMO=MKI$(ULTIMO%)
440 LSET BULTACT=MKI$(ULTIMO%)
442 PUT #1,1
444 CLOSE
446 '
490 '*********************************
500 '***PRESENTAMOS EL MENU GENERAL***
510 '*********************************
520 TIT$="- MENU GENERAL APLICACIONES -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 4,X%:PRINT TIT$
560 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
570 COLOR 15:LOCATE 7,5:PRINT "1.- ";:COLOR 11:PRINT "APLICACION DE CONTABILIDAD."
580 COLOR 15:LOCATE 9,5:PRINT "2.- ";:COLOR 11:PRINT "APLICACION DE LA FLOTA."
590 COLOR 15:LOCATE 11,5:PRINT "3.- ";:COLOR 11:PRINT "APLICACION DE FACTURACION."
600 COLOR 15:LOCATE 13,5:PRINT "4.- ";:COLOR 11:PRINT "No se usa."
610 COLOR 15:LOCATE 15,5:PRINT "5.- ";:COLOR 11:PRINT "No se usa."
620 COLOR 15:LOCATE 17,5:PRINT "6.- ";:COLOR 11:PRINT "No se usa."
630 COLOR 15:LOCATE 7,43:PRINT " 7.- ";:COLOR 11:PRINT "No se usa."
640 COLOR 15:LOCATE 9,43:PRINT " 8.- ";:COLOR 11:PRINT "No se usa."
650 COLOR 15:LOCATE 11,43:PRINT " 9.- ";:COLOR 11:PRINT "No se usa."
660 COLOR 15:LOCATE 13,43:PRINT "10.- ";:COLOR 11:PRINT "No se usa."
670 COLOR 15:LOCATE 15,43:PRINT "11.- ";:COLOR 11:PRINT "No se usa."
680 COLOR 15:LOCATE 17,43:PRINT "12.- ";:COLOR 11:PRINT "COPIA DE SEGURIDAD FICHEROS."
690 '
700 LOCATE 19,32:COLOR 12:PRINT " ESC ";:COLOR 10
710 PRINT " Finalizar"
712 LOCATE 19,60:PRINT "OPCION : <  >":COLOR 15
720 '
730 'Pedimos opcion.
740 F.C1%=19:C.C1%=70:L.E%=2:BO%=0:GOSUB 64000
750 IF I.T%=4 THEN 9000   'FINALIZAR
760 IF I.T%<>0 THEN 740
770 OPC$=D.E$
780 OPC%=VAL(OPC$)
790 IF OPC%<1 OR OPC%>12 THEN 740
800 ON OPC% GOTO 1010,1110,1210,1310,1410,1510,1610,1710,1810,1910,2010,2110
810 '
820 '
1000 '                 ********************************
1010 '                 ***APLICACION DE CONTABILIDAD***
1019 '                 ********************************
1020 DEFSTR M
1021 FICHE1$="\"+DIRE$+"\FCONCOMA.CLA":FICHE2$="\"+DIRE$+"\FCONCOMA.GUA"
1022 NAME FICHE1$ AS FICHE2$
1023 NAME FICHE2$ AS FICHE1$
1024 CLOSE
1025 GOSUB 30860
1026 IF MFDIA=FDIA$ THEN 1048
1028 IF MSW="1" THEN 1048
1030 CLOSE
1032 OPEN "O",#1,"FENCADEN.DAT"
1034 WRITE #1,"A","ACOCONSU.BAT","PCONMENU"
1036 CLOSE
1038 RUN "PCONDESM"
1040 '
1048 CLOSE
1049 RUN "PCONMENU"
1050 '
1100 '                 *****************************
1110 '                 ***APLICACION DE ALMACENES***
1120 '                 *****************************
1130 CLOSE
1138 'RUN "PALMMENU"
1140 GOTO 500
1150 '
1200 '                 *******************************
1210 '                 ***APLICACION DE FACTURACION***
1220 '                 *******************************
1230 CLOSE
1238 RUN "PFACMENU"
1240 GOTO 500
1250 '
1300 '                 ******************************
1310 '                 ***                        ***
1320 '                 ******************************
1330 CLOSE
1340 GOTO 500
1350 '
1400 '                 *****************************
1410 '                 ***                       ***
1420 '                 *****************************
1430 CLOSE
1440 GOTO 500
1450 '
1500 '                 ***************************
1510 '                 ***                     ***
1520 '                 ***************************
1530 CLOSE
1540 GOTO 500
1550 '
1600 '                 ***********************
1610 '                 ***CAMBIO DE EMPRESA***
1620 '                 ***********************
1630 CLOSE
1640 GOTO 500
1650 '
1700 '                 ******************************
1710 '                 ***INICIALIZACION DE DISCOS***
1720 '                 ******************************
1722 '
1724 CLOSE
1726 GOTO 500
1728 '
1760 '
1810 '                 ***********************
1820 '                 ***                 ***
1830 '                 ***********************
1840 CLOSE
1850 GOTO 500
1910 '                 ***********************
1920 '                 ***                 ***
1930 '                 ***********************
1940 CLOSE
1950 GOTO 500
1960 '
2010 '                 ***********************
2020 '                 ***                 ***
2030 '                 ***********************
2040 CLOSE
2050 GOTO 500
2060 '
2110 '                 ***********************
2120 '                 ***                 ***
2130 '                 ***********************
2135 SHELL "JMGBACK.BAT"
2140 CLOSE
2150 GOTO 500
2160 '
8000 '**************************
8010 '***CARATULA DE PANTALLA***
8020 '**************************
8030 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8040 LOCATE 2,1:PRINT CHR$(186)
8050 COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10
8060 LOCATE 2,34:PRINT "Aplicaci¢n : ";:COLOR 11:PRINT "MENU GENERAL"
8070 LOCATE 2,63:COLOR 10:PRINT "FECHA : ";:COLOR 11:PRINT FECHA$
8080 COLOR 10:LOCATE 2,80:PRINT CHR$(186)
8090 LOCATE 3,1:PRINT CHR$(200) STRING$(78,205) CHR$(188)
8140 LOCATE 21,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8150 LOCATE 22,1:PRINT CHR$(186)
8160 COLOR 11:LOCATE 22,3:PRINT "Mensajes :":COLOR 10
8170 LOCATE 22,80:PRINT CHR$(186)
8180 LOCATE 23,1:PRINT CHR$(200) STRING$(78,205) CHR$(188);
8190 RETURN
8200 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 SYSTEM
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERL=1022 THEN RESUME 1030
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '***************************************
30010 '***APERTURA FICHERO DE PLAN CONTABLE***
30020 '***************************************
30022 FICHE1$="\"+DIRE$+"\FCONPLAC.DIR"
30024 FICHE2$="\"+DIRE$+"\FCONPLAC.DAT"
30030 'APERTURA FICHERO DE INDICES.
30040 OPEN "R",#1,FICHE1$,19
30042 'REGISTRO 1.
30050 FIELD #1,2 AS DULTIMO,2 AS DALTAS,2 AS DULTACT,12 AS DETIQUETA,1 AS DSTATUS
30052 'REGISTRO DE DATOS.
30054 FIELD #1,7 AS DCUENTA,2 AS DNREG,9 AS DFILLER,1 AS DSTATUS
30060 GET #1,1
30070 IF DETIQUETA="FCONPLAC.DIR" THEN 30110  'CONTINUAR
30080 GOSUB 8500:MEN$="NO EXISTE FICHERO INDICES PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30090 A$=INPUT$(1)
30100 GOTO 9000
30110 'CONTINUAR.
30120 ULTDIR%=CVI(DULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30122 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30130 ALTDIR%=CVI(DALTAS)     'NUMERO DE ALTAS EN DIR.
30140 ACTDIR%=CVI(DULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30142 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30150 '
30160 'APERTURA FICHERO DE DATOS.
30170 OPEN "R",#2,FICHE2$,390
30180 'REGISTRO 1.
30190 FIELD #2,2 AS PULTIMO,2 AS PALTAS,12 AS PETIQUETA,200 AS PFIL1,133 AS PFIL2,1 AS PSTATUS,40 AS PFIL1000
30200 'REGISTRO DE DATOS CLIENTES.
30210 FIELD #2,7 AS PCUENTA,40 AS PNOMBRE,40 AS PDIREC,5 AS PCPOS,30 AS PPOBLA,40 AS PDIPAG,5 AS PCPPAG,30 AS PPOPAG,14 AS PNIF,11 AS PTELE1,11 AS PTELE2,8 AS PBANCO,15 AS PCTA,11 AS PPROVE,1 AS PACEPTA,6 AS PRIESGO,4 AS PGFI,2 AS PIVA,110 AS PRESTO
30220 FIELD #2,250 AS PANTE1,30 AS PANTE2,2 AS PRECARGO,1 AS PFPOFI,4 AS PDCTOFI,1 AS PFPBLA,4 AS PDCTBLA,2 AS PDIA1,2 AS PDIA2,2 AS PDIA3,7 AS PR1,7 AS PR2,3 AS PZONA,1 AS PAGRUPA,4 AS PCOMIS,28 AS PFIL3,1 AS PACTIVO,1 AS PSTATUS,40 AS PCONTACTO
30230 GET #2,1
30240 IF PETIQUETA="FCONPLAC.DAT" THEN 30280 'CONTINUAR.
30250 GOSUB 8500:MEN$="NO EXISTE EL FICHERO DE PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30260 A$=INPUT$(1)
30270 GOTO 9000
30280 'CONTINUAR.
30290 ULTDAT%=CVI(PULTIMO)    'ULTIMO DE DAT.
30300 ALTDAT%=CVI(PALTAS)     'ALTAS DE DAT.
30310 '
30320 RETURN
30330 '
30850 '***********************************************
30860 '***APERTURA FICHERO DE MOVIMIENTOS CONTABLES***
30870 '***********************************************
30880 FICHE3$="\"+DIRE$+"\FCONMOVI.DAT":DEFSTR M
30890 OPEN "R",#3,FICHE3$,100
30900 'REGISTRO 0 DE DATOS.
30910 FIELD #3,5 AS MNORDEN,4 AS MULTIMO,4 AS MALTAS,12 AS METIQUETA,1 AS MSW,6 AS MFDIA,67 AS MFI1,1 AS MSTATUS
30912 'REGISTRO DE DATOS.
30914 FIELD #3,5 AS MNORDEN,6 AS MFECASI,7 AS MCUENTA,7 AS MCONTRA,30 AS MCONCEP,1 AS MDEHA,8 AS MIMPORTE,1 AS MNVTO,6 AS MFECVTO,1 AS MPAGO1,1 AS MPAGO2,1 AS MPAGO3,25 AS MFI2,1 AS MSTATUS
30916 'LEEMOS EL REGISTRO 0.
30918 GET #3,1
30920 IF METIQUETA="FCONMOVI.DAT" THEN 30928 'CONTINUAR.
30922 GOSUB 8500:MEN$="NO EXISTE FICHERO DE MOVIMIENTOS CONTABLES, PULSE UNA TECLA":GOSUB 8500
30924 A$=INPUT$(1)
30926 GOTO 9000
30928 'CONTINUAR
30930 ULTMOV!=CVS(MULTIMO)    'ULTIMO MOVIMIENTOS INTRODUCIDO
30932 ALTMOV!=CVS(MALTAS)     'ALTAS INTRODUCIDAS.
30934 '
30936 RETURN
30938 '
31000 '***************************************************
31010 '***APERTURA FICHERO GRUPOS Y SUBGRUPOS CONTABLES***
31020 '***************************************************
31030 FICHE1$="\"+DIRE$+"\FCONCONC.DAT"
31040 '
31050 'APERTURA FICHERO DE DATOS.
31060 OPEN "R",#1,FICHE1$,44
31070 'REGISTRO 1.
31080 FIELD #1,2 AS GULTIMO,2 AS GALTAS,2 AS GULTACT,12 AS GETIQUETA,25 AS GFILLER,1 AS GSTATUS
31090 'REGISTRO DE DATOS.
31100 FIELD #1,3 AS GRUPO,40 AS GDENO,1 AS GSTATUS
31110 GET #1,1
31120 IF GETIQUETA="FCONCONC.DAT" THEN 31160  'CONTINUAR
31130 GOSUB 8500:MEN$="NO EXISTE FICHERO GRUPOS Y SUBGRUPOS CONTABLES, PULSE UNA TECLA":GOSUB 8500
31140 A$=INPUT$(1)
31150 GOTO 9000
31160 'CONTINUAR.
31170 ULTDIR%=CVI(GULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
31180 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
31190 ALTDIR%=CVI(GALTAS)     'NUMERO DE ALTAS EN DIR.
31200 ACTDIR%=CVI(GULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
31210 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
31220 '
31230 RETURN
31240 '
31250 '***********************************
31252 '***APERTURA FICHERO DE ARTICULOS***
31254 '***********************************
31256 FICHE3$="\"+DIRE$+"\FALMARTI.DIR"
31258 FICHE4$="\"+DIRE$+"\FALMARTI.DAT"
31260 'APERTURA FICHERO DE INDICES.
31262 OPEN "R",#3,FICHE3$,19
31264 'REGISTRO 1.
31266 FIELD #3,2 AS ADULTIMO,2 AS ADALTAS,2 AS ADULTACT,12 AS ADETIQUETA,1 AS ADSTATUS
31268 'REGISTRO DE DATOS.
31270 FIELD #3,8 AS ADARTICULO,2 AS ADNREG,8 AS ADFILLER,1 AS ADSTATUS
31272 GET #3,1
31274 IF ADETIQUETA="FALMARTI.DIR" THEN 31282  'CONTINUAR
31276 GOSUB 8500:MEN$="NO EXISTE FICHERO INDICES ARTICULOS, PULSE UNA TECLA":GOSUB 8500
31278 A$=INPUT$(1)
31280 GOTO 9000
31282 'CONTINUAR.
31284 ULTDIRA%=CVI(ADULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
31286 ULTIMOA%=ULTDIRA%         'ULTIMO PARA LA BUSQUEDA.
31288 ALTDIRA%=CVI(ADALTAS)     'NUMERO DE ALTAS EN DIR.
31290 ACTDIRA%=CVI(ADULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
31292 N.RA%=ACTDIRA%            'NUMERO DE REGISTROS EN EL DIR.
31294 '
31296 'APERTURA FICHERO DE DATOS.
31298 OPEN "R",#4,FICHE4$,200
31300 'REGISTRO 1.
31302 FIELD #4,2 AS AULTIMO,2 AS AALTAS,12 AS AETIQUETA,183 AS AFIL000,1 AS ASTATUS
31310 GET #4,1
31312 IF AETIQUETA="FALMARTI.DAT" THEN 31320 'CONTINUAR.
31314 GOSUB 8500:MEN$="NO EXISTE EL FICHERO DE ARTICULOS, PULSE UNA TECLA":GOSUB 8500
31316 A$=INPUT$(1)
31318 GOTO 9000
31320 'CONTINUAR.
31322 ULTDATA%=CVI(AULTIMO)    'ULTIMO DE DAT.
31324 ALTDATA%=CVI(AALTAS)     'ALTAS DE DAT.
31326 '
31328 RETURN
31330 '
31400 '*****************************
31402 '***APERTURA FICHERO BANCOS***
31404 '*****************************
31406 FICHE1$="\"+DIRE$+"\FFACBANC.DAT"
31408 '
31410 'APERTURA FICHERO DE DATOS.
31412 OPEN "R",#1,FICHE1$,125
31414 'REGISTRO 1.
31416 FIELD #1,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
31418 'REGISTRO DE DATOS.
31420 FIELD #1,8 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,1 AS BFIL001,1 AS BSTATUS
31422 GET #1,1
31424 IF BETIQUETA="FFACBANC.DAT" THEN 31432  'CONTINUAR
31426 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
31428 A$=INPUT$(1)
31430 GOTO 9000
31432 'CONTINUAR.
31434 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
31436 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
31438 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
31440 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
31442 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
31444 '
31446 RETURN
31448 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
