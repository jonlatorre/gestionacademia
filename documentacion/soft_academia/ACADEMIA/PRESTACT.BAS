10 '                       PRESFACT.BAS
20 '                      --------------
30 '    GESTION FICHERO DE FACTURAS ESCUELA DE  HOSTELERIA
40 '   ----------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 GOSUB 33000
135 OPEN "I",#4,"FRESCLA.DIA"
136 LINE INPUT #4,TODO$
137 CLAVE$=LEFT$(TODO$,4)
138 CLOSE
140 DEFSTR H,C,F,O,G,P
150 OPTION BASE 1
160 DIM TABRUT$(7),DIAS%(12),TABENT$(30)
174 DATA 31,28,31,30,31,30,31,31,30,31,30,31
176 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
178 '
200 DATA "    HACER  FACTURAS    "
202 DATA "    COBRAR FACTURAS    "
215 DATA "    BORRAR FACTURAS    "
220 DATA " CONSULTAS DE FACTURAS "
225 DATA "    LISTAR FACTURAS    "
227 DATA " PASAR FACTURAS A CAJA "
230 DATA "    FIN DE PROGRAMA    "
232 FOR I%=1 TO 7
234    READ TABRUT$(I%)
236 NEXT I%
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
310 '******************************
320 'Presentamos el Menu del Programa.
330 GOSUB 8000
332 TIT$="- GESTION FICHERO DE FACTURAS RESTAURANTE DE HOSTELERIA -"
334 X%=80-LEN(TIT$):X%=INT(X%/2)
340 COLOR 11:LOCATE 7,X%:PRINT TIT$
350 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
360 FOR I%=10 TO 16
370    LOCATE I%,29:PRINT TABRUT$(I%-9)
380 NEXT I%
390 '
400 GOSUB 8500:COLOR 11
410 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulsando la Tecla ";
420 COLOR 15:PRINT " ENTER "
430 'COLOR 7,0,0
440 '
450 C.F%=10:C%=29:L.F%=16:N.L%=7
460 GOSUB 50000
470 '
480 ON I.A% GOTO 510,5500,2500,2500,6000,6100,9000
490 '
500 '
510 '
520 '
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 GOSUB 8000:GOSUB 31000
532 GOSUB 32000
534       '*******************************
536       '***ENTRADA NUMERO DE FACTURA***
538       '*******************************
540       COLOR 15
542       LOCATE 1,1:N.FACTU!=ULFA!+1
544       N$=STR$(N.FACTU!):N$=LEFT$(CERO$,6-LEN(N$))+RIGHT$(N$,LEN(N$)-1)
546       GET #3,N.FACTU!
548       '
550       GUAPRI#=N.FACTU!:GOSUB 64770:LOCATE 4,41-L.C%:PRINT GUAPRI$
619       '
740       '***************************
741       '***ENTRADA NUMERO DE MESA**
742       '***************************
743       F.C1%=4:C.C1%=58:L.E%=2:BO%=2:ESC%=0:GOSUB 63000
744       IF I.T%=0 THEN MESA$=D.E$:GOTO 750
745       IF I.T%=5 THEN 740
746       IF I.T%=1 THEN 740
747       IF I.T%=3 THEN LOCATE 4,58:PRINT SPC(2):GOTO 740
748       IF I.T%=4 THEN 4000               'Fin Entrada.
749       '
750       '
751       LOCATE 4,58:PRINT MESA$
752       '
760       '***************************
761       '***ENTRADA NUMERO CAMARERO*
762       '***************************
763       F.C1%=4:C.C1%=77:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
764       IF I.T%=0 THEN CAMA$=D.E$:GOTO 770
765       IF I.T%=5 THEN 760
766       IF I.T%=1 THEN 760
767       IF I.T%=3 THEN LOCATE 4,58:PRINT SPC(2):GOTO 740
768       IF I.T%=4 THEN 4000               'Fin Entrada.
769       '
770       '
771       LOCATE 4,77:PRINT CAMA$
772       PRI$="1"
870       FOR F%=8 TO 18
872       GOSUB 32500        'Apertura Fichero HIBRIDO
874       GOSUB 34000
876       GOSUB 7650
878       N.OPERA=OLTI!+1
880       GET #4,N.OPERA!
888       LOCATE F%+2,23:COLOR 12:PRINT "C¢digo ";
889       COLOR 15:PRINT " 9999 ";
890       COLOR 12:PRINT " Final de Factura."
892       COLOR 15
900       '***********************
905       '***ENTRADA DE CODIGO***
910       '***********************
915       F.C1%=F%:C.C1%=2:L.E%=4:BO%=5:GOSUB 63000
920       IF I.T%=0 THEN CODI%=VAL(D.E$):GOTO 950
925       IF I.T%=5 THEN 915
930       IF I.T%=1 THEN CODI%=0:GOTO 950
935       IF I.T%=3 THEN LOCATE F%,2:PRINT SPC(5):GOTO 870
940       IF I.T%=4 THEN 4000               'Fin Entrada.
945       '
950       IF CODI%=9999 THEN LOCATE F%+2,2:PRINT SPC(78):GOTO 2300
952       IF CODI%=0 THEN 1500
955       GET #6,CODI%
960       IF GSTA="O" THEN 965
961       GOSUB 8500:MEN$="Este c¢digo no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,12:PRINT SPC(5)
962       GOSUB 7650:GOTO 900
965       GUAPRI#=CODI%:GOSUB 64770
966       LOCATE F%,2:PRINT SPC(5)
968       LOCATE F%,6-L.C%:PRINT GUAPRI$
970       LOCATE F%,9:PRINT LEFT$(GDES,38)
972       PRECIO!=CVS(GPRE):TESTO$=LEFT$(GDES,40)
974       LOCATE F%+2,2:PRINT SPC(78)
1000      '***********************
1005      '***ENTRADA CANTIDAD ***
1010      '***********************
1015      F.C1%=F%:C.C1%=49:L.E%=4:BO%=5:GOSUB 63000
1020      IF I.T%=0 THEN CANTI%=VAL(D.E$):GOTO 1055
1025      IF I.T%=5 THEN 1015
1030      IF I.T%=1 THEN 1015
1035      IF I.T%=3 THEN LOCATE F%,1:PRINT SPC(46):GOTO 900
1040      IF I.T%=4 THEN 4000               'Fin Entrada.
1045      IF I.T%=2 THEN 1055
1050      '
1055      '
1060      GUAPRI#=CANTI%:GOSUB 64770
1061      LOCATE F%,49:PRINT SPC(5):LOCATE F%,53-L.C%:PRINT GUAPRI$
1062      IMPOR#=PRECIO!*CANTI%
1063      GUAPRI#=IMPOR#:GOSUB 64770
1064      LOCATE F%,70-L.C%:PRINT GUAPRI$
1065      '
1150       '***********************
1152       '***ENTRADA DE FOPERA***
1154       '***********************
1156       F.C1%=F%:C.C1%=73:L.E%=6:BO%=8:GOSUB 63000
1158       IF I.T%=0 THEN NACI$=D.E$:GOTO 1172
1160       IF I.T%=5 THEN 1156
1162       IF I.T%=1 THEN NACI$=LEFT$(FECHA$,2)+MID$(FECHA$,4,2)+RIGHT$(FECHA$,2):GOTO 1172
1164       IF I.T%=3 THEN LOCATE F%,49:PRINT SPC(22):GOTO 1000
1166       IF I.T%=4 THEN 4000               'Fin Entrada.
1168       IF I.T%=2 THEN 1172
1170       '
1172       FECSUB$=NACI$:GOSUB 60000
1174       IF ERF%<>1 THEN 1180
1176       BEEP:GOSUB 8500:MEN$="Fecha de Operaci¢n Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,73:PRINT SPC(8)
1178       GOSUB 7650:GOTO 1156
1180       IF LEFT$(FAMD$,2)<"92" THEN 1176
1182       LOCATE F%,73:PRINT FDMA$:FENA$=FAMD$
1184       GOTO 2000
1500      LOCATE F%+2,2:PRINT SPC(78)
1520       '***********************
1522       '***ENTRADA DEL TESTO***
1524       '***********************
1526       F.C1%=F%:C.C1%=9:L.E%=38:BO%=38:GOSUB 64000
1528       IF I.T%=0 THEN TESTO$=D.E$:GOTO 1540
1530       IF I.T%=5 THEN 1526
1532       IF I.T%=1 THEN 1526
1534       IF I.T%=3 THEN LOCATE F%,2:PRINT SPC(5):GOTO 900
1536       IF I.T%=4 THEN 4000               'Fin Entrada.
1538       '
1540       '
1542       LOCATE F%,9:PRINT TESTO$
1544       PRECIO!=0:CANTI%=0
1546       '
1548      '***********************
1550      '***ENTRADA IMPORTE   **
1552      '***********************
1554      F.C1%=F%:C.C1%=56:L.E%=12:BO%=15:GOSUB 63000
1556      IF I.T%=0 THEN IMPOR#=VAL(D.E$):GOTO 1570
1558      IF I.T%=5 THEN 1554
1560      IF I.T%=1 THEN 1554
1562      IF I.T%=3 THEN LOCATE F%,1:PRINT SPC(46):GOTO 1520
1564      IF I.T%=4 THEN 4000               'Fin Entrada.
1566      IF I.T%=2 THEN 1570
1568      '
1570      '
1572      GUAPRI#=IMPOR#:GOSUB 64770
1574      LOCATE F%,56:PRINT SPC(15):LOCATE F%,70-L.C%:PRINT GUAPRI$
1576      GOTO 1150
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   LOCATE 22,15:COLOR 10:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT "  ENTER  ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT "  F1 "
2016   '
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2030
2027   LOCATE F%,2:PRINT SPC(79):F%=F%-1:GOTO 2250
2030   LSET OFACT=MKS$(N.FACTU!):LSET OSTA="O":LSET OMAR="A":LSET OCOD=MKI$(CODI%):LSET OFEC=FENA$
2032   LSET OTES=TESTO$:LSET OCAN=MKI$(CANTI%):LSET OPRE=MKS$(PRECIO!):LSET OIMP=MKD$(IMPOR#):LSET ORES=""
2034   PUT #4,N.OPERA!
2036   GET #4,1
2038   LSET OULT=MKS$(N.OPERA!)
2040   PUT #4,1
2042   '
2043   IF PRI$="1" THEN PRIOPE!=N.OPERA!:PRI$="0"
2050   ULTOPE!=N.OPERA!
2060   TOTAL#=TOTAL#+IMPOR#
2250   CLOSE
2252   NEXT F%
2253   '
2254   '
2258   '
2260   '
2264   FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
2265   '
2266   GOTO 870
2300 GOSUB 32000
2302 COLOR 10:LOCATE 20,28:PRINT "Iva incluido Si o No : < >":COLOR 15
2304 LOCATE 20,52:A$=INPUT$(1)
2306 IF A$<>"S" AND A$<>"N" THEN BEEP:GOTO 2302
2308 LOCATE 20,52:PRINT A$:IVA$=A$
2310 IF IVA$="S" THEN 2312
2311 IVA#=INT(TOTAL#*6/100):TOTAL#=TOTAL#+IVA#
2312 LSET FSTA="A":LSET FPRI=MKS$(PRIOPE!):LSET FULT=MKS$(ULTOPE!)
2314 LSET FIMP=MKD$(TOTAL#):LSET FMES=MESA$:LSET FCAM=CAMA$:LSET FTIP="P"
2316 LSET FIVA=IVA$:LSET FFEC=FENA$
2318 PUT #3,N.FACTU!
2320 '
2322 GET #3,1
2324 LSET FULTIMO=MKS$(N.FACTU!)
2326 PUT #3,1
2327 TOTAL#=0
2328 '
2330 GET #3,N.FACTU!
2332 'GOTO 4490
2333 CLOSE:GOTO 300
2490 '**********************************************
2495 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
2500 '**********************************************
2530 '
2535 '
2540 '
2545 GOSUB 8000:GOSUB 31000
2550 GOSUB 32000:GOSUB 32500
2560 GOSUB 7500
2565       '*******************************
2570       '***ENTRADA NUMERO DE FACTURA***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=4:C.C1%=36:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=5 THEN 2585
2615       IF I.T%=4 THEN 4000               'Fin Entrada.
2625       GOTO 2570
2630       '
2635       '
2640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 4,36:PRINT SPC(6):LOCATE 4,41-L.C%:PRINT GUAPRI$
2642       N.L!=VAL(CUENTA$)
2645       IF N.L!<2 OR N.L!>ULFA! THEN GOSUB 8500:MEN$="Este N£mero de Factura no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 4,36:PRINT SPC(6):GOTO 2560
2650       GET #3,N.L!
2651       IF FSTA<>"A" THEN GOSUB 8500:MEN$="Este N£mero de Factura no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 4,36:PRINT SPC(6):GOTO 2560
2652       IF I.A%=3 AND FTIP<>"P" THEN GOSUB 8500:MEN$="Esta cobrada no se puede Borrar, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 4,36:PRINT SPC(6):GOTO 2560
2653       '
2655       '
2660       GOSUB 2700   'PRESENTAR DATOS EN PANTALLA.
2665       IF I.A%=2 THEN 5500
2667       IF I.A%=3 THEN 2990
2670       GOTO 3250
2675       '
2700      '*******************************
2702      '*** PONER DATOS EN PANTALLA ***
2704      '*******************************
2706      '
2710      LOCATE 4,58:PRINT FMES
2720      LOCATE 4,77:PRINT FCAM
2722      REG!=CVS(FPRI)-1
2724      FOR F%=8 TO 19
2726      REG!=REG!+1
2727      IF REG!>CVS(FULT) THEN GOTO 2790
2728      GET #4,REG!
2729      IF CVI(OCOD)=0 THEN LOCATE F%,1:PRINT "VARIOS":GOTO 2736
2730      GUAPRI#=CVI(OCOD):GOSUB 64770
2732      LOCATE F%,2:PRINT SPC(5)
2734      LOCATE F%,6-L.C%:PRINT GUAPRI$
2736      LOCATE F%,9:PRINT LEFT$(OTES,38)
2737      IF CVI(OCAN)=0 THEN 2742
2738      GUAPRI#=CVI(OCAN):GOSUB 64770
2740      LOCATE F%,49:PRINT SPC(5):LOCATE F%,53-L.C%:PRINT GUAPRI$
2742      IMPOR#=CVD(OIMP)
2743      TTT#=TTT#+IMPOR#
2744      GUAPRI#=IMPOR#:GOSUB 64770
2746      LOCATE F%,70-L.C%:PRINT GUAPRI$
2748      LOCATE F%,73:PRINT RIGHT$(OFEC,2)"-"MID$(OFEC,3,2)"-"LEFT$(OFEC,2)
2750      NEXT F%
2752      GOSUB 8500:MEN$="Puede haber m s datos, para verlos - ENTER -, si no - F1 -":GOSUB 8500
2754      A$=INPUT$(1)
2756      IF ASC(A$)<13 OR ASC(A$)>14 THEN 2754
2758      IF ASC(A$)=14 THEN 2790
2760      FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
2762      GOTO 2724
2790      RETURN
2800      '
2980 '*************************
2990 '***TRATAMIENTO DE BAJA***
3000 '*************************
3010 GOSUB 8500:COLOR 10
3020 LOCATE 22,15:PRINT "DAR DE BAJA ";
3030 COLOR 15:PRINT " ENTER ";
3040 COLOR 10:PRINT " NO DAR DE BAJA ";
3050 COLOR 15:PRINT " F1 "
3055 A$=INPUT$(1)
3057 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3055
3060 IF ASC(A$)=14 THEN 2545
3062 LSET FSTA="B"
3064 '
3065 PUT #3,N.L!
3066 '
3120 CLOSE
3134 '
3136 GOTO 2540
3138 '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3270 A$=INPUT$(1)      ':GOSUB 7650:LOCATE 8,56:PRINT SPC(20)
3280 CLOSE:GOTO 2540
3290 '
3300 '
3310 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4496 WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4498 LPRINT CHR$(27)+"&l1O"+CHR$(27)+"&a98L";
4500 GOSUB 8000
4502 COLOR 11:LOCATE 10,22:PRINT USING "- LISTADO DE LA FACTURA NUMERO ##### -";N.FACTU!
4504 LOCATE 12,22:PRINT STRING$(38,205)
4779 LPRINT
4780 LPRINT TAB(7) "ESCUELA DE HOSTELERIA DE SANTURCE S.L."
4782 LPRINT TAB(7) "Genaro Ora , 6 - Tel. 461 95 55 - 48980 SANTURCE"
4783 LPRINT TAB(7) "C.I.F. : B-48-300.073"
4784 LPRINT TAB(7) "Restaurante : Jos‚ Miguel de Barandiaran, 3"
4785 LPRINT TAB(7) "Tel. 462 58 23 - Fax 461 57 23 - 48980 SANTURCE"
4786 LPRINT:LPRINT
4788 LPRINT TAB(7) STRING$(68,196)
4790 LPRINT TAB(7) "FACTURA NUMERO :";
4792 GUAPRI#=N.FACTU!:GOSUB 64770
4794 LPRINT TAB(29-L.C%) GUAPRI$;
4796 LPRINT TAB(61) "FECHA "FECHA$
4798 LPRINT TAB(7) STRING$(68,196)
4800 LPRINT TAB(7) "NUMERO DE MESA : " FMES;
4802 LPRINT TAB(61) "CAMARERO : " FCAM
4804 LPRINT TAB(7) STRING$(68,196):LPRINT
4806 LPRINT TAB(7) "Cantidad            C O N C E P T O S          Precio  I M P O R T E"
4808 LPRINT TAB(7) "ÄÄÄÄÄÄÄÄ  ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ  ÄÄÄÄÄÄ  ÄÄÄÄÄÄÄÄÄÄÄÄÄ"
4809 TOTAL#=0:TOFAC#=0
4810 FOR A!=CVS(FPRI) TO CVS(FULT)
4812 GET #4,A!
4813 IF CVI(OCAN)=0 THEN 4818
4814 GUAPRI#=CVI(OCAN):GOSUB 64770
4816 LPRINT TAB(12-L.C%) GUAPRI$;
4818 LPRINT TAB(17) LEFT$(OTES,35);
4819 IF CVS(OPRE)=0 THEN 4824
4820 GUAPRI#=CVS(OPRE):GOSUB 64770
4822 LPRINT TAB(59-L.C%) GUAPRI$;
4824 GUAPRI#=CVD(OIMP):GOSUB 64770
4826 LPRINT TAB(74-L.C%) GUAPRI$
4828 TOFAC#=TOFAC#+CVD(OIMP)
4830 NEXT A!
4832 LPRINT TAB(62) STRING$(13,196)
4834 LPRINT TAB(45) "SUMA ...........";
4836 GUAPRI#=TOFAC#:GOSUB 64770
4838 LPRINT TAB(74-L.C%) GUAPRI$
4840 IF FIVA="S" THEN LPRINT TAB(45) "I.V.A. .........      Incluido":GOTO 4854
4842 IVA#=INT(TOFAC#*6/100):GUAPRI#=IVA#:GOSUB 64770
4844 LPRINT TAB(45) "I.V.A. 6 % .....";
4846 LPRINT TAB(74-L.C%) GUAPRI$
4848 LPRINT TAB(62) STRING$(13,196)
4854 TOTAL#=TOFAC#+IVA#:GUAPRI#=TOTAL#:GOSUB 64770
4856 LPRINT TAB(45) "IMPORTE TOTAL ..";
4858 LPRINT TAB(74-L.C%) GUAPRI$
4860 LPRINT CHR$(12)
4862 CLOSE
4864 GOTO 300
5490 '**********************************************
5495 '***PARTE COMUN DEL COBRO DE FACTURAS       ***
5500 '**********************************************
5530 '
5535 '
5540 '
5545 GOSUB 8000:GOSUB 31500
5550 GOSUB 32000:GOSUB 34500
5560 GOSUB 7500
5565       '*******************************
5570       '***ENTRADA NUMERO DE FACTURA***
5575       '*******************************
5580       COLOR 15
5585       F.C1%=9:C.C1%=46:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
5590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 5635 'Entrada OK
5595       IF I.T%=5 THEN 5585
5615       IF I.T%=4 THEN 4000               'Fin Entrada.
5625       GOTO 5570
5630       '
5635       '
5640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 9,46:PRINT SPC(6):LOCATE 9,51-L.C%:PRINT GUAPRI$
5642       N.L!=VAL(CUENTA$)
5645       IF N.L!<2 OR N.L!>ULFA! THEN GOSUB 8500:MEN$="Este N£mero de Factura no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 9,46:PRINT SPC(6):GOTO 5560
5650       GET #3,N.L!
5651       IF FSTA<>"A" THEN GOSUB 8500:MEN$="Este N£mero de Factura no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 9,46:PRINT SPC(6):GOTO 5560
5652       IF FTIP<>"P" THEN GOSUB 8500:MEN$="Esta Factura ya ha sido cobrada, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 9,46:PRINT SPC(6):GOTO 5560
5653       IMPOR#=CVD(FIMP)
5654       GUAPRI#=IMPOR#:GOSUB 64770
5655       LOCATE 11,60-L.C%:PRINT GUAPRI$
5660       LOCATE 16,18:COLOR 12:PRINT "- 1 - ";
5662       COLOR 15:PRINT "Met lico  ";
5664       COLOR 12:PRINT "- 2 - ";
5666       COLOR 15:PRINT "Talones  ";
5668       COLOR 12:PRINT "- 3 - ";
5670       COLOR 15:PRINT "Tarjetas "
5672       '
5674       '
5676      '***********************
5678      '***ENTRADA TIPO OPERA**
5680      '***********************
5682      F.C1%=13:C.C1%=46:L.E%=1:BO%=1:GOSUB 63000
5684      IF I.T%=0 THEN TIPO$=D.E$:GOTO 5698
5686      IF I.T%=5 THEN 5682
5688      IF I.T%=1 THEN 5682
5690      IF I.T%=3 THEN 5682
5692      IF I.T%=4 THEN 4000               'Fin Entrada.
5694      IF I.T%=2 THEN 5698
5696      '
5698      IF TIPO$<"1" OR TIPO$>"3" THEN BEEP:LOCATE 13,46:PRINT SPC(1):GOTO 5682
5700      LOCATE 13,50
5702      IF TIPO$="1" THEN PRINT "METALICO":FOPA$="M":GOTO 5710
5704      IF TIPO$="2" THEN PRINT "TALONES":FOPA$="T":GOTO 5710
5706      PRINT "TARJETAS":FOPA$="J":GOTO 5710
5708      '
5710      LOCATE 16,2:PRINT SPC(78)
5712   '*************************************
5714   '***PEDIMOS CONFORMIDAD A LOS DATOS***
5716   '*************************************
5718   GOSUB 8500
5720   LOCATE 22,15:COLOR 10:PRINT "Datos Correctos ";
5722   COLOR 15:PRINT "  ENTER  ";
5724   COLOR 10:PRINT " Datos Erroneos ";
5726   COLOR 15:PRINT "  F1 "
5728   '
5730   '
5732   A$=INPUT$(1)
5734   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 5732
5736   '
5738   IF ASC(A$)=13 THEN 5750
5740   CLOSE:GOTO 5500
5750   '
5752   LSET FTIP=FOPA$
5754   PUT #3,N.L!
5756   '
5758   IF FOPA$="M" THEN METAL#=METAL#+IMPOR#:GOTO 5766
5760   IF FOPA$="T" THEN TALON#=TALON#+IMPOR#:GOTO 5766
5762   TARJE#=TARJE#+IMPOR#:GOTO 5766
5764   '
5766   LSET PMET=MKD$(METAL#)
5768   LSET PTAL=MKD$(TALON#)
5770   LSET PTAR=MKD$(TARJE#)
5772   PUT #7,1
5774   CLOSE
5776   GOTO 5500
5778   '
5780   '
6000 RUN "PRESLIFA"
6100 RUN "PRESACCA"
6200 '
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7540 'COLOR 15:PRINT " ENTER =>";
7550 'COLOR 11:PRINT " Salto  ";
7560 COLOR 15:PRINT " F7 =>";
7570 COLOR 11:PRINT " Fin de la Opci¢n. ";
7580 'COLOR 15:PRINT " F8 =>";
7590 'COLOR 11:PRINT " M.Izda. "
7600 COLOR 15
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7680 COLOR 15:PRINT "ENTER =>";
7690 COLOR 11:PRINT " Saltar. ";
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin Altas.";
7720 COLOR 15:PRINT " F8 =>";
7730 COLOR 11:PRINT " Campo Atras. ";
7740 COLOR 15:PRINT " F10 =>";
7750 COLOR 11:PRINT " Dup."
7760 COLOR 15
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,36:PRINT "Aplicaci¢n : RESTAURANTE":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESCAJA"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO DIARIO  ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30090 ' Apertura del fichero de datos   FRESCAJA.DIA
30095 '
30100 OPEN "R",#1,FICHERO$,90
30105 '
30110 FIELD #1,1 AS HSTA,8 AS HINI,8 AS HCOB,8 AS HPAG,8 AS HCTA,8 AS HPTA,8 AS HCPL,8 AS HPPL,8 AS HCOT,8 AS HPOT,8 AS HFIN,9 AS HRES
30114 '
30128 RETURN
30130 '
30140 '
30500 '**********************************
30510 '***APERTURA FICHERO OPERACIONES***
30520 '**********************************
30525 'Comprobamos la existencia del fichero.
30552 '
30590 ' Apertura del fichero de datos   FRESOPER.DAT
30595 '
30600 OPEN "R",#2,"FRESOPER.DAT",80
30605 '
30610 FIELD #2,4 AS CNUM,1 AS CSTA,1 AS CMAR,4 AS CCLA,6 AS CFEC,1 AS CCOP,1 AS CTIP,35 AS CTES,8 AS CIMP,19 AS CRES
30611 FIELD #2,4 AS CULT,1 AS CST1,1 AS CMA1,74 AS CFIL
30614 '
30616 GET #2,1
30617 'LSET CULT=MKS$(1)
30618 'LSET CST1="A":LSET CMA1="A":LSET CFIL=""
30619 'PUT #2,1
30620 'CLOSE
30621 'STOP
30622 '
30624 ULTI!=CVS(CULT)
30628 RETURN
30630 '
30640 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 11
31031 IF I.A%=1 THEN LOCATE 4,1:PRINT "ALTAS DE FACTURAS":GOTO 31035
31032 IF I.A%=2 THEN LOCATE 4,1:PRINT "COBRO DE FACTURAS":GOTO 31035
31033 IF I.A%=3 THEN LOCATE 4,1:PRINT "BAJAS DE FACTURAS":GOTO 31035
31034 LOCATE 4,1:PRINT "CONSULTA FACTURAS"
31035 LOCATE 4,22:PRINT "Factura N§ : <      >"
31036 LOCATE 4,47:PRINT "Mesa N§ : <  >"
31038 LOCATE 4,65:PRINT "Camarero : <   >"
31040 LOCATE 5,1:PRINT STRING$(17,205)
31041 LOCATE 5,22:PRINT STRING$(21,205)
31042 LOCATE 5,47:PRINT STRING$(14,205)
31044 LOCATE 5,65:PRINT STRING$(16,205)
31050 LOCATE 6,1:PRINT "CODIGO             C O N C E P T O              CANTI   I M P O R T E    FECHA."
31054 LOCATE 7,1:PRINT "ÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍ"
31084 RETURN
31500 '**********************************************
31510 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31520 '**********************************************
31530 COLOR 11
31531 '
31534 LOCATE 6,31:PRINT "- COBRO DE FACTURAS -"
31535 LOCATE 7,31:PRINT STRING$(21,205)
31536 LOCATE 9,29:PRINT "Factura N§.   : <      >"
31538 LOCATE 11,29:PRINT "Importe       : <               >"
31540 LOCATE 13,29:PRINT "Forma de pago : < >"
31554 COLOR 15
31584 RETURN
32000 '****************************************
32010 '***APERTURA FICHERO FACTURAS REDUCIDO***
32020 '****************************************
32025 'Comprobamos la existencia del fichero.
32090 ' Apertura del fichero de datos   FRESFACT.RED
32095 '
32100 OPEN "R",#3,"FRESFACT.RED",30
32105 '
32110 FIELD #3,1 AS FSTA,4 AS FPRI,4 AS FULT,8 AS FIMP,2 AS FMES,3 AS FCAM,1 AS FTIP,1 AS FIVA,6 AS FFEC
32112 FIELD #3,1 AS FST1,4 AS FULTIMO,25 AS FRE1
32114 '
32116 GET #3,1
32118  'LSET FST1="A":LSET FULTIMO=MKS$(1):LSET FRE1=""
32120  'PUT #3,1
32122  'CLOSE:STOP
32124 ULFA!=CVS(FULTIMO)
32126 '
32128 RETURN
32130 '
32140 '
32500 '**********************************
32510 '***APERTURA FICHERO DE FACTURAS***
32520 '**********************************
32590 ' Apertura del fichero de datos   FRESFACT.DAT
32595 '
32600 OPEN "R",#4,"FRESFACT.DAT",80
32605 '
32610 FIELD #4,4 AS OFACT,1 AS OSTA,1 AS OMAR,6 AS OFEC,2 AS OCOD,40 AS OTES,2 AS OCAN,4 AS OPRE,8 AS OIMP,12 AS ORES
32611 FIELD #4,4 AS OULT,1 AS OST1,1 AS OMA1,74 AS OFIL
32614 '
32616 GET #4,1
32617 'LSET OULT=MKS$(1)
32618 'LSET OST1="A":LSET OMA1="A":LSET OFIL=""
32619 'PUT #4,1
32620 'CLOSE
32621 'STOP
32624 OLTI!=CVS(OULT)
32628 RETURN
32630 '
32640 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*******************************
34010 '***APERTURA FICHERO CODIGOS ***
34020 '*******************************
34030 '
34040 ' Apertura del fichero de datos   FRESCODI.DAT
34050 '
34060 OPEN "R",#6,"FRESCODI.DAT",80
34070 '
34080 FIELD #6,4 AS GODI,50 AS GDES,4 AS GPRE,21 AS GLIB,1 AS GSTA
34090 '
34100 '
34110 RETURN
34120 '
34130 '
34500 '*******************************
34510 '***APERTURA FICHERO TIPOS PAGO*
34520 '*******************************
34530 '
34540 ' Apertura del fichero de datos   FRESTIPA.DAT
34550 '
34560 OPEN "R",#7,"FRESTIPA.DAT",40
34570 '
34580 FIELD #7,8 AS PMET,8 AS PTAL,8 AS PTAR,16 AS PLIB
34590 '
34591 GET #7,1
34592  'LSET PMET=MKD$(0):LSET PTAL=MKD$(0):LSET PTAR=MKD$(0):LSET PLIB=""
34593  'PUT #7,1
34594  'CLOSE:STOP
34600 '
34610 METAL#=CVD(PMET):TALON#=CVD(PTAL):TARJE#=CVD(PTAR)
34620 RETURN
34630 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 12:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 15
50300 RETURN
50340 '
50510 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
