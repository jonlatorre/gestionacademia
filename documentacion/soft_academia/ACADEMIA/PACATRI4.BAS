10 '                       PACATRI4.BAS
20 '                      --------------
30 '        LISTADO DE CALIFICACIONES ALUMNO A ALUMNO
40 '        -----------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(100,"")
100 '
130 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,5,2)+"-"+LEFT$(FEDIA$,4)
134 '
140 DEFSTR C,A,F,J,N,G
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),JAL%(12),FAL%(12),PERI$(12)
170 '
172 '
174 '
176 '
178 '
200 DATA "Enero","Febrero","Marzo","Abril","Mayo","Junio","Julio","Agosto","Setiembre","Octubre","Noviembre","Diciembre"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '*********************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
455 '
460 '
465 '
500 TIT$="- LISTAR CALIFICACIONES ALUMNO A ALUMNO -"
505 '
510 '
511 GOSUB 8000
512 COLOR 10
514 LOCATE 5,18:PRINT TIT$
516 LOCATE 6,18:PRINT STRING$(LEN(TIT$),205)
700 '
702 '
704 '
706 '
718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
720 GOSUB 30000:GOSUB 32000:GOSUB 32500:GOSUB 31500
724 GOSUB 35000
728 GOSUB 7650
730       '*******************************
732       '***ENTRADA NUMERO DE ALUMNO ***
734       '*******************************
736       COLOR 15
738       F.C1%=8:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 758 'Entrada OK
741       IF I.T%=4 THEN 9000
742       BEEP:LOCATE 8,20:PRINT SPC(6):GOTO 732
756       '
758       REG%=VAL(CUENTA$)
760       GUAPRI#=VAL(CUENTA$):N.D%=0:GOSUB 64770:LOCATE 8,25-L.C%:PRINT GUAPRI$
762     ' GET #2,REG%  'BUSCAR REGISTRO
764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N즡ero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
765       GET #2,REG%  'BUSCAR REGISTRO
766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
770       LOCATE 8,28:PRINT CUCUCU$
774       '
776       LOCATE 10,20:PRINT CGRUPO
778       GET #4,VAL(CGRUPO):CURSO$=GCURSO:ESCUE$=GESCUE
780       LOCATE 10,25:PRINT GCURSO
878       '
880       '
1000       '******************************
1002       '***ENTRADA NUMERO TRIMESTRE***
1004       '******************************
1006       F.C1%=12:C.C1%=20:L.E%=1:BO%=1:ESC%=1:GOSUB 63000
1008       IF I.T%=0 THEN TRI%=VAL(D.E$):GOTO 1020
1010       '
1012       GOTO 1006
1018       '
1020       IF TRI%<1 OR TRI%>3 THEN 1006
1026       '
1028       LOCATE 12,20:PRINT D.E$
1029       '
4200      '**********************
4210      '***ENTRADA PROFES  ***
4220      '**********************
4230      F.C1%=14:C.C1%=20:L.E%=30:BO%=30:GOSUB 64000
4240      IF I.T%=0 THEN PROFE$=D.E$:GOTO 4260
4250      GOTO 4230
4260      '
4270      LOCATE 14,20:PRINT PROFE$
4280   '*************************************
4290   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4300   '*************************************
4310   GOSUB 8500
4320   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4330   COLOR 15:PRINT " ENTER ";
4340   COLOR 10:PRINT " Datos Erroneos ";
4350   COLOR 15:PRINT " F1 "
4360   COLOR 10
4370   '
4380   A$=INPUT$(1)
4390   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4380
4400   '
4410   IF ASC(A$)=13 THEN 4450
4420   CLOSE
4430   GOTO 300
4440   '
4450   '
4480   WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4490   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B";
4495  'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
4520   GET #5,VAL(CUENTA$)
4530   '
4540  FA2%=CVI(AENE)+CVI(AFEB)+CVI(AMAR)
4550  JA2%=CVI(JENE)+CVI(JFEB)+CVI(JMAR)
4552  FA3%=CVI(AABR)+CVI(AMAY)+CVI(AJUN)
4554  JA3%=CVI(JABR)+CVI(JMAY)+CVI(JJUN)
4560  FAL%=CVI(AOCT)+CVI(ANOV)+CVI(ADIC)
4570  JAL%=CVI(JOCT)+CVI(JNOV)+CVI(JDIC)
4580  GET #2,VAL(CUENTA$)
4590  GET #6,VAL(CUENTA$)
4592  FE1#=VAL(FEDIA$):FE2#=VAL(CFENA):DIF#=FE1#-FE2#
4594  IF DIF#>199999! THEN NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOTO 4598
4596  NOM$="Sres.":APE1$=CAPELL1:APE2$=CAPELL2
4598  GOSUB 40000
4600  '
4610  DESTI$=CUCUCU$
4620  NOM$=CPOBLA:APE1$="-":APE2$=CPROVI:GOSUB 40000
4630 PUEBLO$=CUCUCU$
4640  NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4650  ALUMNO$=CUCUCU$:LC%=LEN(ALUMNO$)
4660  FOR A%=1 TO 4:LPRINT:NEXT A%
4670  LPRINT TAB(11) "SCHOOL OF ENGLISH";:LPRINT TAB(45) LEFT$(DESTI$,35)
4680  LPRINT TAB(11) "GENARO ORAA N 6";:LPRINT TAB(45) LEFT$(CDIREC,35)
4690  LPRINT TAB(11) "SANTURCE - VIZCAYA";:LPRINT TAB(45) LEFT$(PUEBLO$,35)
4700  LPRINT TAB(11) "Tel괽ono 4.93.70.05"
4710  FOR A%=1 TO 5:LPRINT:NEXT A%
4720  LPRINT TAB(23) "N O T A S    T R I M E S T R A L E S"
4730  LPRINT TAB(23) STRING$(36,205):LPRINT
4740  LPRINT TAB(7) "Nombre del Alumno : " ALUMNO$
4750  LPRINT TAB(7) "Curso ........... : " CURSO$
4760  LPRINT TAB(7) "Profesores ...... : " PROFE$:LPRINT
4770  LPRINT TAB(7) "                                   PRIMER.      SEGUNDO      TERCER. "
4780  LPRINT TAB(7) "      C O N C E P T O S           TRIMESTRE    TRIMESTRE    TRIMESTRE"
4790  LPRINT TAB(7) "------------------------------    ---------    ---------    ---------":LPRINT
4800  LPRINT TAB(7) "Faltas de asistencia .........";
4810  LPRINT TAB(46) USING "##";FAL%;
4815  IF TRI%=1 THEN LPRINT TAB(59) "  ":GOTO 4830
4820  LPRINT TAB(59) USING "##";FA2%;
4825  IF TRI%=2 THEN LPRINT TAB(72) "  ":GOTO 4830
4827  LPRINT TAB(72) USING "##";FA3%
4830  LPRINT TAB(7) "Faltas justificadas ..........";
4840  LPRINT TAB(46) USING "##";JAL%;
4845  IF TRI%=1 THEN LPRINT TAB(59) "  ":GOTO 4860
4850  LPRINT TAB(59) USING "##";JA2%;
4855  IF TRI%=2 THEN LPRINT TAB(72) "  ":GOTO 4860
4857  LPRINT TAB(72) USING "##";JA3%
4860  LPRINT
4870  LPRINT TAB(7) "Control n즡ero 1 .............";
4880  IF CVI(NCOS11)=0 THEN 4905
4885  IF CVI(NCOE11)=999 AND CVI(NCOS11)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 4905
4890  GUAPRI#=CVI(NCOE11)*100/CVI(NCOS11):N.D%=2:GOSUB 64770
4900  LPRINT TAB(47-L.C%) GUAPRI$ " %";
4905  IF TRI%=1 THEN LPRINT:GOTO 4970
4910  IF CVI(NCOS21)=0 THEN LPRINT:GOTO 4935
4915  IF CVI(NCOE21)=999 AND CVI(NCOS21)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 4935
4920  GUAPRI#=CVI(NCOE21)*100/CVI(NCOS21):N.D%=2:GOSUB 64770
4930  LPRINT TAB(60-L.C%) GUAPRI$ " %";
4935  IF TRI%=2 THEN LPRINT:GOTO 4970
4940  IF CVI(NCOS31)=0 THEN LPRINT:GOTO 4970
4945  IF CVI(NCOE31)=999 AND CVI(NCOS31)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 4970
4950  GUAPRI#=CVI(NCOE31)*100/CVI(NCOS31):N.D%=2:GOSUB 64770
4960  LPRINT TAB(73-L.C%) GUAPRI$ " %"
4970  LPRINT TAB(7) "Control n즡ero 2 .............";
4980  IF CVI(NCOS12)=0 THEN 5005
4985  IF CVI(NCOE12)=999 AND CVI(NCOS12)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5005
4990  GUAPRI#=CVI(NCOE12)*100/CVI(NCOS12):N.D%=2:GOSUB 64770
5000  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5005  IF TRI%=1 THEN LPRINT:GOTO 5070
5010  IF CVI(NCOS22)=0 THEN LPRINT:GOTO 5035
5015  IF CVI(NCOE22)=999 AND CVI(NCOS22)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 5035
5020  GUAPRI#=CVI(NCOE22)*100/CVI(NCOS22):N.D%=2:GOSUB 64770
5030  LPRINT TAB(60-L.C%) GUAPRI$ " %";
5035  IF TRI%=2 THEN LPRINT:GOTO 5070
5040  IF CVI(NCOS32)=0 THEN LPRINT:GOTO 5070
5045  IF CVI(NCOE32)=999 AND CVI(NCOS32)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 5070
5050  GUAPRI#=CVI(NCOE32)*100/CVI(NCOS32):N.D%=2:GOSUB 64770
5060  LPRINT TAB(73-L.C%) GUAPRI$ " %"
5070  IF ESCUE$="S" THEN LPRINT TAB(7) "Control n즡ero 3 .............";:GOTO 5090
5080  LPRINT TAB(7) "EXAMEN FIN DE TRIMESTRE ......";
5090  IF CVI(NCOS13)=0 THEN 5115
5095  IF CVI(NCOE13)=999 AND CVI(NCOS13)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5115
5100  GUAPRI#=CVI(NCOE13)*100/CVI(NCOS13):N.D%=2:GOSUB 64770
5110  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5115  IF TRI%=1 THEN LPRINT:GOTO 5180
5120  IF CVI(NCOS23)=0 THEN LPRINT:GOTO 5145
5125  IF CVI(NCOE23)=999 AND CVI(NCOS23)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 5145
5130  GUAPRI#=CVI(NCOE23)*100/CVI(NCOS23):N.D%=2:GOSUB 64770
5140  LPRINT TAB(60-L.C%) GUAPRI$ " %";
5145  IF TRI%=2 THEN LPRINT:GOTO 5180
5150  IF CVI(NCOS33)=0 THEN LPRINT:GOTO 5180
5155  IF CVI(NCOE33)=999 AND CVI(NCOS33)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 5180
5160  GUAPRI#=CVI(NCOE33)*100/CVI(NCOS33):N.D%=2:GOSUB 64770
5170  LPRINT TAB(73-L.C%) GUAPRI$ " %"
5180  LPRINT:LPRINT:LPRINT TAB(7) "Comportamiento ...............";
5190  IF NCOMP1="M" THEN LPRINT TAB(42) "Malo";:GOTO 5235
5200  IF NCOMP1="R" THEN LPRINT TAB(42) "Regular";:GOTO 5235
5210  IF NCOMP1="B" THEN LPRINT TAB(42) "Bueno";:GOTO 5235
5220  IF NCOMP1="E" THEN LPRINT TAB(42) "Muy Bueno";:GOTO 5235
5230  LPRINT TAB(42) "         ";
5235  IF TRI%=1 THEN 5276
5240  IF NCOMP2="M" THEN LPRINT TAB(55) "Malo";:GOTO 5271
5250  IF NCOMP2="R" THEN LPRINT TAB(55) "Regular";:GOTO 5271
5260  IF NCOMP2="B" THEN LPRINT TAB(55) "Bueno";:GOTO 5271
5265  IF NCOMP2="E" THEN LPRINT TAB(55) "Muy Bueno";:GOTO 5271
5270  LPRINT TAB(55) "         ";
5271  IF TRI%=2 THEN 5276
5272  IF NCOMP3="M" THEN LPRINT TAB(68) "Malo":GOTO 5280
5273  IF NCOMP3="R" THEN LPRINT TAB(68) "Regular":GOTO 5280
5274  IF NCOMP3="B" THEN LPRINT TAB(68) "Bueno":GOTO 5280
5275  IF NCOMP3="E" THEN LPRINT TAB(68) "Muy Bueno":GOTO 5280
5276  LPRINT TAB(68) "         "
5280  IF ESCUE$="N" THEN 5760
5290  LPRINT TAB(7) "Realizaci줻 de Tareas ........";
5300  IF NTARE1="S" THEN LPRINT TAB(42) "Siempre";:GOTO 5332
5305  IF NTARE1="C" THEN LPRINT TAB(42) "Casi siempre";:GOTO 5332
5310  IF NTARE1="A" THEN LPRINT TAB(42) "A veces";:GOTO 5332
5315  IF NTARE1="P" THEN LPRINT TAB(42) "Pocas veces";:GOTO 5332
5320  IF NTARE1="N" THEN LPRINT TAB(42) "Nunca";:GOTO 5332
5330  LPRINT TAB(42) "     ";
5332  IF TRI%=1 THEN 5348
5333  IF NTARE2="S" THEN LPRINT TAB(55) "Siempre";:GOTO 5340
5334  IF NTARE2="C" THEN LPRINT TAB(55) "Casi siempre";:GOTO 5340
5335  IF NTARE2="A" THEN LPRINT TAB(55) "A veces";:GOTO 5340
5336  IF NTARE2="P" THEN LPRINT TAB(55) "Pocas veces";:GOTO 5340
5337  IF NTARE2="N" THEN LPRINT TAB(55) "Nunca";:GOTO 5340
5338  LPRINT TAB(55) "     ";
5340  IF TRI%=2 THEN 5348
5342  IF NTARE3="S" THEN LPRINT TAB(68) "Siempre":GOTO 5430
5343  IF NTARE3="C" THEN LPRINT TAB(68) "Casi siempre":GOTO 5430
5344  IF NTARE3="A" THEN LPRINT TAB(68) "A veces":GOTO 5430
5345  IF NTARE3="P" THEN LPRINT TAB(68) "Pocas veces":GOTO 5430
5346  IF NTARE3="N" THEN LPRINT TAB(68) "Nunca":GOTO 5430
5348  LPRINT TAB(68) "     ":GOTO 5430
5350  LPRINT
5360  LPRINT TAB(7) "De  0,00 %  a  49,99 %  컴>  Suspenso     "
5370  LPRINT TAB(7) "De 50,00 %  a  59,99 %  컴>  Apto         "
5380  LPRINT TAB(7) "De 60,00 %  a  69,99 %  컴>  Bien         "
5390  LPRINT TAB(7) "De 70,00 %  a  89,99 %  컴>  Notable      "
5400  LPRINT TAB(7) "De 90,00 %  a 100,00 %  컴>  Sobresaliente"
5410  LPRINT
5420  GOTO 5760
5430  LPRINT
5440  LPRINT TAB(7) "EXAMEN FIN DE TRIMESTRE"
5450  LPRINT TAB(7) "-----------------------"
5460  LPRINT TAB(7) "Gram쟴ica y vocabulario ......";
5470  IF CVI(NGRS01)=0 THEN 5500
5475  IF CVI(NGRE01)=999 AND CVI(NGRS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5500
5480  GUAPRI#=CVI(NGRE01)*100/CVI(NGRS01):N.D%=2:GOSUB 64770
5490  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5500  IF CVI(NGRS02)=0 THEN 5530
5505  IF CVI(NGRE02)=999 AND CVI(NGRS02)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 5530
5510  GUAPRI#=CVI(NGRE02)*100/CVI(NGRS02):N.D%=2:GOSUB 64770
5520  LPRINT TAB(60-L.C%) GUAPRI$ " %";
5530  IF CVI(NGRS03)=0 THEN 5560
5532  IF CVI(NGRE03)=999 AND CVI(NGRS03)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 5560
5535  IF TRI%=2 THEN 5560
5540  GUAPRI#=CVI(NGRE03)*100/CVI(NGRS03):N.D%=2:GOSUB 64770
5550  LPRINT TAB(73-L.C%) GUAPRI$ " %"
5560  LPRINT TAB(7) "Comprensi줻 y expresi죓n oral.";
5570  IF CVI(NEXS01)=0 THEN 5600
5575  IF CVI(NEXE01)=999 AND CVI(NEXS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5600
5580  GUAPRI#=CVI(NEXE01)*100/CVI(NEXS01):N.D%=2:GOSUB 64770
5590  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5600  IF CVI(NEXS02)=0 THEN 5630
5605  IF CVI(NEXE02)=999 AND CVI(NEXS02)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 5630
5610  GUAPRI#=CVI(NEXE02)*100/CVI(NEXS02):N.D%=2:GOSUB 64770
5620  LPRINT TAB(60-L.C%) GUAPRI$ " %";
5630  IF CVI(NEXS03)=0 THEN 5660
5632  IF CVI(NEXE03)=999 AND CVI(NEXS03)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 5660
5635  IF TRI%=2 THEN 5660
5640  GUAPRI#=CVI(NEXE03)*100/CVI(NEXS03):N.D%=2:GOSUB 64770
5650  LPRINT TAB(73-L.C%) GUAPRI$ " %"
5660  LPRINT TAB(7) "Literatura y composici줻 .....";
5670  IF CVI(NLIS01)=0 THEN 5700
5675  IF CVI(NLIE01)=999 AND CVI(NLIS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5700
5680  GUAPRI#=CVI(NLIE01)*100/CVI(NLIS01):N.D%=2:GOSUB 64770
5690  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5700  IF CVI(NLIS02)=0 THEN 5730
5705  IF CVI(NLIE02)=999 AND CVI(NLIS02)=999 THEN LPRINT TAB(54) "NO PRESEN";:GOTO 5730
5710  GUAPRI#=CVI(NLIE02)*100/CVI(NLIS02):N.D%=2:GOSUB 64770
5720  LPRINT TAB(60-L.C%) GUAPRI$ " %";
5730  IF CVI(NLIS03)=0 THEN LPRINT:GOTO 5760
5732  IF CVI(NLIE03)=999 AND CVI(NLIS03)=999 THEN LPRINT TAB(67) "NO PRESEN":GOTO 5760
5735  IF TRI%=2 THEN LPRINT:GOTO 5760
5740  GUAPRI#=CVI(NLIE03)*100/CVI(NLIS03):N.D%=2:GOSUB 64770
5750  LPRINT TAB(73-L.C%) GUAPRI$ " %"
5760  LPRINT
5770  LPRINT:LPRINT TAB(7) "Observaciones generales :":LPRINT
5780  LPRINT TAB(7) STRING$(69,196)
5790  LPRINT TAB(7) STRING$(69,196)
5800  LPRINT TAB(7) STRING$(69,196)
5810  LPRINT TAB(7) STRING$(69,196)
5820  LPRINT TAB(7) STRING$(69,196)
5830  LPRINT
5840  LPRINT TAB(51) "Firma de los padres"
5850  FOR A%=1 TO 3:LPRINT:NEXT A%
5860  LPRINT TAB(7) "Comentarios de los padres :":LPRINT
5870  LPRINT TAB(7) STRING$(69,196)
5880  LPRINT TAB(7) STRING$(69,196)
5890  LPRINT TAB(7) STRING$(69,196):LPRINT
5900  IF ESCUE$="N" THEN 5920
5905  IF LEFT$(GCURSO,5)="ADVAN" OR LEFT$(GCURSO,5)="FURTHE" THEN LPRINT TAB(7) "Este centro considera que el nivel requerido para aprobar es un 65 %":GOTO 5970
5910  LPRINT TAB(7) "Este centro considera que el nivel requerido para aprobar es un 70 %":GOTO 5970
5920  LPRINT TAB(7) "De  0,00 %  a  49,99 %  컴>  Suspenso     "
5930  LPRINT TAB(7) "De 50,00 %  a  59,99 %  컴>  Apto         "
5940  LPRINT TAB(7) "De 60,00 %  a  69,99 %  컴>  Bien         "
5950  LPRINT TAB(7) "De 70,00 %  a  89,99 %  컴>  Notable      "
5960  LPRINT TAB(7) "De 90,00 %  a 100,00 %  컴>  Sobresaliente"
5970  LPRINT CHR$(12)
5980  '
5990 CLOSE
6000 GOTO 300
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,13
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin de listar calificaciones."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,33:PRINT "Aplicaci줻 : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACAFANO"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
31500 '********************************
31502 '***APERTURA FICHERO DE FALTAS***
31504 '********************************
31506 'FICHE1$="\"+ESTA$+"\FACAAUSE.DAT"
31508 '
31510 'APERTURA FICHERO DE DATOS.
31512 OPEN "R",#5,"FACAAUSE.DAT",48
31514 'REGISTRO DE DATOS.
31516 FIELD #5,2 AS AENE,2 AS AFEB,2 AS AMAR,2 AS AABR,2 AS AMAY,2 AS AJUN,2 AS AJUL,2 AS AAGO,2 AS ASET,2 AS AOCT,2 AS ANOV,2 AS ADIC
31517 '
31518 FIELD #5,24 AS JTOD,2 AS JENE,2 AS JFEB,2 AS JMAR,2 AS JABR,2 AS JMAY,2 AS JJUN,2 AS JJUL,2 AS JAGO,2 AS JSET,2 AS JOCT,2 AS JNOV,2 AS JDIC
31520 '
31587 RETURN
31588 '
31590 '
32000 '***APERTURA FICHERO DE NOTAS ***
32010 '********************************
32020 'FICHE1$="\"+ESTA$+"\FACANOTA.DAT"
32030 'DEFSTR N
32040 'APERTURA FICHERO DE DATOS.
32050 OPEN "R",#6,"FACANOTA.DAT",100
32060 'REGISTRO DE DATOS.
32070 FIELD #6,2 AS NCOE11,2 AS NCOS11,2 AS NCOE12,2 AS NCOS12,2 AS NCOE13,2 AS NCOS13
32080 FIELD #6,12 AS NRE001,2 AS NCOE21,2 AS NCOS21,2 AS NCOE22,2 AS NCOS22,2 AS NCOE23,2 AS NCOS23
32090 FIELD #6,24 AS NRE002,2 AS NCOE31,2 AS NCOS31,2 AS NCOE32,2 AS NCOS32,2 AS NCOE33,2 AS NCOS33
32100 FIELD #6,36 AS NRE003,2 AS NGRE01,2 AS NGRS01,2 AS NGRE02,2 AS NGRS02,2 AS NGRE03,2 AS NGRS03
32110 FIELD #6,48 AS NRE004,2 AS NEXE01,2 AS NEXS01,2 AS NEXE02,2 AS NEXS02,2 AS NEXE03,2 AS NEXS03
32120 FIELD #6,60 AS NRE005,2 AS NLIE01,2 AS NLIS01,2 AS NLIE02,2 AS NLIS02,2 AS NLIE03,2 AS NLIS03
32130 FIELD #6,72 AS NRE006,1 AS NCOMP1,1 AS NCOMP2,1 AS NCOMP3,1 AS NTARE1,1 AS NTARE2,1 AS NTARE3,22 AS NRE007
32140 '
32150 RETURN
32160 '
32500 '*******************************
32510 '***APERTURA FICHERO GRUPOS  ***
32520 '*******************************
32530 'Comprobamos la existencia del fichero.
32540 ' Apertura del fichero de datos   FACAGRUP.DAT
32550 OPEN "R",#4,"FACAGRUP.DAT",380
32560 FIELD #4,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32570 FIELD #4,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32580 FIELD #4,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB
32590 FIELD #4,150 AS GPRI2,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,1 AS GSB,1 AS GESCUE,14 AS GLIBR,200 AS GALUS
32600 FIELD #4,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32610 FIELD #4,180 AS GTODO,200 AS GALUS
32620 GET #4,1
32630 IF GETIQUETA="FACAGRUP.DAT" THEN 32650
32640 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32650 ULTGRU%=CVI(GULTIMO)
32660 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '*********************************
35002 '*** PANTALLA DE DATOS A METER ***
35004 '*********************************
35006 COLOR 11:LOCATE 8,5:PRINT "Alumno N.  : <      >" STRING$(50," ")
35008 LOCATE 10,5:PRINT "Grupo       : <   >"
35014 LOCATE 12,5:PRINT "Trimestre   : < >"
35016 LOCATE 14,5:PRINT "Profesores  : <"STRING$(30," ")">"
35024 '
35026 RETURN
35028 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,4):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1900  OR ANO% > 2099 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="" OR D.R$="" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0     ':N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN GUAPRI$=GUAPRI$+".000":GOTO 64794
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
