10 '                       PACAFACT.BAS
20 '                      --------------
30 '     GENERACION DEL FICHERO DE FACTURACION ACADEMIAS
40 '     -----------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,4)
134 FEFACT$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)
135 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,B,F
150 OPTION BASE 1
200 DIM TABENT$(30),SIFA$(999)
205 FOR A%=1 TO 999:SIFA$(A%)="N":NEXT A%
210 GOSUB 34000
302 IF ULTDIR%=ACTDIR% THEN CLOSE:GOTO 332  'CONTINUAR CON EL PROGRAMA.
304 '
306 FICBAT$="ACABANCO.BAT;"+DIRE$+";"+PROGRAMA$
308 '
310 CLS
312 SHELL FICBAT$
314 CLS
316 CLOSE
318 GOSUB 34010
320 GET #3,1
322 ULTIMO%=ALTDIR%+1
324 LSET BULTIMO=MKI$(ULTIMO%)
326 LSET BULTACT=MKI$(ULTIMO%)
328 PUT #3,1
330 CLOSE
332 '
336 GOSUB 34010
340 DIM BA$(N.B%-1),N.NU%(N.B%-1),N.IM#(N.B%-1)
342 FOR A%=2 TO N.B%
348 GET #3,A%
350 BA$(A%-1)=BANCO:N.NU%(A%-1)=0:N.IM#(A%-1)=0
360 NEXT A%
370 GOSUB 32000
380 DIM N.NUG%(ULTGRU%),N.IMG#(ULTGRU%)
390 FOR A%=1 TO ULTGRU%
400 N.NUG%(A%)=0:N.IMG#(A%)=0
410 NEXT A%
3000 CLOSE
3010 GOSUB 8000
3020 COLOR 10:LOCATE 6,24:PRINT "- GENERAR FICHERO DE FACTURACION -"
3025 LOCATE 7,24:PRINT STRING$(34,205):COLOR 11
3030 LOCATE 10,26:PRINT "1.- FACTURACION TOTAL."
3040 LOCATE 12,26:PRINT "2.- FACTURACION GRUPOS SUELTOS."
3050 LOCATE 14,26:PRINT "Teclear Opcion : < >"
3060 F.C1%=14:C.C1%=44:L.E%=1:BO%=1:ESC%=0:GOSUB 63000
3070 IF I.T%<>0 THEN 3060
3080 IF D.E$="1" THEN 3210
3090 IF D.E$<>"2" THEN BEEP:GOTO 3060
3100 LOCATE 16,26:PRINT "Teclear GRUPO  : <   >"
3110 COLOR 12:LOCATE 18,26:PRINT "- F7 - Final de Grupos."
3115 COLOR 15
3120 F.C1%=16:C.C1%=44:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
3130 IF I.T%=0 THEN G%=VAL(D.E$):GOTO 3150
3135 IF I.T%=4 THEN 4490
3140 GOTO 3100
3150 LOCATE 16,44:PRINT USING "###";G%
3155 GOSUB 8500:MEN$="Grupo Correcto pulsar ENTER, Incorrecto pulsar F1":GOSUB 8500
3160 A$=INPUT$(1)
3165 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3160
3170 IF ASC(A$)=14 THEN 3100
3180 SIFA$(G%)="S"
3190 GOTO 3100
3200 '
3210 FOR A%=1 TO 999:SIFA$(A%)="S":NEXT A%
4490 '*************************
4491 '***CREAR FICHERO FACT.***
4492 '*************************
4494 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
4496 GOSUB 8500:MEN$="Para cobrar todo el m‚s * ENTER *, medio m‚s * F1 *":GOSUB 8500
4497 A$=INPUT$(1)
4498 IF ASC(A$)<13 OR ASC(A$)>14 THEN 4497
4499 IF ASC(A$)=13 THEN MITAD%=1:GOTO 4501
4500 MITAD%=2
4501 GOSUB 8000
4504 '
4520 COLOR 10:LOCATE 6,24:PRINT "- GENERAR FICHERO DE FACTURACION -"
4530 LOCATE 7,24:PRINT STRING$(34,205)
4550 COLOR 11:LOCATE 12,28:PRINT "MES A FACTURAR  : <      >"
4552 COLOR 15:LOCATE 12,47:PRINT RIGHT$(FEFACT$,2)+LEFT$(FEFACT$,4)
4554 GOSUB 8500
4556 LOCATE 22,15
4558 COLOR 11:PRINT "Si el Mes es correcto pulsa ";
4560 COLOR 15:PRINT " ENTER ";
4562 COLOR 11:PRINT " Si no pulsa ";
4564 COLOR 15:PRINT " F1"
4566 A$=INPUT$(1)
4568 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4566
4570 IF ASC(A$)=13 THEN 4770
4572 LOCATE 12,48:PRINT SPC(4)
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=12:C.C1%=47:L.E%=6:BO%=6:ESC%=0:GOSUB 63000
4710 IF I.T%<>0 THEN 4700
4720 FEFACT$=RIGHT$(D.E$,4)+LEFT$(D.E$,2):LOCATE 12,47:PRINT D.E$:GOTO 4554
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 '
4780 '
4801   LIN%=90:PAG%=0
4802   GOSUB 8500:MEN$=" Espere por favor ........":GOSUB 8500
4803   GOSUB 35000
4804   GOSUB 32000
4818 GOSUB 30000
4820 GOSUB 34000:N.ALU%=1
4830 FOR REG%=2 TO ULTIMO%
4831   GET #2,REG%
4832   'IF CGRUPO<"100" THEN 4900
4833   'IF CGRUPO>"132" THEN 4900
4834   'IF CGRUPO="101" THEN 4900
4835   'IF CGRUPO="105" THEN 4900
4836   'IF CGRUPO="107" THEN 4900
4837   'IF CGRUPO="116" THEN 4900
4838   'IF CGRUPO="127" THEN 4900
4842   IF CSTATUS<>"O" THEN 4900 'AL NEXT
4844   IF CMARCA="L" THEN 4900
4845  'IF CFACT=>FEFACT$ THEN 4900
4846   IF SIFA$(VAL(CGRUPO))="N" THEN 4900
4848   N.ALU%=N.ALU%+1:GET #4,N.ALU%
4850   NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4852   LSET FAOMB=CUCUCU$:LSET FDIRE=CDIREC:LSET FALUM=CCUENTA
4854   LSET FPOBL=CPOBLA:LSET FSTAT="O":LSET FFEEX=FEDIA$
4855   IF LEFT$(CBANCO,1)<>" " THEN LSET FBANC=CBANCO:LSET FCUEN=CCUBAN:GOTO 4858
4856   LSET FBANC="9999":LSET FCUEN="                 "
4858   BUSCA$=CBANCO:GOSUB 34260
4860   LSET FOFIC=CSUCUR:LSET FDIGI=CDIGI:LSET FSUCU=BNOM
4862  IF CCUESP="S" THEN PRI!=CVS(CIMPOR):GOTO 4868
4864  GET #6,VAL(CGRUPO)
4866  PRI!=CVS(GCUOTA)
4868  PRIE!=(PRI!/MITAD%):LSET FIMPO=MKS$(PRIE!)
4869  LSET FFIL="    "
4870  PUT #4,N.ALU%
4872 'LSET CFACT=FEFACT$
4874 'PUR #2,REG%
4876 FOR A%=1 TO N.B%-1
4878     IF BA$(A%)=FBANC THEN N.NU%(A%)=N.NU%(A%)+1:N.IM#(A%)=N.IM#(A%)+CVS(FIMPO):GOTO 4882
4880 NEXT A%
4882 N.NUG%(VAL(CGRUPO))=N.NUG%(VAL(CGRUPO))+1
4884 N.IMG#(VAL(CGRUPO))=N.IMG#(VAL(CGRUPO))+CVS(FIMPO)
4886 'PRINT N.ALU%
4900 NEXT REG%
4902 'STOP
4910  GET #4,1
4915  LSET FSTATUS="A":LSET FALTA=MKI$(N.ALU%):LSET FFACT=FEFACT$:LSET FRES=STRING$(191," "):LSET FETIQ="FACAFACT.DAT"
4920   PUT #4,1
4922 CLOSE #4
4923 'GOSUB 35000:GET #4,1:PRINT CVI(FALTA):STOP
4924 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4926   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B";
4928  'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4930   IF N.ALU%=1 THEN GOSUB 8500:MEN$="No hay Alumnos para Facturar, pulse una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9040
4935     LPRINT TAB(26) EMPRESA$;
4940     LPRINT TAB(65) "RESUMEN DE LA FACTURACION DEL DIA " FECHA$
4950     LPRINT TAB(26) STRING$(LEN(EMPRESA$),205);
4955     LPRINT TAB(65) STRING$(44,205)
4960     LPRINT
4962     LPRINT TAB(47) "L I S T A D O    P O R    B A N C O S"
4964     LPRINT TAB(47) STRING$(37,205):LPRINT
4965     LPRINT TAB(26) "          B A N C O";
4970     LPRINT TAB(71) "RECIBOS.";
4975     LPRINT TAB(94) " I M P O R T E"
4980     LPRINT TAB(26) STRING$(30,205);
4985     LPRINT TAB(71) STRING$(8,205);
4990     LPRINT TAB(94) STRING$(15,205)
4995     FOR A%=1 TO N.B%-1
5000     IF N.NU%(A%)=0 THEN 5060
5020     BUSCA$=BA$(A%):GOSUB 34260
5030     LPRINT TAB(26) BNOM;
5040     GUAPRI#=N.NU%(A%):N.D%=0:GOSUB 64770:LPRINT TAB(77-L.C%) GUAPRI$;
5045     N.TO%=N.TO%+N.NU%(A%)
5050     GUAPRI#=N.IM#(A%):N.D%=2:GOSUB 64770:LPRINT TAB(108-L.C%) GUAPRI$
5055     N.TO#=N.TO#+N.IM#(A%)
5060     NEXT A%
5062     LPRINT
5064     LPRINT TAB(58) "Total ....";
5066     GUAPRI#=N.TO%:N.D%=0:GOSUB 64770:LPRINT TAB(77-L.C%) GUAPRI$;
5068     GUAPRI#=N.TO#:N.D%=2:GOSUB 64770:LPRINT TAB(108-L.C%) GUAPRI$
5070     LPRINT CHR$(12)
5100     FOR A%=1 TO ULTGRU%
5230     IF LIN%>60 THEN GOSUB 7000
5240     IF N.NUG%(A%)=0 THEN 5310
5250     GET #6,A%
5260     LPRINT TAB(26) USING"###";VAL(GGRUPO);
5265     LPRINT TAB(40) GCURSO;
5270     GUAPRI#=N.NUG%(A%):N.D%=0:GOSUB 64770:LPRINT TAB(77-L.C%) GUAPRI$;
5280     N.TOG%=N.TOG%+N.NUG%(A%)
5290     GUAPRI#=N.IMG#(A%):N.D%=2:GOSUB 64770:LPRINT TAB(108-L.C%) GUAPRI$
5300     N.TOG#=N.TOG#+N.IMG#(A%):LIN%=LIN%+1
5310     NEXT A%
5320     LPRINT
5330     LPRINT TAB(58) "Total ....";
5340     GUAPRI#=N.TOG%:N.D%=0:GOSUB 64770:LPRINT TAB(77-L.C%) GUAPRI$;
5350     GUAPRI#=N.TOG#:N.D%=2:GOSUB 64770:LPRINT TAB(108-L.C%) GUAPRI$
5360     LPRINT CHR$(12)
5370     '
5380     GOTO 9000
5390     '
6900     '*********************************
6910     '***CABECERAS DE LISTADO GRUPOS***
6920     '*********************************
6930     '
7000     IF LIN%=90 THEN 7020
7010     LPRINT CHR$(12)
7020     LIN%=0:PAG%=PAG%+1
7100     LPRINT TAB(26) EMPRESA$;
7110     LPRINT TAB(65) "RESUMEN DE LA FACTURACION DEL DIA " FECHA$
7120     LPRINT TAB(26) STRING$(LEN(EMPRESA$),205);
7130     LPRINT TAB(65) STRING$(44,205)
7140     LPRINT
7150     LPRINT TAB(26) "L I S T A D O    P O R    G R U P O S";
7155     LPRINT TAB(97) USING"PAGINA : ###";PAG%
7160     LPRINT TAB(26) STRING$(37,205);
7165     LPRINT TAB(97) STRING$(12,205):LPRINT
7170     LPRINT TAB(26) "GRUPO             C U R S O";
7180     LPRINT TAB(71) "RECIBOS.";
7190     LPRINT TAB(94) " I M P O R T E"
7200     LPRINT TAB(26) STRING$(5,205);
7205     LPRINT TAB(40) STRING$(15,205);
7210     LPRINT TAB(71) STRING$(8,205);
7220     LPRINT TAB(94) STRING$(15,205):LPRINT
7225     LIN%=9
7230     RETURN
7990     GOTO 9000
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,33:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9035 SHELL "ACAFACT.BAT"
9040 RUN "PACADISF"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FVIDCLIE.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,6 AS CFEFAC,3 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
32000 '*******************************
32010 '***APERTURA FICHERO GRUPOS  ***
32020 '*******************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FACAGRUP.DAT",380
32060 FIELD #6,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32070 FIELD #6,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32080 FIELD #6,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,16 AS GLIBR,200 AS GALUS
32090 FIELD #6,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32100 FIELD #6,180 AS GTODO,200 AS GALUS
32110 GET #6,1
32120 IF GETIQUETA="FACAGRUP.DAT" THEN 32140
32130 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140 ULTGRU%=CVI(GULTIMO)
32150 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*****************************
34010 '***APERTURA FICHERO BANCOS***
34020 '*****************************
34030 FICHE1$="FACABANC.DAT"
34040 '
34050 'APERTURA FICHERO DE DATOS.
34060 OPEN "R",#3,FICHE1$,125
34070 'REGISTRO 1.
34080 FIELD #3,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
34090 'REGISTRO DE DATOS.
34100 FIELD #3,4 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,5 AS BFIL001,1 AS BSTATUS
34110 GET #3,1
34120 IF BETIQUETA="FACABANC.DAT" THEN 34160  'CONTINUAR
34130 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
34140 A$=INPUT$(1)
34150 GOTO 9000
34160 'CONTINUAR.
34170 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
34180 ULTBAN%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
34190 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
34200 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
34210 N.B%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
34220 '
34230 RETURN
34240 '
34250 '*************************
34260 '***LECTURA AZAR BANCOS***
34270 '*************************
34280 'El argumento de busqueda sobre 'BUSCA$'
34290 'El numero de registros sobre 'N.B%'
34300 R.B%=0           'Donde devuelve el nro. de registro.
34310 R.W%=0           'Donde devuelve el nro. de registro del DIR.
34320 SW1%=0         'SW indicador de la busqueda.
34330 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34340 '
34350 R.I%=INT((N.B%/2)+.5)
34360 R.C%=R.I%
34370 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34490
34380 IF R.I%>N.B% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.B%:GOTO 34490
34390 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34400 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 34660      'MIRAR STATUS
34410 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34420 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34440
34430 R.I%=R.I%+R.C%
34440 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34370
34450 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34370
34460 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34370
34470 REG2%=R.I%:SW100%=0
34480 GOTO 34370
34490 'REGISTRO NO BUSCADO.
34500 XX%=0
34510 FOR AXX%=DES% TO HAS%
34520   GET #3,AXX%
34530   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=AXX%:R.B%=AXX%:XX%=1:AXX%=HAS%
34540 NEXT AXX%
34550 IF XX%=1 THEN 34660
34560 '
34570 '
34580 'Registro NO BUSCADO
34590 XX%=0
34600 IF N.B%=0 THEN N.B%=1
34610 FOR JMG%=N.B% TO ULTBAN%
34620     GET #3,JMG%
34630     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
34640 NEXT JMG%
34650 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
34660 'MIRAMOS STATUS.
34670 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
34680 WSTATUS$=""
34690 RETURN
34700 '
35000 '*************************************
35010 '***APERTURA FICHERO DE FACTURACION***
35020 '*************************************
35030 OPEN "R",#4,"FACAFACT.DAT",210
35040 FIELD #4,1 AS FSTATUS,2 AS FALTA,6 AS FFACT,12 AS FETIQ,189 AS FRES
35050 FIELD #4,1 AS FSTAT,5 AS FALUM,4 AS FIMPO,8 AS FFEEX,50 AS FAOMB,40 AS FDIRE,20 AS FPOBL,4 AS FBANC,30 AS FSUCU,10 AS FCUEN,4 AS FOFIC,2 AS FDIGI,32 AS FFIL
35120 RETURN
35130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
