10 '                       PAGEOPCL.BAS
20 '                      --------------
30 '     GESTION DE OPERACIONES CON CLIENTES DE LA AGENCIA
40 '     -------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
133 FEHOY$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
134 GOSUB 33000
135 OPEN "I",#4,"FAGECLA.DIA"
136 LINE INPUT #4,TODO$
137 CLAVE$=LEFT$(TODO$,4)
138 CLOSE
140 DEFSTR H,C,O,P,X,T
150 OPTION BASE 1
160 DIM TABRUT$(6),DIAS%(12),TABENT$(30),ORI$(2)
162 DATA "Original para la Agencia","Original para la Direcci¢n"
164 FOR A%=1 TO 2:READ ORI$(A%):NEXT A%
174 DATA 31,28,31,30,31,30,31,31,30,31,30,31
176 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
178 '
198 DATA "  GESTION DE FICHAS DE CLIENTES  "
200 DATA "        COBROS A CLIENTES        "
202 DATA " ANULACION DE COBROS DE CLIENTES "
220 DATA " CONSULTAS DE COBROS A  CLIENTES "
225 DATA "  LISTADOS DE COBROS A CLIENTES  "
230 DATA "FINAL DE OPERACIONES CON CLIENTES"
232 FOR I%=1 TO 6
234    READ TABRUT$(I%)
236 NEXT I%
240 DIM TA(31),TB(9),TC(9),LITMES$(12)
242 DIM TODI%(24):KEY OFF:IMPMAN$="000000000"
244 DATA "UNO ","DOS ","TRES ","CUATRO ","CINCO ","SEIS ","SIETE ","OCHO ","NUEVE ","DIEZ ","ONCE ","DOCE ","TRECE ","CATORCE ","QUINCE ","DIECISEIS "
246 DATA "DIECISIETE ","DIECIOCHO ","DIECINUEVE ","VEINTE ","VEINTIUNA ","VEINTIDOS ","VEINTITRES ","VEINTICUATRO ","VEINTICINCO ","VEINTISEIS ","VEINTISIETE ","VEINTIOCHO ","VEINTINUEVE ","TREINTA ","TREINTA Y UNO "
248 FOR I%=1 TO 31:READ TA(I%):NEXT I%
250 DATA "DIEZ ","CIENTO ","VEINTE ",DOSCIENT,"TREINTA ",TRESCIENT,"CUARENTA ",CUATROCIENT,"CINCUENTA ",QUINIENT,"SESENTA ",SEISCIENT,"SETENTA ",SETECIENT,"OCHENTA ",OCHOCIENT,"NOVENTA ",NOVECIENT
252 FOR I%=1 TO 9:READ TB(I%),TC(I%):NEXT I%
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
310 '******************************
320 'Presentamos el Menu del Programa.
330 GOSUB 8000
332 TIT$="- GESTION GENERAL DE OPERACIONES CON CLIENTES -"
334 X%=80-LEN(TIT$):X%=INT(X%/2)
340 COLOR 11:LOCATE 6,X%:PRINT TIT$
350 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
360 FOR I%=9 TO 14
370    LOCATE I%,20:PRINT TABRUT$(I%-8)
380 NEXT I%
390 '
400 GOSUB 8500:COLOR 11
410 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulsando la Tecla ";
420 COLOR 15:PRINT " ENTER "
430 'COLOR 7,0,0
440 '
450 C.F%=9:C%=20:L.F%=14:N.L%=6
460 GOSUB 50000
470 '
480 ON I.A% GOTO 6000,510,2500,2500,4490,9000
490 '
500 '
510 '
520 '
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
550 '
570 '
590 '
610 MARGEN$="I"
618 '
619 GOSUB 8000:GOSUB 31000
620 FOR F%=8 TO 18
625 GOSUB 30500:GOSUB 35000:GOSUB 35500
630 '
640 GOSUB 7650
700       '*******************************
705       '***ENTRADA NUMERO DE ALUMNO ***
710       '*******************************
715       COLOR 15
720       LOCATE 1,1:N.ORDEN!=ULTI!+1
725       N$=STR$(N.ORDEN!):N$=LEFT$(CERO$,6-LEN(N$))+RIGHT$(N$,LEN(N$)-1)
730       GET #2,N.ORDEN!
735       '
740       '
745       '
750       GUAPRI#=N.ORDEN!:GOSUB 64770:LOCATE F%,6-L.C%:PRINT GUAPRI$
755       '
800       '***********************
805       '***ENTRADA DE FOPERA***
810       '***********************
815       F.C1%=F%:C.C1%=12:L.E%=6:BO%=8:GOSUB 63000
820       IF I.T%=0 THEN NACI$=D.E$:GOTO 865
825       IF I.T%=5 THEN 815
830       IF I.T%=1 THEN NACI$=LEFT$(FECHA$,2)+MID$(FECHA$,4,2)+RIGHT$(FECHA$,2):GOTO 865
840       IF I.T%=3 THEN LOCATE F%,12:PRINT SPC(8):GOTO 810
850       IF I.T%=4 THEN 4000               'Fin Entrada.
855       IF I.T%=2 THEN 865
860       '
865       FECSUB$=NACI$:GOSUB 60000
870       IF ERF%<>1 THEN 885
875       BEEP:GOSUB 8500:MEN$="Fecha de Operaci¢n Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,12:PRINT SPC(8)
880       GOSUB 7650:GOTO 805
885       IF LEFT$(FAMD$,2)<"94" THEN 875
887       LOCATE F%,12:PRINT FDMA$:FENA$=FAMD$
890       '
900       '*******************************
902       '***ENTRADA NUMERO DE FICHA  ***
904       '*******************************
906       COLOR 15
908       F.C1%=F%:C.C1%=25:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
910       IF I.T%=0 THEN NPRO%=VAL(D.E$):GOTO 928 'Entrada OK
912       IF I.T%=5 THEN 908
914       IF I.T%=1 AND I.A%=2 THEN 908
916       IF I.T%=1 AND I.A%=3 THEN NPRO%=CVI(OPRO):GOTO 928
917       IF I.T%=3 AND I.A%=2 THEN LOCATE F%,12:PRINT SPC(8):GOTO 810
918       IF I.T%=3 THEN 810                 'Retroceder Campo.
920       IF I.T%=4 THEN 4000               'Fin Entrada.
922       IF I.T%=2 AND I.A%=2 THEN 928
924       IF I.T%=2 THEN 902
926       '
928       LOCATE F%,25:PRINT SPC(6)
930       GUAPRI#=NPRO%:GOSUB 64770:LOCATE F%,30-L.C%:PRINT GUAPRI$
932       PRO%=NPRO%
934       IF PRO%<2 OR PRO%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Ficha no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,25:PRINT SPC(6):GOTO 900
936       GET #4,PRO%
937       IF CMARC="C" THEN GOSUB 8500:MEN$="Ficha ANULADA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,25:PRINT SPC(6):GOTO 900
938       IF CMARC="F" THEN GOSUB 8500:MEN$="Ficha FINIQUITADA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,25:PRINT SPC(6):GOTO 900
939       COLOR 12:LOCATE F%+2,11:PRINT CNOMB:COLOR 15
940       GOSUB 8500
941       LOCATE 22,15:COLOR 10:PRINT "Cliente Correcto ";
942       COLOR 15:PRINT "  ENTER  ";
943       COLOR 10:PRINT " Err¢neo ";
944       COLOR 15:PRINT "  F1 "
945       A$=INPUT$(1)
946       IF ASC(A$)<13 OR ASC(A$)>14 THEN 945
948       IF ASC(A$)=13 THEN 963
949       LOCATE F%,25:PRINT SPC(6)
963       IF I.A%=1 THEN GOSUB 7650:GOTO 965
964       GOSUB 7500
965       LOCATE F%+2,2:PRINT SPC(78)
968       LOCATE F%+2,12:COLOR 12:PRINT "- 1 - ";
970       COLOR 15:PRINT "Met lico  ";
972       COLOR 12:PRINT "- 2 - ";
974       COLOR 15:PRINT "Talones  ";
976       COLOR 12:PRINT "- 3 - ";
978       COLOR 15:PRINT "Tarjetas  ";
980       COLOR 12:PRINT "- 4 - ";
982       COLOR 15:PRINT "Otros"
1000      '***********************
1005      '***ENTRADA TIPO OPERA**
1010      '***********************
1015      F.C1%=F%:C.C1%=37:L.E%=1:BO%=1:GOSUB 63000
1020      IF I.T%=0 THEN TIPO$=D.E$:GOTO 1055
1025      IF I.T%=5 THEN 1015
1030      IF I.T%=1 AND I.A%=2 THEN 1015
1032      IF I.T%=1 THEN TIPO$=OTIP:GOTO 1055
1035      IF I.T%=3 AND I.A%=2 THEN LOCATE F%,25:PRINT SPC(6):LOCATE F%+2,2:PRINT SPC(78):GOTO 900
1037      IF I.T%=3 THEN LOCATE F%+2,2:PRINT SPC(78):GOTO 900
1040      IF I.T%=4 THEN 4000               'Fin Entrada.
1045      IF I.T%=2 AND I.A%=2 THEN 1055
1047      IF I.T%=2 THEN 1015
1050      '
1055      IF TIPO$<"1" OR TIPO$>"4" THEN BEEP:LOCATE F%,36:PRINT SPC(3):GOTO 1015
1060      LOCATE F%,36
1061      IF TIPO$="1" THEN PRINT "MET":GOTO 1065
1062      IF TIPO$="2" THEN PRINT "TAL":GOTO 1065
1063      IF TIPO$="3" THEN PRINT "TAR":GOTO 1065
1064      PRINT "OTR"
1065      LOCATE F%+2,2:PRINT SPC(78)
1100       '***********************
1110       '***ENTRADA CONCEPTO ***
1120       '***********************
1130       F.C1%=F%:C.C1%=44:L.E%=21:BO%=21:GOSUB 64000
1140       IF I.T%=0 THEN CONC$=D.E$:GOTO 1210
1150       IF I.T%=5 THEN 1130
1160       IF I.T%=1 AND I.A%=2 THEN 1130
1165       IF I.T%=1 AND I.A%=3 THEN CONC$=OTES:GOTO 1210
1170       IF I.T%=3 AND I.A%=2 THEN LOCATE F%,36:PRINT SPC(3):GOTO 968
1175       IF I.T%=3 THEN 968
1180       IF I.T%=4 THEN 4000               'Fin Entrada.
1190       IF I.T%=2 AND I.A%=2 THEN 1210
1195       IF I.T%=2 THEN 1130
1200       '
1210       '
1220       LOCATE F%,44:PRINT CONC$
1430       '*******************************
1440       '***ENTRADA IMPORTE OPERACION***
1450       '*******************************
1460       F.C1%=F%:C.C1%=69:L.E%=9:BO%=12:ESC%=0:GOSUB 63000
1470       IF I.T%=0 THEN IMPAG#=VAL(D.E$):GOTO 1540
1480       IF I.T%=5 THEN 1460
1490       IF I.T%=1 AND I.A%=2 THEN 1460
1495       IF I.T%=1 AND I.A%=3 THEN IMPAG#=CVD(OPAG):GOTO 1540
1500       IF I.T%=3 AND I.A%=2 THEN LOCATE F%,55:PRINT SPC(12):GOTO 1130
1505       IF I.T%=3 THEN 1130
1510       IF I.T%=4 THEN 4000               'Fin Entrada.
1520       IF I.T%=2 AND I.A%=2 THEN 1540
1525       IF I.T%=2 THEN 1440
1530       '
1540       LOCATE F%,69:PRINT SPC(12):GUAPRI#=IMPAG#:GOSUB 64770
1550       LOCATE F%,80-L.C%:PRINT GUAPRI$
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   LOCATE 22,15:COLOR 10:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT "  ENTER  ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT "  F1 "
2016   '
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2030
2027   LOCATE F%,1:PRINT SPC(80):F%=F%-1:GOTO 2250
2030   LSET ONUM=MKS$(N.ORDEN!):LSET OSTA="O":LSET OMAR="A":LSET OCLA=CLAVE$:LSET OFEC=FENA$:LSET OCOP="C":LSET OTIP=TIPO$
2032   LSET OTES=CONC$:LSET OIMP=MKD$(0):LSET ORES="":LSET OPRO=MKI$(NPRO%):LSET OPAG=MKD$(IMPAG#):LSET OPOC="C"
2034   PUT #2,N.ORDEN!
2036   GET #2,1
2038   LSET OULT=MKS$(N.ORDEN!)
2040   PUT #2,1
2042   '
2043   FICHERO$="FAGECAJA.A"+LEFT$(FEHOY$,2)
2050   GOSUB 30000
2052   ME%=VAL(MID$(FEHOY$,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(FEHOY$,2))
2053   XDIA%=ME%*31+DI%
2054   GET #1,XDIA%
2058   INIC#=CVD(HINI):COME#=CVD(HCOB):COTA#=CVD(HCTA):COPL#=CVD(HCPL):COOT#=CVD(HCOT)
2060   FINA#=CVD(HFIN):PAME#=CVD(HPAG):PATA#=CVD(HPTA):PAPL#=CVD(HPPL):PAOT#=CVD(HPOT)
2065   '
2070  'IF COPA$="C" THEN 2090
2072  'IF TIPO$="1" THEN PAME#=PAME#+IMPAG#:FINA#=FINA#-IMPAG#:GOTO 2100
2074  'IF TIPO$="2" THEN PATA#=PATA#+IMPAG#:GOTO 2100
2076  'IF TIPO$="3" THEN PAPL#=PAPL#+IMPAG#:GOTO 2100
2078  'PAOT#=PAOT#+IMPAG#
2080  'GOTO 2100
2090   IF TIPO$="1" THEN COME#=COME#+IMPAG#:FINA#=FINA#+IMPAG#:GOTO 2100
2092   IF TIPO$="2" THEN COTA#=COTA#+IMPAG#:GOTO 2100
2094   IF TIPO$="3" THEN COPL#=COPL#+IMPAG#:GOTO 2100
2096   COOT#=COOT#+IMPAG#
2098   GOTO 2100
2100   '
2102   LSET HINI=MKD$(INIC#):LSET HCOB=MKD$(COME#):LSET HCTA=MKD$(COTA#)
2104   LSET HCPL=MKD$(COPL#):LSET HCOT=MKD$(COOT#)
2106   LSET HFIN=MKD$(FINA#):LSET HPAG=MKD$(PAME#):LSET HPTA=MKD$(PATA#)
2108   LSET HPPL=MKD$(PAPL#):LSET HPOT=MKD$(PAOT#)
2110   PUT #1,XDIA%
2112  '
2114  TOCU#=CVD(CPACU):TOPE#=CVD(CPAPE)
2116  TOCU#=TOCU#+IMPAG#
2118  TOPE#=TOPE#-IMPAG#
2120  LSET CPACU=MKD$(TOCU#):LSET CPAPE=MKD$(TOPE#)
2122  IF CVD(CNETO)=CVD(CPACU) THEN LSET CMARC="F":GOTO 2126
2124  LSET CMARC="A"
2126  PUT #4,PRO%
2128  '
2200   CLOSE
2205   CANTI#=IMPAG#:NUMERO%=PRO%
2210   GOSUB 10000
2215   '
2220  '
2250   CLOSE
2252   NEXT F%
2253   '
2254   '
2258   '
2260   '
2264   FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
2265   '
2266   GOTO 619
2490 '**********************************************
2495 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
2500 '**********************************************
2505 '
2530 GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
2535 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
2540 '
2545 GOSUB 8000:GOSUB 31000
2550 FOR F%=8 TO 19
2555 GOSUB 30500:GOSUB 35500
2560 GOSUB 7500
2565       '*******************************
2570       '***ENTRADA NUMERO DE ALUMNO ***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=F%:C.C1%=1:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=5 THEN 2585
2615       IF I.T%=4 THEN 4000               'Fin Entrada.
2625       GOTO 2570
2630       '
2635       '
2640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE F%,1:PRINT SPC(6):LOCATE F%,6-L.C%:PRINT GUAPRI$
2642       N.L!=VAL(CUENTA$)
2645       IF N.L!<2 OR N.L!>ULTI!  THEN GOSUB 8500:MEN$="Este N£mero de Operaci¢n no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2650       GET #2,N.L!
2655       IF CLAVE$="F311" OR CLAVE$="f311" THEN 2670
2660       IF OCLA=CLAVE$ THEN 2670
2665       GOSUB 8500:MEN$="Operaci¢n no disponible, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2670       IF OMAR="B" THEN GOSUB 8500:MEN$="Operaci¢n dada de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2675       GOSUB 2700   'PRESENTAR DATOS EN PANTALLA.
2680       IF I.A%=3 THEN 2990
2685       GOTO 3250
2690       '
2700      '*******************************
2702      '*** PONER DATOS EN PANTALLA ***
2704      '*******************************
2706      '
2710      LOCATE F%,12:PRINT RIGHT$(OFEC,2)"-"MID$(OFEC,3,2)"-"LEFT$(OFEC,2)
2720      GUAPRI#=CVI(OPRO):GOSUB 64770
2722      LOCATE F%,30-L.C%:PRINT GUAPRI$
2730      LOCATE F%,37
2732      IF OTIP="1" THEN PRINT "MET":GOTO 2740
2734      IF OTIP="2" THEN PRINT "TAL":GOTO 2740
2736      IF OTIP="3" THEN PRINT "TAR":GOTO 2740
2738      PRINT "OTR"
2740      LOCATE F%,44:PRINT OTES
2750   '  LOCATE F%,55:PRINT SPC(12):GUAPRI#=CVD(OIMP):GOSUB 64770
2760   '  LOCATE F%,66-L.C%:PRINT GUAPRI$
2765      LOCATE F%,69:PRINT SPC(12):GUAPRI#=CVD(OPAG):GOSUB 64770
2770      LOCATE F%,80-L.C%:PRINT GUAPRI$
2780      '
2790      RETURN
2800      '
2980 '*************************
2990 '***TRATAMIENTO DE BAJA***
3000 '*************************
3010 GOSUB 8500:COLOR 10
3020 LOCATE 22,15:PRINT "Para anular la Operaci¢n ";
3030 COLOR 15:PRINT " ENTER ";
3040 COLOR 10:PRINT " Para no anular ";
3050 COLOR 15:PRINT " F1 "
3055 A$=INPUT$(1)
3057 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3055
3060 IF ASC(A$)=14 THEN 2545
3062 LSET OMAR="B"
3064 '
3065 PUT #2,N.L!
3066 '
3068   '
3070   FICHERO$="FAGECAJA.A"+LEFT$(FEHOY$,2)
3072   GOSUB 30000
3074   ME%=VAL(MID$(FEHOY$,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(FEHOY$,2))
3076   XDIA%=ME%*31+DI%
3078   GET #1,XDIA%
3080   INIC#=CVD(HINI):COME#=CVD(HCOB):COTA#=CVD(HCTA):COPL#=CVD(HCPL):COOT#=CVD(HCOT)
3082   FINA#=CVD(HFIN):PAME#=CVD(HPAG):PATA#=CVD(HPTA):PAPL#=CVD(HPPL):PAOT#=CVD(HPOT)
3084   '
3086   'IF COPA$="C" THEN 3098
3088   IF OTIP="1" THEN PAME#=PAME#+CVD(OPAG):FINA#=FINA#-CVD(OPAG):GOTO 3108
3090   IF OTIP="2" THEN PATA#=PATA#+CVD(OPAG):GOTO 3108
3092   IF OTIP="3" THEN PAPL#=PAPL#+CVD(OPAG):GOTO 3108
3094   PAOT#=PAOT#+CVD(OPAG)
3096   GOTO 3108
3098  'IF OTIP="1" THEN COME#=COME#-IMPOR#:FINA#=FINA#-IMPOR#
3100  'IF OTIP="2" THEN COTA#=COTA#-IMPOR#
3102  'IF OTIP="3" THEN COPL#=COPL#-IMPOR#
3104  'COOT#=COOT#-IMPOR#
3106  'GOTO 3108
3108   '
3110   LSET HINI=MKD$(INIC#):LSET HCOB=MKD$(COME#):LSET HCTA=MKD$(COTA#)
3112   LSET HCPL=MKD$(COPL#):LSET HCOT=MKD$(COOT#)
3114   LSET HFIN=MKD$(FINA#):LSET HPAG=MKD$(PAME#):LSET HPTA=MKD$(PATA#)
3116   LSET HPPL=MKD$(PAPL#):LSET HPOT=MKD$(PAOT#)
3118   PUT #1,XDIA%
3120   GOSUB 35000
3122   GET #4,CVI(OPRO)
3124  TOCU#=CVD(CPACU):TOPE#=CVD(CPAPE)
3126  TOCU#=TOCU#-CVD(OPAG)
3128  TOPE#=TOPE#+CVD(OPAG)
3130  LSET CPACU=MKD$(TOCU#):LSET CPAPE=MKD$(TOPE#)
3132  IF CVD(CPACU)=CVD(CNETO) THEN LSET CMARC="F":GOTO 3136
3134  LSET CMARC="A"
3136   CANTI#=CVD(OPAG):NUMERO%=CVI(OPRO)
3138  '
3140  PUT #4,CVI(OPRO)
3150   CLOSE
3160   GOSUB 10000
3170   CLOSE
3172   NEXT F%
3182   FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
3184   '
3186   GOTO 2550
3188   '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3270 A$=INPUT$(1)      ':GOSUB 7650:LOCATE 8,56:PRINT SPC(20)
3280 GOTO 3170
3290 '
3300 '
3310 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4496 CLOSE:WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4498 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4500 GOSUB 8000
4502 COLOR 11:LOCATE 6,11:PRINT "- LISTADOS ESPECIALES DE CLIENTES DE LA AGENCIA DE VIAJES -"
4504 LOCATE 7,11:PRINT STRING$(59,205)
4506 LOCATE 10,18:PRINT "1.- LISTADO DE CLIENTES POR NUMERO DE FICHA"
4508 LOCATE 12,18:PRINT "2.- LISTADO OPERACIONES CLIENTES POR FECHAS"
4510 LOCATE 14,18:PRINT "3.- FINAL DE LOS LISTADOS"
4512 LOCATE 18,31:PRINT "Teclear Opci¢n : < >"
4514 LOCATE 18,49:T$=INPUT$(1)
4516 IF T$<"1" OR T$>"3" THEN BEEP:GOTO 4514
4518 COLOR 15:LOCATE 18,49:PRINT T$
4522   GOSUB 8500
4524   LOCATE 22,15:COLOR 10:PRINT "Petici¢n correcta ";
4526   COLOR 15:PRINT "  ENTER  ";
4528   COLOR 10:PRINT " Petici¢n err¢nea ";
4530   COLOR 15:PRINT "  F1 "
4532   A$=INPUT$(1)
4534   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4532
4536   IF ASC(A$)=14 THEN 4500
4538   IF T$="3" THEN 300
4540   IF T$="1" THEN LI$="- LISTADO DE CLIENTES POR NUMERO DE FICHAS -":GOTO 4544
4542   GOTO 4730
4544   GOSUB 8000
4546   COLOR 10:LOCATE 6,19:PRINT LI$
4548   LOCATE 7,19:PRINT STRING$(44,205)
4550   LOCATE 12,15:PRINT "Desde la Ficha : <      >  hasta la Ficha : <      >"
4552   COLOR 15:LOCATE 16,28:PRINT "F7 ==>  ";:COLOR 11:PRINT "Final de la Opci¢n":COLOR 15
4554       '***********************
4556       '***ENTRADA DE FDESDE***
4558       '***********************
4560       F.C1%=12:C.C1%=33:L.E%=5:BO%=6:GOSUB 63000
4562       IF I.T%=0 THEN DESDE%=VAL(D.E$):GOTO 4570
4564       IF I.T%=4 THEN 4500
4566       GOTO 4554
4568       '
4570       GUAPRI#=DESDE%:GOSUB 64770
4572       LOCATE 12,33:PRINT SPC(6)
4574       LOCATE 12,38-L.C%:PRINT GUAPRI$
4576       '***********************
4578       '***ENTRADA DE FHASTA***
4580       '***********************
4582       F.C1%=12:C.C1%=60:L.E%=5:BO%=6:GOSUB 63000
4584       IF I.T%=0 THEN HASTA%=VAL(D.E$):GOTO 4592
4586       IF I.T%=4 THEN 4500
4588       GOTO 4576
4590       '
4592       GUAPRI#=HASTA%:GOSUB 64770
4594       LOCATE 12,60:PRINT SPC(6)
4596       LOCATE 12,65-L.C%:PRINT GUAPRI$
4598   GOSUB 8500
4600   LOCATE 22,15:COLOR 10:PRINT "Fechas correctas ";
4602   COLOR 15:PRINT "  ENTER  ";
4604   COLOR 10:PRINT " Fechas err¢neas ";
4606   COLOR 15:PRINT "  F1 "
4608   A$=INPUT$(1)
4610   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4608
4612   IF ASC(A$)=14 THEN 4540
4614   '
4616   IF HASTA%<DESDE% THEN GOSUB 8500:MEN$="Fichas err¢neas, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4540
4618 GOSUB 4690
4620 CLOSE
4622 GOTO 4490
4624 '***************************
4626 '***LISTADO LINEA DETALLE***
4628 '***************************
4630 '
4632 IF LIN%<65 THEN 4640
4634 IF LIN%=90 THEN 4638
4636 LPRINT CHR$(12)
4638 LIN%=0:PAG%=PAG%+1:GOSUB 4668
4640 GUAPRI#=A%:GOSUB 64770
4642 LPRINT TAB(14-L.C%) GUAPRI$;
4644 LPRINT TAB(22) CNOMB;
4646 LPRINT TAB(68) CDIRE;
4648 LPRINT TAB(115) CTELE
4650 'GUAPRI#=CVD(CNETO):GOSUB 64770
4652 'LPRINT TAB(124-L.C%) GUAPRI$
4654 LIN%=LIN%+1
4656 '
4658 RETURN
4660 '
4662 '***************************
4664 '***CABECERAS DEL LISTADO***
4666 '***************************
4668 LPRINT TAB(9) "AGENCIA DE VIAJES  E.I.D.E.";
4670 LPRINT TAB(47) "LISTADO DE CLIENTES POR NUMERO DE FICHA AL DIA "FECHA$;
4672 LPRINT TAB(113) USING "HOJA.- : ###";PAG%
4674 LPRINT TAB(9) STRING$(27,205);
4676 LPRINT TAB(47) STRING$(55,205);
4678 LPRINT TAB(113) STRING$(12,205)
4680 LPRINT
4682 LPRINT TAB(9) "NUMERO                  NOMBRE DEL CLIENTE                            DIRECCION DEL CLIENTE                TELEFONO "
4684 LPRINT TAB(9) "ÍÍÍÍÍÍ       ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ      ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ       ÍÍÍÍÍÍÍÍÍÍ":LPRINT:LIN%=6
4686 '
4688 RETURN
4690 '*************************
4692 '***INFORME DEL FICHERO***
4694 '*************************
4696 GOSUB 8500:MEN$="Espere por favor........":GOSUB 8500
4698 GOSUB 35000:GOSUB 35500
4700 FF$=""
4702 LIN%=90
4704 PAG%=0
4706 FOR A%=DESDE% TO HASTA%
4708 GET #4,A%
4710 IF CSTAT<>"O" THEN 4716
4712 IF CMARC="C" THEN 4716
4714    GOSUB 4624
4716 NEXT A%
4718 '
4720 IF LIN%=90 THEN GOSUB 8500:MEN$="No tenemos los datos solicitados, pulse una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4490
4722 LPRINT CHR$(12)
4724 RETURN
4726 '
4728 '
4730 '
4800   LI$="- LISTADO DE OPERACIONES DE CLIENTES ENTRE FECHAS -"
4802   GOSUB 8000
4804   COLOR 10:LOCATE 6,15:PRINT LI$
4806   LOCATE 7,19:PRINT STRING$(52,205)
4808   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
4810   COLOR 15:LOCATE 16,28:PRINT "F7 ==>  ";:COLOR 11:PRINT "Final de la Opci¢n":COLOR 15
4812       '***********************
4814       '***ENTRADA DE FDESDE***
4816       '***********************
4818       F.C1%=12:C.C1%=31:L.E%=6:BO%=8:GOSUB 63000
4820       IF I.T%=0 THEN DESDE$=D.E$:GOTO 4828
4822       IF I.T%=4 THEN 4500
4824       GOTO 4812
4826       '
4828       FECSUB$=DESDE$:GOSUB 60000
4830       IF ERF%<>1 THEN 4836
4832       BEEP:GOSUB 8500:MEN$="Fecha Desde Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,31:PRINT SPC(8)
4834       GOTO 4814
4836       LOCATE 12,31:PRINT FDMA$:FDES$=FAMD$:FLI1$=FDMA$
4838       '***********************
4840       '***ENTRADA DE FHASTA***
4842       '***********************
4844       F.C1%=12:C.C1%=58:L.E%=6:BO%=8:GOSUB 63000
4846       IF I.T%=0 THEN HASTA$=D.E$:GOTO 4854
4848       IF I.T%=4 THEN 4500
4850       GOTO 4838
4852       '
4854       FECSUB$=HASTA$:GOSUB 60000
4856       IF ERF%<>1 THEN 4862
4858       BEEP:GOSUB 8500:MEN$="Fecha Hasta Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,58:PRINT SPC(8)
4860       GOTO 4840
4862       LOCATE 12,58:PRINT FDMA$:FHAS$=FAMD$:FLI2$=FDMA$
4864   GOSUB 8500
4866   LOCATE 22,15:COLOR 10:PRINT "Fechas correctas ";
4868   COLOR 15:PRINT "  ENTER  ";
4870   COLOR 10:PRINT " Fechas err¢neas ";
4872   COLOR 15:PRINT "  F1 "
4874   A$=INPUT$(1)
4876   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4874
4878   IF ASC(A$)=14 THEN 4540
4880   '
4882   IF FHAS$<FDES$ THEN GOSUB 8500:MEN$="Fechas err¢neas, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4540
4884   '
4886 '********************************
4888 '***LISTADOS GENERALES DE CAJA***
4890 '********************************
4892 '
4894 '
4896 GOSUB 30500
4898 OPEN "O",#5,"FAGEOPER.SEL"
4900 FOR NN!=2 TO ULTI!
4902 GET #2,NN!
4904 IF OSTA<>"O" THEN 4942
4906 IF OMAR="B" THEN 4942
4908 IF OPOC<>"C" THEN 4942
4910 IF OFEC<FDES$ THEN 4942
4912 IF OFEC>FHAS$ THEN 4942
4914 GOTO 4924
4916 IF CLAVE$="F311" THEN 4924
4918 '
4920 IF OCLA="A91Z" OR OCLA="AI23" THEN 4924
4922 GOTO 4942
4924 '
4926 NUPR%=CVI(OPRO):NUPR$=STR$(NUPR%):CE%=LEN(NUPR$)-1
4928 NUPR$=LEFT$(CERO$,6-CE%)+RIGHT$(NUPR$,CE%)
4930 NOPE!=CVS(ONUM):NOPE$=STR$(NOPE!):CE%=LEN(NOPE$)-1
4932 NOPE$=LEFT$(CERO$,6-CE%)+RIGHT$(NOPE$,CE%)
4934 CANT#=CVD(OPAG):CANT$=STR$(CANT#):CE%=LEN(CANT$)-1
4936 CANT$=LEFT$(CERO$,15-CE%)+RIGHT$(CANT$,CE%)
4938 SALIDA$=NOPE$+CANT$+OCLA+OFEC+OCOP+OTIP+OTES+NUPR$+OPOC+"****"
4940 PRINT #5,SALIDA$
4942 NEXT NN!
4944 CLOSE
4946 SHELL "AGEFECH.BAT"
4948 GOSUB 5054
4950 CLOSE
4952 GOTO 4490
4954 '***************************
4956 '***LISTADO LINEA DETALLE***
4958 '***************************
4960 '
4962 IF LIN%<65 THEN 4970
4964 IF LIN%=90 THEN 4968
4966 LPRINT CHR$(12)
4968 LIN%=0:PAG%=PAG%+1:GOSUB 5032
4970 GUAPRI#=VAL(XNUM):GOSUB 64770
4972 LPRINT TAB(15-L.C%) GUAPRI$;
4974 'IF XPOC<>"P" THEN 4980
4976 'GUAPRI#=VAL(XPRO):GOSUB 64770
4978 'LPRINT TAB(22-L.C%) GUAPRI$;:GOTO 4986
4980 'IF XPOC<>"C" THEN 4998
4982 GUAPRI#=VAL(XPRO):GOSUB 64770
4984 LPRINT TAB(28-L.C%) GUAPRI$;
4986 'IF XPOC<>"P" THEN 4992
4988 'GET #3,VAL(XPRO)
4990 'LPRINT TAB(33) LEFT$(PNOMB,40);:GOTO 4998
4992 '
4994 GET #4,VAL(XPRO)
4996 LPRINT TAB(33) LEFT$(CNOMB,40);
4998 '
5000 IF XTIP="1" THEN LPRINT TAB(75) "META";:GOTO 5008
5002 IF XTIP="2" THEN LPRINT TAB(75) "TALO";:GOTO 5008
5004 IF XTIP="3" THEN LPRINT TAB(75) "TARJ";:GOTO 5008
5006 LPRINT TAB(75) "OTRO";
5008 '
5010 LPRINT TAB(81) XTES;
5012 GUAPRI#=VAL(XIMP):GOSUB 64770
5014 IF XCOP="C" THEN LPRINT TAB(115-L.C%) GUAPRI$:GOTO 5018
5016 LPRINT TAB(128-L.C%) GUAPRI$
5018 LIN%=LIN%+1
5020 '
5022 RETURN
5024 '
5026 '***************************
5028 '***CABECERAS DEL LISTADO***
5030 '***************************
5032 LPRINT TAB(9) "AGENCIA DE VIAJES  E.I.D.E.";
5034 LPRINT TAB(47) "OPERACIONES DE CLIENTES DESDE EL "FLI1$" HASTA EL "FLI2$;
5036 LPRINT TAB(117) USING "HOJA.- : ###";PAG%
5038 LPRINT TAB(9) STRING$(27,205);
5040 LPRINT TAB(47) STRING$(59,205);
5042 LPRINT TAB(117) STRING$(12,205)
5044 LPRINT
5046 LPRINT TAB(9) "OPERACION     CLIENTE              NOMBRE DEL CLIENTE             TIPO       DESCRIPCION          COBROS        PAGOS   "
5048 LPRINT TAB(9) "ÍÍÍÍÍÍÍÍÍ    ÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍ ÍÍÍÍÍÍÍÍÍÍÍÍ":LPRINT:LIN%=6
5050 '
5052 RETURN
5054 '*************************
5056 '***INFORME DEL FICHERO***
5058 '*************************
5060 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
5062 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
5064 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
5066 GOSUB 8000
5068   LI$="- LISTADO OPERACIONES DE CLIENTES POR FECHAS -"
5070   COLOR 10:LOCATE 6,18:PRINT LI$
5072   LOCATE 7,18:PRINT STRING$(46,205)
5074   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
5076       LOCATE 12,31:PRINT FLI1$
5078       LOCATE 12,58:PRINT FLI2$
5080   GOSUB 8500:MEN$="Espere por favor........":GOSUB 8500
5082 OPEN "I",#5,"FAGEOPER.CLA"
5084 GOSUB 35000:GOSUB 35500
5086 FF$=""
5088 LIN%=90
5090 PAG%=0
5092 '
5094 GOSUB 5178   'LECTURA SECUENCIAL COBROS Y PAGOS PENDIENTES.
5096 '
5098 IF FF$="FF" THEN GOSUB 8500:MEN$="No tenemos los datos pedidos, pulse una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4490
5100 TOCO#=0:TOPA#=0
5102 '
5104 WHILE FF$<>"FF"
5106   FECH$=XFEC
5108   TODICO#=0:TODIPA#=0
5110   '
5112  WHILE FF$<>"FF" AND FECH$=XFEC
5114    '
5116    IMPORTE#=VAL(XIMP)
5118    IF XCOP="C" THEN TODICO#=TODICO#+IMPORTE#:GOTO 5122
5120    TODIPA#=TODIPA#+IMPORTE#
5122    '
5124    GOSUB 4954   'LINEA DE DETALLE
5126    GOSUB 5178   'HACER LECTURA SECUENCIAL
5128     '
5130  WEND
5132  'ROTURA POR DIA.
5134  GUAPRI#=TODICO#:GOSUB 64770
5136  LPRINT
5138  LPRINT TAB(81) "Total dia "RIGHT$(FECH$,2)"-"MID$(FECH$,3,2)"-"LEFT$(FECH$,2);
5140  LPRINT TAB(115-L.C%) GUAPRI$;
5142  GUAPRI#=TODIPA#:GOSUB 64770
5144  LPRINT TAB(128-L.C%) GUAPRI$
5146  TOPA#=TOPA#+TODIPA#:TOCO#=TOCO#+TODICO#
5148  LPRINT:LIN%=LIN%+3
5150  '
5152 WEND
5154 'ROTURA POR FIN DE FICHERO.
5156  GUAPRI#=TOCO#:GOSUB 64770
5158  LPRINT
5160  LPRINT TAB(81) "Total general ";
5162  LPRINT TAB(115-L.C%) GUAPRI$;
5164  GUAPRI#=TOPA#:GOSUB 64770
5166  LPRINT TAB(128-L.C%) GUAPRI$
5168 LPRINT CHR$(12)
5170 '
5172 CLOSE
5174 GOTO 4490
5176 '
5178 '***************************
5180 '***LEER FICHERO DE DATOS***
5182 '***************************
5184 '
5186 IF EOF(5) THEN FF$="FF":RETURN
5188 LINE INPUT #5,TODO$
5190 XNUM=LEFT$(TODO$,6)
5192 XIMP=MID$(TODO$,7,15)
5194 XCLA=MID$(TODO$,22,4)
5196 XFEC=MID$(TODO$,26,6)
5198 XCOP=MID$(TODO$,32,1)
5200 XTIP=MID$(TODO$,33,1)
5202 XTES=MID$(TODO$,34,21)
5204 XPRO=MID$(TODO$,55,6)
5206 XPOC=MID$(TODO$,61,1)
5208 '
5210 RETURN
5212 '
5214 '
6000 '
6002 RUN "PAGEFICH"
6004 '
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7540 'COLOR 15:PRINT " ENTER =>";
7550 'COLOR 11:PRINT " Salto  ";
7560 COLOR 15:PRINT " F7 =>";
7570 COLOR 11:PRINT " Fin de la Opci¢n. ";
7580 'COLOR 15:PRINT " F8 =>";
7590 'COLOR 11:PRINT " M.Izda. "
7600 COLOR 15
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7680 COLOR 15:PRINT "ENTER =>";
7690 COLOR 11:PRINT " Saltar. ";
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin Altas.";
7720 COLOR 15:PRINT " F8 =>";
7730 COLOR 11:PRINT " Campo Atras. ";
7740 COLOR 15:PRINT " F10 =>";
7750 COLOR 11:PRINT " Dup."
7760 COLOR 15
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,36:PRINT "Aplicaci¢n : C A J A    ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PAGECAJA"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9144 IF ERL=35500 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero de Fichas de Clientes inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
10000 '*********************************************
10002 '***LISTADO DE LA PRIMERA FICHA DEL CLIENTE***
10004 '*********************************************
10006 '
10008 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
10010 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
10012 GOSUB 35000:GOSUB 35500
10014 GET #4,NUMERO%
10016 GET #3,CVI(CNPRO)
10018 FOR GG%=1 TO 2
10020 LPRINT TAB(21) "VIAJES   E.I.D.E.   S.L.";
10025 GUAPRI#=NUMERO%:GOSUB 64770
10030 LPRINT TAB(105) "N£mero de Ficha :";
10035 LPRINT TAB(130-L.C%) GUAPRI$
10040 '
10045 LPRINT TAB(21) "M ximo Garc¡a Garrido, 2"
10047 LPRINT TAB(21) "S A N T U R C E    48980";
10050 LPRINT TAB(105) "Fecha Servicio  : " RIGHT$(CFSER,2)"-"MID$(CFSER,3,2)"-"LEFT$(CFSER,2)
10055 '
10060 LPRINT TAB(21) "Tf. 4839250  Fax 4615723"
10065 LPRINT TAB(105) "Fecha Reserva   : " RIGHT$(CFRES,2)"-"MID$(CFRES,3,2)"-"LEFT$(CFRES,2)
10070 '
10075 LPRINT
10080 LPRINT TAB(105) "Fecha Fin Cobro : " RIGHT$(CFCOF,2)"-"MID$(CFCOF,3,2)"-"LEFT$(CFCOF,2)
10085 LPRINT:LPRINT:LPRINT:LPRINT
10090 LPRINT TAB(21) "Se¤ores ........... : " CNOMB
10095 LPRINT
10100 LPRINT TAB(21) "Direcci¢n ......... : " CDIRE
10105 LPRINT
10110 LPRINT TAB(21) "Tel‚fono .......... : " CTELE
10115 LPRINT:LPRINT:LPRINT
10120 LPRINT TAB(21) "Descripci¢n ....... : " CDESC
10125 LPRINT
10130 LPRINT TAB(21) "Proveedor ......... : " PNOMB
10135 LPRINT
10140 IF CCLAV="AI23" THEN EMPLE$="AINHOA":GOTO 10160
10145 IF CCLAV="A91Z" THEN EMPLE$="ANGELICA":GOTO 10160
10150 IF CCLAV="F311" THEN EMPLE$="ANTONIO":GOTO 10160
10155 EMPLE$="DESCONOCIDO"
10160 LPRINT TAB(21) "Empleado/a ........ : " EMPLE$
10165 LPRINT
10170 LPRINT TAB(21) "N§. Bono-Billetes . : " CNUBO
10175 LPRINT:LPRINT:LPRINT:LPRINT
10180 GUAPRI#=CVD(CBRUT):GOSUB 64770
10185 LPRINT TAB(51) "Importe Bruto ..... : ";
10190 LPRINT TAB(85-L.C%) GUAPRI$
10195 LPRINT
10200 GUAPRI#=CVI(CTIDE):GOSUB 64770
10205 LPRINT TAB(51) "Descuento (%) ..... : ";
10210 LPRINT TAB(85-L.C%) GUAPRI$
10215 LPRINT
10220 GUAPRI#=CVD(CNETO):GOSUB 64770
10225 LPRINT TAB(51) "Importe Neto ...... : ";
10230 LPRINT TAB(85-L.C%) GUAPRI$
10235 LPRINT
10240 GUAPRI#=CVD(CPACU):GOSUB 64770
10245 LPRINT TAB(51) "Entrega a Cuenta .. : ";
10250 LPRINT TAB(85-L.C%) GUAPRI$
10255 LPRINT
10260 GUAPRI#=CVD(CPAPE):GOSUB 64770
10265 LPRINT TAB(51) "Resto a Cobrar .... : ";
10270 LPRINT TAB(85-L.C%) GUAPRI$
10275 'FOR A%=1 TO 2:LPRINT:NEXT A%
10277 LPRINT TAB(10) FECHA$" - "ORI$(GG%)
10279 FOR A%=1 TO 4:LPRINT:NEXT A%
10280 '
10285 LPRINT TAB(10) "VIAJES   E.I.D.E.   S.L.";
10290 LPRINT TAB(116) "FECHA RESERVA"
10295 LPRINT TAB(10) "M ximo Garcia Garrido, 2";
10297 IF CMARC="F" THEN LPRINT TAB(50) "     R E C I B O   D E   L I Q U I D A C I O N":GOTO 10305
10300 LPRINT TAB(50) "R E C I B O   D E   E N T R E G A   A   C U E N T A";
10305 LPRINT TAB(113) "ÚÄÂÄ¿  ÚÄÂÄ¿  ÚÄÂÄ¿"
10310 LPRINT TAB(10) "S A N T U R C E    48980";
10315 LPRINT TAB(113) "³"MID$(CFRES,5,1)"³"MID$(CFRES,6,1)"³  ³"MID$(CFRES,3,1)"³"MID$(CFRES,4,1)"³  ³"LEFT$(CFRES,1)"³"MID$(CFRES,2,1)"³"
10320 LPRINT TAB(10) "Tf. 4839250  Fax 4615723";
10325 LPRINT TAB(113) "ÀÄÁÄÙ  ÀÄÁÄÙ  ÀÄÁÄÙ"
10330 LPRINT
10335 LPRINT TAB(10) "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿          ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿"
10340 '
10345 LPRINT TAB(10) "³       E M P L E A D O       ³          ³       NUMERO DE FICHA       ³";
10347 LPRINT TAB(116) "FECHA SERVICIO"
10350 LPRINT TAB(10) "³                             ³          ³                             ³";
10355 LPRINT TAB(113) "ÚÄÂÄ¿  ÚÄÂÄ¿  ÚÄÂÄ¿"
10360 LPRINT TAB(10) "³";
10362 LPRINT TAB(20) EMPLE$;
10364 LPRINT TAB(40) "³";
10366 LPRINT TAB(51) "³";
10368 GUAPRI#=NUMERO%:GOSUB 64770
10370 LPRINT TAB(70-L.C%) GUAPRI$;
10372 LPRINT TAB(81) "³";
10375 LPRINT TAB(113) "³"MID$(CFSER,5,1)"³"MID$(CFSER,6,1)"³  ³"MID$(CFSER,3,1)"³"MID$(CFSER,4,1)"³  ³"LEFT$(CFSER,1)"³"MID$(CFSER,2,1)"³"
10380 LPRINT TAB(10) "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ          ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
10385 LPRINT TAB(113) "ÀÄÁÄÙ  ÀÄÁÄÙ  ÀÄÁÄÙ"
10390 LPRINT
10395 LPRINT TAB(10) "VIAJES E.I.D.E. HA RECIBIDO DE LOS SE¥ORES : " CNOMB
10400 LPRINT
10405 LPRINT TAB(10) "LA CANTIDAD DE PTAS. ";
10410 IMPOR#=CANTI#
10415 GOSUB 64600
10420 LPRINT TAB(31) LIT1$
10425 LPRINT TAB(10) LIT2$
10427 IF CMARC="F" THEN LPRINT TAB(10) "EN CONCEPTO DE LIQUIDACION TOTAL.";:GOTO 10432
10430 LPRINT TAB(10) "EN CONCEPTO DE ENTREGA A CUENTA.";
10432 LPRINT TAB(100) "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿"
10435 '
10437 GUAPRI#=CANTI#
10438 GOSUB 64770
10440 GUAPRI$="PTAS. ÷÷"+GUAPRI$+"÷÷":L.C%=INT(LEN(GUAPRI$)/2)
10445 LPRINT TAB(100) "³";
10450 LPRINT TAB(115-L.C%) GUAPRI$;
10455 LPRINT TAB(130) "³"
10460 LPRINT TAB(100) "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ"
10462 IF I.A%=3 THEN LPRINT TAB(53) "A N U L A C I O N   D E   C O B R O"
10465 LPRINT CHR$(12)
10467 NEXT GG%
10470 CLOSE
10480 RETURN
10485 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30090 ' Apertura del fichero de datos   FRESCAJA.DIA
30095 '
30100 OPEN "R",#1,FICHERO$,90
30105 '
30110 FIELD #1,1 AS HSTA,8 AS HINI,8 AS HCOB,8 AS HPAG,8 AS HCTA,8 AS HPTA,8 AS HCPL,8 AS HPPL,8 AS HCOT,8 AS HPOT,8 AS HFIN,9 AS HRES
30114 '
30128 RETURN
30130 '
30140 '
30500 '**********************************
30510 '***APERTURA FICHERO OPERACIONES***
30520 '**********************************
30525 'Comprobamos la existencia del fichero.
30552 '
30590 ' Apertura del fichero de datos   FRESOPER.DAT
30595 '
30600 OPEN "R",#2,"FAGEOPER.DAT",80
30605 '
30610 FIELD #2,4 AS ONUM,1 AS OSTA,1 AS OMAR,4 AS OCLA,6 AS OFEC,1 AS OCOP,1 AS OTIP,21 AS OTES,8 AS OIMP,8 AS OPAG,2 AS OPRO,1 AS OPOC,22 AS ORES
30611 FIELD #2,4 AS OULT,1 AS OST1,1 AS OMA1,74 AS OFIL
30614 '
30616  GET #2,1
30617 'LSET OULT=MKS$(1)
30618 'LSET OST1="A":LSET OMA1="A":LSET OFIL=""
30619 'PUT #2,1
30620 'CLOSE
30621 'STOP
30622 '
30624 ULTI!=CVS(OULT)
30628 RETURN
30630 '
30640 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 11
31040 LOCATE 4,23:PRINT TABRUT$(I.A%)
31042 LOCATE 5,23:PRINT STRING$(LEN(TABRUT$(I.A%)),205)
31050 LOCATE 6,1:PRINT "NUMERO      FECHA.      FICHA.     TIP        C O N C E P T O       IMPO.COBRADO"
31054 LOCATE 7,1:PRINT "------     --------     ------     ---     ---------------------    ------------"
31084 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** AGENCIA DE VIAJES EIDE ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '**********************************
35002 '***APERTURA FICHERO PROVEEDORES***
35004 '**********************************
35006 '
35008 ' Apertura del fichero de Proveedores FAGEPROV.DAT
35010 OPEN "R",#3,"FAGEPROV.DAT",400
35012 FIELD #3,1 AS PSTAT,1 AS PMARC,5 AS PCUEN,9 AS PNCIF,60 AS PNOMB,60 AS PDIRE,30 AS PPOBL,15 AS PPROV,5 AS PCOPO,10 AS PTELE,20 AS PNACI
35014 FIELD #3,216 AS TODO1$,10 AS PNFAX,30 AS PCON1,10 AS PTC01,30 AS PCON2,10 AS PTC02,30 AS PCON3,10 AS PTC03,8 AS PPAGO,8 AS PCOBR,38 AS PFILL
35016 '
35018 FIELD #3,1 AS PSTA1,1 AS PMAR1,2 AS PULTI,12 AS PFICH,250 AS CEST1$,134 AS CEST2$
35020  GET #3,1
35022  IF PFICH<>"FAGEPROV.DAT" THEN 9144
35024  ULTPRO%=CVI(PULTI)
35030  '
35032 RETURN
35500 '**********************************
35502 '***APERTURA FICHERO DE CLIENTES***
35504 '**********************************
35506 '
35508 ' Apertura del fichero de Fichas FAGEFICH.DAT
35510 OPEN "R",#4,"FAGEFICH.DAT",250
35512 FIELD #4,1 AS CSTAT,1 AS CMARC,5 AS CNUME,40 AS CNOMB,40 AS CDIRE,10 AS CTELE,6 AS CFSER
35514 FIELD #4,103 AS TODO1$,6 AS CFRES,40 AS CDESC,2 AS CNPRO,4 AS CCLAV,40 AS CNUBO,8 AS CBRUT,2 AS CTIDE,8 AS CNETO,8 AS CPACU,8 AS CPAPE,6 AS CFCOF,4 AS COACU,4 AS COFIN,1 AS CTIPO,6 AS CFILL
35516 '
35518 FIELD #4,1 AS CSTA1,1 AS CMAR1,2 AS CULTI,12 AS CFICH,234 AS CEST1$
35520  GET #4,1
35522  IF CFICH<>"FAGEFICH.DAT" THEN 9144
35524  ULTIMO%=CVI(CULTI)
35525 'LSET CSTA1="A":LSET CMAR1="A":LSET CULTI=MKI$(1):LSET CEST1$="":LSET CFICH="FAGEFICH.DAT"
35526 'PUT #4,1
35527 'CLOSE:STOP
35530  '
35532 RETURN
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 12:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 15
50300 RETURN
50340 '
50510 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64600  '********************
64602 '***RUTINA PESETAS***
64604 '********************
64606 LIT$="":LIT1$="":LIT2$="":IND33%=0:IF IMPOR#=0 THEN LIT1$=LIT1$+"CERO.":LIT$=LIT1$:GOTO 64638
64608 'Desglose del importe
64610 IMPOR$=STR$(IMPOR#):IMPOR$=RIGHT$(IMPOR$,LEN(IMPOR$)-1)
64612 IMPOR$=LEFT$(IMPMAN$,11-LEN(IMPOR$))+IMPOR$
64614 IMPOR2$=LEFT$(IMPOR$,2)
64616 IMPOR$=RIGHT$(IMPOR$,9)
64618 GUAJE%=VAL(IMPOR2$)
64620 GUA1%=VAL(LEFT$(IMPOR2$,1))
64622 GUA2%=VAL(RIGHT$(IMPOR2$,1))
64624 '
64626 '
64628 PESETA$=RIGHT$(IMPOR$,3):PESETA%=VAL(PESETA$):MYM$=LEFT$(IMPOR$,6):MYM!=VAL(MYM$):DECPTA$=RIGHT$(PESETA$,2):DECPTA%=VAL(DECPTA$):DIG7%=VAL(LEFT$(PESETA$,1)):DIG8%=VAL(LEFT$(DECPTA$,1)):DIG9%=VAL(RIGHT$(DECPTA$,1))
64630 MILLON$=LEFT$(MYM$,3):MILLON%=VAL(MILLON$):MILES$=RIGHT$(MYM$,3):MILES%=VAL(MILES$):DECMIL$=RIGHT$(MILES$,2):DECMIL%=VAL(DECMIL$):DIG4%=VAL(LEFT$(MILES$,1)):DIG5%=VAL(LEFT$(DECMIL$,1)):DIG6%=VAL(RIGHT$(DECMIL$,1))
64632 DECMLL$=RIGHT$(MILLON$,2):DECMLL%=VAL(DECMLL$):DIG1%=VAL(LEFT$(MILLON$,1)):DIG2%=VAL(LEFT$(DECMLL$,1)):DIG3%=VAL(RIGHT$(DECMLL$,1))
64634 'Tratamiento particular
64636 GOSUB 64740:GOSUB 64646:GOSUB 64662:GOSUB 64678
64638 'FIN RUTINA
64640 LIT1$=LIT1$+STRING$(99-LEN(LIT1$),CHR$(247))
64642 LIT2$=LIT2$+STRING$(120-LEN(LIT2$),CHR$(247))
64644 RETURN
64646 'Tratamiento millones
64648 IF MILLON%=0 THEN 64660
64650 IF MILLON%=1 THEN MAN$="UN MILLON ":GOSUB 64698:GOTO 64660
64652 IF MILLON%=100 THEN MAN$="CIEN MILLONES ":GOSUB 64698:GOTO 64660
64654 DECENA%=DECMLL%:DIGA%=DIG1%:DIGB%=DIG2%:DIGC%=DIG3%:IND90%=1
64656 GOSUB 64708 'Tratamiento comun
64658 IND90%=0:MAN$="MILLONES ":GOSUB 64698 'Salida a literales
64660 RETURN
64662 'Tratamiento miles
64664 IF MILES%=0 THEN 64676
64666 IF MILES%=1 THEN 64674
64668 IF MILES%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64674
64670 DECENA%=DECMIL%:DIGA%=DIG4%:DIGB%=DIG5%:DIGC%=DIG6%
64672 GOSUB 64708 'Tratamiento comun
64674 MAN$="MIL ":GOSUB 64698 'Salida a literales
64676 RETURN
64678 'Tratamiento pesetas
64680 IF PESETA%=0 THEN 64692
64682 'IF PESETA%=1 AND MYM>0 THEN MAN$="Y ":GOSUB 1700 'Salida a literales
64684 IF PESETA%=1 THEN MAN$="UNA ":GOSUB 64698:GOTO 64692
64686 IF PESETA%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64692
64688 DECENA%=DECPTA%:DIGA%=DIG7%:DIGB%=DIG8%:DIGC%=DIG9%
64690 GOSUB 64708 'Tratmiento comun
64692 IF LIT2$="" THEN LIT1$=LEFT$(LIT1$,LEN(LIT1$)-1)+"." ELSE LIT2$=LEFT$(LIT2$,LEN(LIT2$)-1)+"."
64694 LIT$=LEFT$(LIT$,LEN(LIT$)-1)+"."
64696 RETURN
64698 'Salida a literales
64700 IF IND33%=1 THEN LIT2$=LIT2$+MAN$:GOTO 64704
64702 IF (99-LEN(LIT1$))>=LEN(MAN$)THEN LIT1$=LIT1$+MAN$ ELSE LIT2$=LIT2$+MAN$:IND33%=1
64704 LIT$=LIT$+MAN$
64706 RETURN
64708 'Tratamiento comun
64710 IF DIGA%=0 THEN 64718 ELSE MAN$=TC(DIGA%)
64712 IF DIGA%=1 THEN 64716
64714 IF IND90%=1 THEN MAN$=MAN$+"OS " ELSE MAN$=MAN$+"AS "
64716 GOSUB 64698 'Salida a literales
64718 IF DIGB%=0 THEN 64726
64720 IF DECENA%>29 THEN MAN$=TB(DIGB%):GOSUB 64698:GOTO 64726
64722 IF IND90%=1 AND DECENA%=21 THEN MAN$="VEINTIUN ":GOSUB 64698:IND60%=1:GOTO 64726
64724 MAN$=TA(DECENA%):GOSUB 64698:IND60%=1
64726 IF IND60%=1 OR DIGC%=0 THEN 64736
64728 IF DIGB%<>0 THEN MAN$="Y ":GOSUB 64698
64730 IF IND90%=1 AND DIGC%=1 THEN MAN$="UN ":GOSUB 64698:GOTO 64736
64732 IF DIGC%=1 THEN MAN$="UNA ":GOSUB 64698:GOTO 64736
64734 MAN$=TA(DIGC%):GOSUB 64698
64736 IND60%=0
64738 RETURN
64740 'Tratamiento miles de millones.
64742 IF GUAJE%=0 THEN 64756
64744 IF GUAJE%=1 THEN 64752
64746 IF GUAJE%=100 THEN MAN$="CIEN ":GOSUB 64698:GOTO 64752
64748 DECENA%=GUAJE%:DIGA%=0:DIGB%=GUA1%:DIGC%=GUA2%
64750 GOSUB 64708 'Tratamiento comun
64752 MAN$="MIL ":GOSUB 64698 'Salida a literales
64754 IF MILLON%=0 THEN MAN$="MILLONES ":GOSUB 64698 'Salida a literales.
64756 RETURN
64758 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
