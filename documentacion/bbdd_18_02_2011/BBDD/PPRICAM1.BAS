10 '                       PACACAM1.BAS
20 '                      --------------
30 '          CAMBIAR ALUMNOS UNO POR UNO DE GRUPO
40 '          ------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,G
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
306 '
308 '
312 'GOSUB 30000
314 'IF ULTIMO%=ULTACT% THEN CLOSE:GOTO 610
316 'GET #2,1
318 'LSET CULTACT=CULTI
320 'PUT #2,1
322 'CLOSE
324 'CLS
326 'SHELL "ACANUME.BAT"
328 '
610 GOSUB 8000
612 COLOR 10
614 LOCATE 5,26:PRINT "- CAMBIAR UN ALUMNO DE GRUPO -"
616 LOCATE 6,26:PRINT STRING$(30,205)
618 TIT$="- CAMBIAR UN ALUMNO DE GRUPO -"
700 '
702 '
704 '
706 '
718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
720 GOSUB 30000:GOSUB 32000
724 COLOR 11:LOCATE 8,6:PRINT "Alumno N§ : <" SPACE$(6) ">"
728 GOSUB 7650
730       '*******************************
732       '***ENTRADA NUMERO DE ALUMNO ***
734       '*******************************
736       COLOR 15
738       F.C1%=8:C.C1%=19:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 758 'Entrada OK
741       IF I.T%=4 THEN 9000
742       GOTO 732
756       '
758       '
760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 8,24-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
762       REG%=VAL(CUENTA$)
764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
765       GET #2,REG%
766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
770       LOCATE 8,27:PRINT CUCUCU$
774       COLOR 11:LOCATE 10,6:PRINT "Grupo Anterior : <" SPACE$(3) ">"
776       COLOR 15:LOCATE 10,24:PRINT CGRUPO:GRUAN$=CGRUPO:CONFI$=CCONFI
778       GET #6,VAL(CGRUPO)
780       LOCATE 10,29:PRINT GCURSO
878       COLOR 11:LOCATE 12,6:PRINT "Grupo Nuevo... : <" SPACE$(3) ">"
880 COLOR 15
976       '**********************
978       '***ENTRADA GRUPO   ***
980       '**********************
982       F.C1%=12:C.C1%=24:L.E%=3:BO%=3:GOSUB 63000
983       IF I.T%=0 THEN GRUPO$=D.E$:GOTO 1000
985       IF I.T%=5 THEN 982
989       IF I.T%=3 THEN GOSUB 8000:GOTO 724
992       IF I.T%=4 THEN 9000               'Fin Entrada.
994       IF I.T%=2 THEN 1000
998       GOTO 982
1000      IF GRUPO$<>GRUAN$ THEN 1003
1001      GOSUB 8500:MEN$="No procede el Cambio, Grupo igual al anterior,  pulsar una tecla ":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 982
1003      IF VAL(GRUPO$)=<ULTGRU% AND VAL(GRUPO$)>1 THEN 1010
1004      GOSUB 8500:MEN$="Este Grupo no existe, pulsar una tecla ":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 982
1010      GET #6,VAL(GRUPO$)
1011    ' IF SICO%=0 THEN 1012 ELSE SICO%=0:GOTO 982
1012      ALTA%=CVI(GALTAS)+1
1013      IF CVI(GALTAS)<CVI(GALUMNOS) THEN 1070
1014      GOSUB 8500:MEN$="Grupo completo, pulse una tecla y ver  el Grupo":GOSUB 8500:A$=INPUT$(1)
1020       GOSUB 10000
1024       GOSUB 7650:GOTO 982
1070       LOCATE 12,24:PRINT GRUPO$:LOCATE 12,29:PRINT GCURSO
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2042
2028   LOCATE 8,2:PRINT SPC(78)
2030   LOCATE 10,2:PRINT SPC(78)
2032   LOCATE 12,2:PRINT SPC(78)
2034   GOTO 724
2042   '
2066   '
2098   '
2100   'REGRABAR EN CASO DE MODIFICACION.
2102   LSET CGRUPO=GRUPO$:PUT #2,REG%
2110   GET #2,1:LSET CSWG="1":PUT #2,1
2260   '
2270   GET #6,VAL(GRUAN$)
2280   ALTA%=CVI(GALTAS)-1
2290   CONF%=CVI(GCONFIR)
2295   NALU$=GALUS
2300   IF CONFI$="S" THEN CONF%=CONF%-1
2302   FOR A%=1 TO 200 STEP 5
2304   IF MID$(NALU$,A%,5)=CUENTA$ THEN MID$(NALU$,A%,5)="     ":GOTO 2308
2306   NEXT A%
2308   GOSUB 8300
2309   LSET GALUS=NALU$
2310   LSET GALTAS=MKI$(ALTA%)
2320   LSET GCONFIR=MKI$(CONF%)
2330   PUT #6,VAL(GRUAN$)
2340   '
2370   GET #6,VAL(GRUPO$)
2380   ALTA%=CVI(GALTAS)+1
2390   CONF%=CVI(GCONFIR)
2395   NALU$=GALUS
2400   IF CONFI$="S" THEN CONF%=CONF%+1
2402   FOR A%=1 TO 200 STEP 5
2404   IF MID$(NALU$,A%,1)=" " THEN MID$(NALU$,A%,5)=CUENTA$:GOTO 2408
2406   NEXT A%
2408   GOSUB 8300
2409   LSET GALUS=NALU$
2410   LSET GALTAS=MKI$(ALTA%)
2420   LSET GCONFIR=MKI$(CONF%)
2430   PUT #6,VAL(GRUPO$)
4420   IF CONFI$="S" THEN GOSUB 9500
4430   CLOSE:GOTO 610
4440 '
4450 '
4460 '
4488 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7672 'COLOR 10:PRINT " F1 =>";
7674 'COLOR 15:PRINT " Ayuda ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Cambios ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8300 '***************************************
8305 '***CLASIFICAR ALUMNOS DE LOS GRUPOS ***
8310 '***************************************
8315 '
8320 FOR MM%=1 TO CVI(GALUMNOS)*5 STEP 5
8325     IF MID$(NALU$,MM%,1)=" " THEN MID$(NALU$,MM%,5)="99999"
8330     FOR NN%=MM%+5 TO CVI(GALUMNOS)*5 STEP 5
8335         IF MID$(NALU$,NN%,1)=" " THEN MID$(NALU$,NN%,5)="99999"
8340         IF MID$(NALU$,MM%,5)<MID$(NALU$,NN%,5) THEN 8365
8345         CMM$=MID$(NALU$,MM%,5):CNN$=MID$(NALU$,NN%,5)
8350         SWAP CMM$,CNN$
8355         MID$(NALU$,MM%,5)=CMM$
8360         MID$(NALU$,NN%,5)=CNN$
8365     NEXT NN%
8370 NEXT MM%
8375 FOR MM%=1 TO CVI(GALUMNOS)*5 STEP 5
8380     IF MID$(NALU$,MM%,5)<>"99999" THEN 8390
8385     MID$(NALU$,MM%,5)="     "
8390 NEXT MM%
8395 RETURN
8400 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACACLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
9500 '************************************
9502 '***LISTADO DE HOJAS DE AFILIACION***
9504 '************************************
9506 '
9508 WIDTH "LPT1:",132:OPEN "LPT1" FOR OUTPUT AS #9
9510 LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(15)
9512 LPRINT TAB(51) "S C H O O L   O F   E N G L I S H"
9514 LPRINT TAB(51) STRING$(33,"-")
9516 LPRINT
9518 LPRINT TAB(47) "M ximo Garcia Garrido, 2 y Genaro Oraa, 6"
9520 LPRINT TAB(47) STRING$(41,"-")
9522 LPRINT
9524 LPRINT TAB(39) "48980 S A N T U R C E    (VIZCAYA)   Tel‚fono 4.61.95.55"
9526 LPRINT TAB(39) STRING$(56,"-")
9528 LPRINT
9530 LPRINT TAB(39) "Grupo : " GRUPO$ "         Curso : " GCURSO
9532 LPRINT
9533 LPRINT TAB(39) "Texto : " GTEXTO:LPRINT
9534 LPRINT TAB(39) "N£mero de Alumno : ";
9535 GET #2,REG%
9536 GUAPRI#=VAL(CCUENTA):GOSUB 64770
9538 LPRINT TAB(63-L.C%) GUAPRI$:LPRINT
9540 NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9542 LPRINT TAB(39) "Nombre del Alumno : " CUCUCU$:LPRINT
9544 '
9546 IF GSB<>"0" THEN 9554
9548 '
9550 LPRINT TAB(55) "DIAS DE CLASE Y HORARIO"
9552 LPRINT TAB(55) STRING$(23,"-"):GOTO 9566
9554 LPRINT TAB(47) "DIAS DE CLASE Y HORARIO DE LA SEMANA -A-"
9556 LPRINT TAB(47) STRING$(40,"-")
9566 LPRINT TAB(47) "    D I A         HORARIO      PISO AULA"
9568 LPRINT TAB(47) "------------- ---------------- ---- ----":LPRINT
9570 IF LEFT$(GLUNESA,1)=" " THEN 9580
9572 LPRINT TAB(47) "Lunes ....... de ";
9574 LPRINT TAB(64) LEFT$(GLUNESA,2)"-"MID$(GLUNESA,3,2)" a "MID$(GLUNESA,5,2)"-"RIGHT$(GLUNESA,2)"  "GPILUA"   "GAULUA
9580 IF LEFT$(GMARTEA,1)=" " THEN 9590
9582 LPRINT TAB(47) "Martes ...... de ";
9584 LPRINT TAB(64) LEFT$(GMARTEA,2)"-"MID$(GMARTEA,3,2)" a "MID$(GMARTEA,5,2)"-"RIGHT$(GMARTEA,2)"  "GPIMAA"   "GAUMAA
9590 IF LEFT$(GMIERCA,1)=" " THEN 9600
9592 LPRINT TAB(47) "Miercoles ... de ";
9594 LPRINT TAB(64) LEFT$(GMIERCA,2)"-"MID$(GMIERCA,3,2)" a "MID$(GMIERCA,5,2)"-"RIGHT$(GMIERCA,2)"  "GPIMIA"   "GAUMIA
9600 IF LEFT$(GJUEVEA,1)=" " THEN 9610
9602 LPRINT TAB(47) "Jueves ...... de ";
9604 LPRINT TAB(64) LEFT$(GJUEVEA,2)"-"MID$(GJUEVEA,3,2)" a "MID$(GJUEVEA,5,2)"-"RIGHT$(GJUEVEA,2)"  "GPIJUA"   "GAUJUA
9610 IF LEFT$(GVIERNA,1)=" " THEN 9620
9612 LPRINT TAB(47) "Viernes ..... de ";
9614 LPRINT TAB(64) LEFT$(GVIERNA,2)"-"MID$(GVIERNA,3,2)" a "MID$(GVIERNA,5,2)"-"RIGHT$(GVIERNA,2)"  "GPIVIA"   "GAUVIA
9620 LPRINT:LPRINT
9621 IF GSB="0" THEN 9660
9622 LPRINT TAB(47) "DIAS DE CLASE Y HORARIO DE LA SEMANA -B-"
9624 LPRINT TAB(47) STRING$(40,"-")
9626 LPRINT TAB(47) "    D I A         HORARIO      PISO AULA"
9628 LPRINT TAB(47) "------------- ---------------- ---- ----":LPRINT
9630 IF LEFT$(GLUNESB,1)=" " THEN 9636
9632 LPRINT TAB(47) "Lunes ....... de ";
9634 LPRINT TAB(64) LEFT$(GLUNESB,2)"-"MID$(GLUNESB,3,2)" a "MID$(GLUNESB,5,2)"-"RIGHT$(GLUNESB,2)"  "GPILUB"   "GAULUB
9636 IF LEFT$(GMARTEB,1)=" " THEN 9642
9638 LPRINT TAB(47) "Martes ...... de ";
9640 LPRINT TAB(64) LEFT$(GMARTEB,2)"-"MID$(GMARTEB,3,2)" a "MID$(GMARTEB,5,2)"-"RIGHT$(GMARTEB,2)"  "GPIMAB"   "GAUMAB
9642 IF LEFT$(GMIERCB,1)=" " THEN 9648
9644 LPRINT TAB(47) "Miercoles ... de ";
9646 LPRINT TAB(64) LEFT$(GMIERCB,2)"-"MID$(GMIERCB,3,2)" a "MID$(GMIERCB,5,2)"-"RIGHT$(GMIERCB,2)"  "GPIMIB"   "GAUMIB
9648 IF LEFT$(GJUEVEB,1)=" " THEN 9654
9650 LPRINT TAB(47) "Jueves ...... de ";
9652 LPRINT TAB(64) LEFT$(GJUEVEB,2)"-"MID$(GJUEVEB,3,2)" a "MID$(GJUEVEB,5,2)"-"RIGHT$(GJUEVEB,2)"  "GPIJUB"   "GAUJUB
9654 IF LEFT$(GVIERNB,1)=" " THEN 9660
9656 LPRINT TAB(47) "Viernes ..... de ";
9658 LPRINT TAB(64) LEFT$(GVIERNB,2)"-"MID$(GVIERNB,3,2)" a "MID$(GVIERNB,5,2)"-"RIGHT$(GVIERNB,2)"  "GPIVIB"   "GAUVIB
9660 LPRINT
9680 LPRINT TAB(39) "En Santurce a "FECHA$
9990 LPRINT CHR$(12)
9995 RETURN
10000 '**************************************
10005 '***SACAR TODO EL GRUPO EN PANTALLA ***
10010 '**************************************
10015 'FOR N.GRU%=2 TO ULTGRU%
10020 'GET #4,N.GRU%
10025 GOSUB 8000
10030 'NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10035 LOCATE 4,3:COLOR 11:PRINT "Grupo : ";:COLOR 15:PRINT GGRUPO
10040 LOCATE 4,16:COLOR 11:PRINT "Curso : ";:COLOR 15:PRINT LEFT$(GCURSO,10)
10045 LOCATE 4,35:COLOR 11:PRINT "Texto : ";:COLOR 15:PRINT LEFT$(GTEXTO,10)
10050 LOCATE 4,54:COLOR 11:PRINT "Altas : ";:COLOR 15:PRINT USING"###";CVI(GALTAS)
10055 LOCATE 4,67:COLOR 11:PRINT "Conf. : ";:COLOR 15:PRINT USING"###";CVI(GCONFIR)
10060 COLOR 10:LOCATE 5,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
10065 COLOR 11:LOCATE 6,3:PRINT "Dias ÍÍÍ>     LUNES       MARTES      MIERCOLES     JUEVES       VIERNES"
10070 LOCATE 7,3:PRINT "           -----------  -----------  -----------  -----------  -----------"
10075 LOCATE 8,3:PRINT "Semana  A":COLOR 15
10080 IF LEFT$(GLUNESA,1)=" " THEN 10090
10085 LOCATE 8,14:PRINT LEFT$(GLUNESA,2)","MID$(GLUNESA,3,2)" "MID$(GLUNESA,5,2)","RIGHT$(GLUNESA,2)
10090 IF LEFT$(GMARTEA,1)=" " THEN 10100
10095 LOCATE 8,27:PRINT LEFT$(GMARTEA,2)","MID$(GMARTEA,3,2)" "MID$(GMARTEA,5,2)","RIGHT$(GMARTEA,2)
10100 IF LEFT$(GMIERCA,1)=" " THEN 10110
10105 LOCATE 8,40:PRINT LEFT$(GMIERCA,2)","MID$(GMIERCA,3,2)" "MID$(GMIERCA,5,2)","RIGHT$(GMIERCA,2)
10110 IF LEFT$(GJUEVEA,1)=" " THEN 10120
10115 LOCATE 8,53:PRINT LEFT$(GJUEVEA,2)","MID$(GJUEVEA,3,2)" "MID$(GJUEVEA,5,2)","RIGHT$(GJUEVEA,2)
10120 IF LEFT$(GVIERNA,1)=" " THEN 10135
10125 LOCATE 8,66:PRINT LEFT$(GVIERNA,2)","MID$(GVIERNA,3,2)" "MID$(GVIERNA,5,2)","RIGHT$(GVIERNA,2)
10130 IF GSB="0" THEN 10190
10135 COLOR 11:LOCATE 9,3:PRINT "Semana  B":COLOR 15
10140 IF LEFT$(GLUNESB,1)=" " THEN 10150
10145 LOCATE 9,14:PRINT LEFT$(GLUNESB,2)","MID$(GLUNESB,3,2)" "MID$(GLUNESB,5,2)","RIGHT$(GLUNESB,2)
10150 IF LEFT$(GMARTEB,1)=" " THEN 10160
10155 LOCATE 9,27:PRINT LEFT$(GMARTEB,2)","MID$(GMARTEB,3,2)" "MID$(GMARTEB,5,2)","RIGHT$(GMARTEB,2)
10160 IF LEFT$(GMIERCB,1)=" " THEN 10170
10165 LOCATE 9,40:PRINT LEFT$(GMIERCB,2)","MID$(GMIERCB,3,2)" "MID$(GMIERCB,5,2)","RIGHT$(GMIERCB,2)
10170 IF LEFT$(GJUEVEB,1)=" " THEN 10180
10175 LOCATE 9,53:PRINT LEFT$(GJUEVEB,2)","MID$(GJUEVEB,3,2)" "MID$(GJUEVEB,5,2)","RIGHT$(GJUEVEB,2)
10180 IF LEFT$(GVIERNB,1)=" " THEN 10190
10185 LOCATE 9,66:PRINT LEFT$(GVIERNB,2)","MID$(GVIERNB,3,2)" "MID$(GVIERNB,5,2)","RIGHT$(GVIERNB,2)
10190 COLOR 10:LOCATE 10,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
10192 IF CVI(GALTAS)=0 THEN 10561
10195 COLOR 11:LOCATE 11,26:PRINT "- ALUMNOS QUE TIENE EL GRUPO -"
10200 LOCATE 12,26:PRINT STRING$(30,205)
10205 FF%=12
10210 IF CVI(GALTAS)<9 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 10220
10215 TT%=40:MAS%=1
10220 FOR A%=1 TO TT% STEP 5
10225     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10230     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10235     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
10240     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10245     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10250     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
10255 NEXT A%
10260 IF MAS%=0 THEN 10561
10265 FF%=12
10270 IF CVI(GALTAS)<17 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 10280
10275 TT%=80:MAS%=1
10280 FOR A%=41 TO TT% STEP 5
10285     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10290     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10295     LOCATE FF%,41:PRINT USING"#####";VAL(CCUENTA)
10300     LOCATE FF%,47:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10305     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10310     LOCATE FF%,54:PRINT LEFT$(CUCUCU$,24)
10315 NEXT A%
10320 IF MAS%=0 THEN 10561
10325 GOSUB 8500:COLOR 11:LOCATE 22,13:PRINT "Hay mas Alumnos en el Grupo, para verlos pulsa ";:COLOR 15:PRINT " ENTER ";:COLOR 11:PRINT ", si n¢ ";:COLOR 15:PRINT " F1"
10330 A$=INPUT$(1)
10335 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 10330
10340 IF ASC(A$)=14 THEN 10561
10345 FOR FF%=13 TO 20
10350  LOCATE FF%,2:PRINT SPC(78)
10355 NEXT FF%
10360 FF%=12
10365 IF CVI(GALTAS)<25 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 10375
10370 TT%=120:MAS%=1
10375 FOR A%=81 TO TT% STEP 5
10380     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10385     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10390     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
10395     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10400     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10405     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
10410 NEXT A%
10415 IF MAS%=0 THEN 10561
10420 FF%=12
10425 IF CVI(GALTAS)<33 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 10435
10430 TT%=160:MAS%=1
10435 FOR A%=121 TO TT% STEP 5
10440     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10445     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10450     LOCATE FF%,41:PRINT USING"#####";VAL(CCUENTA)
10455     LOCATE FF%,47:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10460     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10465     LOCATE FF%,54:PRINT LEFT$(CUCUCU$,24)
10470 NEXT A%
10475 IF MAS%=0 THEN 10561
10480 GOSUB 8500:COLOR 11:LOCATE 22,13:PRINT "Hay mas Alumnos en el Grupo, para verlos pulsa ";:COLOR 15:PRINT " ENTER ";:COLOR 11:PRINT ", si n¢ ";:COLOR 15:PRINT " F1"
10485 A$=INPUT$(1)
10490 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 10485
10495 IF ASC(A$)=14 THEN 10561
10500 FOR FF%=13 TO 20
10505  LOCATE FF%,2:PRINT SPC(78)
10510 NEXT FF%
10515 FF%=12
10520 TT%=200:MAS%=1
10525 FOR A%=161 TO TT% STEP 5
10530     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10535     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10540     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
10545     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10550     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10555     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
10560 NEXT A%
10561 GOSUB 8500:COLOR 11:LOCATE 22,13:PRINT "Pulsar una tecla para continuar"
10562 A$=INPUT$(1)
10563 'IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 10561
10564 'IF ASC(A$)=13 THEN SICO%=0:GOTO 10569
10565 'SICO%=1
10567 'GOTO 10015
10569 GOSUB 8000
10614 LOCATE 5,26:PRINT "- CAMBIAR UN ALUMNO DE GRUPO -"
10616 LOCATE 6,26:PRINT STRING$(30,205)
10724 COLOR 11:LOCATE 8,6:PRINT "Alumno N§ : <" SPACE$(6) ">"
10728 GOSUB 7650
10760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 8,24-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
10762       GET #2,REG%  'BUSQUEDA DICOTOMICA.
10768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10770       LOCATE 8,27:PRINT CUCUCU$
10774       COLOR 11:LOCATE 10,6:PRINT "Grupo Anterior : <" SPACE$(3) ">"
10776       COLOR 15:LOCATE 10,24:PRINT CGRUPO:GRUAN$=CGRUPO:CONFI$=CCONFI
10778       GET #6,VAL(CGRUPO)
10780       LOCATE 10,29:PRINT GCURSO
10878       COLOR 11:LOCATE 12,6:PRINT "Grupo Nuevo... : <" SPACE$(3) ">"
10880 COLOR 15
10882       GET #6,VAL(GRUPO$)
11000      RETURN
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 'OPEN "R",#1,FALUDIR$,65
30054 'FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 'GET #1,1
30058 'ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,20 AS CSUCUR,17 AS CCUBAN,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,237 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 '
30128 RETURN
30130 '
30140 '
32000 '*******************************
32010 '***APERTURA FICHERO GRUPOS  ***
32020 '*******************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FACAGRUP.DAT",380
32060 FIELD #6,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32070 FIELD #6,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32080 FIELD #6,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,1 AS GSB,15 AS GLIBR,200 AS GALUS
32090 FIELD #6,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32100 FIELD #6,180 AS GTODO,200 AS GALUS
32110 GET #6,1
32120 IF GETIQUETA="FACAGRUP.DAT" THEN 32140
32130 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140 ULTGRU%=CVI(GULTIMO)
32150 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
