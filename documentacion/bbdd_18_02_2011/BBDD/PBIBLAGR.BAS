10 '                       PBIBLAGR.BAS
20 '                      --------------
30 '           LISTADO ALFABETICO DE LIBROS POR TEMAS
40 '           --------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(60," ")
100 GOSUB 33000
132 EMPRESA$="*** ESCUELA DE IDIOMAS ***"
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR C,G,D,F
150 OPTION BASE 1
200 DIM TABENT$(30)
590 '
600 GOTO 4490
3310 '
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 IF LIN%>63 THEN GOSUB 3740   '--HACER CABECERAS--
3550 LPRINT TAB(6) CTEMAS;
3560 LPRINT TAB(10) CNOMBRE;
3570 GUAPRI#=VAL(CCUENTA):GOSUB 64770:LPRINT TAB(56-L.C%) GUAPRI$;
3580 LPRINT TAB(58) LEFT$(CAUTOR,30);
3590 LPRINT TAB(89) LEFT$(CEDITOR,30);
3600 IF CSITUA="0" THEN LPRINT TAB(120) "BIBLIOTECA":GOTO 3622
3610 LPRINT TAB(120) "EN USO"
3622 '
3642 LIN%=LIN%+1
3650 '
3660 RETURN
3670 '
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 IF LIN%= 90 THEN 3760
3757 LPRINT CHR$(12)
3760 PAG%=PAG%+1:LIN%=0
3770 LPRINT TAB(6) EMPRESA$;
3780 LPRINT TAB(50) "RELACION POR TEMAS  DE LA BIBLIOTECA AL " FECHA$ ;
3790 LPRINT TAB(112) USING "HOJA NUMERO : #,###";PAG%
3800 LPRINT TAB(6) STRING$(LEN(EMPRESA$),205);
3810 LPRINT TAB(50) STRING$(48,205);
3820 LPRINT TAB(112) STRING$(19,205)
3840 LPRINT
3850 LPRINT TAB(6) "TEM";
3860 LPRINT TAB(10) "            T  I  T  U  L  O";
3870 LPRINT TAB(51) "NUMERO";
3872 LPRINT TAB(58) "       A  U  T  O  R";
3874 LPRINT TAB(89) "       E  D  I  T  O  R";
3876 LPRINT TAB(120) " SITUACION"
3900 '
3910 LPRINT TAB(6) STRING$(3,205);
3912 LPRINT TAB(10) STRING$(40,205);
3914 LPRINT TAB(51) STRING$(6,205);
3916 LPRINT TAB(58) STRING$(30,205);
3918 LPRINT TAB(89) STRING$(30,205);
3920 LPRINT TAB(120) STRING$(11,205)
3926 LPRINT
3928 LIN%=LIN%+6
3930 RETURN
3940 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4491 '*************************
4492 '
4493 '
4495 'FALUDIR$="FBIBLIOT.ALF":FALUDAT$="FBIBLIOT.DAT"
4496 'GOSUB 30000
4498 'IF CSWA="0" THEN CLOSE:GOTO 4501
4499 'GET #2,1:LSET CSWA="0":PUT #2,1:CLOSE
4500 SHELL "BIBNUME.BAT"
4501 GOSUB 8000
4502 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4503 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4504 '
4510 FALUDIR$="FBIBLIOT.ALF":FALUDAT$="FBIBLIOT.NUM"
4520 COLOR 10:LOCATE 6,21:PRINT "- LISTADO DE LIBROS POR ORDEN DE TEMAS -"
4530 LOCATE 7,20:PRINT STRING$(41,205)
4550 COLOR 11:LOCATE 12,16:PRINT "DESDE EL TEMA : <   >        HASTA EL TEMA : <   >"
4552 COLOR 15:LOCATE 14,25:PRINT " F10";
4554 COLOR 11:PRINT " Volver al Men£ Anterior"
4560 COLOR 15
4590 'Pedimos numero de Orden DESDE.
4593 '
4600 F.C1%=12:C.C1%=33:L.E%=3:BO%=3:GOSUB 64000
4610 IF I.T%=0 THEN LOCATE 12,33:PRINT D.E$:GOTO 4640
4620 IF I.T%=2 THEN CLOSE:GOTO 9000
4630 GOTO 4600
4640 DESDE$=D.E$
4645 IF LEN(DESDE$)<>3 THEN DESDE$=DESDE$+LEFT$(BLANCO$,3-LEN(DESDE$))
4650 '
4660 '
4670 '
4680 GOSUB 8500:MEN$="- HASTA=ZZZ, hasta el Fin de Fichero -":GOSUB 8500
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=12:C.C1%=62:L.E%=3:BO%=3:GOSUB 64000
4710 IF I.T%=0 THEN LOCATE 12,62:PRINT D.E$:GOTO 4740
4720 IF I.T%=2 THEN CLOSE:GOTO 9000
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 HASTA$=D.E$
4780 IF LEN(HASTA$)<>3 THEN HASTA$=HASTA$+LEFT$(BLANCO$,3-LEN(HASTA$))
4790 'Comprobamos DESDE con HASTA.
4800 IF DESDE$>HASTA$ THEN BEEP:GOSUB 8500:MEN$="Tema -DESDE- mayor de -HASTA-, pursar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4801 PAG%=0:LIN%=90
4802   GOSUB 8500
4803   COLOR 10:LOCATE 22,15:PRINT " Datos Correctos ";
4804   COLOR 15:PRINT " ENTER ";
4805   COLOR 10:PRINT " Datos Erroneos ";
4806   COLOR 15:PRINT " F1 "
4807   COLOR 10
4814 A$=INPUT$(1)
4816 IF ASC(A$)=14 THEN 4550
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4825 '
4830 FOR REG%=2 TO ULTIMO%
4840   GET #2,REG%
4842   IF CTEMAS<DESDE$ THEN 4900     'AL NEXT
4844   IF CTEMAS>HASTA$ THEN REG%=ULTIMO%:GOTO 4900  'AL NEXT
4846  'N.L%=CVI(FULTI)
4848  'GET #2,N.L%
4850   IF CSTATUS<>"O" THEN 4900 'AL NEXT
4852   IF CMARCA="L" THEN 4900
4890   GOSUB 3500   'LINEA DE DETALLE.
4900 NEXT REG%
4901 IF LIN%=90 THEN GOSUB 8500:MEN$="No disponemos de los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4902 LPRINT CHR$(12)
4914 '
4964 CLOSE
4966 GOTO 4501
4968 '
4970 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : BIBLIOTECA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 10:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PBIBLIOT"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FVIDCLIE.CLA inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,50
30054 FIELD #1,2 AS FULTI,3 AS FTEMA,40 AS FLLAMA,4 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30060 'LSET FULTI=MKI$(1):LSET FTEMA="":LSET FLLAMA=""
30061 'LSET FILER="":LSET FSTA="A"
30062 'PUT #1,1
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,40 AS CNOMBRE,40 AS CAUTOR,40 AS CEDITOR,3 AS CTEMAS,6 AS CFEAL,7 AS CTELEFO,40 AS CALUMNO,6 AS CFESA,6 AS CFEDE
30112 FIELD #2,194 AS TODO1$,1 AS CSITUA,6 AS CFEBA,4 AS CVECES,1 AS CMARCA,40 AS COBSER,24 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,1 AS CSWA,250 AS REST1$,9 AS REST2$
30115 GET #2,1
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1)
30124 'LSET CULTACT=MKI$(1):LSET CSWA="0":LSET REST1$="":LSET REST2$=""
30126 'PUT #2,1
30128 '
30138 '
30150 '
30500 OPEN "R",#4,"FBIBQUIE.DAT",70
30510 FIELD #4,5 AS DCUENTA,1 AS DSTATUS,40 AS DNOMBRE,7 AS DTELEFO,6 AS DDESDE,6 AS DHASTA,5 AS DLIBRE
30514 FIELD #4,5 AS DCUEN1,1 AS DSTAT,2 AS DULTI,62 AS DRESTO
30515 GET #4,1
30517  DULTI%=CVI(DULTI)
30522 'LSET DCUEN1="":LSET DSTAT="A":LSET DULTI=MKI$(1)
30524 'LSET DRESTO=""
30526 'PUT #4,1
30528 'CLOSE
30538 'STOP
30540 RETURN
30550 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
