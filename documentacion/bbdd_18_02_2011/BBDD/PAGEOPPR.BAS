10 '                       PAGEOPPR.BAS
20 '                      --------------
30 '   GESTION DE OPERACIONES CON PROVEEDORES DE LA AGENCIA
40 '   ----------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
133 FEHOY$=MID$(DATE$,4,2)+LEFT$(DATE$,2)+RIGHT$(DATE$,2)
134 GOSUB 33000
135 OPEN "I",#4,"FAGECLA.DIA"
136 LINE INPUT #4,TODO$
137 CLAVE$=LEFT$(TODO$,4)
138 CLOSE
140 DEFSTR H,C,O,P,X
150 OPTION BASE 1
160 DIM TABRUT$(5),DIAS%(12),TABENT$(30)
174 DATA 31,28,31,30,31,30,31,31,30,31,30,31
176 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
178 '
200 DATA " OPERACIONES DE PAGO A PROVEEDORES "
202 DATA "  ANULACION DE PAGO A PROVEEDORES  "
215 DATA "  CONSULTA DE PAGOS A PROVEEDORES  "
220 DATA " LISTAR OPERACIONES DE PROVEEDORES "
230 DATA "    FINAL DE PAGO A PROVEEDORES    "
232 FOR I%=1 TO 5
234    READ TABRUT$(I%)
236 NEXT I%
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
310 '******************************
320 'Presentamos el Menu del Programa.
330 GOSUB 8000
332 TIT$="- GESTION GENERAL DE PAGOS A PROVEEDORES DE LA AGENCIA -"
334 X%=80-LEN(TIT$):X%=INT(X%/2)
340 COLOR 11:LOCATE 7,X%:PRINT TIT$
350 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
360 FOR I%=10 TO 14
370    LOCATE I%,20:PRINT TABRUT$(I%-9)
380 NEXT I%
390 '
400 GOSUB 8500:COLOR 11
410 LOCATE 22,15:PRINT "Seleccione Opci¢n con las Flechas y Pulsando la Tecla ";
420 COLOR 15:PRINT " ENTER "
430 'COLOR 7,0,0
440 '
450 C.F%=10:C%=20:L.F%=14:N.L%=5
460 GOSUB 50000
470 '
480 ON I.A% GOTO 510,2500,2500,4490,9000
490 '
500 '
510 '
520 '
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
550 '
570 '
590 '
610 MARGEN$="I"
618 GOSUB 8000:GOSUB 31000
619 '
620 FOR F%=8 TO 18
625 GOSUB 30500:GOSUB 35000
630 '
640 GOSUB 7650
700       '*******************************
705       '***ENTRADA NUMERO DE ALUMNO ***
710       '*******************************
715       COLOR 15
720       LOCATE 1,1:N.ORDEN!=ULTI!+1
725       N$=STR$(N.ORDEN!):N$=LEFT$(CERO$,6-LEN(N$))+RIGHT$(N$,LEN(N$)-1)
730       GET #2,N.ORDEN!
735       '
740       '
745       '
750       GUAPRI#=N.ORDEN!:GOSUB 64770:LOCATE F%,6-L.C%:PRINT GUAPRI$
755       '
800       '***********************
805       '***ENTRADA DE FOPERA***
810       '***********************
815       F.C1%=F%:C.C1%=9:L.E%=6:BO%=8:GOSUB 63000
820       IF I.T%=0 THEN NACI$=D.E$:GOTO 865
825       IF I.T%=5 THEN 815
830       IF I.T%=1 AND I.A%=1 THEN NACI$=FEHOY$:GOTO 865
835       IF I.T%=1 AND I.A%=3 THEN NACI$=RIGHT$(OFEC,2)+MID$(OFEC,3,2)+LEFT$(OFEC,2):GOTO 865
840       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,9:PRINT SPC(8):GOTO 810
845       IF I.T%=3 THEN 810
850       IF I.T%=4 THEN 4000               'Fin Entrada.
855       IF I.T%=2 AND I.A%=1 THEN 865
857       IF I.T%=2 THEN 815
860       '
865       FECSUB$=NACI$:GOSUB 60000
870       IF ERF%<>1 THEN 885
875       BEEP:GOSUB 8500:MEN$="Fecha de Operaci¢n Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,9:PRINT SPC(8)
880       GOSUB 7650:GOTO 805
885       IF LEFT$(FAMD$,2)<"94" THEN 875
887       LOCATE F%,9:PRINT FDMA$:FENA$=FAMD$
890      'LOCATE F%+2,23:COLOR 12:PRINT "- C - ";
891      'COLOR 15:PRINT "Cobros    ";
892      'COLOR 12:PRINT "- P - ";
893      'COLOR 15:PRINT "Pagos"
900       '*******************************
902       '***ENTRADA NUMERO PROVEEDOR ***
904       '*******************************
906       COLOR 15
908       F.C1%=F%:C.C1%=19:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
910       IF I.T%=0 THEN NPRO%=VAL(D.E$):GOTO 928 'Entrada OK
912       IF I.T%=5 THEN 908
914       IF I.T%=1 AND I.A%=1 THEN 908
916       IF I.T%=1 AND I.A%=3 THEN NPRO%=CVI(OPRO):GOTO 928
917       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,9:PRINT SPC(8):GOTO 810
918       IF I.T%=3 THEN 810                 'Retroceder Campo.
920       IF I.T%=4 THEN 4000               'Fin Entrada.
922       IF I.T%=2 AND I.A%=1 THEN 928
924       IF I.T%=2 THEN 902
926       '
928       LOCATE F%,19:PRINT SPC(6)
930       GUAPRI#=NPRO%:GOSUB 64770:LOCATE F%,24-L.C%:PRINT GUAPRI$
932       PRO%=NPRO%
934       IF PRO%<2 OR PRO%>ULTPRO% THEN GOSUB 8500:MEN$="Este N£mero de Proveedor no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,19:PRINT SPC(6):GOTO 900
936       GET #3,PRO%
938       IF PMARC="L" THEN GOSUB 8500:MEN$="Proveedor dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,19:PRINT SPC(6):GOTO 900
939       COLOR 12:LOCATE F%+2,11:PRINT PNOMB:COLOR 15
940       GOSUB 8500
941       LOCATE 22,15:COLOR 10:PRINT "Proveedor Correcto ";
942       COLOR 15:PRINT "  ENTER  ";
943       COLOR 10:PRINT " Err¢neo ";
944       COLOR 15:PRINT "  F1 "
945       A$=INPUT$(1)
946       IF ASC(A$)<13 OR ASC(A$)>14 THEN 945
948       IF ASC(A$)=13 THEN 963
949       LOCATE F%,19:PRINT SPC(6)
963       IF I.A%=1 THEN GOSUB 7650:GOTO 965
964       GOSUB 7500
965       LOCATE F%+2,2:PRINT SPC(78)
968       LOCATE F%+2,12:COLOR 12:PRINT "- 1 - ";
970       COLOR 15:PRINT "Met lico  ";
972       COLOR 12:PRINT "- 2 - ";
974       COLOR 15:PRINT "Talones  ";
976       COLOR 12:PRINT "- 3 - ";
978       COLOR 15:PRINT "Tarjetas  ";
980       COLOR 12:PRINT "- 4 - ";
982       COLOR 15:PRINT "Otros"
1000      '***********************
1005      '***ENTRADA TIPO OPERA**
1010      '***********************
1015      F.C1%=F%:C.C1%=28:L.E%=1:BO%=1:GOSUB 63000
1020      IF I.T%=0 THEN TIPO$=D.E$:GOTO 1055
1025      IF I.T%=5 THEN 1015
1030      IF I.T%=1 AND I.A%=1 THEN 1015
1032      IF I.T%=1 THEN TIPO$=OTIP:GOTO 1055
1035      IF I.T%=3 AND I.A%=1 THEN LOCATE F%,19:PRINT SPC(6):LOCATE F%+2,2:PRINT SPC(78):GOTO 900
1037      IF I.T%=3 THEN LOCATE F%+2,2:PRINT SPC(78):GOTO 900
1040      IF I.T%=4 THEN 4000               'Fin Entrada.
1045      IF I.T%=2 AND I.A%=1 THEN 1055
1047      IF I.T%=2 THEN 1015
1050      '
1055      IF TIPO$<"1" OR TIPO$>"4" THEN BEEP:LOCATE F%,27:PRINT SPC(3):GOTO 1015
1060      LOCATE F%,27
1061      IF TIPO$="1" THEN PRINT "MET":GOTO 1065
1062      IF TIPO$="2" THEN PRINT "TAL":GOTO 1065
1063      IF TIPO$="3" THEN PRINT "TAR":GOTO 1065
1064      PRINT "OTR"
1065      LOCATE F%+2,2:PRINT SPC(78)
1100       '***********************
1110       '***ENTRADA CONCEPTO ***
1120       '***********************
1130       F.C1%=F%:C.C1%=32:L.E%=21:BO%=21:GOSUB 64000
1140       IF I.T%=0 THEN CONC$=D.E$:GOTO 1210
1150       IF I.T%=5 THEN 1130
1160       IF I.T%=1 AND I.A%=1 THEN 1130
1165       IF I.T%=1 AND I.A%=3 THEN CONC$=OTES:GOTO 1210
1170       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,27:PRINT SPC(3):GOTO 968
1175       IF I.T%=3 THEN 968
1180       IF I.T%=4 THEN 4000               'Fin Entrada.
1190       IF I.T%=2 AND I.A%=1 THEN 1210
1195       IF I.T%=2 THEN 1130
1200       '
1210       '
1220       LOCATE F%,32:PRINT CONC$
1230       '*******************************
1240       '***ENTRADA IMPORTE OPERACION***
1250       '*******************************
1260       F.C1%=F%:C.C1%=55:L.E%=9:BO%=12:ESC%=0:GOSUB 63000
1270       IF I.T%=0 THEN IMPOR#=VAL(D.E$):GOTO 1340
1280       IF I.T%=5 THEN 1260
1290       IF I.T%=1 AND I.A%=1 THEN 1260
1295       IF I.T%=1 AND I.A%=3 THEN IMPOR#=CVD(OIMP):GOTO 1340
1300       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,29:PRINT SPC(35):GOTO 1120
1305       IF I.T%=3 THEN 1120
1310       IF I.T%=4 THEN 4000               'Fin Entrada.
1320       IF I.T%=2 AND I.A%=1 THEN 1340
1325       IF I.T%=2 THEN 1240
1330       '
1340       LOCATE F%,55:PRINT SPC(12):GUAPRI#=IMPOR#:GOSUB 64770
1350       LOCATE F%,66-L.C%:PRINT GUAPRI$
1430       '*******************************
1440       '***ENTRADA IMPORTE OPERACION***
1450       '*******************************
1460       F.C1%=F%:C.C1%=69:L.E%=9:BO%=12:ESC%=0:GOSUB 63000
1470       IF I.T%=0 THEN IMPAG#=VAL(D.E$):GOTO 1540
1480       IF I.T%=5 THEN 1460
1490       IF I.T%=1 AND I.A%=1 THEN 1460
1495       IF I.T%=1 AND I.A%=3 THEN IMPAG#=CVD(OPAG):GOTO 1540
1500       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,55:PRINT SPC(12):GOTO 1230
1505       IF I.T%=3 THEN 1230
1510       IF I.T%=4 THEN 4000               'Fin Entrada.
1520       IF I.T%=2 AND I.A%=1 THEN 1540
1525       IF I.T%=2 THEN 1440
1530       '
1540       LOCATE F%,69:PRINT SPC(12):GUAPRI#=IMPAG#:GOSUB 64770
1550       LOCATE F%,80-L.C%:PRINT GUAPRI$
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   LOCATE 22,15:COLOR 10:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT "  ENTER  ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT "  F1 "
2016   '
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2030
2027   LOCATE F%,1:PRINT SPC(80):F%=F%-1:GOTO 2250
2030   LSET ONUM=MKS$(N.ORDEN!):LSET OSTA="O":LSET OMAR="A":LSET OCLA=CLAVE$:LSET OFEC=FENA$:LSET OCOP="P":LSET OTIP=TIPO$
2032   LSET OTES=CONC$:LSET OIMP=MKD$(IMPOR#):LSET ORES="":LSET OPRO=MKI$(NPRO%):LSET OPAG=MKD$(IMPAG#):LSET OPOC="P"
2034   PUT #2,N.ORDEN!
2036   GET #2,1
2038   LSET OULT=MKS$(N.ORDEN!)
2040   PUT #2,1
2042   '
2043   FICHERO$="FAGECAJA.A"+LEFT$(FENA$,2)
2050   GOSUB 30000
2052   ME%=VAL(MID$(FENA$,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(FENA$,2))
2053   XDIA%=ME%*31+DI%
2054   GET #1,XDIA%
2058   INIC#=CVD(HINI):COME#=CVD(HCOB):COTA#=CVD(HCTA):COPL#=CVD(HCPL):COOT#=CVD(HCOT)
2060   FINA#=CVD(HFIN):PAME#=CVD(HPAG):PATA#=CVD(HPTA):PAPL#=CVD(HPPL):PAOT#=CVD(HPOT)
2065   '
2070   'IF COPA$="C" THEN 2090
2072   IF TIPO$="1" THEN PAME#=PAME#+IMPAG#:FINA#=FINA#-IMPAG#:GOTO 2100
2074   IF TIPO$="2" THEN PATA#=PATA#+IMPAG#:GOTO 2100
2076   IF TIPO$="3" THEN PAPL#=PAPL#+IMPAG#:GOTO 2100
2078   PAOT#=PAOT#+IMPAG#
2080   GOTO 2100
2090   IF TIPO$="1" THEN COME#=COME#+IMPOR#:FINA#=FINA#+IMPOR#:GOTO 2100
2092   IF TIPO$="2" THEN COTA#=COTA#+IMPOR#:GOTO 2100
2094   IF TIPO$="3" THEN COPL#=COPL#+IMPOR#:GOTO 2100
2096   COOT#=COOT#+IMPOR#
2098   GOTO 2100
2100   '
2102   LSET HINI=MKD$(INIC#):LSET HCOB=MKD$(COME#):LSET HCTA=MKD$(COTA#)
2104   LSET HCPL=MKD$(COPL#):LSET HCOT=MKD$(COOT#)
2106   LSET HFIN=MKD$(FINA#):LSET HPAG=MKD$(PAME#):LSET HPTA=MKD$(PATA#)
2108   LSET HPPL=MKD$(PAPL#):LSET HPOT=MKD$(PAOT#)
2110   PUT #1,XDIA%
2112  '
2114  TOPA#=CVD(PPAGO):TOCO#=CVD(PCOBR)
2116  TOPA#=TOPA#+IMPOR#
2118  TOCO#=TOCO#+IMPOR#-IMPAG#
2120  LSET PPAGO=MKD$(TOPA#):LSET PCOBR=MKD$(TOCO#)
2122  PUT #3,PRO%
2124  '
2250   CLOSE
2252   NEXT F%
2253   '
2254   '
2258   '
2260   '
2264   FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
2265   '
2266   GOTO 619
2490 '**********************************************
2495 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
2500 '**********************************************
2505 '
2530 GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
2535 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
2540 '
2545 GOSUB 8000:GOSUB 31000
2550 FOR F%=8 TO 19
2555 GOSUB 30500
2560 GOSUB 7500
2565       '*******************************
2570       '***ENTRADA NUMERO DE ALUMNO ***
2575       '*******************************
2580       COLOR 15
2585       F.C1%=F%:C.C1%=1:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2590       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2635 'Entrada OK
2595       IF I.T%=5 THEN 2585
2615       IF I.T%=4 THEN 4000               'Fin Entrada.
2625       GOTO 2570
2630       '
2635       '
2640       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE F%,1:PRINT SPC(6):LOCATE F%,6-L.C%:PRINT GUAPRI$
2642       N.L!=VAL(CUENTA$)
2645       IF N.L!<2 OR N.L!>ULTI!  THEN GOSUB 8500:MEN$="Este N£mero de Operaci¢n no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2650       GET #2,N.L!
2655       IF CLAVE$="F311" OR CLAVE$="f311" THEN 2670
2660       IF OCLA=CLAVE$ THEN 2670
2665       GOSUB 8500:MEN$="Operaci¢n no disponible, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2670       IF OMAR="B" THEN GOSUB 8500:MEN$="Operaci¢n dada de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE F%,1:PRINT SPC(6):GOTO 2560
2675       GOSUB 2700   'PRESENTAR DATOS EN PANTALLA.
2680       IF I.A%=2 THEN 2990
2685       GOTO 3250
2690       '
2700      '*******************************
2702      '*** PONER DATOS EN PANTALLA ***
2704      '*******************************
2706      '
2710      LOCATE F%,9:PRINT RIGHT$(OFEC,2)"-"MID$(OFEC,3,2)"-"LEFT$(OFEC,2)
2720      GUAPRI#=CVI(OPRO):GOSUB 64770
2722      LOCATE F%,24-L.C%:PRINT GUAPRI$
2730      LOCATE F%,27
2732      IF OTIP="1" THEN PRINT "MET":GOTO 2740
2734      IF OTIP="2" THEN PRINT "TAL":GOTO 2740
2736      IF OTIP="3" THEN PRINT "TAR":GOTO 2740
2738      PRINT "OTR"
2740      LOCATE F%,32:PRINT OTES
2750      LOCATE F%,55:PRINT SPC(12):GUAPRI#=CVD(OIMP):GOSUB 64770
2760      LOCATE F%,66-L.C%:PRINT GUAPRI$
2765      LOCATE F%,69:PRINT SPC(12):GUAPRI#=CVD(OPAG):GOSUB 64770
2770      LOCATE F%,80-L.C%:PRINT GUAPRI$
2780      '
2790      RETURN
2800      '
2980 '*************************
2990 '***TRATAMIENTO DE BAJA***
3000 '*************************
3010 GOSUB 8500:COLOR 10
3020 LOCATE 22,15:PRINT "Para anular la Operaci¢n ";
3030 COLOR 15:PRINT " ENTER ";
3040 COLOR 10:PRINT " Para no anular ";
3050 COLOR 15:PRINT " F1 "
3055 A$=INPUT$(1)
3057 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3055
3060 IF ASC(A$)=14 THEN 2545
3062 LSET OMAR="B"
3064 '
3065 PUT #2,N.L!
3066 '
3068   '
3070   FICHERO$="FAGECAJA.A"+LEFT$(CFEC,2)
3072   GOSUB 30000
3074   ME%=VAL(MID$(CFEC,3,2)):ME%=ME%-1:DI%=VAL(RIGHT$(CFEC,2))
3076   XDIA%=ME%*31+DI%
3078   GET #1,XDIA%
3080   INIC#=CVD(HINI):COME#=CVD(HCOB):COTA#=CVD(HCTA):COPL#=CVD(HCPL):COOT#=CVD(HCOT)
3082   FINA#=CVD(HFIN):PAME#=CVD(HPAG):PATA#=CVD(HPTA):PAPL#=CVD(HPPL):PAOT#=CVD(HPOT)
3084   '
3086   'IF COPA$="C" THEN 3098
3088   IF OTIP="1" THEN PAME#=PAME#+CVD(OPAG):FINA#=FINA#-CVD(OPAG):GOTO 3108
3090   IF OTIP="2" THEN PATA#=PATA#+CVD(OPAG):GOTO 3108
3092   IF OTIP="3" THEN PAPL#=PAPL#+CVD(OPAG):GOTO 3108
3094   PAOT#=PAOT#+CVD(OPAG)
3096   GOTO 3108
3098  'IF OTIP="1" THEN COME#=COME#-IMPOR#:FINA#=FINA#-IMPOR#
3100  'IF OTIP="2" THEN COTA#=COTA#-IMPOR#
3102  'IF OTIP="3" THEN COPL#=COPL#-IMPOR#
3104  'COOT#=COOT#-IMPOR#
3106  'GOTO 3108
3108   '
3110   LSET HINI=MKD$(INIC#):LSET HCOB=MKD$(COME#):LSET HCTA=MKD$(COTA#)
3112   LSET HCPL=MKD$(COPL#):LSET HCOT=MKD$(COOT#)
3114   LSET HFIN=MKD$(FINA#):LSET HPAG=MKD$(PAME#):LSET HPTA=MKD$(PATA#)
3116   LSET HPPL=MKD$(PAPL#):LSET HPOT=MKD$(PAOT#)
3118   PUT #1,XDIA%
3120   GOSUB 35000
3122   GET #3,CVI(OPRO)
3124  TOPA#=CVD(PPAGO):TOCO#=CVD(PCOBR)
3126  TOPA#=TOPA#-CVD(OIMP)
3128  TOCO#=TOCO#-CVD(OIMP)+CVD(OPAG)
3130  LSET PPAGO=MKD$(TOPA#):LSET PCOBR=MKD$(TOCO#)
3132  PUT #3,CVI(OPRO)
3134  '
3170   CLOSE
3172   NEXT F%
3182   FOR F%=8 TO 19:LOCATE F%,2:PRINT SPC(79):NEXT F%
3184   '
3186   GOTO 2550
3188   '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3270 A$=INPUT$(1)      ':GOSUB 7650:LOCATE 8,56:PRINT SPC(20)
3280 GOTO 3170
3290 '
3300 '
3310 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4540   '
4542   LI$="- LISTAR OPERACIONES DE PROVEEDORES ENTRE FECHAS -"
4550   GOSUB 8000
4552   COLOR 10:LOCATE 6,16:PRINT LI$
4554   LOCATE 7,16:PRINT STRING$(50,205)
4556   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
4558   COLOR 15:LOCATE 16,28:PRINT "F7 ==>  ";:COLOR 11:PRINT "Final de la Opci¢n":COLOR 15
4600       '***********************
4602       '***ENTRADA DE FDESDE***
4604       '***********************
4606       F.C1%=12:C.C1%=31:L.E%=6:BO%=8:GOSUB 63000
4608       IF I.T%=0 THEN DESDE$=D.E$:GOTO 4614
4609       IF I.T%=4 THEN 4000
4610       GOTO 4600
4612       '
4614       FECSUB$=DESDE$:GOSUB 60000
4616       IF ERF%<>1 THEN 4622
4618       BEEP:GOSUB 8500:MEN$="Fecha Desde Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,31:PRINT SPC(8)
4620       GOTO 4602
4622       LOCATE 12,31:PRINT FDMA$:FDES$=FAMD$:FLI1$=FDMA$
4700       '***********************
4702       '***ENTRADA DE FHASTA***
4704       '***********************
4706       F.C1%=12:C.C1%=58:L.E%=6:BO%=8:GOSUB 63000
4708       IF I.T%=0 THEN HASTA$=D.E$:GOTO 4714
4709       IF I.T%=4 THEN 4000
4710       GOTO 4700
4712       '
4714       FECSUB$=HASTA$:GOSUB 60000
4716       IF ERF%<>1 THEN 4722
4718       BEEP:GOSUB 8500:MEN$="Fecha Hasta Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,58:PRINT SPC(8)
4720       GOTO 4702
4722       LOCATE 12,58:PRINT FDMA$:FHAS$=FAMD$:FLI2$=FDMA$
4730   GOSUB 8500
4732   LOCATE 22,15:COLOR 10:PRINT "Fechas correctas ";
4734   COLOR 15:PRINT "  ENTER  ";
4736   COLOR 10:PRINT " Fechas err¢neas ";
4738   COLOR 15:PRINT "  F1 "
4740   A$=INPUT$(1)
4742   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4740
4744   IF ASC(A$)=14 THEN 4540
4746   '
4748   IF FHAS$<FDES$ THEN GOSUB 8500:MEN$="Fechas err¢neas, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4540
4750   '
5500 '********************************
5502 '***LISTADOS GENERALES DE CAJA***
5504 '********************************
5506 '
5508 '
5510 GOSUB 30500
5512 OPEN "O",#5,"FAGEOPER.SEL"
5514 FOR NN!=2 TO ULTI!
5516 GET #2,NN!
5518 IF OSTA<>"O" THEN 5560
5520 IF OMAR="B" THEN 5560
5521 IF OPOC<>"P" THEN 5560
5522 IF OFEC<FDES$ THEN 5560
5524 IF OFEC>FHAS$ THEN 5560
5525 GOTO 5540
5526 IF CLAVE$="F311" THEN 5540
5527 '
5528 IF OCLA="A91Z" OR OCLA="AI23" THEN 5540
5530 GOTO 5560
5540 '
5542 NUPR%=CVI(OPRO):NUPR$=STR$(NUPR%):CE%=LEN(NUPR$)-1
5544 NUPR$=LEFT$(CERO$,6-CE%)+RIGHT$(NUPR$,CE%)
5546 NOPE!=CVS(ONUM):NOPE$=STR$(NOPE!):CE%=LEN(NOPE$)-1
5548 NOPE$=LEFT$(CERO$,6-CE%)+RIGHT$(NOPE$,CE%)
5550 CANT#=CVD(OPAG):CANT$=STR$(CANT#):CE%=LEN(CANT$)-1
5552 CANT$=LEFT$(CERO$,15-CE%)+RIGHT$(CANT$,CE%)
5554 SALIDA$=NOPE$+CANT$+OCLA+OFEC+OCOP+OTIP+OTES+NUPR$+OPOC+"****"
5556 PRINT #5,SALIDA$
5560 NEXT NN!
5562 CLOSE
5564 SHELL "AGEFECH.BAT"
5566 GOSUB 6000
5568 CLOSE
5570 GOTO 4490
5600 '***************************
5602 '***LISTADO LINEA DETALLE***
5604 '***************************
5605 '
5606 IF LIN%<65 THEN 5610
5607 IF LIN%=90 THEN 5609
5608 LPRINT CHR$(12)
5609 LIN%=0:PAG%=PAG%+1:GOSUB 5700
5610 GUAPRI#=VAL(XNUM):GOSUB 64770
5611 LPRINT TAB(15-L.C%) GUAPRI$;
5612 IF XPOC<>"P" THEN 5615
5613 'GUAPRI#=VAL(XPRO):GOSUB 64770
5614 'LPRINT TAB(22-L.C%) GUAPRI$;:GOTO 5618
5615 'IF XPOC<>"C" THEN 5624
5616 GUAPRI#=VAL(XPRO):GOSUB 64770
5617 LPRINT TAB(29-L.C%) GUAPRI$;
5618 'IF XPOC<>"P" THEN 5621
5619 GET #3,VAL(XPRO)
5620 LPRINT TAB(32) LEFT$(PNOMB,40);:GOTO 5624
5621 '
5622 'GET #4,VAL(XPRO)
5623 'LPRINT TAB(33) LEFT$(CNOMB,40);
5624 '
5625 IF XTIP="1" THEN LPRINT TAB(75) "META";:GOTO 5629
5626 IF XTIP="2" THEN LPRINT TAB(75) "TALO";:GOTO 5629
5627 IF XTIP="3" THEN LPRINT TAB(75) "TARJ";:GOTO 5629
5628 LPRINT TAB(75) "OTRO";
5629 '
5630 LPRINT TAB(81) XTES;
5632 GUAPRI#=VAL(XIMP):GOSUB 64770
5634 IF XCOP="C" THEN LPRINT TAB(115-L.C%) GUAPRI$:GOTO 5640
5636 LPRINT TAB(128-L.C%) GUAPRI$
5640 LIN%=LIN%+1
5642 '
5644 RETURN
5646 '
5690 '***************************
5692 '***CABECERAS DEL LISTADO***
5694 '***************************
5700 LPRINT TAB(9) "AGENCIA DE VIAJES  E.I.D.E.";
5701 LPRINT TAB(45) "OPERACIONES DE PROVEEDORES DESDE EL "FLI1$" HASTA EL "FLI2$;
5702 LPRINT TAB(117) USING "HOJA.- : ###";PAG%
5704 LPRINT TAB(9) STRING$(27,205);
5705 LPRINT TAB(45) STRING$(62,205);
5706 LPRINT TAB(117) STRING$(12,205)
5708 LPRINT
5716 LPRINT TAB(9) "OPERACION    PROVEEDOR            NOMBRE DEL PROVEEDOR            TIPO       DESCRIPCION          COBROS        PAGOS   "
5718 LPRINT TAB(9) "ÍÍÍÍÍÍÍÍÍ    ÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ  ÍÍÍÍÍÍÍÍÍÍÍÍ ÍÍÍÍÍÍÍÍÍÍÍÍ":LPRINT:LIN%=6
5720 '
5722 RETURN
6000 '*************************
6002 '***INFORME DEL FICHERO***
6004 '*************************
6006 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
6008 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
6010 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
6012 GOSUB 8000
6014   LI$="- LISTADO GENERAL DE CAJA ENTRE DOS FECHAS -"
6016   COLOR 10:LOCATE 6,19:PRINT LI$
6018   LOCATE 7,19:PRINT STRING$(44,205)
6020   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
6022       LOCATE 12,31:PRINT FLI1$
6024       LOCATE 12,58:PRINT FLI2$
6026   GOSUB 8500:MEN$="Espere por favor........":GOSUB 8500
6028 OPEN "I",#5,"FAGEOPER.CLA"
6030 GOSUB 35000:GOSUB 35500
6050 FF$=""
6060 LIN%=90
6070 PAG%=0
6080 '
6140 GOSUB 6600   'LECTURA SECUENCIAL COBROS Y PAGOS PENDIENTES.
6150 '
6160 IF FF$="FF" THEN GOSUB 8500:MEN$="No tenemos los datos pedidos, pulse una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4490
6170 TOCO#=0:TOPA#=0
6200 '
6210 WHILE FF$<>"FF"
6220   FECH$=XFEC
6230   TODICO#=0:TODIPA#=0
6290   '
6300  WHILE FF$<>"FF" AND FECH$=XFEC
6310    '
6320    IMPORTE#=VAL(XIMP)
6330    IF XCOP="C" THEN TODICO#=TODICO#+IMPORTE#:GOTO 6380
6370    TODIPA#=TODIPA#+IMPORTE#
6380    '
6410    GOSUB 5600   'LINEA DE DETALLE
6420    GOSUB 6600   'HACER LECTURA SECUENCIAL
6430     '
6440  WEND
6450  'ROTURA POR DIA.
6460  GUAPRI#=TODICO#:GOSUB 64770
6462  LPRINT
6464  LPRINT TAB(81) "Total dia "RIGHT$(FECH$,2)"-"MID$(FECH$,3,2)"-"LEFT$(FECH$,2);
6466  LPRINT TAB(115-L.C%) GUAPRI$;
6468  GUAPRI#=TODIPA#:GOSUB 64770
6470  LPRINT TAB(128-L.C%) GUAPRI$
6472  TOPA#=TOPA#+TODIPA#:TOCO#=TOCO#+TODICO#
6474  LPRINT:LIN%=LIN%+3
6480  '
6490 WEND
6500 'ROTURA POR FIN DE FICHERO.
6510  GUAPRI#=TOCO#:GOSUB 64770
6512  LPRINT
6514  LPRINT TAB(81) "Total general ";
6516  LPRINT TAB(115-L.C%) GUAPRI$;
6518  GUAPRI#=TOPA#:GOSUB 64770
6520  LPRINT TAB(128-L.C%) GUAPRI$
6530 LPRINT CHR$(12)
6540 '
6550 CLOSE
6560 GOTO 4490
6570 '
6600 '***************************
6602 '***LEER FICHERO DE DATOS***
6604 '***************************
6606 '
6608 IF EOF(5) THEN FF$="FF":RETURN
6610 LINE INPUT #5,TODO$
6612 XNUM=LEFT$(TODO$,6)
6614 XIMP=MID$(TODO$,7,15)
6616 XCLA=MID$(TODO$,22,4)
6618 XFEC=MID$(TODO$,26,6)
6620 XCOP=MID$(TODO$,32,1)
6622 XTIP=MID$(TODO$,33,1)
6624 XTES=MID$(TODO$,34,21)
6625 XPRO=MID$(TODO$,55,6)
6626 XPOC=MID$(TODO$,61,1)
6627 '
6628 RETURN
6630 '
6632 '
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7540 'COLOR 15:PRINT " ENTER =>";
7550 'COLOR 11:PRINT " Salto  ";
7560 COLOR 15:PRINT " F7 =>";
7570 COLOR 11:PRINT " Fin de la Opci¢n. ";
7580 'COLOR 15:PRINT " F8 =>";
7590 'COLOR 11:PRINT " M.Izda. "
7600 COLOR 15
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7680 COLOR 15:PRINT "ENTER =>";
7690 COLOR 11:PRINT " Saltar. ";
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin Altas.";
7720 COLOR 15:PRINT " F8 =>";
7730 COLOR 11:PRINT " Campo Atras. ";
7740 COLOR 15:PRINT " F10 =>";
7750 COLOR 11:PRINT " Dup."
7760 COLOR 15
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,36:PRINT "Aplicaci¢n : C A J A    ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PAGECAJA"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9144 IF ERL=35022 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FAGEPROV.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9145 IF ERL=35522 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FAGEFICH.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30090 ' Apertura del fichero de datos   FRESCAJA.DIA
30095 '
30100 OPEN "R",#1,FICHERO$,90
30105 '
30110 FIELD #1,1 AS HSTA,8 AS HINI,8 AS HCOB,8 AS HPAG,8 AS HCTA,8 AS HPTA,8 AS HCPL,8 AS HPPL,8 AS HCOT,8 AS HPOT,8 AS HFIN,9 AS HRES
30114 '
30128 RETURN
30130 '
30140 '
30500 '**********************************
30510 '***APERTURA FICHERO OPERACIONES***
30520 '**********************************
30525 'Comprobamos la existencia del fichero.
30552 '
30590 ' Apertura del fichero de datos   FRESOPER.DAT
30595 '
30600 OPEN "R",#2,"FAGEOPER.DAT",80
30605 '
30610 FIELD #2,4 AS ONUM,1 AS OSTA,1 AS OMAR,4 AS OCLA,6 AS OFEC,1 AS OCOP,1 AS OTIP,21 AS OTES,8 AS OIMP,8 AS OPAG,2 AS OPRO,1 AS OPOC,22 AS ORES
30611 FIELD #2,4 AS OULT,1 AS OST1,1 AS OMA1,74 AS OFIL
30614 '
30616  GET #2,1
30617 'LSET OULT=MKS$(1)
30618 'LSET OST1="A":LSET OMA1="A":LSET OFIL=""
30619 'PUT #2,1
30620 'CLOSE
30621 'STOP
30622 '
30624 ULTI!=CVS(OULT)
30628 RETURN
30630 '
30640 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 11
31040 LOCATE 4,23:PRINT TABRUT$(I.A%)
31042 LOCATE 5,23:PRINT STRING$(LEN(TABRUT$(I.A%)),205)
31050 LOCATE 6,1:PRINT "NUMERO   FECHA.   PROVE.  TIP     C O N C E P T O     IMPOR. BRUTO  IMP. A PAGAR"
31054 LOCATE 7,1:PRINT "------  --------  ------  ---  ---------------------  ------------  ------------"
31084 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** AGENCIA DE VIAJES EIDE ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '**********************************
35002 '***APERTURA FICHERO PROVEEDORES***
35004 '**********************************
35006 '
35008 ' Apertura del fichero de Proveedores FAGEPROV.DAT
35010 OPEN "R",#3,"FAGEPROV.DAT",400
35012 FIELD #3,1 AS PSTAT,1 AS PMARC,5 AS PCUEN,9 AS PNCIF,60 AS PNOMB,60 AS PDIRE,30 AS PPOBL,15 AS PPROV,5 AS PCOPO,10 AS PTELE,20 AS PNACI
35014 FIELD #3,216 AS TODO1$,10 AS PNFAX,30 AS PCON1,10 AS PTC01,30 AS PCON2,10 AS PTC02,30 AS PCON3,10 AS PTC03,8 AS PPAGO,8 AS PCOBR,38 AS PFILL
35016 '
35018 FIELD #3,1 AS PSTA1,1 AS PMAR1,2 AS PULTI,12 AS PFICH,250 AS CEST1$,134 AS CEST2$
35020  GET #3,1
35022  IF PFICH<>"FAGEPROV.DAT" THEN 9144
35024  ULTPRO%=CVI(PULTI)
35030  '
35032 RETURN
35500 '**********************************
35502 '***APERTURA FICHERO DE CLIENTES***
35504 '**********************************
35506 '
35508 ' Apertura del fichero de Fichas FAGEFICH.DAT
35510 OPEN "R",#4,"FAGEFICH.DAT",250
35512 FIELD #4,1 AS CSTAT,1 AS CMARC,5 AS CNUME,40 AS CNOMB,40 AS CDIRE,10 AS CTELE,6 AS CFSER
35514 FIELD #4,103 AS TODO1$,6 AS CFRES,40 AS CDESC,2 AS CNPRO,4 AS CCLAV,40 AS CNUBO,8 AS CBRUT,2 AS CTIDE,8 AS CNETO,8 AS CPACU,8 AS CPAPE,6 AS CFCOF,4 AS COACU,4 AS COFIN,1 AS CTIPO,6 AS CFILL
35516 '
35518 FIELD #4,1 AS CSTA1,1 AS CMAR1,2 AS CULTI,12 AS CFICH,234 AS CEST1$
35520  GET #4,1
35522  IF CFICH<>"FAGEFICH.DAT" THEN 9145
35524  ULTIMO%=CVI(CULTI)
35525 'LSET CSTA1="A":LSET CMAR1="A":LSET CULTI=MKI$(1):LSET CEST1$="":LSET CFICH="FAGEFICH.DAT"
35526 'PUT #4,1
35527 'CLOSE:STOP
35530  '
35532 RETURN
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 12:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 15
50300 RETURN
50340 '
50510 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
