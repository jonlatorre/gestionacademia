10 '                        PACAARRE.BAS
20 '                    ---------------------
30 '           PROGRAMA DE ARRANQUE DE APLICACION ACADEMIA
50 ON ERROR GOTO 9110
52 DEFSTR C,F
60 KEY OFF
62 FOR I%=1 TO 10
70    KEY I%,CHR$(0)
80 NEXT I%
85 KEY 1,CHR$(14)
87 GOSUB 33000
90 OPTION BASE 1:DIM DIAS%(12),TABENT$(30):KEY 10,CHR$(7)
100 DATA 31,28,31,30,31,30,31,30,31,31,30,31
102 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
111 '
112 GOSUB 60000:FOR H!=1 TO 3000!:B=B:NEXT H!
113 '
114 GOSUB 61000
117 FOR H!=1 TO 3000!:B=B:NEXT H!:CLS
130 COLOR 10:GOSUB 8000:COLOR 11:GOSUB 8100:COLOR 12:GOSUB 8110:COLOR 13:GOSUB 8120:COLOR 13:GOSUB 8130:COLOR 12:GOSUB 8140:COLOR 11:GOSUB 8150:COLOR 10:GOSUB 8160
140 LOCATE 11,28:PRINT "ID.USUARIO      : XXX "
150 LOCATE 12,28:PRINT "CONTRASE¥A      : XXXX "
160 LOCATE 13,28:PRINT "FECHA OPERACION : DDMMAA "
170 'LOCATE 13,28:PRINT "HORA            : HHMM "
175 COLOR 15:LOCATE 11,45:PRINT " ";
180 '
190 F.C1%=11:C.C1%=46:L.E%=3:BO%=3:GOSUB 64000
195 IF I.T%<> 0 THEN 190 ELSE N$=D.E$
200 IF N$<>"MSL" AND N$<>"msl" AND N$<>"MEJ" AND N$<>"mej" AND N$<>"RSG" AND N$<>"rsg" AND N$<>"JMP" AND N$<>"jmp" THEN BEEP:GOTO 140
202 'LOCATE 12,45:PRINT " ";
205 LOCATE 12,46,1,0,7:C$=INPUT$(1):PRINT " ";:C$=C$+INPUT$(1)
206 PRINT " ";:C$=C$+INPUT$(1)
207 PRINT " ";:C$=C$+INPUT$(1)
208 PRINT " "
220 IF N$<>"MSL" AND N$<>"msl" THEN 250
230 IF C$<>"1248" THEN BEEP:GOTO 140
240 GOTO 270
250 IF N$<>"JMP" AND N$<>"jmp" THEN 262
260 IF C$<>"3112" THEN BEEP:GOTO 140
261 GOTO 270
262 IF N$<>"MEJ" AND N$<>"mej" THEN 265
263 IF C$<>"0260" THEN BEEP:GOTO 140
264 GOTO 270
265 IF N$<>"RSG" AND N$<>"rsg" THEN 268
266 IF C$<>"0162" THEN BEEP:GOTO 140
267 GOTO 270
268 IF N$<>"EIO" AND N$<>"eio" THEN 140
269 IF C$<>"3333" THEN BEEP:GOTO 140
270 '
280 FECHA$=MID$(DATE$,4,2)+"-"+LEFT$(DATE$,2)+"-"+RIGHT$(DATE$,2)
285 FAMD$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
300 COLOR 15:LOCATE 13,46:PRINT FECHA$
310 COLOR 10:LOCATE 15,19:PRINT "Si la Fecha es Correcta ";
320 COLOR 15:PRINT " ENTER ";
330 COLOR 10:PRINT ", Si no ";
340 COLOR 15:PRINT " F1 ":LOCATE 14,45,0:PRINT " ";
350 A$=INPUT$(1)
360 IF ASC(A$)< 13 OR ASC(A$)>14 THEN BEEP:GOTO 350
365 IF ASC(A$)=13 THEN 1400
370 LOCATE 15,18:PRINT SPC(46):LOCATE 13,46:PRINT SPC(8)
372 'LOCATE 13,45,5:PRINT " ";
1270 F.C1%=13:C.C1%=46:L.E%=6:BO%=8:ESC%=1:GOSUB 63000
1272 IF I.T%<> 0 THEN 1270
1290 FECSUB$=D.E$:GOSUB 40000
1300 IF ERF%=1 THEN BEEP:GOTO 1270
1310 FECHA$=FDMA$
1320 LOCATE 13,46:PRINT FDMA$
1370 DATE$=MID$(FDMA$,4,3)+LEFT$(FDMA$,3)+"19"+RIGHT$(FDMA$,2)
1375 GOTO 310
1400 ' GOSUB 9500
1500 'QUITA$="0":EMPRESA$="* SCHOOL OF ENGLISH *":NIF$="111111":OTROS$="8812"
1510 'OPEN "O",#3,"FVIDCOM.DAT"  'FICHERO EQUIVALENTE A LA COMMMON
1520 'WRITE #3,EMPRESA$,FECHA$,NIF$,QUITA$,OTROS$
1530 'CLOSE #3
1540 'GOTO 9000
6200 '
6210 'Miramos si debemos hacer el sort del dir
6215 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
6220 GOSUB 30000
6230 IF CSWA="0" THEN 9000
6240 '
6300 '************************
6310 '***HACER SORT DE DIR.***
6320 '************************
6322 GET #2,1
6323 LSET CSWA="0"
6324 'LSET CULTACT=CULTI
6326 PUT #2,1
6328 '
6330 CLOSE
6340 CLS
6350 'SHELL "ACANUME.BAT"
6355 SHELL "ACAALFA.BAT"
6360 GOTO 9000
6370 '
6400 '
7000 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 FOR J%=2 TO 22 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8060 LOCATE 23,1,0:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8070 RETURN
8080 '
8090 '
8100 '***********************************************
8102 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8103 '***********************************************
8104 '
8105 LOCATE 2,3:PRINT CHR$(201)+STRING$(74,205)+CHR$(187)
8106 FOR J%=3 TO 22 :LOCATE J%,3:PRINT CHR$(186):LOCATE J%,78 :PRINT CHR$(186):NEXT
8107 LOCATE 22,3,0:PRINT CHR$(200)+STRING$(74,205)+CHR$(188);
8108 RETURN
8109 '
8110 '***********************************************
8112 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8113 '***********************************************
8114 '
8115 LOCATE 3,5:PRINT CHR$(201)+STRING$(70,205)+CHR$(187)
8116 FOR J%=4 TO 21 :LOCATE J%,5:PRINT CHR$(186):LOCATE J%,76 :PRINT CHR$(186):NEXT
8117 LOCATE 21,5,0:PRINT CHR$(200)+STRING$(70,205)+CHR$(188);
8118 RETURN
8119 '
8120 '***********************************************
8122 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8123 '***********************************************
8124 '
8125 LOCATE 4,7:PRINT CHR$(201)+STRING$(66,205)+CHR$(187)
8126 FOR J%=5 TO 20 :LOCATE J%,7:PRINT CHR$(186):LOCATE J%,74 :PRINT CHR$(186):NEXT
8127 LOCATE 20,7,0:PRINT CHR$(200)+STRING$(66,205)+CHR$(188);
8128 RETURN
8129 '
8130 '***********************************************
8132 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8133 '***********************************************
8134 '
8135 LOCATE 5,9:PRINT CHR$(201)+STRING$(62,205)+CHR$(187)
8136 FOR J%=6 TO 19 :LOCATE J%,9:PRINT CHR$(186):LOCATE J%,72 :PRINT CHR$(186):NEXT
8137 LOCATE 19,9,0:PRINT CHR$(200)+STRING$(62,205)+CHR$(188);
8138 RETURN
8139 '
8140 '***********************************************
8142 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8143 '***********************************************
8144 '
8145 LOCATE 6,11:PRINT CHR$(201)+STRING$(58,205)+CHR$(187)
8146 FOR J%=7 TO 18 :LOCATE J%,11:PRINT CHR$(186):LOCATE J%,70 :PRINT CHR$(186):NEXT
8147 LOCATE 18,11,0:PRINT CHR$(200)+STRING$(58,205)+CHR$(188);
8148 RETURN
8149 '
8150 '***********************************************
8152 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8153 '***********************************************
8154 '
8155 LOCATE 7,13:PRINT CHR$(201)+STRING$(54,205)+CHR$(187)
8156 FOR J%=8 TO 17 :LOCATE J%,13:PRINT CHR$(186):LOCATE J%,68 :PRINT CHR$(186):NEXT
8157 LOCATE 17,13,0:PRINT CHR$(200)+STRING$(54,205)+CHR$(188);
8158 RETURN
8159 '
8160 '***********************************************
8162 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8163 '***********************************************
8164 '
8165 LOCATE 8,15:PRINT CHR$(201)+STRING$(50,205)+CHR$(187)
8166 FOR J%=9 TO 16 :LOCATE J%,15:PRINT CHR$(186):LOCATE J%,66 :PRINT CHR$(186):NEXT
8167 LOCATE 16,15,1,0,7:PRINT CHR$(200)+STRING$(50,205)+CHR$(188);
8168 RETURN
8169 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 1500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9025 CLOSE
9030 RUN "PACAINIC"      'PROGRAMA DE DISTRIBUCION DE FICHEROS.
9040 '
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 '
9140 GOTO 9190
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9190 ON ERROR GOTO 0
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,20 AS CSUCUR,17 AS CCUBAN,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,237 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 '
30128 RETURN
30130 '
30140 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
60000 CLS:COLOR 15:LOCATE 2,1,0:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
60010  FOR J%=3 TO 22:LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80:PRINT CHR$(186);:NEXT
60020  LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
60030 '
60050 COLOR 12
60052 FOR A%=3 TO 22
60053 LOCATE A%,2:PRINT STRING$(78,"²")
60054 NEXT A%
60060 COLOR 10
60065 LOCATE 3,74:PRINT STRING$(6,"²")
60130 N%=8
60140 FOR A%=4 TO 21
60150 LOCATE A%,80-N%:PRINT STRING$(6,"²")
60160 N%=N%+4
60170 NEXT A%
60171 LOCATE 22,2:PRINT STRING$(6,"²")
60172 LOCATE 3,2:PRINT STRING$(6,"²")
60173 N%=4
60174 FOR A%=4 TO 21
60176 LOCATE A%,N%:PRINT STRING$(6,"²")
60178 N%=N%+4
60180 NEXT A%
60182 LOCATE 22,74:PRINT STRING$(6,"²")
60183 COLOR 15:LOCATE 12,2:PRINT STRING$(78,"²")
60184 LOCATE 13,2:PRINT STRING$(78,"²")
60186 FOR A%=3 TO 22
60187 LOCATE A%,38:PRINT STRING$(6,"²")
60188 NEXT A%
60190 'END
60200 COLOR 13:LOCATE 8,28,0:PRINT CHR$(201)+STRING$(23,205)+CHR$(187)
60210  FOR J%=9 TO 15:LOCATE J%,28:PRINT CHR$(186)+STRING$(23," ")+CHR$(186):NEXT
60220  LOCATE 16,28:PRINT CHR$(200)+STRING$(23,205)+CHR$(188);
60230 COLOR 15
60240 LOCATE 10,31:PRINT "A P L I C A C I O N"
60250 LOCATE 12,31:PRINT "  A C A D E M I A  "
60260 LOCATE 14,31:PRINT "    I N G L E S    "
60270 RETURN
61000 CLS
61010 LOCATE 10,31:PRINT "A P L I C A C I O N"
61020 LOCATE 12,31:PRINT "  A C A D E M I A  "
61030 LOCATE 14,31:PRINT "    I N G L E S    "
61040 COLOR 9:LOCATE 8,28,0:PRINT CHR$(201)+STRING$(23,205)+CHR$(187)
61050  FOR J%=9 TO 15:LOCATE J%,28:PRINT CHR$(186):LOCATE J%,52:PRINT CHR$(186):NEXT
61060  LOCATE 16,28:PRINT CHR$(200)+STRING$(23,205)+CHR$(188);
61070 COLOR 15:N%=77
61080 LOCATE 23,2:PRINT STRING$(78,"²")
61090 FOR A%=22 TO 17 STEP -1
61100 LOCATE A%,80-N%:PRINT STRING$(N%,"²")
61110 N%=N%-4
61120 NEXT A%
61130 LOCATE 16,27:PRINT "²"
61140 FOR A%=16 TO 10 STEP -1
61150 LOCATE A%,53:PRINT STRING$(27,"²")
61160 NEXT A%
61170 N%=25:COLOR 12
61180 FOR A%=9 TO 3 STEP -1
61190 LOCATE A%,80-N%:PRINT STRING$(N%,"²")
61200 N%=N%-4
61210 NEXT A%
61220 COLOR 11
61230 N%=1
61240 FOR A%=22 TO 16 STEP -1
61250 LOCATE A%,2:PRINT STRING$(N%,"²")
61260 N%=N%+4
61270 NEXT A%
61280 FOR A%=15 TO 9 STEP -1
61290 LOCATE A%,2:PRINT STRING$(26,"²")
61300 NEXT A%
61310 LOCATE 9,53:PRINT STRING$(2,"²")
61320 LOCATE 8,2:PRINT STRING$(26,"²")
61330 LOCATE 8,53:PRINT STRING$(6,"²")
61340 N%=61
61350 FOR A%=7 TO 3 STEP -1
61360 LOCATE A%,2:PRINT STRING$(N%,"²")
61370 N%=N%+4
61380 NEXT A%
61390 LOCATE 2,2:PRINT STRING$(78,"²")
61391 COLOR 10
61392 FOR A%=4 TO 8
61393 LOCATE A%,6:PRINT STRING$(2,"²")
61394 NEXT A%
61395 LOCATE 5,4:PRINT STRING$(6,"²")
61400 COLOR 15:LOCATE 1,1,0:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
61410  FOR J%=2 TO 23:LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80:PRINT CHR$(186);:NEXT
61420  LOCATE 24,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
61430 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
