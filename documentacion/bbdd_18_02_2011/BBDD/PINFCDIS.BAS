10 '                       PINFCDIS.BAS
20 '                      --------------
30 '       CREAR DISCO DE FACTURACION PARA LA B. B. K.
40 '       -------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(80," ")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 FEFACT$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)
135 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,B,F,R
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),MESES$(12)
162 DATA 31,28,31,30,31,30,31,31,30,31,30,31
164 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
166 '
168 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SETIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
170 FOR I%=1 TO 12:READ MESES$(I%):NEXT I%
200 '
210 GOSUB 4200
220 GOTO 9000
230 '
240 '
4000 '************************************
4010 '***LECTURA FICHERO DE OPERACIONES***
4020 '************************************
4025 REG%=REG%+1
4027 IF REG%>ULTIMO% THEN FF$="FF":RETURN
4030 GET #4,REG%
4035 'IF FBANC<DESDE$ THEN 4025
4040 'IF FBANC>HASTA$ THEN FF$="FF":RETURN
4090 RETURN
4100 '
4200 '*********************************
4202 '***PRESENTAMOS EL MENU GENERAL***
4204 '*********************************
4206 TIT$="- SACAR DISCO DE FACTURACION PARA LA B.B.K. -"
4208 X%=80-LEN(TIT$):X%=INT(X%/2)
4210 GOSUB 8000
4212 COLOR 11:LOCATE 6,X%:PRINT TIT$
4214 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
4216 COLOR 15:LOCATE 9,21:PRINT "1.- ";:COLOR 10:PRINT "Disco para entregar en la B.B.K."
4218 COLOR 15:LOCATE 10,21:PRINT "2.- ";:COLOR 10:PRINT "Disco para pasar por CAJA-CASH."
4220 LOCATE 13,33:COLOR 11:PRINT " Opci¢n : < > "
4222 LOCATE 15,26:PRINT "Fecha del cargo : <          >"
4224 COLOR 12:LOCATE 17,32:PRINT " ESC ";:COLOR 15:PRINT " Finalizar"
4226 '
4228 '********************
4230 '***PEDIMOS OPCION***
4232 '********************
4234 F.C1%=13:C.C1%=44:L.E%=1:L.D%=0:NEG%=0:BO%=1:GOSUB 63000
4236 IF I.T%=0 THEN OPC$=D.E$:GOTO 4248
4238 IF I.T%=1 THEN 4234
4240 IF I.T%=2 THEN 4234
4242 IF I.T%=3 THEN 4234
4244 IF I.T%=4 THEN 9000
4246 GOTO 4234
4248 IF OPC$<"1" OR OPC$>"2" THEN 4234
4250 LOCATE 13,44:PRINT OPC$
4252 '
4550       '****************************
4552       '***ENTRADA DE FECHA CARGO***
4554       '****************************
4556       F.C1%=15:C.C1%=45:L.E%=8:BO%=10:GOSUB 63000
4558       IF I.T%=0 THEN NACI$=D.E$:GOTO 4576
4560       GOTO 4550
4574       '
4576       FECSUB$=NACI$:GOSUB 60000
4578       IF ERF%<>1 THEN 4586
4580       BEEP:GOSUB 8500:MEN$="Fecha de Cargo Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 15,45:PRINT SPC(10)
4582       '
4584       GOTO 4552
4586       LOCATE 15,45:PRINT FDMA$
4588       NACI$=LEFT$(NACI$,4)+RIGHT$(NACI$,2)
4590   '
4592   '
4594   GOSUB 8500
4596   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4598   COLOR 15:PRINT " ENTER ";
4600   COLOR 10:PRINT " Datos Err¢neos ";
4602   COLOR 15:PRINT " F1 "
4604   COLOR 10
4606   '
4608   A$=INPUT$(1)
4610   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4608
4612   '
4614   IF ASC(A$)=14 THEN 4200
4616   '
4700 CONCE$="CUOTA MENSUAL       "
4702 MES%=VAL(MID$(NACI$,3,2))
4704 CONCE$=CONCE$+MESES$(MES%)+" DEL "+RIGHT$(NACI$,2)
4706 IF LEN(CONCE$)<40 THEN CONCE$=CONCE$+LEFT$(BLANCO$,40-LEN(CONCE$))
4710 GOSUB 8000
4712 COLOR 11:LOCATE 8,X%:PRINT TIT$
4714 LOCATE 9,X%:PRINT STRING$(LEN(TIT$),205)
4720 LOCATE 13,13:COLOR 11:PRINT "Concepto : <                                        > "
4722 LOCATE 13,25:PRINT CONCE$
4724   GOSUB 8500
4725   COLOR 10:LOCATE 22,15:PRINT "Concepto Correcto ";
4726   COLOR 15:PRINT " ENTER ";
4727   COLOR 10:PRINT " Concepto Err¢neo ";
4728   COLOR 15:PRINT " F1 "
4729   COLOR 10
4730   '
4731   A$=INPUT$(1)
4732   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4730
4733   '
4734   IF ASC(A$)=13 THEN 4822
4735   '
4740 LOCATE 13,25:PRINT SPC(40)
4750       '*******************************
4752       '***ENTRADA DE NUEVO CONCEPTO***
4754       '*******************************
4756       F.C1%=13:C.C1%=25:L.E%=40:BO%=40:GOSUB 64000
4758       IF I.T%=0 THEN CONCE$=D.E$:GOTO 4776
4760       GOTO 4750
4774       '
4776       IF LEN(CONCE$)<40 THEN CONCE$=CONCE$+STRING$(40-LEN(CONCE$)," ")
4778       '
4786       LOCATE 13,25:PRINT CONCE$
4790       GOTO 4724
4822 '
4823 '
4824 '
4825 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4826 GOSUB 35000   'APERTURA FICHERO DE FACTURACION
4828 OPEN "O",#5,"RECIBOS.DAT"
4829 '
4830 NUREG%=0:TOIMP#=0
5050 '
5052 '
5060 R11="51":R12="80":R13="B48474647000"
5062 R14=LEFT$(FECHA$,2)+MID$(FECHA$,4,2)+RIGHT$(FECHA$,2)
5064 R15=LEFT$(BLANCO$,6)
5066 R16="ESCUELAS INTERNACIONALES E.I.D.E.  S.L. "
5068 R17=LEFT$(BLANCO$,20):R18="2095":R19="0554"
5070 R110=LEFT$(BLANCO$,66)
5072 SALIDA$=""
5074 SALIDA$=SALIDA$+R11+R12+R13+R14+R15
5076 SALIDA$=SALIDA$+R16+R17+R18+R19+R110
5078 PRINT #5,SALIDA$
5079 '
5080 R21="53":R22="80":R23="B48474647000"
5082 R24=LEFT$(FECHA$,2)+MID$(FECHA$,4,2)+RIGHT$(FECHA$,2)
5084 R25=NACI$
5086 R26="ESCUELAS INTERNACIONALES E.I.D.E.  S.L. "
5088 IF OPC$="1" THEN R27="20950554113830544211":R28=LEFT$(BLANCO$,8):R29="01":GOTO 5090
5089 R27="20950554173830544084":R28=LEFT$(BLANCO$,8):R29="01"
5090 R210=LEFT$(BLANCO$,64)
5092 SALIDA$=""
5094 SALIDA$=SALIDA$+R21+R22+R23+R24+R25
5096 SALIDA$=SALIDA$+R26+R27+R28+R29+R210
5098 PRINT #5,SALIDA$
5099 '
5110 FOR REG%=2 TO ULTIMO%
5112 GET #4,REG%
5114 IF FBANC="    " THEN 5172
5115 IF FBANC="9999" THEN 5174
5116 '
5120 R31="56":R32="80":R33="B48474647000"
5122 R34="0000090"+FALUM
5124 R35=LEFT$(FAOMB,40)
5126 R36=FBANC+FOFIC+FDIGI+FCUEN
5128 IMPO#=INT(CVS(FIMPO)*100):IMPO$=STR$(IMPO#):L%=LEN(IMPO$)-1
5130 R37=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5132 R38=NACI$
5134 R39="00000"+FALUM
5136 'CONCE$="CUOTA MENSUAL       "
5138 'MES%=VAL(MID$(NACI$,3,2))
5140 'CONCE$=CONCE$+MESES$(MES%)+" DEL "+RIGHT$(NACI$,2)
5142 'IF LEN(CONCE$)<40 THEN CONCE$=CONCE$+LEFT$(BLANCO$,40-LEN(CONCE$))
5144 R310=CONCE$
5146 R311=LEFT$(BLANCO$,8)
5148 SALIDA$=""
5150 SALIDA$=SALIDA$+R31+R32+R33+R34+R35
5160 SALIDA$=SALIDA$+R36+R37+R38+R39+R310+R311
5162 PRINT #5,SALIDA$
5164 '
5166 NUREG%=NUREG%+1
5168 TOIMP#=TOIMP#+IMPO#
5170 '
5172 NEXT REG%
5174 '
5176 IF NUREG%>0 THEN 5180
5178 GOSUB 8500:MEN$="No se ha efectuado la FACTURACION, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 9000
5180 '
5220 R41="58":R42="80":R43="B48474647000"
5222 R44=LEFT$(BLANCO$,72)
5226 R46=LEFT$(BLANCO$,6)
5228 IMPO$=STR$(TOIMP#):L%=LEN(IMPO$)-1
5230 R45=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5232 IMPO$=STR$(NUREG%):L%=LEN(IMPO$)-1
5234 R47=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5236 IMPO$=STR$(NUREG%+2):L%=LEN(IMPO$)-1
5238 R48=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5244 R49=LEFT$(BLANCO$,38)
5246 '
5248 SALIDA$=""
5250 SALIDA$=SALIDA$+R41+R42+R43+R44+R45
5260 SALIDA$=SALIDA$+R46+R47+R48+R49
5262 PRINT #5,SALIDA$
5264 '
5320 R41="59":R42="80":R43="B48474647000"
5322 R44=LEFT$(BLANCO$,52)
5326 R46=LEFT$(BLANCO$,16)
5328 '
5330 R45="0001"
5332 IMPO$=STR$(TOIMP#):L%=LEN(IMPO$)-1
5334 R47=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5336 IMPO$=STR$(NUREG%):L%=LEN(IMPO$)-1
5338 R49=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5344 R48=LEFT$(BLANCO$,6)
5346 IMPO$=STR$(NUREG%+4):L%=LEN(IMPO$)-1
5348 R410=LEFT$(CERO$,10-L%)+RIGHT$(IMPO$,L%)
5350 R411=LEFT$(BLANCO$,38)
5352 SALIDA$=""
5354 SALIDA$=SALIDA$+R41+R42+R43+R44+R45
5356 SALIDA$=SALIDA$+R46+R47+R48+R49+R410+R411
5358 PRINT #5,SALIDA$
5360 '
5362 CLOSE
5364 '
6000 GOSUB 8000
6002 TIT$="- CREAR DISCO DE FACTURACION PARA LA B.B.K. -"
6004 X%=80-LEN(TIT$):X%=INT(X%/2)
6006 COLOR 10:LOCATE 7,X%:PRINT TIT$
6008 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
6010 COLOR 12:LOCATE 10,6:PRINT "MUCHA ATENCION:"
6012 COLOR 15:LOCATE 12,15:PRINT "PARA FINALIZAR TIENES QUE PONER EN LA UNIDAD * A * UN DISKETT"
6014 LOCATE 14,6:PRINT "FORMATEADO PARA ENVIAR LA FACTURACION A LA B.B.K."
6016 LOCATE 18,21:PRINT "CUANDO ESTE PREPARADO TECLEA  ";:COLOR 12:PRINT "F1  F2  F3"
6018 A$=INPUT$(1)
6020 IF ASC(A$)<>14 THEN 6000
6022 A$=INPUT$(1)
6024 IF ASC(A$)<>15 THEN 6000
6026 A$=INPUT$(1)
6028 IF ASC(A$)<>16 THEN 6000
6030 '
6032 SHELL "INFDBBK.BAT"
6034 '
6036 GOTO 9000
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PINFDISF"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FVIDCLIE.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ACADEMIA INFORMATICA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '*************************************
35010 '***APERTURA FICHERO DE FACTURACION***
35020 '*************************************
35030 OPEN "R",#4,"FINFFACT.CLA",210
35040 FIELD #4,1 AS FSTATUS,2 AS FALTA,4 AS FFACT,12 AS FETIQ,191 AS FRES
35050 FIELD #4,1 AS FSTAT,5 AS FALUM,4 AS FIMPO,6 AS FFEEX,50 AS FAOMB,40 AS FDIRE,20 AS FPOBL,4 AS FBANC,30 AS FSUCU,10 AS FCUEN,4 AS FOFIC,2 AS FDIGI,34 AS FFIL
35060 GET #4,1
35070 IF FETIQ="FINFFACT.DAT" THEN 35090
35080 GOSUB 8500:MEN$="No tenemos fichero de Facturacion, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 9000
35090 ULTIMO%=CVI(FALTA)
35120 RETURN
35130 '
37000 '*******************************************************************
37010 '*                                                                 *
37020 '*  RUTINA PARA VERIFICAR NUMEROS DE CUENTA Y SUS DIGITOS          *
37030 '*                                                                 *
37040 '*  ENTRADA NUMERO CUENTA EN -- CUPRI$ --                          *
37050 '*                                                                 *
37060 '*  SALIDA BUENA   DI%     = 0                                     *
37070 '*                 CUPRIF$ = CUENTA FORMATEADA PARA FICHEROS       *
37080 '*                 CUPRIE$ = CUENTA EDITADA PARA LISTADOS          *
37090 '*                                                                 *
37100 '*  SALIDA MALA    DI% = 1                                         *
37110 '*                                                                 *
37120 '*******************************************************************
37130 'IF MID$(D.E$,1,3)="000" THEN CUPRIF$=" ":CUPRIE$="   ":D.E$=" ":DI%=0:NNU$=" ":RETURN
37140 DIG%=0:N1%=0:N2%=0:N3%=0:N4%=0:N5%=0:N6%=0:N7%=0:N8%=0:N9%=0:DI%=0:N10%=0
37150 IF MID$(CUPRI$,1,2) <> "20" THEN 37210
37155 IF LEN(CUPRI$)<7 THEN 37730
37160 LN%=LEN(CUPRI$)-5:LC%=11-LEN(CUPRI$)
37170 CUPRIF$=MID$(CUPRI$,1,5)+LEFT$(CERO$,LC%)+RIGHT$(CUPRI$,LN%)
37180 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,3)+"-"+MID$(CUPRIF$,6,5)+"-"+MID$(CUPRIF$,11,1)
37190 IF EDI$="1" THEN EDI$="0":RETURN
37200 D.E$="00"+MID$(CUPRIF$,3,9):GOTO 37380
37210 IF MID$(CUPRI$,1,2) <> "27" THEN 37280
37220 IF LEN(CUPRI$)<7 THEN 37730
37230 LN%=LEN(CUPRI$)-5:LC%=11-LEN(CUPRI$)
37240 CUPRIF$=MID$(CUPRI$,1,5)+LEFT$(CERO$,LC%)+RIGHT$(CUPRI$,LN%)
37250 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,3)+"-"+MID$(CUPRIF$,6,5)+"-"+MID$(CUPRIF$,11,1)
37260 IF EDI$="1" THEN EDI$="0":RETURN
37270 D.E$="00"+MID$(CUPRIF$,1,2)+LEFT$(CERO$,7-LN%)+RIGHT$(CUPRIF$,LN%):GOTO 37380
37280 ' IF MID$(CUPRI$,1,2) = "10" OR MID$(CUPRI$,1,2)="15" OR MID$(CUPRI$,1,2)="30" THEN 37300
37285  IF MID$(CUPRI$,1,2) = "10" OR MID$(CUPRI$,1,2)="15" THEN 37300
37290 GOTO 37730
37300 IF LEN(CUPRI$)<4 THEN 37730
37305 LN%=LEN(CUPRI$)-2:LC%=11-LEN(CUPRI$)
37310 CUPRIF$=MID$(CUPRI$,1,2)+LEFT$(CERO$,LC%)+MID$(CUPRI$,3,LN%)
37320 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,5)+"."+MID$(CUPRIF$,8,3)+"-"+MID$(CUPRIF$,11,1)
37330 IF EDI$="1" THEN EDI$="0":RETURN
37340 D.E$=MID$(CUPRIF$,1,2)+LEFT$(CERO$,LC%)+RIGHT$(CUPRIF$,LN%)
37350 ' IF MID$(D.E$,1,2) = "30" THEN 37370
37360 IF MID$(D.E$,4,2)<"11" THEN MID$(D.E$,1,2)="00"
37365 GOTO 37380
37370 ' IF MID$(D.E$,3,2)<"05" THEN MID$(D.E$,1,2)="00"
37380 '
37390 'IF MID$(D.E$,4,2)<"11" THEN 55925
37400 N1$=STR$(VAL(MID$(D.E$,10,1))*2)
37410 IF LEN(N1$)=2 THEN 37430
37420 N1%=N1%+VAL(MID$(N1$,3,1))
37430 N1%=N1%+VAL(MID$(N1$,2,1))
37440 N2%=VAL(MID$(D.E$,9,1))
37450 N3$=STR$(VAL(MID$(D.E$,8,1))*2)
37460 IF LEN(N3$)=2 THEN 37480
37470 N3%=N3%+VAL(MID$(N3$,3,1))
37480 N3%=N3%+VAL(MID$(N3$,2,1))
37490 N4%=VAL(MID$(D.E$,7,1))
37500 N5$=STR$(VAL(MID$(D.E$,6,1))*2)
37510 IF LEN(N5$)=2 THEN 37530
37520 N5%=N5%+VAL(MID$(N5$,3,1))
37530 N5%=N5%+VAL(MID$(N5$,2,1))
37540 N6%=VAL(MID$(D.E$,5,1))
37550 N7$=STR$(VAL(MID$(D.E$,4,1))*2)
37560 IF LEN(N7$)=2 THEN 37580
37570 N7%=N7%+VAL(MID$(N7$,3,1))
37580 N7%=N7%+VAL(MID$(N7$,2,1))
37590 N8%=VAL(MID$(D.E$,3,1))
37600 N9$=STR$(VAL(MID$(D.E$,2,1))*2)
37610 IF LEN(N9$)=2 THEN 37630
37620 N9%=N9%+VAL(MID$(N9$,3,1))
37630 N9%=N9%+VAL(MID$(N9$,2,1))
37640 N10%=VAL(MID$(D.E$,1,1))
37650 DIG%=N1%+N2%+N3%+N4%+N5%+N6%+N7%+N8%+N9%+N10%
37660 DIG%=100-DIG%
37670 D$=STR$(DIG%)
37680 'IF I.A%<> 1 THEN 37720
37690 'D.A$=LEFT$(D.E$,LEN(D.E$)-1)+RIGHT$(D$,1):D.E$=D.A$
37700 'D.A$=LEFT$(CUPRIF$,LEN(CUPRIF$)-1)+RIGHT$(D$,1):CUPRIF$=D.A$
37710 'D.A$=LEFT$(CUPRIE$,LEN(CUPRIE$)-1)+RIGHT$(D$,1):CUPRIE$=D.A$
37720 IF RIGHT$(D$,1)=RIGHT$(D.E$,1) THEN DI%=0:RETURN
37730 DI%=1:RETURN
37740 '
37750 ' ***** FINAL DE LA RUTINA DE NUMERO DE CUENTA *****
37760 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAAAA sobre FECSUB$
60040 '   -  Devuelve sobre FDMA$ la fecha en formato DD-MM-AAAA
60045 '   -  Devuelve sobre FAMD$ la fecha en formato AAAAMMDD
60050 '   -  Utiliza ERF% de Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,4):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 '
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1900  OR ANO% > 2999 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
