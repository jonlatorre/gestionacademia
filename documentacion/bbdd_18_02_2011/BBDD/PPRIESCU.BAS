10 '                         PACAESCU.BAS
20 '                       ---------------
30 '              GESTION FICHERO DE ESCUELA-IDIOMAS
40 '              ----------------------------------
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0"):GUION$=STRING$(15,"-")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"-"+MID$(FDIA$,3,2)+"-"+LEFT$(FDIA$,2)
94 '
96 DEFSTR B
98 '
100 '
110 '
120 '
130 '
140 '
306 '**********************
307 '***LISTADO POR NRO.***
308 '**********************
309 GOTO 500 'SI ESTA EL FICHERO ACTUALIZADO.
310 GOSUB 30010   'APERTURA FICHERO DE PLAN CONTABLE.
311 CLOSE
312 IF ULTDIR%=ACTDIR% THEN 500   'CONTINUAR CON EL PROGRAMA.
313 '
314 FICBAT$="ACAESCUE.BAT;"+DIRE$+";"+PROGRAMA$
315 '
316 CLS
317 SHELL FICBAT$
318 CLS
319 CLOSE
320 GOSUB 30010
321 GET #1,1
324 LSET BULTACT=MKI$(ULTIMO%)
325 PUT #1,1
326 CLOSE
327 '
490 '*********************************
500 '***PRESENTAMOS EL MENU GENERAL***
510 '*********************************
520 TIT$="- MENU FICHERO DE ESCUELA DE IDIOMAS (INGLES) -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 4,X%:PRINT TIT$
560 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
570 LOCATE 7,26:PRINT "1.- INTRODUCIR EXPEDIENTES."
580 LOCATE 8,26:PRINT "2.- BAJAS DE EXPEDIENTES."
590 LOCATE 9,26:PRINT "3.- MODIFICACION DE EXPEDIENTES."
600 LOCATE 10,26:PRINT "4.- CONSULTAS DE EXPEDIENTES."
610 LOCATE 11,26:PRINT "5.- LISTADO POR CURSOS PARA JUNIO."
620 LOCATE 12,26:PRINT "6.- LISTADO POR CURSOS PARA SEPTIEMBRE."
630 LOCATE 13,26:PRINT "7.- LISTADO POR CURSOS TOTAL EXPEDIENTES."
640 LOCATE 14,26:PRINT "8.- CALIFICACIONES DE CURSOS."
650 LOCATE 15,26:PRINT "9.- CAMBIO DE CURSO AUTOMATICO A LOS APTOS."
660 LOCATE 16,25:PRINT "10.- No se usa."
662 LOCATE 17,25:PRINT "11.- No se usa."
670 LOCATE 19,24:COLOR 12:PRINT " ESC ";
680 COLOR 15:PRINT " Finalizar ";
690 COLOR 11:PRINT " OPCION : <  > "
700 '
710 'PEDIMOS OPCION.
720 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
730 IF I.T%=4 THEN COLOR 15:GOTO 9000
740 IF I.T%<>0 THEN 720
750 COLOR 15
760 OPC%=VAL(D.E$)
770 IF OPC%<1 OR OPC%>11 THEN 670
780 ON OPC% GOTO 1010,1010,1010,1010,5070,5070,5070,900,910,500,500
790 '
800 '
810 '
900 RUN "PACANOTA"
910 RUN "PACAAPAU"
1000 '*****************************************************
1010 '***TRATAMIENTO COMUN DE ALTA,BAJA.MODI. Y CONSULTA***
1020 '*****************************************************
1030 IF OPC%=1 THEN TITA$="- INTRODUCIR EXPEDIENTES (INGLES) -":GOTO 1080
1040 IF OPC%=2 THEN TITA$="- BAJAS DE EXPEDIENTES (INGLES) -":GOTO 1080
1050 IF OPC%=3 THEN TITA$="- MODIFICACION DE EXPEDIENTES (INGLES) -":GOTO 1080
1060 TITA$="- CONSULTAS DE EXPEDIENTES (INGLES) -"
1070 '
1080 '
1090 '
1100 '
1110 '
1112 GOSUB 30010  'APERTURA FICHERO DE EXPEDIENTES
1114 '
1116 '
1118 GOSUB 31000  'PRESENTAR PANTALLA ENTRADA DE DATOS.
1119 GOSUB 7650
1120 '*****************************
1130 '***PEDIMOS NUMERO EXPEDIENTE*
1140 '*****************************
1150 F.C1%=7:C.C1%=37:L.E%=6:L.D%=0:NEG%=0:BO%=6:GOSUB 63000
1160 IF I.T%=0 THEN NBAN$=D.E$:GOTO 1240
1170 IF I.T%=1 THEN 1150
1180 '
1190 IF I.T%=2 THEN 1150
1210 IF I.T%=3 THEN 1150
1220 IF I.T%=4 THEN 6800   'FIN DE ENTRADA
1230 GOTO 1150
1240 'HACER LECTURA DEL FICHERO.
1250 LOCATE 7,37:PRINT NBAN$
1820 '
2260 BUSCA$=NBAN$
2270 GOSUB 30410    'LECTURA AL AZAR.
2271 COLOR 15
2272 '
2280 IF SW1%=1 AND OPC%=1 THEN 2830   'CONTINUAR ALTA Y CLAVE NO BUSCADA.
2290 IF SW1%=0 AND OPC%=1 THEN GOSUB 8500:MEN$="-YA EXISTE, PULSE UNA TECLA-":GOSUB 8500:A$=INPUT$(1):GOSUB 4610:GOSUB 7650:GOTO 1118
2292 IF SW1%=1 AND OPC%<>1 THEN GOSUB 8500:MEN$="-NO EXISTE, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 4610:GOSUB 7650:GOTO 1118
2294 IF BMARCA="B" AND OPC%=2 THEN GOSUB 8500:MEN$="-EXPEDIENTE DADO DE BAJA, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 4610:GOSUB 7650:GOTO 1118
2300 '
2310 'PRESENTAMOS EL REGISTRO EN PANTALLA.
2324 LOCATE 9,14:PRINT BNOM
2330 IF BCUR1="1" THEN COLOR 12:LOCATE 12,4:PRINT " Curso 1ง":COLOR 15
2340 IF BCUR2="2" THEN COLOR 12:LOCATE 12,20:PRINT " Curso 2ง":COLOR 15
2350 IF BCUR3="3" THEN COLOR 12:LOCATE 12,36:PRINT " Curso 3ง":COLOR 15
2360 IF BCUR4="4" THEN COLOR 12:LOCATE 12,52:PRINT " Curso 4ง":COLOR 15
2370 IF BCUR5="5" THEN COLOR 12:LOCATE 12,68:PRINT " Curso 5ง":COLOR 15
2380 IF OPC%<>4 THEN 2640
2390 IF BCUR1=" " THEN 2400
2391 IF BJUN1="A" THEN LOCATE 19,3:PRINT "APTO.":GOTO 2394
2392 IF BJUN1="N" THEN LOCATE 19,3:PRINT "NO A.":GOTO 2394
2393 LOCATE 19,3:PRINT "NO P."
2394 IF BSEP1="A" THEN LOCATE 19,10:PRINT "APTO.":GOTO 2400
2395 IF BSEP1="N" THEN LOCATE 19,10:PRINT "NO A.":GOTO 2400
2396 LOCATE 19,10:PRINT "NO P."
2400 IF BCUR2=" " THEN 2410
2401 IF BJUN2="A" THEN LOCATE 19,19:PRINT "APTO.":GOTO 2404
2402 IF BJUN2="N" THEN LOCATE 19,19:PRINT "NO A.":GOTO 2404
2403 LOCATE 19,19:PRINT "NO P."
2404 IF BSEP2="A" THEN LOCATE 19,26:PRINT "APTO.":GOTO 2410
2405 IF BSEP2="N" THEN LOCATE 19,26:PRINT "NO A.":GOTO 2410
2406 LOCATE 19,26:PRINT "NO P."
2410 IF BCUR3=" " THEN 2420
2411 IF BJUN3="A" THEN LOCATE 19,35:PRINT "APTO.":GOTO 2414
2412 IF BJUN3="N" THEN LOCATE 19,35:PRINT "NO A.":GOTO 2414
2413 LOCATE 19,35:PRINT "NO P."
2414 IF BSEP3="A" THEN LOCATE 19,42:PRINT "APTO.":GOTO 2420
2415 IF BSEP3="N" THEN LOCATE 19,42:PRINT "NO A.":GOTO 2420
2416 LOCATE 19,42:PRINT "NO P."
2420 IF BCUR4=" " THEN 2430
2421 IF BJUN4="A" THEN LOCATE 19,51:PRINT "APTO.":GOTO 2424
2422 IF BJUN4="N" THEN LOCATE 19,51:PRINT "NO A.":GOTO 2424
2423 LOCATE 19,51:PRINT "NO P."
2424 IF BSEP4="A" THEN LOCATE 19,58:PRINT "APTO.":GOTO 2430
2425 IF BSEP4="N" THEN LOCATE 19,58:PRINT "NO A.":GOTO 2430
2426 LOCATE 19,58:PRINT "NO P."
2430 IF BCUR5=" " THEN 2530
2431 IF BJUN5="A" THEN LOCATE 19,67:PRINT "APTO.":GOTO 2434
2432 IF BJUN5="N" THEN LOCATE 19,67:PRINT "NO A.":GOTO 2434
2433 LOCATE 19,67:PRINT "NO P."
2434 IF BSEP5="A" THEN LOCATE 19,74:PRINT "APTO.":GOTO 2530
2435 IF BSEP5="N" THEN LOCATE 19,74:PRINT "NO A.":GOTO 2530
2436 LOCATE 19,74:PRINT "NO P."
2530 '
2535 IF BMARCA="B" THEN COLOR 26:LOCATE 7,46:PRINT "DADO DE BAJA":COLOR 15
2640 IF OPC%=2 THEN 4400    'TRATAR BAJA.
2650 IF OPC%=4 THEN 4320    'TRATAR CONSULTA.
2660 '
2670 '
2830 '*****************************
2840 '***ENTRADA NOMBRE ALUMNO  ***
2850 '*****************************
2860 F.C1%=9:C.C1%=14:L.E%=60:BO%=60:GOSUB 64000
2870 IF I.T%=0 THEN DIREC$=D.E$:GOTO 2960
2880 IF I.T%=1 AND OPC%=3 THEN DIREC$=BNOM:GOTO 2960
2890 IF I.T%=1 THEN DIREC$=STRING$(60," "):GOTO 2960
2900 IF I.T%=2 AND OPC%=1 THEN 2960
2910 IF I.T%=2 THEN 2860
2920 IF I.T%=3 AND OPC%=1 THEN LOCATE 9,14:PRINT SPC(60):GOTO 1130
2940 IF I.T%=4 THEN 6800  'FIN DE DATOS.
2950 GOTO 2860
2960 LOCATE 9,14:PRINT DIREC$
2970 '
3000 GOSUB 50000
3590 '*************************************
3600 '***PEDIMOS CONFORMIDAD A LOS DATOS***
3610 '*************************************
3620 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" ,DATOS ERRONEOS -F1-":GOSUB 8500
3630 A$=INPUT$(1)
3640 IF ASC(A$)<13 OR ASC(A$)>14 THEN 3630
3650 IF ASC(A$)=14 THEN GOSUB 7650:GOTO 1118
3652 '
3654 IF OPC%=3 OR WSTATUS$="L" THEN GB1%=R.B%:GOTO 3700
3660 '
3670 '
3680 'GRABAR DATOS.
3690 ULTDIR%=ULTDIR%+1:GB1%=ULTDIR%
3700 GET #1,GB1%
3710 LSET BANCO=NBAN$
3712 LSET BNOM=DIREC$
3720 LSET BFIL001=STRING$(17," ")
3725 LSET BMARCA="A"
3730 LSET BSTATUS="O"
3731 LSET BCUR1=I.1$
3732 LSET BCUR2=I.2$
3733 LSET BCUR3=I.3$
3734 LSET BCUR4=I.4$
3735 LSET BCUR5=I.5$
3740 '
3750 PUT #1,GB1%
3760 '
4040 'REGRABAMOS REGISTRO DE DATOS.
4042 IF OPC%=3 THEN 4100
4050 GET #1,1
4060 ALTDIR%=ALTDIR%+1
4070 LSET BULTIMO=MKI$(ULTDIR%)
4090 PUT #1,1
4100 CLOSE
4110 '
4112 'GOSUB 4610    'BORRAR PANTALLA
4114 'GOSUB 30010   'APERTURA FICHERO
4116 '
4118 GOTO 1080     'OTRO.
4120 '
4310 '*****************************
4320 '***TRATAMIENTO DE CONSULTA***
4330 '*****************************
4340 GOSUB 8500:MEN$="PULSE UNA TECLA PARA NUEVA CONSULTA":GOSUB 8500
4350 A$=INPUT$(1)
4360 GOTO 4100
4362 '
4370 '
4380 '
4390 '*************************
4400 '***TRATAMIENTO DE BAJA***
4410 '*************************
4420 GOSUB 8500:MEN$="DAR DE BAJA "+ENTER$+" , NO DAR DE BAJA -F1-":GOSUB 8500
4430 A$=INPUT$(1)
4440 IF ASC(A$)<13 OR ASC(A$)>14 THEN 4430
4450 IF ASC(A$)=14 THEN 4100   'NO DAR DE BAJA
4460 'DAR DE BAJA.
4470 LSET BMARCA="B"
4480 PUT #1,R.B%
4490 'REGRABAMOS REGISTRO 1
4492 'GET #1,1
4500 'ALTDIR%=ALTDIR%-1
4510 'LSET BALTAS=MKI$(ALTDIR%)
4520 'PUT #1,1
4530 '
4540 GOTO 4100
4550 '
4600 '**********************************
4610 '***BORRADO DATOS DE LA PANTALLA***
4620 '**********************************
4630 LOCATE 7,37:PRINT SPC(6)
4640 'LOCATE 9,37:PRINT SPC(2)
4650 LOCATE 9,14:PRINT SPC(60)
4690 '
4700 RETURN
4710 '
5060 '**********************
5070 '***LISTADO POR NRO.***
5080 '**********************
5090 GOTO 5500 'MIRAMOS SI ESTA EL FICHERO ACTUALIZADO.
5100 GOSUB 30010   'APERTURA FICHERO DE PLAN CONTABLE.
5110 CLOSE
5120 IF ULTDIR%=ACTDIR% THEN 5210  'CONTINUAR CON EL PROGRAMA.
5130 '
5140 FICBAT$="ACAESCUE.BAT;"+DIRE$+";"+PROGRAMA$
5150 CLS
5155 SHELL FICBAT$
5160 CLS
5165 CLOSE
5170 GOSUB 30010
5175 GET #1,1
5180 LSET BULTACT=MKI$(ULTIMO%)
5190 PUT #1,1
5200 CLOSE
5210 '
5500 '***********************************************************
5502 '***LISTADO NUMERICO FICHERO GRUPOS Y SUBGRUPOS CONTABLES***
5504 '***********************************************************
5506 TIT$="- LISTADO FICHERO EXPEDIENTES ESCUELA IDIOMAS (INGLES) -"
5508 '
5510 '
5512 X%=80-LEN(TIT$)
5514 X%=INT(X%/2)
5516 GOSUB 8000
5518 LOCATE 6,X%:PRINT TIT$
5520 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
5522 LOCATE 11,12:PRINT "TECLLEA EL NUMERO DE CURSO A LISTAR ...... : < >"
5524 LOCATE 14,29:COLOR 0,7,0:PRINT " ESC ";:COLOR 7,0,0:PRINT " Terminar Listado"
5530 '
5540 'PEDIMOS HASTA.
5542 F.C1%=11:C.C1%=58:L.E%=1:L.D%=0:NEG%=0:BO%=1:GOSUB 63000
5544 IF I.T%=0 THEN HASTA$=D.E$:GOTO 5550
5546 IF I.T%=4 THEN CLOSE:GOTO 500
5548 GOTO 5552
5550 LOCATE 11,58:PRINT HASTA$
5552 IF HASTA$>"5" OR HASTA$<"1" THEN PLAY "A":GOTO 5530
5554 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1-":GOSUB 8500
5556 A$=INPUT$(1)
5558 IF ASC(A$)<13 OR ASC(A$)>14 THEN 5556
5560 IF ASC(A$)=14 THEN GOSUB 8500:GOTO 5530
5562 '
5590 GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
5592 WIDTH "LPT1:",140:OPEN "LPT1:" FOR OUTPUT AS #9
5594 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
5596 '
5600 GOSUB 30010  'ABRIR INDICE Y DATOS.
5602 ULTIMO%=ACTDIR%
5604 '
5614 '
5616 '
5618 REG%=1:PAG%=0:LIN%=90:FF$="":NCLI%=0
5620 '
5622 GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5624 '
5626 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 5502
5628 '
5630 WHILE FF$<>"FF"
5634   'LINEA DE DETALLE
5636   IF LIN%>65 THEN GOSUB 5746  'HACER CABECERAS.
5638   LPRINT TAB(7) LEFT$(BANCO,3)"."MID$(BANCO,4,3);
5640   LPRINT TAB(21) BNOM
5650   LPRINT TAB(6) STRING$(120,"-")
5666   LIN%=LIN%+2:NCLI%=NCLI%+1
5716   GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5718   '
5720  WEND
5722  LPRINT TAB(5):LPRINT USING "TOTAL ALUMNOS DEL CURSO : #####";NCLI%
5724  LIN%=88:NCLI%=0
5726  LPRINT CHR$(12)
5730 '
5732 'LPRINT
5734 'LPRINT TAB(5):LPRINT USING "TOTAL BANCOS LISTADOS : #####";NCLI%
5736 'LPRINT CHR$(12)
5738 CLOSE
5740 GOTO 5500
5742 '
5744 '***************************
5745 '***CABECERAS DEL LISTADO***
5746 '*** 'T CHR$(12):LIN%=0:RETURN
5747 IF OPC%=5 THEN TIT2$="  EXPEDIENTES ESCUELA IDIOMAS CONVOCATORIA JUNIO DEL "+RIGHT$(FECHA$,2)+"   ":GOTO 5751
5748 IF OPC%=6 THEN TIT2$="  EXPEDIENTES ESCUELA IDIOMAS CONVOCATORIA SETIEMBRE DEL "+RIGHT$(FECHA$,2)+"   ":GOTO 5751
5750 TIT2$="EXPEDIENTES ESCUELA IDIOMAS MATRICULADOS PARA EL AฅO "+RIGHT$(FECHA$,2)
5751 IF LIN%<>90 THEN LPRINT CHR$(12)
5752 PAG%=PAG%+1
5754 LPRINT TAB(6) EMPRESA$;
5756 LPRINT TAB(46) TIT2$;
5758 LPRINT TAB(113):LPRINT USING "PAGINA : ####";PAG%
5760 LPRINT TAB(6) STRING$(LEN(EMPRESA$),"=");
5762 LPRINT TAB(46) STRING$(LEN(TIT2$),"=");
5764 LPRINT TAB(113) STRING$(13,"=")
5765 '
5766 LPRINT
5768 LPRINT TAB(56) USING "CURSO ## DE INGLES";VAL(HASTA$)
5770 LPRINT TAB(56) STRING$(18,"=")
5772 LPRINT
5780 LPRINT TAB(6) "EXPEDIENTE";
5782 LPRINT TAB(33) "A P E L L I D O S   Y   N O M B R E";
5784 LPRINT TAB(94) "C A L I F I C A C I O N"
5790 '
5792 LPRINT TAB(6) STRING$(10,"=");
5794 LPRINT TAB(21) STRING$(60,"=");
5796 LPRINT TAB(86) STRING$(40,"=")
5802 'LPRINT
5804 '
5890 '
5892 LIN%=8
5893 RETURN
5894 '********************************
5896 '***LECTURA DE INDICES Y DATOS***
5898 '********************************
5900 REG%=REG%+1
5901 TECLA$=INKEY$:IF TECLA$="" THEN 5903
5902 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
5903 IF REG%>ULTIMO% THEN FF$="FF":RETURN
5904 GET #1,REG%
5916 IF BSTATUS<>"O" THEN 5900
5918 IF BMARCA="B" THEN 5900
5919 IF OPC%=7 THEN 6020
5920 IF BCUR1=HASTA$ AND BJUN1<>"A" THEN RETURN
5922 IF BCUR2=HASTA$ AND BJUN2<>"A" THEN RETURN
5924 IF BCUR3=HASTA$ AND BJUN3<>"A" THEN RETURN
5926 IF BCUR4=HASTA$ AND BJUN4<>"A" THEN RETURN
5928 IF BCUR5=HASTA$ AND BJUN5<>"A" THEN RETURN
5942 GOTO 5900
6000 '
6020 IF BCUR1=HASTA$ THEN RETURN
6022 IF BCUR2=HASTA$ THEN RETURN
6024 IF BCUR3=HASTA$ THEN RETURN
6026 IF BCUR4=HASTA$ THEN RETURN
6028 IF BCUR5=HASTA$ THEN RETURN
6042 GOTO 5900
6800 '**************************
6810 '***FIN ENTRADA DE DATOS***
6820 '**************************
6830 CLOSE:COLOR 15
6840 GOTO 500
6850 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Opciขn. ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atrs. ";
7732 COLOR 10:PRINT " F10 =>";
7734 COLOR 15:PRINT " Duplicar Campo. "
7740 '
7750 RETURN
7770 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaciขn : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 RUN "PACAINIC"
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '*****************************
30010 '***APERTURA FICHERO BANCOS***
30020 '*****************************
30022 FICHE1$="\"+DIRE$+"\FACAESCU.DAT"
30024 '
30030 'APERTURA FICHERO DE DATOS.
30040 OPEN "R",#1,FICHE1$,100
30042 'REGISTRO 1.
30050 FIELD #1,1 AS BSTATUS,12 AS BETIQUETA,2 AS BULTIMO,2 AS BULTACT,83 AS BFILLER
30052 'REGISTRO DE DATOS.
30054 FIELD #1,1 AS BSTATUS,6 AS BANCO,60 AS BNOM,1 AS BCUR1,1 AS BJUN1,1 AS BSEP1,1 AS BCUR2,1 AS BJUN2,1 AS BSEP2,1 AS BCUR3,1 AS BJUN3,1 AS BSEP3,1 AS BCUR4,1 AS BJUN4,1 AS BSEP4,1 AS BCUR5,1 AS BJUN5,1 AS BSEP5,1 AS BMARCA,17 AS BFIL001
30060 GET #1,1
30070 IF BETIQUETA="FACAESCU.DAT" THEN 30110  'CONTINUAR
30080 GOSUB 8500:MEN$="NO EXISTE FICHERO ESCUELA IDIOMAS, PULSE UNA TECLA":GOSUB 8500
30090 A$=INPUT$(1)
30100 GOTO 9000
30110 'CONTINUAR.
30120 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30122 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30140 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30142 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30150 '
30160 RETURN
30170 '
30400 '*************************
30410 '***LECTURA AZAR BANCOS***
30420 '*************************
30430 'El argumento de busqueda sobre 'BUSCA$'
30440 'El numero de registros sobre 'N.R%'
30450 R.B%=0           'Donde devuelve el nro. de registro.
30452 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30460 SW1%=0         'SW indicador de la busqueda.
30470 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30480 '
30490 R.I%=INT((N.R%/2)+.5)
30500 R.C%=R.I%
30510 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30630
30520 IF R.I%>N.R% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 30630
30530 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
30540 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 30800      'MIRAR STATUS
30550 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30560 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 30580
30570 R.I%=R.I%+R.C%
30580 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30510
30590 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30510
30600 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30510
30610 REG2%=R.I%:SW100%=0
30620 GOTO 30510
30630 'REGISTRO NO BUSCADO.
30640 XX%=0
30650 FOR A%=DES% TO HAS%
30660   GET #1,A%
30670   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
30680 NEXT A%
30690 IF XX%=1 THEN 30800
30700 '
30710 '
30720 'Registro NO BUSCADO
30730 XX%=0
30740 IF N.R%=0 THEN N.R%=1
30750 FOR JMG%=N.R% TO ULTIMO%
30760     GET #1,JMG%
30770     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
30780 NEXT JMG%
30790 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30800 'MIRAMOS STATUS.
30810 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30820 WSTATUS$=""
30830 RETURN
30840 '
31000 '********************************
31010 '***PRESENTAR ENTRADA DE DATOS***
31020 '********************************
31030 X%=80-LEN(TITA$):X%=X%/2
31040 GOSUB 8000
31042 LOCATE 4,X%:PRINT TITA$
31044 LOCATE 5,X%:PRINT STRING$(LEN(TITA$),205)
31046 LOCATE 7,18:PRINT "NRO. EXPEDIENTE : <" SPACE$(6) ">"
31048 'LOCATE 9,18:PRINT "CURSO           : <" SPACE$(2) ">"
31050 LOCATE 9,4:PRINT "ALUMNO : <" SPACE$(60) ">"
31060 '
31070 COLOR 10:LOCATE 11,3:PRINT CHR$(201)+STRING$(10,205)+CHR$(187);
31080  LOCATE 11,19:PRINT CHR$(201)+STRING$(10,205)+CHR$(187);
31090  LOCATE 11,35:PRINT CHR$(201)+STRING$(10,205)+CHR$(187);
31100  LOCATE 11,51:PRINT CHR$(201)+STRING$(10,205)+CHR$(187);
31110  LOCATE 11,67:PRINT CHR$(201)+STRING$(10,205)+CHR$(187)
31120  LOCATE 12,3:PRINT CHR$(186)+" Curso 1ง "+CHR$(186);
31130  LOCATE 12,19:PRINT CHR$(186)+" Curso 2ง "+CHR$(186);
31140  LOCATE 12,35:PRINT CHR$(186)+" Curso 3ง "+CHR$(186);
31150  LOCATE 12,51:PRINT CHR$(186)+" Curso 4ง "+CHR$(186);
31160  LOCATE 12,67:PRINT CHR$(186)+" Curso 5ง "+CHR$(186)
31170  LOCATE 13,3:PRINT CHR$(200)+STRING$(10,205)+CHR$(188);
31180  LOCATE 13,19:PRINT CHR$(200)+STRING$(10,205)+CHR$(188);
31190  LOCATE 13,35:PRINT CHR$(200)+STRING$(10,205)+CHR$(188);
31200  LOCATE 13,51:PRINT CHR$(200)+STRING$(10,205)+CHR$(188);
31210  LOCATE 13,67:PRINT CHR$(200)+STRING$(10,205)+CHR$(188)
31220  IF OPC%<>4 THEN RETURN
31230  LOCATE 15,3:PRINT "CALIFICACION";
31231  LOCATE 15,19:PRINT "CALIFICACION";
31232  LOCATE 15,35:PRINT "CALIFICACION";
31233  LOCATE 15,51:PRINT "CALIFICACION";
31234  LOCATE 15,67:PRINT "CALIFICACION"
31235  LOCATE 16,3:PRINT STRING$(12,205);
31236  LOCATE 16,19:PRINT STRING$(12,205);
31237  LOCATE 16,35:PRINT STRING$(12,205);
31238  LOCATE 16,51:PRINT STRING$(12,205);
31239  LOCATE 16,67:PRINT STRING$(12,205)
31240  LOCATE 17,3:PRINT "JUNIO  STBRE";
31241  LOCATE 17,19:PRINT "JUNIO  STBRE";
31242  LOCATE 17,35:PRINT "JUNIO  STBRE";
31243  LOCATE 17,51:PRINT "JUNIO  STBRE";
31244  LOCATE 17,67:PRINT "JUNIO  STBRE"
31250  LOCATE 18,3:PRINT "อออออ  อออออ";
31251  LOCATE 18,19:PRINT "อออออ  อออออ";
31252  LOCATE 18,35:PRINT "อออออ  อออออ";
31253  LOCATE 18,51:PRINT "อออออ  อออออ";
31254  LOCATE 18,67:PRINT "อออออ  อออออ"
32070 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0)
50110 I.1$=" ":COLOR 28:LOCATE 12,4:PRINT " Curso 1ง"
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 THEN I.1$="1":GOTO 50210
50122 COLOR 10:LOCATE 12,4:PRINT " Curso 1ง"
50123 IF R$=CHR$(0)+CHR$(75) THEN 50110
50124 IF R$="4" THEN 50110
50125 IF R$="6" THEN 50210
50130 IF R$=CHR$(0)+CHR$(77) THEN 50210
50140 GOTO 50110
50210 I.2$=" ":COLOR 28:LOCATE 12,20:PRINT " Curso 2ง"
50220 R$=INKEY$:IF R$="" THEN 50220
50221 IF ASC(R$)=13 THEN I.2$="2":GOTO 50310
50222 COLOR 10:LOCATE 12,20:PRINT " Curso 2ง"
50223 IF R$=CHR$(0)+CHR$(75) THEN 50110
50224 IF R$="4" THEN 50110
50225 IF R$="6" THEN 50310
50230 IF R$=CHR$(0)+CHR$(77) THEN 50310
50240 GOTO 50210
50310 I.3$=" ":COLOR 28:LOCATE 12,36:PRINT " Curso 3ง"
50320 R$=INKEY$:IF R$="" THEN 50320
50321 IF ASC(R$)=13 THEN I.3$="3":GOTO 50410
50322 COLOR 10:LOCATE 12,36:PRINT " Curso 3ง"
50323 IF R$=CHR$(0)+CHR$(75) THEN 50210
50324 IF R$="4" THEN 50210
50325 IF R$="6" THEN 50410
50330 IF R$=CHR$(0)+CHR$(77) THEN 50410
50340 GOTO 50310
50410 I.4$=" ":COLOR 28:LOCATE 12,52:PRINT " Curso 4ง"
50420 R$=INKEY$:IF R$="" THEN 50420
50421 IF ASC(R$)=13 THEN I.4$="4":GOTO 50510
50422 COLOR 10:LOCATE 12,52:PRINT " Curso 4ง"
50423 IF R$=CHR$(0)+CHR$(75) THEN 50310
50424 IF R$="4" THEN 50310
50425 IF R$="6" THEN 50510
50430 IF R$=CHR$(0)+CHR$(77) THEN 50510
50440 GOTO 50410
50510 I.5$=" ":COLOR 28:LOCATE 12,68:PRINT " Curso 5ง":COLOR 10
50520 R$=INKEY$:IF R$="" THEN 50520
50521 IF ASC(R$)=13 THEN I.5$="5":RETURN
50522 COLOR 10:LOCATE 12,68:PRINT " Curso 5ง"
50523 IF R$=CHR$(0)+CHR$(75) THEN 50410
50524 IF R$="4" THEN 50410
50540 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63152  IF ASC(D.R$)=14 THEN I.T%=5:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64122  IF ASC(D.R$)=14 THEN JMG%=L.E%:I.T%=5:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="ค" OR D.R$="ฅ" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
