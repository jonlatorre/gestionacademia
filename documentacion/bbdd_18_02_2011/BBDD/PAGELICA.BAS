10 '                       PAGELICA.BAS
20 '                      --------------
30 '             LISTADO DE DIRECCIONES DE CARTAS
40 '             --------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR C,G,P,F
150 OPTION BASE 1
200 DIM TABENT$(30)
520 '
570 '
590 '
600 GOTO 4490
3310 '
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 '
3532 '
3550 FOR A%=1 TO 11:LPRINT:NEXT A%
3560 LPRINT TAB(CC%) LEFT$(PNOMB,50)
3570 LPRINT TAB(CC%) LEFT$(PDIRE,50)
3575 NOM$=PPOBL:APE1$="  C.P.":APE2$=PCOPO:GOSUB 40000
3580 LPRINT TAB(CC%) LEFT$(NOMBRE$,35)
3590 LPRINT TAB(CC%) PPROV
3642 LIN%=1
3650 '
3660 RETURN
3670 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4495 '
4496 GOSUB 6000
4501 GOSUB 8000
4511 '
4512 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4513 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4514 COLOR 10
4520 LOCATE 6,17:PRINT "- LISTADO DE DIRECCIONES PARA CORRESPONDENCIA -":LOCATE 7,17:PRINT STRING$(47,205)
4550 COLOR 11:LOCATE 12,12:PRINT "DESDE EL NUMERO : <     >    HASTA EL NUMERO : <     >"
4552 COLOR 15:LOCATE 14,25:PRINT " F10";
4554 COLOR 11:PRINT " Volver al Men£ Anterior"
4560 COLOR 15
4590 'Pedimos numero de Orden DESDE.
4600 F.C1%=12:C.C1%=31:L.E%=5:BO%=5:ESC%=0:GOSUB 63000
4610 IF I.T%=0 THEN LOCATE 12,31:PRINT D.E$:GOTO 4640
4620 IF I.T%=2 THEN CLOSE:GOTO 4490
4630 GOTO 4600
4640 DESDE$=D.E$
4645 '
4650 '
4660 '
4670 '
4680 GOSUB 8500:MEN$="- HASTA=99999, hasta el Fin de Fichero -":GOSUB 8500
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=12:C.C1%=60:L.E%=5:BO%=5:ESC%=0:GOSUB 63000
4710 IF I.T%=0 THEN LOCATE 12,60:PRINT D.E$:GOTO 4740
4720 IF I.T%=2 THEN CLOSE:GOTO 4490
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 HASTA$=D.E$
4780 '
4790 'Comprobamos DESDE con HASTA.
4800 IF DESDE$>HASTA$ THEN BEEP:GOSUB 8500:MEN$="N£mero. -DESDE- mayor que -HASTA-, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4801 PAG%=0:LIN%=90
4802   GOSUB 8500
4803   COLOR 10:LOCATE 22,15:PRINT " Datos Correctos ";
4804   COLOR 15:PRINT " ENTER ";
4805   COLOR 10:PRINT " Datos Erroneos ";
4806   COLOR 15:PRINT " F1 "
4807   COLOR 10
4814 A$=INPUT$(1)
4816 IF ASC(A$)=14 THEN CLOSE:GOTO 4490
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4830 FOR REG%=2 TO ULTIMO%
4840   GET #1,REG%
4842   IF REG%<VAL(DESDE$) THEN 4900     'AL NEXT
4844   IF REG%>VAL(HASTA$) THEN REG%=ULTIMO%:GOTO 4900  'AL NEXT
4846   'N.L%=CVI(DNORDEN$)
4848   'GET #2,N.L%
4850   IF PSTAT<>"O" THEN 4900 'AL NEXT
4860   IF PMARC="L" THEN 4900
4890   GOSUB 3500   'LINEA DE DETALLE.
4895 LPRINT CHR$(12)
4900 NEXT REG%
4901 IF LIN%=90 THEN GOSUB 8500:MEN$="No disponemos de los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4490
4902 '
4904 CLOSE
4906 GOTO 4490
4908 '
4914 '
4970 '
6000 '*********************************
6005 '*** SELECIONAR OPCION LISTADO ***
6010 '*********************************
6015 '
6020 GOSUB 8000
6025 COLOR 11:LOCATE 5,21:PRINT "*** LISTADO DE DIRECCIONES EN CARTAS ***"
6030 LOCATE 6,21:PRINT STRING$(40,205)
6035 COLOR 15:LOCATE 8,28:PRINT "1.- ";
6040 COLOR 10:PRINT "Direcci¢n Izquierda."
6045 COLOR 15:LOCATE 10,28:PRINT "2.- ";
6047 COLOR 10:PRINT "Hojas de E.I.D.E."
6050 COLOR 15:LOCATE 12,28:PRINT "3.- ";
6055 COLOR 10:PRINT "Direcci¢n Derecha."
6060 COLOR 15:LOCATE 14,28:PRINT "4.- ";
6065 COLOR 10:PRINT "No usado.      "
6070 COLOR 15:LOCATE 16,28:PRINT "5.- ";
6075 COLOR 10:PRINT "Final de listados."
6080 COLOR 15:LOCATE 19,26:PRINT "Teclea la opcion deseada : < >"
6085 LOCATE 19,54:A$=INPUT$(1)
6090 IF A$<"1" OR A$>"5" THEN 6020
6095 LOCATE 19,54:PRINT A$
6100 IF A$="5" THEN 9000
6102 IF A$="4" THEN 6000
6105 IF A$="1" THEN CC%=26:RETURN
6110 IF A$="2" THEN CC%=40:RETURN
6115 IF A$="3" THEN CC%=75:RETURN
6120 RETURN
6125 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,37:PRINT "Aplicaci¢n : PROVEEDORES":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PAGEPROV"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9144 GOSUB 8500:MEN$="Fichero de PROVEEDORES inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30525 '
30590 ' Apertura del fichero de Proveedores FAGEPROV.DAT
30600 OPEN "R",#1,"FAGEPROV.ALF",400
30610 FIELD #1,1 AS PSTAT,1 AS PMARC,5 AS PCUEN,9 AS PNCIF,60 AS PNOMB,60 AS PDIRE,30 AS PPOBL,15 AS PPROV,5 AS PCOPO,10 AS PTELE,20 AS PNACI
30612 FIELD #1,216 AS TODO1$,10 AS PNFAX,30 AS PCON1,10 AS PTC01,30 AS PCON2,10 AS PTC02,30 AS PCON3,10 AS PTC03,8 AS PPAGO,8 AS PCOBR,38 AS PFILL
30613 '
30614 FIELD #1,1 AS PSTA1,1 AS PMAR1,2 AS PULTI,12 AS PFICH,250 AS CEST1$,134 AS CEST2$
30615  GET #1,1
30616  IF PFICH<>"FAGEPROV.DAT" THEN 9144
30617  ULTIMO%=CVI(PULTI)
30620 'LSET PSTA1="A":LSET PMAR1="A":LSET PULTI=MKI$(1)
30622 'LSET PFICH="FAGEPROV.DAT":LSET CEST1$="":LSET CEST2$=""
30630  '
30640 'PUT #1,1
30652 'CLOSE:STOP
30940 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** AGENCIA DE VIAJES EIDE ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
