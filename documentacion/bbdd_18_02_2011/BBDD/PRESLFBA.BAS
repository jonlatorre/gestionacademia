10 '                       PRESLFBA.BAS
20 '                      --------------
30 '           LISTADOS DE LA FACTURACION PARA BANCOS
40 '       ---------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 FEFACT$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)
135 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,B,F
150 OPTION BASE 1
200 DIM TABENT$(30)
210 GOSUB 4490
220 GOTO 9000
230 '
240 '
3490 '**********************
3500 '***LINEA DE DETALLE***
3510 '**********************
3520 '
3530 IF LIN%>65 THEN GOSUB 3740   '--HACER CABECERAS--
3532 GUAPRI#=REG%-1:N.D%=0:GOSUB 64770
3534 LPRINT TAB(8-L.C%) GUAPRI$;
3538 LPRINT TAB(13) LEFT$(FAOMB,40);
3540 LPRINT TAB(66) FOFIC " - " FDIGI;
3550 '
3552 LPRINT TAB(96) FCUEN;
3560 '
3570 '
3572 GUAPRI#=CVD(FIMPO):N.D%=2:GOSUB 64770
3580 LPRINT TAB(127-L.C%) GUAPRI$
3596 TOTGOP#=TOTGOP#+1
3630 TOTGEF#=TOTGEF#+GUAPRI#
3640 LIN%=LIN%+1
3650 '
3660 RETURN
3670 '
3730 '****************************
3740 '***CABECERAS DEL LISTADO ***
3750 '****************************
3752 IF LIN% = 90 THEN 3760
3754 LPRINT CHR$(12)
3760 PAG%=PAG%+1:LIN%=0
3770 LPRINT TAB(3) EMPRESA$;
3780 LPRINT TAB(52) "EXTRACTO DE RECIBOS PARA BANCOS DEL DIA " FECHA$;
3790 LPRINT TAB(116):LPRINT USING "PAGINA : ###";PAG%
3800 LPRINT TAB(3) STRING$(30,205);
3810 LPRINT TAB(52) STRING$(48,205);
3820 LPRINT TAB(116) STRING$(12,205)
3830 LPRINT
3832 LPRINT TAB(3) FSUCU:QUI%=0
3834 FOR A%=30 TO 2 STEP -1
3835 IF MID$(FSUCU,A%,1)=" " THEN QUI%=QUI%+1:GOTO 3837
3836 GOTO 3838
3837 NEXT A%
3838 LPRINT TAB(3) STRING$(30-QUI%,205)
3839 LPRINT
3840 '
3845 LPRINT TAB(3) "NUMERO";
3850 LPRINT TAB(16) "N O M B R E    Y    A P E L L I D O S";
3860 LPRINT TAB(66) "OFICINA - DIGITO";
3870 LPRINT TAB(94) "NUMERO CUENTA";
3872 LPRINT TAB(114) "I M P O R T E"
3900 '
3910 '
3911 LPRINT TAB(3) STRING$(6,205);
3912 LPRINT TAB(13) STRING$(40,205);
3914 LPRINT TAB(58) STRING$(30,205);
3916 LPRINT TAB(93) STRING$(15,205);
3918 LPRINT TAB(113) STRING$(15,205)
3930 LPRINT
3932 LIN%=9
3938 RETURN
3940 '
4000 '************************************
4010 '***LECTURA FICHERO DE OPERACIONES***
4020 '************************************
4025 REG%=REG%+1
4027 IF REG%>ULTIMO% THEN FF$="FF":RETURN
4030 GET #4,REG%
4035 IF FBANC<DESDE$ THEN 4025
4040 IF FBANC>HASTA$ THEN FF$="FF":RETURN
4090 RETURN
4100 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4500 '*************************
4502 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4503 LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4504 '
4510 GOSUB 8000
4512 TIT$="- LISTADO DE EXTRACTOS PARA BANCOS -"
4514 X%=80-LEN(TIT$):X%=INT(X%/2)
4520 COLOR 10:LOCATE 6,X%:PRINT TIT$
4530 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
4570 COLOR 11
4580 LOCATE 12,12:PRINT "DESDE EL BANCO N% : <    >     HASTA EL BANCO : <    >"
4581 LOCATE 14,24:PRINT "Para finalizar pulsar la tecla ";
4582 COLOR 15:PRINT "F7 "
4585 LOCATE 18,16:COLOR 12:PRINT "EL BANCO 9999 CORRESPONDE A PAGOS EN METALICO"
4590 COLOR 15
4792 'Pedimos fecha desde.
4794 F.C1%=12:C.C1%=33:L.E%=4:BO%=4:ESC%=1:GOSUB 63000
4796 IF I.T%=0 THEN LOCATE 12,33:PRINT D.E$:GOTO 4800
4797 IF I.T%=4 THEN 9000
4798 GOTO 4792
4800 '
4804 DESDE$=D.E$
4806 'Pedimos fecha hasta.
4807 F.C1%=12:C.C1%=61:L.E%=4:BO%=4:ESC%=1:GOSUB 63000
4808 IF I.T%=0 THEN LOCATE 12,61:PRINT D.E$:GOTO 4811
4809 IF I.T%=4 THEN 9000
4810 GOTO 4806
4811 '
4812 HASTA$=D.E$
4813 IF DESDE$>HASTA$ THEN 4570
4814 GOSUB 8500
4815 COLOR 11:LOCATE 22,15:PRINT "Bancos correctos, pulsar ";
4816 COLOR 15:PRINT " ENTER ";
4817 COLOR 11:PRINT " Err¢neos pulsar ";
4818 COLOR 15:PRINT " F1"
4819 A$=INPUT$(1)
4820 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4819
4821 IF ASC(A$)=14 THEN 4570
4822 '
4823 '
4824 '
4825 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4826 GOSUB 35000   'APERTURA FICHERO DE FACTURACION
4828 TOTGGOP#=0    'TOTAL GENERAL DE RECIBOS
4829 TOTGGRE#=0    'TOTAL GENERAL DE IMPORTES
5050 FF$="":LIN%=90:PAG%=0
5052 '
5060 GOSUB 4000      'hacer lectura fichero de facturaci¢n
5070 IF FF$="FF" THEN GOSUB 8500:MEN$="No tenemos los Datos Solicitados, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4570
5088  '
5090 WHILE FF$<>"FF"
5100  BAN$=FBANC
5142  TOTGOP#=0     'TOTAL RECIBOS BANCO
5144  TOTGEF#=0     'TOTAL IMPORTES POR BANCO
5149  WHILE FF$<>"FF" AND BAN$=FBANC
5150   '
5220     GOSUB 3500      'HACER LINEA DE DETALLE
5230     GOSUB 4000      'LECTURA DE OPERACIONES.
5240       '
5300  WEND
5400   'ROTURA POR BANCO
5410  'IF LIN%>50 THEN GOSUB 3740   'HACER CABECERAS.
5420   LPRINT
5430   LPRINT TAB(3) "Total Recibos ...... ";
5432   GUAPRI#=TOTGOP#:N.D%=0:GOSUB 64770:LPRINT TAB(33-L.C%) GUAPRI$
5440   LPRINT TAB(3) "Total Importe ...... ";
5450   GUAPRI#=TOTGEF#:N.D%=2:GOSUB 64770:LPRINT TAB(33-L.C%) GUAPRI$
5460   LIN%=80:PAG%=0
5522  TOTGGOP#=TOTGGOP#+TOTGOP#
5524  TOTGGRE#=TOTGGRE#+TOTGEF#
5530  '
5540 WEND
5542 'DAMOS EL TOTAL GENERAL.
5544 LPRINT CHR$(12)
5550 PAG%=PAG%+1
5551 LPRINT TAB(3) EMPRESA$;
5552 LPRINT TAB(52) "TOTAL DE RECIBOS LISTADOS BANCOS EL DIA " FECHA$;
5553 LPRINT TAB(116):LPRINT USING "PAGINA : ###";PAG%
5554 LPRINT TAB(3) STRING$(30,205);
5555 LPRINT TAB(52) STRING$(48,205);
5556 LPRINT TAB(116) STRING$(12,205)
5557 LPRINT
5560 '
5570  LPRINT
5580  LPRINT TAB(52) "Total Registros Listados ....... ";
5582  GUAPRI#=TOTGGOP#:N.D%=0:GOSUB 64770:LPRINT TAB(98-L.C%) GUAPRI$
5584  LPRINT
5590  LPRINT TAB(52) "Total Importes  Listados ....... ";
5600  GUAPRI#=TOTGGRE#:N.D%=2:GOSUB 64770:LPRINT TAB(98-L.C%) GUAPRI$
5650  LPRINT CHR$(12)
5660  CLOSE:GOSUB 8500:GOTO 4570
5670  '
5700  '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : HOSTELERIA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESDISF"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FVIDCLIE.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ESCUELA DE HOSTELERIA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '*************************************
35010 '***APERTURA FICHERO DE FACTURACION***
35020 '*************************************
35030 OPEN "R",#4,"FRESCOBR.CLA",210
35040 FIELD #4,1 AS FSTATUS,2 AS FOLTI,4 AS FFACT,12 AS FETIQ,191 AS FRES
35050 FIELD #4,1 AS FSTAT,5 AS FALUM,8 AS FIMPO,6 AS FFEEX,50 AS FAOMB,40 AS FDIRE,20 AS FPOBL,4 AS FBANC,30 AS FSUCU,10 AS FCUEN,4 AS FOFIC,2 AS FDIGI,30 AS FFIL
35060 GET #4,1
35070 IF FETIQ="FRESCOBR.DAT" THEN 35090
35080 GOSUB 8500:MEN$="No tenemos fichero de Facturacion, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 9000
35090 ULTIMO%=CVI(FOLTI)
35120 RETURN
35130 '
37000 '*******************************************************************
37010 '*                                                                 *
37020 '*  RUTINA PARA VERIFICAR NUMEROS DE CUENTA Y SUS DIGITOS          *
37030 '*                                                                 *
37040 '*  ENTRADA NUMERO CUENTA EN -- CUPRI$ --                          *
37050 '*                                                                 *
37060 '*  SALIDA BUENA   DI%     = 0                                     *
37070 '*                 CUPRIF$ = CUENTA FORMATEADA PARA FICHEROS       *
37080 '*                 CUPRIE$ = CUENTA EDITADA PARA LISTADOS          *
37090 '*                                                                 *
37100 '*  SALIDA MALA    DI% = 1                                         *
37110 '*                                                                 *
37120 '*******************************************************************
37130 'IF MID$(D.E$,1,3)="000" THEN CUPRIF$=" ":CUPRIE$="   ":D.E$=" ":DI%=0:NNU$=" ":RETURN
37140 DIG%=0:N1%=0:N2%=0:N3%=0:N4%=0:N5%=0:N6%=0:N7%=0:N8%=0:N9%=0:DI%=0:N10%=0
37150 IF MID$(CUPRI$,1,2) <> "20" THEN 37210
37155 IF LEN(CUPRI$)<7 THEN 37730
37160 LN%=LEN(CUPRI$)-5:LC%=11-LEN(CUPRI$)
37170 CUPRIF$=MID$(CUPRI$,1,5)+LEFT$(CERO$,LC%)+RIGHT$(CUPRI$,LN%)
37180 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,3)+"-"+MID$(CUPRIF$,6,5)+"-"+MID$(CUPRIF$,11,1)
37190 IF EDI$="1" THEN EDI$="0":RETURN
37200 D.E$="00"+MID$(CUPRIF$,3,9):GOTO 37380
37210 IF MID$(CUPRI$,1,2) <> "27" THEN 37280
37220 IF LEN(CUPRI$)<7 THEN 37730
37230 LN%=LEN(CUPRI$)-5:LC%=11-LEN(CUPRI$)
37240 CUPRIF$=MID$(CUPRI$,1,5)+LEFT$(CERO$,LC%)+RIGHT$(CUPRI$,LN%)
37250 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,3)+"-"+MID$(CUPRIF$,6,5)+"-"+MID$(CUPRIF$,11,1)
37260 IF EDI$="1" THEN EDI$="0":RETURN
37270 D.E$="00"+MID$(CUPRIF$,1,2)+LEFT$(CERO$,7-LN%)+RIGHT$(CUPRIF$,LN%):GOTO 37380
37280 ' IF MID$(CUPRI$,1,2) = "10" OR MID$(CUPRI$,1,2)="15" OR MID$(CUPRI$,1,2)="30" THEN 37300
37285  IF MID$(CUPRI$,1,2) = "10" OR MID$(CUPRI$,1,2)="15" THEN 37300
37290 GOTO 37730
37300 IF LEN(CUPRI$)<4 THEN 37730
37305 LN%=LEN(CUPRI$)-2:LC%=11-LEN(CUPRI$)
37310 CUPRIF$=MID$(CUPRI$,1,2)+LEFT$(CERO$,LC%)+MID$(CUPRI$,3,LN%)
37320 CUPRIE$=MID$(CUPRIF$,1,2)+"-"+MID$(CUPRIF$,3,5)+"."+MID$(CUPRIF$,8,3)+"-"+MID$(CUPRIF$,11,1)
37330 IF EDI$="1" THEN EDI$="0":RETURN
37340 D.E$=MID$(CUPRIF$,1,2)+LEFT$(CERO$,LC%)+RIGHT$(CUPRIF$,LN%)
37350 ' IF MID$(D.E$,1,2) = "30" THEN 37370
37360 IF MID$(D.E$,4,2)<"11" THEN MID$(D.E$,1,2)="00"
37365 GOTO 37380
37370 ' IF MID$(D.E$,3,2)<"05" THEN MID$(D.E$,1,2)="00"
37380 '
37390 'IF MID$(D.E$,4,2)<"11" THEN 55925
37400 N1$=STR$(VAL(MID$(D.E$,10,1))*2)
37410 IF LEN(N1$)=2 THEN 37430
37420 N1%=N1%+VAL(MID$(N1$,3,1))
37430 N1%=N1%+VAL(MID$(N1$,2,1))
37440 N2%=VAL(MID$(D.E$,9,1))
37450 N3$=STR$(VAL(MID$(D.E$,8,1))*2)
37460 IF LEN(N3$)=2 THEN 37480
37470 N3%=N3%+VAL(MID$(N3$,3,1))
37480 N3%=N3%+VAL(MID$(N3$,2,1))
37490 N4%=VAL(MID$(D.E$,7,1))
37500 N5$=STR$(VAL(MID$(D.E$,6,1))*2)
37510 IF LEN(N5$)=2 THEN 37530
37520 N5%=N5%+VAL(MID$(N5$,3,1))
37530 N5%=N5%+VAL(MID$(N5$,2,1))
37540 N6%=VAL(MID$(D.E$,5,1))
37550 N7$=STR$(VAL(MID$(D.E$,4,1))*2)
37560 IF LEN(N7$)=2 THEN 37580
37570 N7%=N7%+VAL(MID$(N7$,3,1))
37580 N7%=N7%+VAL(MID$(N7$,2,1))
37590 N8%=VAL(MID$(D.E$,3,1))
37600 N9$=STR$(VAL(MID$(D.E$,2,1))*2)
37610 IF LEN(N9$)=2 THEN 37630
37620 N9%=N9%+VAL(MID$(N9$,3,1))
37630 N9%=N9%+VAL(MID$(N9$,2,1))
37640 N10%=VAL(MID$(D.E$,1,1))
37650 DIG%=N1%+N2%+N3%+N4%+N5%+N6%+N7%+N8%+N9%+N10%
37660 DIG%=100-DIG%
37670 D$=STR$(DIG%)
37680 'IF I.A%<> 1 THEN 37720
37690 'D.A$=LEFT$(D.E$,LEN(D.E$)-1)+RIGHT$(D$,1):D.E$=D.A$
37700 'D.A$=LEFT$(CUPRIF$,LEN(CUPRIF$)-1)+RIGHT$(D$,1):CUPRIF$=D.A$
37710 'D.A$=LEFT$(CUPRIE$,LEN(CUPRIE$)-1)+RIGHT$(D$,1):CUPRIE$=D.A$
37720 IF RIGHT$(D$,1)=RIGHT$(D.E$,1) THEN DI%=0:RETURN
37730 DI%=1:RETURN
37740 '
37750 ' ***** FINAL DE LA RUTINA DE NUMERO DE CUENTA *****
37760 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63135 IF D.R$="," THEN D.R$=".":GOTO 63150
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 IF N.D%=2 THEN 64788
64786 N.D%=0
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 AND N.D%=2 THEN DEC$=",00":ENT$=GUAPRI$:GOTO 64812
64801 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:GOTO 64835
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
