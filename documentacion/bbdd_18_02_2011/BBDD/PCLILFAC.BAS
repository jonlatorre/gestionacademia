10 '                       PCLILFAC.BAS
20 '                      --------------
30 '  LISTADO NUMERICO DE CLIENTES DE NUEVA MARGEN A FACTURAR
40 '  -------------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR C,G,P,F
150 OPTION BASE 1
200 DIM TABENT$(30)
520 '
570 '
590 '
600 GOTO 4490
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 IF LIN%>57 THEN GOSUB 3740   '--HACER CABECERAS--
3532 '
3550 GUAPRI#=VAL(CCUEN):GOSUB 64770:LPRINT TAB(12-L.C%) GUAPRI$;
3560 LPRINT TAB(15) CNOMB;
3570 LPRINT TAB(76) LEFT$(CCUCO,4)"  "MID$(CCUCO,5,4)"  "MID$(CCUCO,9,2)"  "RIGHT$(CCUCO,10);
3580 GUAPRI#=CVD(CIMPO):GOSUB 64770
3590 LPRINT TAB(128-L.C%) GUAPRI$
3600 TOTAL#=TOTAL#+CVD(CIMPO)
3642 LPRINT:LIN%=LIN%+2
3650 '
3660 RETURN
3670 '
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 IF LIN%= 90 THEN 3760
3757 LPRINT CHR$(12)
3760 PAG%=PAG%+1:LIN%=0
3770 LPRINT TAB(7) EMPRESA$;
3780 LPRINT TAB(43) "RELACION DE CLIENTES A FACTURAR DEL PERIODICO AL " FECHA$ ;:GOTO 3790
3790 LPRINT TAB(111) USING "HOJA NUMERO : #,###";PAG%
3800 LPRINT TAB(7) STRING$(LEN(EMPRESA$),"-");
3805 LPRINT TAB(43) STRING$(57,"-");:GOTO 3820
3820 LPRINT TAB(111) STRING$(19,"-")
3840 LPRINT
3850 LPRINT TAB(7) "NUMERO";
3860 LPRINT TAB(31) "NOMBRE DEL CLIENTE";
3870 LPRINT TAB(87) " CUENTA  ";
3878 LPRINT TAB(120) "IMPORTE."
3900 '
3910 LPRINT TAB(7) STRING$(6,"-");
3912 LPRINT TAB(15) STRING$(50,"-");
3914 LPRINT TAB(67) STRING$(50,"-");
3918 LPRINT TAB(119) STRING$(10,"-")
3926 'LPRINT
3928 LIN%=LIN%+7
3930 RETURN
3940 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4495 '
4496 GOSUB 8000
4501 '
4502 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4511 '
4512 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4513 LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
4514 COLOR 10
4520 LOCATE 6,19:PRINT "- LISTADO NUMERICO DE CLIENTES A FACTURAR -":LOCATE 7,19:PRINT STRING$(43,205)
4801 PAG%=0:LIN%=90
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4830 FOR REG%=2 TO ULTCLI%
4840   GET #2,REG%
4842  'IF REG%<VAL(DESDE$) THEN 4900     'AL NEXT
4844  'IF REG%>VAL(HASTA$) THEN REG%=ULTCLI%:GOTO 4900  'AL NEXT
4846   '
4848   '
4850   IF CSTAT<>"O" THEN 4900 'AL NEXT
4860   IF CMARC="L" THEN 4900
4890   GOSUB 3500   'LINEA DE DETALLE.
4900 NEXT REG%
4905 LPRINT:LPRINT
4910 LPRINT TAB(108) "TOTAL : ";
4915 GUAPRI#=TOTAL#:GOSUB 64770
4920 LPRINT TAB(128-L.C%) GUAPRI$
4951 IF LIN%=90 THEN GOSUB 8500:MEN$="No disponemos de los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4952 LPRINT CHR$(12)
4954 CLOSE
4956 GOTO 9000
4958 '
4964 '
4970 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,35:PRINT "Aplicaci¢n : CLIENTES ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PCLICLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9144 GOSUB 8500:MEN$="Fichero de Clientes inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30525 '
30590 ' Apertura del fichero de Proveedores FAGEPROV.DAT
30600 OPEN "R",#2,"FCLICLIE.DAT",260
30610 FIELD #2,1 AS CSTAT,1 AS CMARC,5 AS CCUEN,9 AS CNCIF,50 AS CNOMB,50 AS CCONT,50 AS CDIRE,30 AS CPOBL,15 AS CPROV,5 AS CCOPO,10 AS CTELE,8 AS CIMPO,20 AS CCUCO,6 AS CFILL
30612 '
30613 FIELD #2,1 AS CSTA1,1 AS CMAR1,2 AS CULTI,12 AS CFICH,2 AS CULTA,242 AS REST1$
30614 '
30615  GET #2,1
30616  IF CFICH<>"FCLICLIE.DAT" THEN 9144
30617  ULTCLI%=CVI(CULTI)
30618  ULTACT%=CVI(CULTA):N.R%=ULTACT%
30621 'LSET CSTA1="A":LSET CMAR1="A":LSET CULTI=MKI$(1)
30622 'LSET CFICH="FCLICLIE.DAT":LSET CULTA=MKI$(1):LSET REST1$=""
30623 'PUT #2,1
30624 'CLOSE:STOP
30652 '
30940 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** NUEVA MARGEN  S.L. ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
