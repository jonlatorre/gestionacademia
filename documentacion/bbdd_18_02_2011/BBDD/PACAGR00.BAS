10 '                       PACAGR00.BAS
20 '                      --------------
30 '              GESTION DE GRUPOS DE ACADEMIAS
40 '              ------------------------------
50 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(50," ")
110 '
120 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR G,S,C
150 OPTION BASE 1
160 DIM TABRUT$(6),DIAS%(12),TABENT$(30)
170 '
180 DATA "     ALTAS DE GRUPOS     "
190 DATA "     BAJAS DE GRUPOS     "
200 DATA " MODIFICACION DE GRUPOS  "
210 DATA "   CONSULTA DE GRUPOS    "
220 DATA "   INFORMES DE GRUPOS    "
222 DATA "     FIN DE PROGRAMA     "
250 '
260 FOR I%=1 TO 6
270    READ TABRUT$(I%)
280 NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12
296    READ DIAS%(I%)
298 NEXT I%
300 '*********************************
302 '***PRESENTAMOS EL MENU GENERAL***
304 '*********************************
306 TIT$="- MENU DE GESTION DE GRUPOS -"
308 X%=80-LEN(TIT$):X%=INT(X%/2)
310 GOSUB 8000
312 LOCATE 4,X%:PRINT TIT$
314 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
316 LOCATE 7,26:PRINT "1.- ALTAS DE GRUPOS."
318 LOCATE 8,26:PRINT "2.- BAJAS DE GRUPOS."
320 LOCATE 9,26:PRINT "3.- MODIFICACION DE GRUPOS."
322 LOCATE 10,26:PRINT "4.- CONSULTA DE GRUPOS."
324 LOCATE 11,26:PRINT "5.- INFORMES DE GRUPOS."
326 LOCATE 12,26:PRINT "6.- No se usa."
328 LOCATE 13,26:PRINT "7.- No se usa."
330 LOCATE 14,26:PRINT "8.- No se usa."
332 LOCATE 15,26:PRINT "9.- No se usa."
334 LOCATE 16,25:PRINT "10.- No se usa."
336 LOCATE 17,25:PRINT "11.- No se usa."
338 LOCATE 19,24:COLOR 12:PRINT " ESC ";
340 COLOR 15:PRINT " Finalizar ";
342 COLOR 11:PRINT " OPCION : <  > "
344 '
346 'PEDIMOS OPCION.
348 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
350 IF I.T%=4 THEN COLOR 15:GOTO 9000
352 IF I.T%<>0 THEN 348
354 COLOR 15
356 I.A%=3
358 IF I.A%<1 OR I.A%>11 THEN 338
360 ON I.A% GOTO 510,2660,2660,2660,302,302,302,302,302,302,302
362 '
364 '
366 '
500 '*************************
510 '***TRATAMIENTO DE ALTA***
520 '*************************
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 GOSUB 32000        'Apertura Fichero de GRUPOS
550 TIT$=TABRUT$(I.A%)
560 GOSUB 8000
570 '
580 GOSUB 31000
590 '
600 '
610 '
615 GOSUB 7650
620 '
630       '*************************************
640       '***ENTRADA NUMERO DE GRUPO        ***
650       '*************************************
660       COLOR 15        'Presentamos Mensaje Teclas de Funcion.
670       GET #6,1:N.ORDEN%=ULTGRU%+1
680       GRUPO$=STR$(N.ORDEN%):LN%=LEN(GRUPO$)-1
690       GRUPO$=LEFT$(CERO$,3-LN%)+RIGHT$(GRUPO$,LN%)
700       IF N.ORDEN%<900 THEN 720
710       GOSUB 8500:MEN$="TENEMOS MUCHOS GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1)
720       LOCATE 6,15:PRINT USING"###";N.ORDEN%:LSET GGRUPO=GRUPO$
730       '
750 '
1440       '***********************
1450       '***ENTRADA  ALUMNOS  **
1460       '***********************
1470       F.C1%=8:C.C1%=36:L.E%=2:BO%=2:ESC%=1:GOSUB 63000
1480       IF I.T%=0 THEN ALUMN%=VAL(D.E$):LSET GALUMNOS=MKI$(ALUMN%):GOTO 1560
1490       IF I.T%=1 AND I.A%=1 THEN 1470
1500       IF I.T%=1 AND I.A%=3 THEN ALUMN%=CVI(GALUMNOS):GOTO 1560
1505       IF I.T%=3 AND I.A%=1 THEN LOCATE 8,13:PRINT SPC(6):GOTO 1450
1510       IF I.T%=3 THEN 1450
1520       IF I.T%=4 THEN 4000               'Fin Entrada.
1530       IF I.T%=2 AND I.A%=1 THEN LSET GALUMNOS=MKI$(ALUMN%):GOTO 1560
1540       IF I.T%=2 THEN 1470
1550       '
1560       LOCATE 8,36:PRINT USING"##";ALUMN%
1570       '
1600       '***********************
1602       '***ENTRADA  ALUMNOS  **
1604       '***********************
1606       F.C1%=10:C.C1%=36:L.E%=2:BO%=2:ESC%=1:GOSUB 63000
1608       IF I.T%=0 THEN ALTAS%=VAL(D.E$):LSET GALTAS=MKI$(ALTAS%):GOTO 1624
1610       IF I.T%=1 AND I.A%=1 THEN 1600
1612       IF I.T%=1 AND I.A%=3 THEN ALTAS%=CVI(GALTAS):GOTO 1624
1614       IF I.T%=3 AND I.A%=1 THEN LOCATE 8,36:PRINT SPC(2):GOTO 1470
1616       IF I.T%=3 THEN 1470
1618       IF I.T%=4 THEN 4000               'Fin Entrada.
1620       IF I.T%=2 AND I.A%=1 THEN LSET GALTAS=MKI$(ALTAS%):GOTO 1624
1622       IF I.T%=2 THEN 1600
1624       '
1626       LOCATE 10,36:PRINT USING"##";ALTAS%
1628       '
1630       '***********************
1632       '***ENTRADA PISO LU-A **
1634       '***********************
1636       F.C1%=12:C.C1%=13:L.E%=50:BO%=50:GOSUB 64000
1638       IF I.T%=0 THEN PILU1$=D.E$:GOTO 1654
1640       IF I.T%=1 AND I.A%=1 THEN 1630
1642       IF I.T%=1 AND I.A%=3 THEN PILU1$=MID$(GALUS,1,50):GOTO 1654
1644       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,36:PRINT SPC(2):GOTO 1606
1646       IF I.T%=3 THEN 1606
1648       IF I.T%=4 THEN 4000               'Fin Entrada.
1650       IF I.T%=2 AND I.A%=1 THEN 1654
1652       IF I.T%=2 THEN 1636
1654       IF LEN(PILU1$)<50 THEN PILU1$=PILU1$+LEFT$(BLANCO$,50-LEN(PILU1$))
1656       LOCATE 12,13:PRINT PILU1$
1658       '
1730       '***********************
1732       '***ENTRADA PISO LU-A **
1734       '***********************
1736       F.C1%=14:C.C1%=13:L.E%=50:BO%=50:GOSUB 64000
1738       IF I.T%=0 THEN PILU2$=D.E$:GOTO 1754
1740       IF I.T%=1 AND I.A%=1 THEN 1730
1742       IF I.T%=1 AND I.A%=3 THEN PILU2$=MID$(GALUS,51,50):GOTO 1754
1744       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,13:PRINT SPC(50):GOTO 1636
1746       IF I.T%=3 THEN 1636
1748       IF I.T%=4 THEN 4000               'Fin Entrada.
1750       IF I.T%=2 AND I.A%=1 THEN 1754
1752       IF I.T%=2 THEN 1736
1754       IF LEN(PILU2$)<50 THEN PILU2$=PILU2$+LEFT$(BLANCO$,50-LEN(PILU2$))
1756       LOCATE 14,13:PRINT PILU2$
1830       '***********************
1832       '***ENTRADA PISO LU-A **
1834       '***********************
1836       F.C1%=16:C.C1%=13:L.E%=50:BO%=50:GOSUB 64000
1838       IF I.T%=0 THEN PILU3$=D.E$:GOTO 1854
1840       IF I.T%=1 AND I.A%=1 THEN 1830
1842       IF I.T%=1 AND I.A%=3 THEN PILU3$=MID$(GALUS,101,50):GOTO 1854
1844       IF I.T%=3 AND I.A%=1 THEN LOCATE 14,13:PRINT SPC(50):GOTO 1736
1846       IF I.T%=3 THEN 1736
1848       IF I.T%=4 THEN 4000               'Fin Entrada.
1850       IF I.T%=2 AND I.A%=1 THEN 1854
1852       IF I.T%=2 THEN 1836
1854       IF LEN(PILU3$)<50 THEN PILU3$=PILU3$+LEFT$(BLANCO$,50-LEN(PILU3$))
1856       LOCATE 16,13:PRINT PILU3$
1930       '***********************
1932       '***ENTRADA PISO LU-A **
1934       '***********************
1936       F.C1%=18:C.C1%=13:L.E%=50:BO%=50:GOSUB 64000
1938       IF I.T%=0 THEN PILU4$=D.E$:GOTO 1954
1940       IF I.T%=1 AND I.A%=1 THEN 1930
1942       IF I.T%=1 AND I.A%=3 THEN PILU4$=MID$(GALUS,151,50):GOTO 1954
1944       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,13:PRINT SPC(50):GOTO 1836
1946       IF I.T%=3 THEN 1836
1948       IF I.T%=4 THEN 4000               'Fin Entrada.
1950       IF I.T%=2 AND I.A%=1 THEN 1954
1952       IF I.T%=2 THEN 1936
1954       IF LEN(PILU4$)<50 THEN PILU4$=PILU4$+LEFT$(BLANCO$,50-LEN(PILU4$))
1956       LOCATE 18,13:PRINT PILU4$
2500       '*************************************
2501       '***PEDIMOS CONFORMIDAD A LOS DATOS***
2502       '*************************************
2505       '
2510       GOSUB 8500:COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2515       COLOR 15:PRINT " ENTER ";
2520       COLOR 10:PRINT " Datos Erroneos ";
2525       COLOR 15:PRINT " F1 "
2530       COLOR 10
2535       A$=INPUT$(1)
2540       IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2535
2545       IF ASC(A$)=14 THEN CLOSE:GOTO 300
2550       'Grabar Datos.
2555       LSET GALUS=PILU1$+PILU2$+PILU3$+PILU4$
2585       PUT #6,N.ORDEN%
2590       CLOSE
2610 '
2618 '
2650 '************************************************
2660 '***TRATAMIENTO COMUN DE BAJA,MODI, Y CONSULTA***
2670 '************************************************
2690 TIT$=TABRUT$(I.A%)
2710 GOSUB 32000:GOSUB 30000
2720 GOSUB 8000:GOSUB 31000
2732 IF I.A%=3 THEN GOSUB 8500:GOSUB 7500
2734 IF I.A%=2 THEN GOSUB 8500:LOCATE 22,14:COLOR 10:PRINT " F7 => ";:COLOR 15:PRINT " F¡n de Bajas"
2736 IF I.A%=4 THEN GOSUB 8500:LOCATE 22,14:COLOR 10:PRINT " F7 => ";:COLOR 15:PRINT " F¡n de Consultas"
2740    '*****************************
2750    '***PEDIMOS NUMERO DE GRUPO **
2760    '*****************************
2770 COLOR 15
2780      F.C1%=6:C.C1%=15:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
2785      IF I.T%=0 THEN CLA$=D.E$:GOTO 2800            'Entrada OK
2790      IF I.T%=4 THEN 4000
2795      GOTO 2780                                     ' -ERROR-
2800      IF LEN(CLA$)<>3 THEN BEEP:GOTO 2780 'Se ha duplicado y no hay Cta.
2810      N.ORDEN%=VAL(CLA$)
2812      IF N.ORDEN%> ULTGRU% OR N.ORDEN%<2 THEN 2840
2814      GET #6,N.ORDEN%
2820      IF I.A%=4 AND CVI(GALTAS)>0 THEN  9500 ELSE 2850
2830      IF GSTATUS="O" THEN  2850
2835      GOSUB 8500:MEN$="ESTE GRUPO ESTA DADO DE BAJA, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 2732
2840      GOSUB 8500:MEN$="ESTE GRUPO NO EXISTE, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 2732
2850    ' LOCATE 4,77:PRINT GESCUE
2855      LOCATE 6,15:PRINT USING"###";VAL(CLA$)
2860    ' LOCATE 6,33:PRINT GCURSO
2870    'LOCATE 6,63:PRINT GTEXTO
2880    'GUAPRI#=CVS(GCUOTA):GOSUB 64770:LOCATE 8,18-L.C%:PRINT GUAPRI$
2910     LOCATE 8,36:PRINT USING"##";CVI(GALUMNOS)
2920     LOCATE 10,36:PRINT USING"##";CVI(GALTAS)
2930    'LOCATE 8,76:PRINT USING"##";CVI(GCONFIR)
2935     LOCATE 12,13:PRINT MID$(GALUS,1,50)
2937     LOCATE 14,13:PRINT MID$(GALUS,51,50)
2939     LOCATE 16,13:PRINT MID$(GALUS,101,50)
2941     LOCATE 18,13:PRINT MID$(GALUS,151,50)
2998     COLOR 15
3000     IF I.A%<>2 THEN 3190     'T. de Modi y consulta.
3010     '
3020     '*************************
3030     '***TRATAMIENTO DE BAJA***
3040     '*************************
3042     IF VAL(GALTAS)=0 THEN 3050
3044     GOSUB 8500:MEN$="NO PODEMOS DAR DE BAJA POR TENER ALUMNOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 2720
3050     GOSUB 8500
3060     COLOR 10:LOCATE 22,15:PRINT "Dar de baja ";
3070     COLOR 15:PRINT " ENTER ";
3080     COLOR 10:PRINT " No dar de baja ";
3090     COLOR 15:PRINT " F1 ":COLOR 10
3100     A$=INPUT$(1)
3110     IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3100
3120     IF ASC(A$)=14 THEN 2720
3140     LSET GSTATUS="L":PUT #6,N.ORDEN%
3150     GOSUB 8500:MEN$="  Grupo dado de baja ":RET%=1:GOSUB 8500
3160     '
3170     GOTO 2720    'A BUSCAR OTRO
3180     '
3190     IF I.A%=3 THEN 3300    'T. DE MODIFICACION.
3200     '
3210     '*****************************
3220     '***TRATAMIENTO DE CONSULTA***
3230     '*****************************
3240     GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3250     A$=INPUT$(1)
3260     GOTO 2720
3270     '
3280     '
3290     '*********************************
3300     '***TRATAMIENTO DE MODIFICACION***
3310     '*********************************
3330     GOSUB 7510   'Presentar mensajes teclas de Funcion Modi.
3340     GOTO 750     '===>PASAMOS POR EL  ALTA
3350     '
3360     '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE
4050 GOTO 302
4060 '
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7540 COLOR 10:PRINT " ENTER =>";
7550 COLOR 15:PRINT " Saltar ";
7560 COLOR 10:PRINT " F7 =>";
7570 COLOR 15:PRINT " Fin Modificaciones ";
7580 COLOR 10:PRINT " F8 =>";
7590 COLOR 15:PRINT " Campo Atr s"
7600 '
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7680 COLOR 10:PRINT " ENTER =>";
7690 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Altas ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup. ";
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15
8120 RETURN
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 CLS:RUN "PACAGRUP"
9050 GOTO 302
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA--":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9132 IF ERL=30220 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero de TARIFAS inesixtente":RET%=1:GOSUB 8500:GOTO 9000
9134 GOTO 9170
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9170 ON ERROR GOTO 0
9180 '
9500 '**************************************
9505 '***SACAR TODO EL GRUPO EN PANTALLA ***
9510 '**************************************
9515 'GET #2,N.ALU%
9520 'GET #6,VAL(GRUPO$)
9525 GOSUB 8000
9530 'NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9535 LOCATE 4,3:COLOR 11:PRINT "Grupo : ";:COLOR 15:PRINT GGRUPO
9540 LOCATE 4,16:COLOR 11:PRINT "Curso : ";:COLOR 15:PRINT LEFT$(GCURSO,10)
9545 LOCATE 4,35:COLOR 11:PRINT "Texto : ";:COLOR 15:PRINT LEFT$(GTEXTO,10)
9547 LOCATE 4,54:COLOR 11:PRINT "Altas : ";:COLOR 15:PRINT USING"###";CVI(GALTAS)
9548 LOCATE 4,67:COLOR 11:PRINT "Conf. : ";:COLOR 15:PRINT USING"###";CVI(GCONFIR)
9550 COLOR 10:LOCATE 5,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
9555 COLOR 11:LOCATE 6,3:PRINT "Dias ÍÍÍ>     LUNES       MARTES      MIERCOLES     JUEVES       VIERNES"
9560 LOCATE 7,3:PRINT "           -----------  -----------  -----------  -----------  -----------"
9565 LOCATE 8,3:PRINT "Semana  A":COLOR 15
9570 IF LEFT$(GLUNESA,1)=" " THEN 9580
9575 LOCATE 8,14:PRINT LEFT$(GLUNESA,2)","MID$(GLUNESA,3,2)" "MID$(GLUNESA,5,2)","RIGHT$(GLUNESA,2)
9580 IF LEFT$(GMARTEA,1)=" " THEN 9590
9585 LOCATE 8,27:PRINT LEFT$(GMARTEA,2)","MID$(GMARTEA,3,2)" "MID$(GMARTEA,5,2)","RIGHT$(GMARTEA,2)
9590 IF LEFT$(GMIERCA,1)=" " THEN 9600
9595 LOCATE 8,40:PRINT LEFT$(GMIERCA,2)","MID$(GMIERCA,3,2)" "MID$(GMIERCA,5,2)","RIGHT$(GMIERCA,2)
9600 IF LEFT$(GJUEVEA,1)=" " THEN 9610
9605 LOCATE 8,53:PRINT LEFT$(GJUEVEA,2)","MID$(GJUEVEA,3,2)" "MID$(GJUEVEA,5,2)","RIGHT$(GJUEVEA,2)
9610 IF LEFT$(GVIERNA,1)=" " THEN 9617
9615 LOCATE 8,66:PRINT LEFT$(GVIERNA,2)","MID$(GVIERNA,3,2)" "MID$(GVIERNA,5,2)","RIGHT$(GVIERNA,2)
9617 IF GSB="0" THEN 9675
9620 COLOR 11:LOCATE 9,3:PRINT "Semana  B":COLOR 15
9625 IF LEFT$(GLUNESB,1)=" " THEN 9635
9630 LOCATE 9,14:PRINT LEFT$(GLUNESB,2)","MID$(GLUNESB,3,2)" "MID$(GLUNESB,5,2)","RIGHT$(GLUNESB,2)
9635 IF LEFT$(GMARTEB,1)=" " THEN 9645
9640 LOCATE 9,27:PRINT LEFT$(GMARTEB,2)","MID$(GMARTEB,3,2)" "MID$(GMARTEB,5,2)","RIGHT$(GMARTEB,2)
9645 IF LEFT$(GMIERCB,1)=" " THEN 9655
9650 LOCATE 9,40:PRINT LEFT$(GMIERCB,2)","MID$(GMIERCB,3,2)" "MID$(GMIERCB,5,2)","RIGHT$(GMIERCB,2)
9655 IF LEFT$(GJUEVEB,1)=" " THEN 9665
9660 LOCATE 9,53:PRINT LEFT$(GJUEVEB,2)","MID$(GJUEVEB,3,2)" "MID$(GJUEVEB,5,2)","RIGHT$(GJUEVEB,2)
9665 IF LEFT$(GVIERNB,1)=" " THEN 9675
9670 LOCATE 9,66:PRINT LEFT$(GVIERNB,2)","MID$(GVIERNB,3,2)" "MID$(GVIERNB,5,2)","RIGHT$(GVIERNB,2)
9675 COLOR 10:LOCATE 10,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
9680 COLOR 11:LOCATE 11,26:PRINT "- ALUMNOS QUE TIENE EL GRUPO -"
9685 LOCATE 12,26:PRINT STRING$(30,205)
9690 FF%=12
9695 IF CVI(GALTAS)<9 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 9705
9700 TT%=40:MAS%=1
9705 FOR A%=1 TO TT% STEP 5
9710     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
9715     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
9720     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
9725     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
9730     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9735     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
9740 NEXT A%
9745 IF MAS%=0 THEN 10050
9750 FF%=12
9755 IF CVI(GALTAS)<17 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 9765
9760 TT%=80:MAS%=1
9765 FOR A%=41 TO TT% STEP 5
9770     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
9775     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
9780     LOCATE FF%,41:PRINT USING"#####";VAL(CCUENTA)
9785     LOCATE FF%,47:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
9790     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9795     LOCATE FF%,54:PRINT LEFT$(CUCUCU$,24)
9800 NEXT A%
9805 IF MAS%=0 THEN 10050
9810 GOSUB 8500:COLOR 11:LOCATE 22,13:PRINT "Hay mas Alumnos en el Grupo, para verlos pulsa ";:COLOR 15:PRINT " ENTER ";:COLOR 11:PRINT ", si n¢ ";:COLOR 15:PRINT " F1"
9815 A$=INPUT$(1)
9820 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 9815
9825 IF ASC(A$)=14 THEN 10050
9830 FOR FF%=13 TO 20
9835  LOCATE FF%,2:PRINT SPC(78)
9840 NEXT FF%
9845 FF%=12
9850 IF CVI(GALTAS)<25 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 9860
9855 TT%=120:MAS%=1
9860 FOR A%=81 TO TT% STEP 5
9865     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
9870     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
9875     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
9880     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
9885     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9890     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
9895 NEXT A%
9900 IF MAS%=0 THEN 10050
9905 FF%=12
9910 IF CVI(GALTAS)<33 THEN TT%=CVI(GALTAS)*5:MAS%=0:GOTO 9920
9915 TT%=160:MAS%=1
9920 FOR A%=121 TO TT% STEP 5
9925     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
9930     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
9935     LOCATE FF%,41:PRINT USING"#####";VAL(CCUENTA)
9940     LOCATE FF%,47:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
9945     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
9950     LOCATE FF%,54:PRINT LEFT$(CUCUCU$,24)
9955 NEXT A%
9960 IF MAS%=0 THEN 10050
9965 GOSUB 8500:COLOR 11:LOCATE 22,13:PRINT "Hay mas Alumnos en el Grupo, para verlos pulsa ";:COLOR 15:PRINT " ENTER ";:COLOR 11:PRINT ", si n¢ ";:COLOR 15:PRINT " F1"
9970 A$=INPUT$(1)
9975 IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 9970
9980 IF ASC(A$)=14 THEN 10050
9985 FOR FF%=13 TO 20
9990  LOCATE FF%,2:PRINT SPC(78)
9995 NEXT FF%
10000 FF%=12
10005 TT%=200:MAS%=1
10010 FOR A%=161 TO TT% STEP 5
10015     FF%=FF%+1:NUM%=VAL(MID$(GALUS,A%,5)):GET #2,NUM%
10020     IF CCUENTA=ALUCON$ THEN COLOR 28 ELSE COLOR 15
10025     LOCATE FF%,3:PRINT USING"#####";VAL(CCUENTA)
10030     LOCATE FF%,9:PRINT MID$(CFENA,3,2) LEFT$(CFENA,2)"-"CCONFI
10035     NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
10040     LOCATE FF%,16:PRINT LEFT$(CUCUCU$,24)
10045 NEXT A%
10050 GOTO 3220
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 'OPEN "R",#1,FALUDIR$,65
30054 'FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 'GET #1,1
30058 'ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,"FACAALUM.DAT",270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,21 AS CKAKA,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 4,X%:PRINT TIT$
31042 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 11:LOCATE 6,3:PRINT "Grupo N§ : <   >"
31052 LOCATE 8,3:PRINT "M ximo de Alumnos por Grupo.. : <  >"
31056 LOCATE 10,3:PRINT "N£mero de Alumnos de Alta ... : <  >"
31058 LOCATE 12,3:PRINT "ALUMNOS  <                                                  >"
31060 LOCATE 14,3:PRINT "         <                                                  >"
31062 LOCATE 16,3:PRINT "         <                                                  >"
31064 LOCATE 18,3:PRINT "         <                                                  >"
31160 COLOR 15:RETURN
31170 '
32000 '*******************************
32010 '***APERTURA FICHERO GRUPOS  ***
32020 '*******************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FACAGRUP.DAT",380
32060 FIELD #6,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32070 FIELD #6,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32080 FIELD #6,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB
32085 FIELD #6,150 AS GPRI2,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,1 AS GSB,1 AS GESCUE,14 AS GLIBR,200 AS GALUS
32090 FIELD #6,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32100 FIELD #6,180 AS GTODO,200 AS GALUS
32110 GET #6,1
32120 IF GETIQUETA="FACAGRUP.DAT" THEN 32140
32130 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140 ULTGRU%=CVI(GULTIMO)
32141 'GET #6,128:PRINT GALUS
32142 'LSET GALUS="01841019640228802309023260250102525027670282802941029620303903104                                                                                           "
32143 'PUT #6,128
32144 'CLOSE:STOP
32150 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 15:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 10:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 10:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 10
50300 RETURN
50340 '
50510 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF ASC(D.R$)=130 OR (ASC(D.R$)>159 AND ASC(D.R$)<166) THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
