10 '                       PTURTRI2.BAS
20 '                      --------------
30 '   CALIFICACIONES TURISMO Y HOSTELERIA CONVOCATORIA DE JUNIO
40 '   ---------------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(50,".")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,A,N,G,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),JAL%(12),FAL%(12),PERI$(12)
170 '
172 '
174 '
176 '
178 '
200 DATA "Enero","Febrero","Marzo","Abril","Mayo","Junio","Julio","Agosto","Setiembre","Octubre","Noviembre","Diciembre"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 'GOTO 500
300 '*********************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
310 '*********************************
315 TIT$="- CALIFICACIONES DE TURISMO Y SECRETARIADO CONVOCATORIA DE JUNIO -"
320 X%=80-LEN(TIT$):X%=INT(X%/2)
325 GOSUB 8000
330 COLOR 11:LOCATE 4,X%:PRINT TIT$
335 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
340 COLOR 15:LOCATE 7,26:PRINT "1.- ";:COLOR 10:PRINT "INTRODUCIR CALIFICACIONES."
345 COLOR 15:LOCATE 9,26:PRINT "2.- ";:COLOR 10:PRINT "MODIFICAR CALIFICACIONES."
350 COLOR 15:LOCATE 11,26:PRINT "3.- ";:COLOR 10:PRINT "CONSULTAR CALIFICACIONES."
370 COLOR 15:LOCATE 13,26:PRINT "4.- ";:COLOR 10:PRINT "LISTAR CALIFICACIONES."
380 COLOR 15:LOCATE 15,26:PRINT "5.- ";:COLOR 10:PRINT "No se usa."
390 COLOR 15:LOCATE 17,26:PRINT "6.- ";:COLOR 10:PRINT "FINAL DE CALIFICACIONES."
395 LOCATE 19,25:COLOR 11:PRINT "Teclear N즡ero de Opci줻 <  >"
400 COLOR 15
405 '
410 '
415 'PEDIMOS OPCION.
420 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
425 'IF I.T%=4 THEN COLOR 15:GOTO 9000
430 IF I.T%<>0 THEN 420
435 COLOR 15
440 I.A%=VAL(D.E$)
445 IF I.A%<1 OR I.A%>6 THEN 395
450 ON I.A% GOTO 500,500,2500,4000,300,9000
455 '
460 '
500 '
600 '
602 '
604 '
700 IF I.A%=1 THEN TIT$="- INTRODUCIR CALIFICACIONES DE JUNIO -":GOTO 704
702  TIT$="- MODIFICAR CALIFICACIONES DE JUNIO -"
704 GOSUB 8000
706 COLOR 11:LOCATE 5,X%:PRINT TIT$
708 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
710 GOSUB 8500:MEN$="Espere por favor.......":GOSUB 8500
712 '
718 FALUDIR$="FTURALUM.ALF":FALUDAT$="FTURALUM.DAT"
719 FAUSDIR$="FTURAUNO.DIR":FAUSDAT$="FTURAUNO.DAT"
720 GOSUB 30000:GOSUB 32000:GOSUB 34000
724 GOSUB 31000
728 GOSUB 7650
730       '************************
735       '***ENTRADA ASIGNATURA***
740       '************************
745       F.C1%=8:C.C1%=22:L.E%=3:BO%=3:GOSUB 63000
750       IF I.T%=0 THEN ASIG$=D.E$:GOTO 785
755       IF I.T%=5 THEN 745
760       IF I.T%=1 THEN 730
765       IF I.T%=3 THEN LOCATE 12,20:PRINT SPC(3):LOCATE 12,28:PRINT SPC(50):GOTO 730
770       IF I.T%=4 THEN CLOSE:GOTO 300                'Fin Entrada.
775       IF I.T%=2 THEN 785
780       GOTO 745
785      '
790      IF VAL(ASIG$)=<ULTGRU% AND VAL(ASIG$)>1 THEN 800
795      GOSUB 8500:MEN$="Esta Asignatura no existe, pulsar una tecla ":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 745
800      GET #6,VAL(ASIG$)
805      '
810      LOCATE 8,22:PRINT ASIG$:LOCATE 8,30:PRINT GTEXTO
815       '**********************
820       '***ENTRADA GRUPO   ***
825       '**********************
830       F.C1%=10:C.C1%=22:L.E%=1:BO%=1:GOSUB 64000
835       IF I.T%=0 THEN GRUPO$=D.E$:GOTO 870
840       IF I.T%=5 THEN 830
845       IF I.T%=1 THEN 815
850       IF I.T%=3 THEN LOCATE 8,22:PRINT SPC(3):LOCATE 8,30:PRINT SPC(50):GOTO 730
855      'IF I.T%=4 THEN 730                'Fin Entrada.
860       IF I.T%=2 THEN 870
865       GOTO 830
870      '
875       LOCATE 10,22:PRINT GRUPO$
880   GOSUB 8500
885   COLOR 10:LOCATE 22,15:PRINT "Asignatura y Grupo Correctos ";
890   COLOR 15:PRINT " ENTER ";
895   COLOR 10:PRINT " Err줻eos ";
900   COLOR 15:PRINT " F1 "
905   COLOR 10
910   '
915   A$=INPUT$(1)
920   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 915
925   '
930   IF ASC(A$)=13 THEN 940
931   LOCATE 8,22:PRINT SPC(3)
932   LOCATE 8,30:PRINT SPC(50)
933   LOCATE 10,22:PRINT SPC(1)
935   GOTO 728
940   '
945   GOSUB 7450
950       '*******************************
955       '***ENTRADA NUMERO DE ALUMNO ***
960       '*******************************
965       COLOR 15
970       F.C1%=14:C.C1%=22:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
975       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 995 'Entrada OK
980       IF I.T%=4 THEN 724
985       GOTO 955
990       '
995       '
1000       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 14,27-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
1005       REG%=VAL(CUENTA$)
1010       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N즡ero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOSUB 7450:GOTO 960
1015       GET #2,REG%
1020       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOSUB 7450:GOTO 960
1025       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
1030       LOCATE 14,32:PRINT CUCUCU$
1035       BURRA$=ASIG$+GRUPO$+CUENTA$:GOSUB 34500
1037       IF SWESC%=0 THEN 1040
1038       GOSUB 8500:MEN$="Este Alumno no esta en esta asignatura, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2028
1040       REG%=CVI(AULTI)
1042       GET #4,REG%
1044       IF I.A%=2 THEN 1090
1046       IF CVI(NNO01)=0 AND CVI(NSO01)=0 THEN 1100
1048       GOSUB 8500:MEN$="Este Alumno solamente se puede modificar, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2028
1090       '
1091       GUAPRI#=CVI(NNO01):GOSUB 64770
1092       LOCATE 16,24-L.C%:PRINT GUAPRI$
1093     ' GUAPRI#=CVI(NSO01):GOSUB 64770
1094     ' LOCATE 16,47-L.C%:PRINT GUAPRI$
1095     ' LOCATE 18,22:PRINT NRE01
1100       '
1105       '***************************
1110       '***ENTRADA CALIFICACION ***
1115       '***************************
1120       F.C1%=16:C.C1%=22:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1125       IF I.T%=0 THEN CALI%=VAL(D.E$):GOTO 1175
1130       IF I.T%=5 THEN 1120
1135       IF I.T%=1 AND I.A%=1 THEN CALI%=0:GOTO 1175
1140       IF I.T%=1 AND I.A%=2 THEN CALI%=CVI(NNO02):GOTO 1175
1145       IF I.T%=3 AND I.A%=1 THEN LOCATE 14,22:PRINT SPC(6):LOCATE 14,32:PRINT SPC(48):GOTO 945
1150       IF I.T%=3 THEN 945
1155       IF I.T%=4 THEN 724                'Fin Entrada.
1160       IF I.T%=2 AND I.A%=1 THEN 1175
1165       IF I.T%=2 THEN 1120
1170       '
1175       GUAPRI#=CALI%:GOSUB 64770
1180       LOCATE 16,22:PRINT SPC(3):LOCATE 16,24-L.C%:PRINT GUAPRI$
1185       '
1190       '***************************
1195       '***ENTRADA PUNTOS TOTAL ***
1200 GOTO 2000
1205       F.C1%=16:C.C1%=45:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1210       IF I.T%=0 THEN SOBRE%=VAL(D.E$):GOTO 1260
1215       IF I.T%=5 THEN 1205
1220       IF I.T%=1 AND I.A%=1 THEN SOBRE%=0:GOTO 1260
1225       IF I.T%=1 AND I.A%=2 THEN SOBRE%=CVI(NSO01):GOTO 1260
1230       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,22:PRINT SPC(3):GOTO 1110
1235       IF I.T%=3 THEN 1110
1240       IF I.T%=4 THEN 724                'Fin Entrada.
1245       IF I.T%=2 AND I.A%=1 THEN 1260
1250       IF I.T%=2 THEN 1205
1255       '
1260       GUAPRI#=SOBRE%:GOSUB 64770
1262       LOCATE 16,45:PRINT SPC(3):LOCATE 16,47-L.C%:PRINT GUAPRI$
1265       '
1270       '**********************
1275       '***ENTRADA TELEFONO***
1280       '**********************
1285       F.C1%=18:C.C1%=22:L.E%=1:BO%=1:GOSUB 64000
1290       IF I.T%=0 THEN RECUPE$=D.E$:GOTO 1340
1295       IF I.T%=5 THEN 1285
1300       IF I.T%=1 AND I.A%=1 THEN RECUPE$=" ":GOTO 1340
1305       IF I.T%=1 AND I.A%=2 THEN RECUPE$=NRE01:GOTO 1340
1310       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,45:PRINT SPC(3):GOTO 1205
1315       IF I.T%=3 THEN 1205
1320       IF I.T%=4 THEN 724                'Fin Entrada.
1325       IF I.T%=2 AND I.A%=1 THEN 1340
1330       IF I.T%=2 THEN 1275
1335       '
1340       IF RECUPE$=" " OR RECUPE$="S" OR RECUPE$="N" THEN 1345
1342       GOTO 1280
1345       LOCATE 18,22:PRINT RECUPE$
1350       '
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2042
2028   LOCATE 14,22:PRINT SPC(6):LOCATE 14,32:PRINT SPC(48)
2030   LOCATE 16,22:PRINT SPC(3):LOCATE 16,45:PRINT SPC(3)
2032   LOCATE 18,22:PRINT SPC(1)
2034   GOTO 940
2042   '
2054   BURRA$=ASIG$+GRUPO$+CUENTA$:GOSUB 34500
2056   IF SWESC%=0 THEN 2058
2057   GOSUB 8500:MEN$="Este Alumno no esta en esta asignatura, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2028
2058   REG%=CVI(AULTI)
2059   GET #4,REG%
2098   '
2260   '
2270   LSET NNO02=MKI$(CALI%)
2275  'LSET NSO01=MKI$(SOBRE%)
2280  'LSET NRE01=RECUPE$
2300   PUT #4,REG%
2302   '
2306   GOTO 2028
2500 '
2505 TIT$="- CONSULTA DE CALIFICACIONES TURISMO Y SECRETARIADO -"
2510 '
2511 GOSUB 8000
2512 COLOR 11
2514 LOCATE 4,15:PRINT TIT$
2516 LOCATE 5,15:PRINT STRING$(LEN(TIT$),205)
2718 FALUDIR$="FTURALUM.ALF":FALUDAT$="FTURALUM.DAT"
2719 FAUSDIR$="FTURAUNO.DIR":FAUSDAT$="FTURAUNO.DAT"
2720 GOSUB 30000:GOSUB 32000:GOSUB 34000
2724 COLOR 10:LOCATE 7,6:PRINT "Alumno N. : <" SPACE$(6) ">"
2728 GOSUB 7650
2730       '*******************************
2732       '***ENTRADA NUMERO DE ALUMNO ***
2734       '*******************************
2736       COLOR 15
2738       F.C1%=7:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2758 'Entrada OK
2741       IF I.T%=4 THEN CLOSE:GOTO 300
2742       BEEP:LOCATE 7,20:PRINT SPC(6):GOTO 2732
2756       '
2758       REG%=VAL(CUENTA$)
2760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 7,25-L.C%:PRINT GUAPRI$
2762     ' GET #2,REG%  'BUSCAR REGISTRO
2764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N즡ero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2765       GET #2,REG%  'BUSCAR REGISTRO
2766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
2770       LOCATE 7,28:PRINT CUCUCU$
2774       LOCATE 9,3:PRINT  "                                       FEBRERO        JUNIO       SETIEMBRE "
2775       LOCATE 10,3:PRINT "     NOMBRE DE LA ASIGNATURA          SOBRE 100     SOBRE 100     SOBRE 100 "
2776       LOCATE 11,3:PRINT "----------------------------------   -----------   -----------   -----------"
2780       FF%=11:LL%=90
2781       FOR AA%=2 TO AULTIMO%
2782       GET #3,AA%
2783       IF RIGHT$(ACLAVE,5)<> CUENTA$ THEN 2900
2784       REG%=CVI(AULTI)
2785       GET #4,REG%
2786       IF CVI(NNO01)<>0 THEN 2793
2787       IF CVI(NNO02)<>0 THEN 2793
2788       IF CVI(NNO03)<>0 THEN 2793
2789       IF CVI(NNO04)<>0 THEN 2793
2790      'IF CVI(NJU02)<>0 THEN 2793
2791      'IF CVI(NJU03)<>0 THEN 2793
2792       GOTO 2900
2793       FF%=FF%+1:LL%=LL%-1
2794       IF FF%<21 THEN 2801
2795       GOSUB 8500:MEN$="Para ver mas datos del mismo - ENTER - , no ver m쟳 - F1 -"
2796       A$=INPUT$(1)
2797       IF ASC(A$)<13 OR ASC(A$)>14 THEN 2796
2798       IF ASC(A$)=13 THEN 2800
2799       FOR N%=7 TO 20:LOCATE N%,2:PRINT SPC(78):NEXT N%:GOTO 2724
2800       FOR N%=12 TO 20:LOCATE N%,2:PRINT SPC(78):NEXT N%
2801       GET #6,VAL(LEFT$(ACLAVE,3))
2802       LOCATE FF%,3:PRINT LEFT$(GTEXTO,34)
2804 'IF CVI(NSO01)=0 THEN 2823
2805 GUAPRI#=CVI(NNO01):GOSUB 64770
2810 LOCATE FF%,46-L.C%:PRINT GUAPRI$
2815 'GUAPRI#=CVI(NSO01):GOSUB 64770
2820 'LOCATE FF%,45-L.C%:PRINT GUAPRI$
2822 'LOCATE FF%,47:PRINT NRE01
2823 'IF CVI(NSO02)=0 THEN 2844
2825 GUAPRI#=CVI(NNO02):GOSUB 64770
2830 LOCATE FF%,60-L.C%:PRINT GUAPRI$
2835 'GUAPRI#=CVI(NSO02):GOSUB 64770
2840 'LOCATE FF%,56-L.C%:PRINT GUAPRI$
2842 'LOCATE FF%,58:PRINT NRE02
2844 'IF CVI(NSO03)=0 THEN 2864
2845 GUAPRI#=CVI(NNO03):GOSUB 64770
2850 LOCATE FF%,74-L.C%:PRINT GUAPRI$
2855 'GUAPRI#=CVI(NSO03):GOSUB 64770
2860 'LOCATE FF%,67-L.C%:PRINT GUAPRI$
2862 'LOCATE FF%,69:PRINT NRE03
2864 'IF CVI(NSO04)=0 THEN 2900
2865 'GUAPRI#=CVI(NNO04):GOSUB 64770
2870 'LOCATE FF%,74-L.C%:PRINT GUAPRI$
2875 'GUAPRI#=CVI(NSO04):GOSUB 64770
2880 'LOCATE FF%,78-L.C%:PRINT GUAPRI$
2900 NEXT AA%
2905 IF LL%=90 THEN GOSUB 8500:MEN$="No tenemos los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2799
2907 GOSUB 8500:MEN$="Pulse una tecla para nueva consulta":GOSUB 8500:A$=INPUT$(1)
2910 GOTO 2799
4000 SHELL "TURNOTA.BAT"
4005 TIT$="- LISTAR CALIFICACIONES TURISMO Y SECRETARIADO -"
4020 GOSUB 8000
4030 COLOR 10
4040 LOCATE 7,18:PRINT TIT$
4050 LOCATE 8,18:PRINT STRING$(LEN(TIT$),205)
4060 COLOR 11:LOCATE 12,16:PRINT "Desde el Alumno : <      >    hasta el : <      >"
4070 COLOR 15:LOCATE 16,25:PRINT "F10 ==> ";:COLOR 10:PRINT "Finalizar el listado"
4080       '**********************
4090       '***ENTRADA DEL GRUPO**
4100       '**********************
4110       COLOR 15:F.C1%=12:C.C1%=35:L.E%=5:BO%=6:GOSUB 63000
4120       IF I.T%=0 THEN DESDE$=D.E$:GOTO 4140
4130       IF I.T%=2 THEN 300
4135       GOTO 4110
4140       '
4150       GUAPRI#=VAL(DESDE$):N.D%=0:GOSUB 64770
4160       LOCATE 12,40-L.C%:PRINT GUAPRI$
4180      COLOR 15
4190      '**********************
4200      '***ENTRADA PROFES  ***
4210      '**********************
4220      F.C1%=12:C.C1%=58:L.E%=5:BO%=6:GOSUB 63000
4230      IF I.T%=0 THEN HASTA$=D.E$:GOTO 4250
4235       IF I.T%=2 THEN 300
4240      GOTO 4220
4250      GUAPRI#=VAL(HASTA$):N.D%=0:GOSUB 64770
4260      LOCATE 12,63-L.C%:PRINT GUAPRI$
4270   '*************************************
4280   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4290   '*************************************
4300   GOSUB 8500
4310   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4320   COLOR 15:PRINT " ENTER ";
4330   COLOR 10:PRINT " Datos Erroneos ";
4340   COLOR 15:PRINT " F1 "
4350   COLOR 10
4360   '
4370   A$=INPUT$(1)
4380   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4370
4390   '
4400   IF ASC(A$)=13 THEN 4440
4410   LOCATE 10,2:PRINT SPC(78)
4420   LOCATE 12,2:PRINT SPC(78)
4430   CLOSE:GOTO 4000
4440   '
4450 FALUDIR$="FTURALUM.ALF":FALUDAT$="FTURALUM.DAT"
4455 FAUSDIR$="FTURAUNO.LIS":FAUSDAT$="FTURAUNO.DAT"
4460 GOSUB 30000:GOSUB 32000:GOSUB 34000
4470   WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4480   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B";
4485  'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
4490   FOR NN%=VAL(DESDE$) TO VAL(HASTA$)
4500   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B";
4505  'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
4510   '
4520   GET #2,NN%
4530  IF CMARCA="L" THEN 6000
4580  NOM$="Sres.":APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4590  DESTI$=CUCUCU$
4600  NOM$=CPOBLA:APE1$="-":APE2$=CPROVI:GOSUB 40000
4610 PUEBLO$=CUCUCU$
4620  NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4630  ALUMNO$=CUCUCU$:LC%=LEN(ALUMNO$)
4640  FOR A%=1 TO 1:LPRINT:NEXT A%
4642  IF CESCUE="T" THEN LPRINT TAB(11) "ESCUELA DE TURISMO";:GOTO 4650
4644  LPRINT TAB(11) "ESCUELA DE SECRETARIADO";
4650  LPRINT TAB(45) LEFT$(DESTI$,35)
4660  LPRINT TAB(11) "GENARO ORAA N 6";:LPRINT TAB(45) LEFT$(CDIREC,35)
4670  LPRINT TAB(11) "SANTURCE - VIZCAYA";:LPRINT TAB(45) LEFT$(PUEBLO$,35)
4680  LPRINT TAB(11) "Tel괽ono 4.61.95.55"
4690  FOR A%=1 TO 5:LPRINT:NEXT A%
4700  LPRINT TAB(20) "N O T A S    C U A T R I M E S T R A L E S"
4710  LPRINT TAB(20) STRING$(42,205):LPRINT:LPRINT:LPRINT
4720  LPRINT TAB(7) "Nombre del Alumno : " ALUMNO$
4730  LPRINT TAB(7) "Curso ........... : " CCURSO    ':LPRINT:LPRINT
4740  'LPRINT 'TAB(7) "Profesores ...... : " PROFE$:LPRINT
4750   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B";
4755  LPRINT:LPRINT:LPRINT
4760  LPRINT TAB(51) "旼컴컴컴컴컴컴컴컴컴컴컴컫컴컴컴컴컴컴컴컴컴컴컴컴쩡컴컴컴컴컴컴컴컴컴컴컴커"
4770  LPRINT TAB(51) "  CONVOCATORIA FEBRERO     CONVOCATORIA JUNIO    CONVOCATORIA SETIEMBRE "
4780  LPRINT TAB(51) "쳐컴컴컴컴컴컫컴컴컫컴컴컵컴컴컴컴컴컴쩡컴컴쩡컴컴탠컴컴컴컴컴컴컴컴컴컴컴캑"
4790  LPRINT TAB(51) "쿎ALIFICACION FAL  JUS 쿎ALIFICACION FAL  JUS       CALIFICACION      "
4800  LPRINT TAB(51) "쳐컴컴컴컴컴컵컴컴컵컴컴컵컴컴컴컴컴컴탠컴컴탠컴컴탠컴컴컴컴컴컴컴컴컴컴컴캑"
4802 FOR RR%=2 TO AULTIMO%
4804 GET #3,RR%
4806 IF VAL(RIGHT$(ACLAVE,5))<NN% THEN 5125
4808 IF VAL(RIGHT$(ACLAVE,5))>NN% THEN RR%=NULTI%:GOTO 5125
4809 GET #4,CVI(AULTI)
4810 GET #6,VAL(LEFT$(NCLAVE,3))
4820  LPRINT TAB(5) LEFT$(GTEXTO,45);:LPRINT TAB(51)"";
4830 IF CVI(NNO01)=0 THEN 4870
4840 IF CVI(NNO01)=999 THEN LPRINT TAB(52) " NO PRESEN. ";:GOTO 4870
4850  GUAPRI#=CVI(NNO01):GOSUB 6500
4860  LPRINT TAB(53) CALI$;
4870  LPRINT  TAB(64) "";
4901 IF CVI(NFA01)=0 THEN 4904
4902 GUAPRI#=CVI(NFA01):N.D%=0:GOSUB 64770
4903 LPRINT TAB(68-L.C%) GUAPRI$;
4904 LPRINT TAB(70) "";
4905 IF CVI(NJU01)=0 THEN 4908
4906 GUAPRI#=CVI(NJU01):N.D%=0:GOSUB 64770
4907 LPRINT TAB(74-L.C%) GUAPRI$;
4908 LPRINT TAB(76) "";
4910 IF CVI(NNO02)=0 THEN 4950
4920 IF CVI(NNO02)=999 THEN LPRINT TAB(77) " NO PRESEN.";:GOTO 4950
4930  GUAPRI#=CVI(NNO02):GOSUB 6500
4940  LPRINT TAB(78) CALI$;
4950  LPRINT  TAB(89) "";
4981 IF CVI(NFA02)=0 THEN 4984
4982 GUAPRI#=CVI(NFA02):N.D%=0:GOSUB 64770
4983 LPRINT TAB(93-L.C%) GUAPRI$;
4984 LPRINT TAB(95) "";
4985 IF CVI(NJU02)=0 THEN 4988
4986 GUAPRI#=CVI(NJU02):N.D%=0:GOSUB 64770
4987 LPRINT TAB(99-L.C%) GUAPRI$;
4988 LPRINT TAB(101) "";
4990 IF CVI(NNO03)=0 THEN 5030
5000 IF CVI(NNO03)=999 THEN LPRINT TAB(108) " NO PRESEN.";:GOTO 5030
5010  GUAPRI#=CVI(NNO03):GOSUB 6500
5020  LPRINT TAB(108) CALI$;
5030 'LPRINT  TAB(114) "";
5061 'IF CVI(NFA03)=0 THEN 5064
5062 'GUAPRI#=CVI(NFA03):N.D%=0:GOSUB 64770
5063 'LPRINT TAB(119-L.C%) GUAPRI$;
5064 'LPRINT TAB(120) "";
5065 'IF CVI(NJU03)=0 THEN 5068
5066 'GUAPRI#=CVI(NJU03):N.D%=0:GOSUB 64770
5067 'LPRINT TAB(125-L.C%) GUAPRI$;
5068 LPRINT TAB(126) ""
5120  LPRINT TAB(51) "쳐컴컴컴컴컴컵컴컴컵컴컴컵컴컴컴컴컴컴탠컴컴탠컴컴탠컴컴컴컴컴컴컴컴컴컴컴캑"
5125 NEXT RR%
5130  LPRINT TAB(51) "읕컴컴컴컴컴컨컴컴컨컴컴컨컴컴컴컴컴컴좔컴컴좔컴컴좔컴컴컴컴컴컴컴컴컴컴컴켸"
5140  LPRINT
5150  'LPRINT TAB(1) CHR$(218) STRING$(125,CHR$(196)) CHR$(191)
5160  'LPRINT TAB(1) CHR$(179) " OBSERVACIONES:";
5165 'LPRINT TAB(1) "OBSERVACIONES:"
5170  'LPRINT TAB(127) CHR$(179)
5180  'LPRINT TAB(1) CHR$(179) STRING$(125," ") CHR$(179)
5190  'LPRINT TAB(1) CHR$(179) STRING$(125," ") CHR$(179)
5200  'LPRINT TAB(1) CHR$(192) STRING$(125,CHR$(196)) CHR$(217)
5210 'LPRINT
5220 'LPRINT TAB(1) "De  0,00 %  a  49,99 %  컴>  Suspenso     ";
5230 'LPRINT TAB(91) "FIRMA DE LOS PADRES"
5240 'LPRINT TAB(1) "De 50,00 %  a  59,99 %  컴>  Apto         ";
5250 'LPRINT TAB(91) "컴컴컴컴컴컴컴컴컴"
5260 'LPRINT TAB(1) "De 60,00 %  a  69,99 %  컴>  Bien         "
5270 'LPRINT TAB(1) "De 70,00 %  a  89,99 %  컴>  Notable      "
5280 'LPRINT TAB(1) "De 90,00 %  a 100,00 %  컴>  Sobresaliente"
5290 'LPRINT
5300 'LPRINT TAB(1) "INGLES : Menos de 70 %  컴>  No Apto      "
5310 'LPRINT TAB(1) "         M쟳   de 70 %  컴>  Apto         "
5320  LPRINT CHR$(12)
6000 NEXT NN%
6010 CLOSE:GOTO 4005
6500 '*******************
6510 '***NOTAS FINALES***
6520 '*******************
6530 IF LEFT$(GTEXTO,6)="INGLES" THEN 6600
6532 IF LEFT$(GTEXTO,6)="ALEMAN" THEN 6600
6534 IF LEFT$(GTEXTO,6)="FRANCE" THEN 6600
6536 'IF LEFT$(GTEXTO,33)="CORRESPONDENCIA COMERCIAL INGLESA" THEN 6600
6540 IF GUAPRI#<50 THEN CALI$="NO APTO":RETURN
6550 IF GUAPRI#<60 THEN CALI$="APTO":RETURN
6560 IF GUAPRI#<70 THEN CALI$="BIEN":RETURN
6570 IF GUAPRI#<90 THEN CALI$="NOTABLE":RETURN
6580 CALI$="SOBRESAL.":RETURN
6590 '
6600 IF GUAPRI#<70 THEN CALI$="NO APTO":RETURN
6602 IF GUAPRI#<80 THEN CALI$="APTO":RETURN
6604 IF GUAPRI#<90 THEN CALI$="NOTABLE":RETURN
6610 CALI$="SOBRESAL.":RETURN
7440 '**************************************************
7450 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7455 '**************************************************
7460 GOSUB 8500:LOCATE 22,15
7465 COLOR 10:PRINT "F7 =>";
7470 COLOR 15:PRINT " Fin Asignatura ";
7472 COLOR 10:PRINT " ENTER";
7474 COLOR 15:PRINT " Saltar ";
7476 COLOR 10:PRINT " F8 =>";
7478 COLOR 15:PRINT " Atras ";
7480 COLOR 10:PRINT " F10 =>";
7482 COLOR 15:PRINT " Duplicar"
7490 '
7495 RETURN
7500 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Final de la Opci줻."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$ :COLOR 10:LOCATE 2,37:PRINT "Aplicaci줻 : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PTURNOTA"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA--":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9132 IF ERL=30220 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero de TARIFAS inesixtente":RET%=1:GOSUB 8500:GOTO 9000
9134 GOTO 9170
9142 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,14 AS CKAKA1,10 AS CCUBAN,7 AS CKAKA2,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,1 AS CESCUE,9 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FTURALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30132 '
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10
31040 LOCATE 8,6:PRINT "Asignatura . : <   >" SPACE$(53)
31042 LOCATE 10,6:PRINT "Grupo ...... : < >"
31046 LOCATE 14,6:PRINT "N Alumno .. : <      >" SPACE$(50)
31048 LOCATE 16,6:PRINT "Puntuaci줻 . : <   >"   ' Sobre ... : <   >"
31050 'LOCATE 18,6:PRINT "Recuperaci줻 : < >"
31160 COLOR 15:RETURN
31170 '
32000 '**********************************
32010 '***APERTURA FICHERO ASIGNATURAS***
32020 '**********************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FTURASIG.DAT",70
32060 FIELD #6,1 AS GSTATUS,1 AS GMARCA,3 AS GGRUPO,1 AS GCICLO,1 AS GCURSO,50 AS GTEXTO,2 AS GALTAS,11 AS GLIBRE
32070 FIELD #6,1 AS GSTAT,1 AS GMAR,2 AS GULTIMO,12 AS GETIQUETA,54 AS GLIB1
32110 GET #6,1
32120  IF GETIQUETA="FTURASIG.DAT" THEN 32140
32130  GOSUB 8500:MEN$="- NO TENEMOS FICHERO DE ASIGNATURAS, PULSAR UNA TECLA -":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140  ULTGRU%=CVI(GULTIMO)
32150  RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** TURISMO Y SECRETARIADO ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '***********************************
34010 '***APERTURA FICHERO NOTAS-FALTAS***
34020 '***********************************
34025 '
34052 OPEN "R",#3,FAUSDIR$,20
34054 FIELD #3,2 AS AULTI,2 AS AULTA,9 AS ACLAVE,4 AS ALIBRE,1 AS ASWA,1 AS AMARCA,1 AS ASTA
34056 GET #3,1
34058  AULTIMO%=CVI(AULTI)
34060  AULACTU%=CVI(AULTA):N.R%=AULACTU%
34061 'LSET AULTI=MKI$(1):LSET AULTA=MKI$(1):LSET ACLAVE="":LSET ALIBRE=""
34062 'LSET ASWA="0":LSET AMARCA="A":LSET ASTA="A"
34063 'PUT #3,1
34090 ' Apertura del fichero de datos   FRESAUNO.DAT
34100 OPEN "R",#4,FAUSDAT$,50
34110 FIELD #4,9 AS NCLAVE,2 AS NNO01,2 AS NSO01,1 AS NRE01,2 AS NFA01,2 AS NJU01
34112 FIELD #4,18 AS NFI01,2 AS NNO02,2 AS NSO02,1 AS NRE02,2 AS NFA02,2 AS NJU02
34113 FIELD #4,27 AS NFI02,2 AS NNO03,2 AS NSO03,1 AS NRE03,2 AS NFA03,2 AS NJU03,2 AS NNO04,2 AS NSO04,8 AS NFIL1,1 AS NMARCA,1 AS NSTAT
34114 FIELD #4,2 AS NULTI,2 AS NULTA,1 AS NSWA,43 AS NFI03,1 AS NMAR1,1 AS NSTA1
34115 GET #4,1
34117  NULTI%=CVI(NULTI)
34118  NULTA%=CVI(NULTA)
34122 'LSET NSTA1="A":LSET NULTI=MKI$(1):LSET NULTA=MKI$(1)
34124 'LSET NSWA="0":LSET NMAR1="A":LSET NFI03=""
34128 'PUT #4,1
34130 'CLOSE
34132 'STOP
34140 RETURN
34150 '
34500 '*************************
34505 '***BUSQUEDA DICOTOMICA***
34510 '*************************
34515   '
34520 'El argumento de busqueda sobre 'BURRA$'
34525 'El numero de registros sobre 'N.R%'
34530 R.A%=0           'Donde devuelve el nro. de registro.
34535 SWESC%=0         'SW indicador de la busqueda.
34540 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34545 '
34550 R.I%=INT((N.R%/2)+.5)
34555 R.C%=R.I%
34560 IF R.I%<1 THEN SWESC%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34705
34565 IF R.I%>N.R% THEN SWESC%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 34705
34570 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34590 RPROV$=ACLAVE
34595 IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
34600 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34605 IF RPROV$>BURRA$ THEN R.I%=R.I%-R.C%:GOTO 34615
34610 R.I%=R.I%+R.C%
34615 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34560
34620 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34560
34625 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34560
34630 REG2%=R.I%:SW100%=0
34635 GOTO 34560
34640 'Registro Buscado.
34645 FOR JMG%=N.R% TO AULTIMO%
34650     GET #3,JMG%
34670 RPROV$=ACLAVE
34675     IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
34680 NEXT JMG%
34700 RETURN
34705 FOR R.I%=DES% TO HAS%
34710     GET #3,R.I%
34730 RPROV$=ACLAVE
34735     IF RPROV$=BURRA$ THEN SWESC%=0:RETURN
34740 NEXT R.I%
34745 GOTO 34640
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 0  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="" OR D.R$="" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0     ':N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN GUAPRI$=GUAPRI$+".000":GOTO 64794
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
