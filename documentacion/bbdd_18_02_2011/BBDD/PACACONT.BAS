10 '                       PACACONT.BAS
20 '                      --------------
30 '        TRATAMIENTO DE LAS FACTURAS PARA CONTABILIDAD
40 '        ---------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(100,"Ä")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,A,F,J,N,G
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),PRE!(5),FAL%(12),PERI$(12)
170 FOR I%=1 TO 5:PRE!(I%)=0:NEXT I%
172 '
174 '
176 '
178 '
200 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SETIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
500 TIT$="- INTRODUCIR DATOS PARA HACER FACTURAS CONTABILIDAD -":GOTO 510
505 '
510 '
511 GOSUB 8000
512 COLOR 10
514 LOCATE 4,14:PRINT TIT$
516 LOCATE 5,14:PRINT STRING$(LEN(TIT$),205)
700 '
702 '
704 '
706 KILL"FACACONT.DAT
718 'FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
720 'GOSUB 30000:GOSUB 32000
724 GOSUB 35000
726 OPC%=1:GOSUB 31500:CLOSE
728 GOSUB 7650
730       '***************************
732       '***ENTRADA FECHA FACTURA***
734       '***************************
736       F.C1%=6:C.C1%=44:L.E%=6:BO%=8:GOSUB 63000
738       IF I.T%=0 THEN NACI$=D.E$:GOTO 752
740       IF I.T%=5 THEN 732
742       IF I.T%=1 THEN 732
744       IF I.T%=3 THEN LOCATE 6,44:PRINT SPC(8):GOTO 732
746       IF I.T%=4 THEN 9000             'Fin Entrada.
748       GOTO 732
750       '
752       FECSUB$=NACI$:GOSUB 60000
754       IF ERF%<>1 THEN 760
756       BEEP:GOSUB 8500:MEN$="Fecha de Factura Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 6,44:PRINT SPC(8)
758       GOSUB 7650:GOTO 732
760       LOCATE 6,44:PRINT FDMA$:FENA$=FAMD$
762       '
764       FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
766       GOSUB 30000:GOSUB 32000:GOSUB 32500
768       OPC%=2:GOSUB 31500
770       '
800       '***************************
802       '***ENTRADA TOTAL FACTURA***
804       '***************************
806       F.C1%=8:C.C1%=44:L.E%=8:BO%=10:ESC%=1:GOSUB 63000
808       IF I.T%=0 THEN IMPOR#=VAL(D.E$):GOTO 818
810       IF I.T%=3 THEN CLOSE:LOCATE 6,44:PRINT SPC(8):GOTO 730
812       IF I.T%=4 THEN 9000               'Fin Entrada.
814       GOTO 802
816       '
818       GUAPRI#=IMPOR#:GOSUB 64770
820       LOCATE 8,53-L.C%:PRINT GUAPRI$
822       '
830       '***************************
832       '***ENTRADA DE 75.000 PTS***
834       '***************************
836       F.C1%=10:C.C1%=44:L.E%=2:BO%=2:ESC%=1:GOSUB 63000
838       IF I.T%=0 THEN DE75%=VAL(D.E$):GOTO 848
840       IF I.T%=3 THEN LOCATE 8,44:PRINT SPC(10):GOTO 804
842       IF I.T%=4 THEN 9000               'Fin Entrada.
844       GOTO 832
846       '
848       GUAPRI#=DE75%:GOSUB 64770
850       LOCATE 10,45-L.C%:PRINT GUAPRI$
852       '
860       '***************************
862       '***ENTRADA DE PRECIO 1  ***
864       '***************************
866       F.C1%=12:C.C1%=44:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
868       IF I.T%=0 THEN PRE1!=VAL(D.E$):GOTO 878
870       IF I.T%=3 THEN LOCATE 10,44:PRINT SPC(2):GOTO 830
872       IF I.T%=4 THEN 9000               'Fin Entrada.
874       GOTO 862
876       '
878       GUAPRI#=PRE1!:GOSUB 64770
880       LOCATE 12,49-L.C%:PRINT GUAPRI$
882       '
890       '***************************
892       '***ENTRADA DE PRECIO 2  ***
894       '***************************
896       F.C1%=14:C.C1%=44:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
898       IF I.T%=0 THEN PRE2!=VAL(D.E$):GOTO 906
900       IF I.T%=3 THEN LOCATE 12,44:PRINT SPC(6):GOTO 862
902       IF I.T%=4 THEN 9000               'Fin Entrada.
904       GOTO 892
906       '
908       GUAPRI#=PRE2!:GOSUB 64770
910       LOCATE 14,49-L.C%:PRINT GUAPRI$
912       '
920       '***************************
922       '***ENTRADA DE PRECIO 3  ***
924       '***************************
926       F.C1%=16:C.C1%=44:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
928       IF I.T%=0 THEN PRE3!=VAL(D.E$):GOTO 936
930       IF I.T%=3 THEN LOCATE 14,44:PRINT SPC(6):GOTO 892
932       IF I.T%=4 THEN 9000               'Fin Entrada.
934       GOTO 922
936       '
938       GUAPRI#=PRE3!:GOSUB 64770
940       LOCATE 16,49-L.C%:PRINT GUAPRI$
942       '
950       '***************************
952       '***ENTRADA DE PRECIO 4  ***
954       '***************************
956       F.C1%=18:C.C1%=44:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
958       IF I.T%=0 THEN PRE4!=VAL(D.E$):GOTO 966
960       IF I.T%=3 THEN LOCATE 16,44:PRINT SPC(6):GOTO 922
962       IF I.T%=4 THEN 9000               'Fin Entrada.
964       GOTO 952
966       '
968       GUAPRI#=PRE4!:GOSUB 64770
970       LOCATE 18,49-L.C%:PRINT GUAPRI$
972       '
980       '***************************
982       '***ENTRADA DE PRECIO 5  ***
984       '***************************
986       F.C1%=20:C.C1%=44:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
988       IF I.T%=0 THEN PRE5!=VAL(D.E$):GOTO 996
990       IF I.T%=3 THEN LOCATE 18,44:PRINT SPC(6):GOTO 952
992       IF I.T%=4 THEN 9000               'Fin Entrada.
994       GOTO 982
996       '
998       GUAPRI#=PRE5!:GOSUB 64770
1000      LOCATE 20,49-L.C%:PRINT GUAPRI$
1002      '
1010 IF IMPOR#<DE75%*75000! THEN CLOSE:GOTO 500
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2042
2028   '
2030   '
2032   '
2034   GOTO 724
2042   '
2050 IF DE75%=0 THEN 2150
2051 NN%=0
2052 FOR AA%=2 TO ULTITU%
2054 GET #4,AA%
2056 ULTI%=ULTI%+1
2058 GET #5,ULTI%
2060 LSET ASTA="O":LSET AMAR="A":LSET ALUM=MKI$(AA%)
2062 LSET ANOM=GNOMBRE:LSET APE1=GAPELL1:LSET APE2=GAPELL2
2068 LSET ACAN=MKS$(75000!)
2078 LSET ACER=""
2080 PUT #5,ULTI%
2082 DE75%=DE75%-1
2084 TOTAL#=TOTAL#+75000!
2086 IF DE75%=0 THEN 2150
2088 NEXT AA%
2090 '
2150 TOPE#=PRE5!*2:NN%=0
2152 FOR AA%=2 TO 5000
2154 GET #6,AA%
2155 IF AA%=1305 THEN 3000
2156 IF AA%=1306 THEN 3000
2157 IF AA%=2012 THEN 3000
2158 IF AA%=2023 THEN 3000
2159 IF CVI(NCOE11)<>0 THEN 2250
2160 IF CVI(NCOE12)<>0 THEN 2250
2161 IF CVI(NCOE13)<>0 THEN 2250
2162 GOTO 3000
2250 '
2252 '
2254 GET #2,AA%
2256 ULTI%=ULTI%+1
2258 GET #5,ULTI%
2260 LSET ASTA="O":LSET AMAR="A":LSET ALUM=MKI$(AA%)
2262 LSET ANOM=CNOMBRE:LSET APE1=CAPELL1:LSET APE2=CAPELL2
2264 NN%=NN%+1
2266 IF NN%>5 THEN NN%=0:GOTO 2264
2268 IF NN%=1 THEN LSET ACAN=MKS$(PRE1!):TOTAL#=TOTAL#+PRE1!
2270 IF NN%=2 THEN LSET ACAN=MKS$(PRE2!):TOTAL#=TOTAL#+PRE2!
2272 IF NN%=3 THEN LSET ACAN=MKS$(PRE3!):TOTAL#=TOTAL#+PRE3!
2274 IF NN%=4 THEN LSET ACAN=MKS$(PRE4!):TOTAL#=TOTAL#+PRE4!
2276 IF NN%=5 THEN LSET ACAN=MKS$(PRE5!):TOTAL#=TOTAL#+PRE5!
2278 LSET ACER=""
2280 PUT #5,ULTI%
2282 RESTO#=IMPOR#-TOTAL#
2284 IF RESTO#>TOPE# THEN 3000
2286 ULTI%=ULTI%+1
2288 GET #5,ULTI%
2290 LSET ASTA="O":LSET AMAR="A":LSET ALUM=MKI$(AA%)
2292 LSET ANOM="XANA                ":LSET APE1="FARPON              ":LSET APE2="LARGO               "
2294 LSET ACAN=MKS$(RESTO#)
2296 LSET ACER=""
2298 PUT #5,ULTI%
2300 GET #5,1
2302 LSET AULT=MKI$(ULTI%)
2304 PUT #5,1
2306 AA%=5000
2308 '
3000 NEXT AA%
3002 '
3003 CLOSE
3004 SHELL "ACACONT.BAT"
3006 '
4000 '
4005 TIT$="- LISTAR FACTURAS DE LA ESCUELA DE IDIOMAS -"
4010 OPC%=2:GOSUB 31500
4011 GOSUB 8000
4012 COLOR 10:GOSUB 10000
4014 LOCATE 5,18:PRINT TIT$
4016 LOCATE 6,18:PRINT STRING$(LEN(TIT$),205)
4018 COLOR 11:LOCATE 10,16:PRINT "N§ de Factura inicial : <      >
4019 '
4020       '**********************
4022       '***ENTRADA DEL GRUPO**
4024       '**********************
4026       COLOR 15:F.C1%=10:C.C1%=41:L.E%=5:BO%=6:GOSUB 63000
4028       IF I.T%=0 THEN FAC!=VAL(D.E$):GOTO 4032
4030       BEEP:GOTO 4026
4032       GUAPRI#=FAC!:GOSUB 64770
4033       LOCATE 10,41:PRINT SPC(6):LOCATE 10,46-L.C%:PRINT GUAPRI$
4034       '
4056   '*************************************
4058   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4060   '*************************************
4062   GOSUB 8500
4064   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4066   COLOR 15:PRINT " ENTER ";
4068   COLOR 10:PRINT " Datos Erroneos ";
4070   COLOR 15:PRINT " F1 "
4072   COLOR 10
4074   '
4076   A$=INPUT$(1)
4078   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4076
4080   '
4082   IF ASC(A$)=13 THEN 4090
4084   LOCATE 10,2:PRINT SPC(78)
4086   LOCATE 12,2:PRINT SPC(78)
4088   CLOSE:GOTO 4000
4090   '
4095   N.FAC!=FAC!-1:LIN%=90:PAG%=0
4100   '
4102   WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4103  'LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(15)
4105   LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
4110   '
4115   FOR AA%=2 TO ULTI%
4120   GET #5,AA%
4125   '
4130   '
4150   XOM$=APE1:XPE1$=APE2:XPE2$=ANOM:GOSUB 40000
4152   DESTI$=CUCUCU$
4155   '
4160   '
4175   IF LIN%<57 THEN 4224
4180   IF LIN%=90 THEN 4190
4185   LPRINT CHR$(12)
4190   LIN%=0:PAG%=PAG%+1
4195   LPRINT TAB(4) "*** E. I. D. E.    CIF: B48474647 ***"
4196   LPRINT TAB(4) "*** GENARO ORAA 6   TFNO: 4619555 ***"
4197   LPRINT TAB(4) "*** S  A  N  T  U  R  C  E  48980 ***";
4198   LPRINT TAB(58) "RELACION DE FACTURAS EMITIDAS EL DIA "FDMA$;
4200   LPRINT TAB(119) USING "HOJA : ##";PAG%
4205   LPRINT TAB(4) STRING$(37,"-");
4207   LPRINT TAB(58) STRING$(45,"-");
4210   LPRINT TAB(119) STRING$(9,"-"):LPRINT
4215  LPRINT TAB(4) " FACTURA                   APELLIDOS Y NOMBRE                                     I  M  P  O  R  T  E  S                   "
4217  LPRINT TAB(4) "----------  --------------------------------------------------  ------------------------------------------------------------"
4219 'LPRINT
4222  LIN%=6
4224  N.FAC!=N.FAC!+1:
4225  GUAPRI#=N.FAC!:GOSUB 64770
4226  LPRINT TAB(11-L.C%) GUAPRI$+"-R";
4227  LPRINT TAB(16) CUCUCU$;
4236  GUAPRI#=CVS(ACAN):GOSUB 64770
4237  LPRINT TAB(76-L.C%) GUAPRI$;
4289  LIN%=LIN%+1
4290  TODO#=TODO#+GUAPRI#
4291  IMPOR#=GUAPRI#:GOSUB 4500
4292  NEXT AA%
4293  LPRINT
4294  LPRINT TAB(33) "TOTAL FACTURADO EL DIA "FDMA$;
4296  GUAPRI#=TODO#:GOSUB 64770
4298  LPRINT TAB(76-L.C%) GUAPRI$;
4299  IMPOR#=TODO#:GOSUB 4500
4300  LPRINT CHR$(12)
4302  CLOSE
4304  GOTO 500
4306  '
4308  '
4500 'IMPOR#=GUAPRI#
4505 IMPOR$=STR$(IMPOR#)
4510 IMPOR$=RIGHT$(IMPOR$,LEN(IMPOR$)-1)
4520 GOSUB 10120
5000 'IMPOR#=GUAPRI#:GOSUB 10120
5002 LPRINT TAB(80) LIT1$
5003 IF LEFT$(LIT2$,3)="***" THEN 5008
5004 LPRINT TAB(80) LIT2$:LIN%=LIN%+1
5005 IF LEFT$(LIT3$,3)="***" THEN 5008
5006 LPRINT TAB(80) LIT3$:LIN%=LIN%+1
5008 LPRINT TAB(4) STRING$(123,"."):LIN%=LIN%+1
5010 RETURN
5012 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,13
7672 COLOR 10:PRINT "ENTER =>";
7674 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Opci¢n.";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s "
7740 'COLOR 10:PRINT " F10 =>";
7750 'COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,26:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 CLS      'RUN "PACAFANO"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
10000 '*******************************************
10010 '***CARGAMOS LAS TABLAS PARA LOS IMPORTES***
10020 '*******************************************
10030 DEFSTR T:DIM TA(31),TB(9),TC(9):IMPMAN$="000000000"
10040 DATA "UNO ","DOS ","TRES ","CUATRO ","CINCO ","SEIS ","SIETE ","OCHO ","NUEVE ","DIEZ ","ONCE ","DOCE ","TRECE ","CATORCE ","QUINCE ","DIECISEIS "
10050 DATA "DIECISIETE ","DIECIOCHO ","DIECINUEVE ","VEINTE ","VEINTIUNA ","VEINTIDOS ","VEINTITRES ","VEINTICUATRO ","VEINTICINCO ","VEINTISEIS ","VEINTISIETE ","VEINTIOCHO ","VEINTINUEVE ","TREINTA ","TREINTA Y UNO "
10060 FOR I%=1 TO 31:READ TA(I%):NEXT I%
10070 DATA "DIEZ ","CIENTO ","VEINTE ",DOSCIENT,"TREINTA ",TRESCIENT,"CUARENTA ",CUATROCIENT,"CINCUENTA ",QUINIENT,"SESENTA ",SEISCIENT,"SETENTA ",SETECIENT,"OCHENTA ",OCHOCIENT,"NOVENTA ",NOVECIENT
10080 FOR I%=1 TO 9:READ TB(I%),TC(I%):NEXT I%
10090 RETURN
10100 '
10110 '********************************************************************
10120 '***RUTINA PESETAS.ENTRADA IMPOR$ E IMPOR#(IMPORTE A TRANSFORMAR).***
10130 '********************************************************************
10140 LIT1$="":LIT2$="":LIT3$="":IND33%=0:IF IMPOR#=0 THEN LIT1$=LIT1$+"CERO.":GOTO 10220
10150 'Desglose del importe
10160 IMPOR$=LEFT$(IMPMAN$,9-LEN(IMPOR$))+IMPOR$
10170 PESETA$=RIGHT$(IMPOR$,3):PESETA%=VAL(PESETA$):MYM$=LEFT$(IMPOR$,6):MYM%=VAL(MYM$):DECPTA$=RIGHT$(PESETA$,2):DECPTA%=VAL(DECPTA$):DIG7%=VAL(LEFT$(PESETA$,1)):DIG8%=VAL(LEFT$(DECPTA$,1)):DIG9%=VAL(RIGHT$(DECPTA$,1))
10180 MILLON$=LEFT$(MYM$,3):MILLON%=VAL(MILLON$):MILES$=RIGHT$(MYM$,3):MILES%=VAL(MILES$):DECMIL$=RIGHT$(MILES$,2):DECMIL%=VAL(DECMIL$):DIG4%=VAL(LEFT$(MILES$,1)):DIG5%=VAL(LEFT$(DECMIL$,1)):DIG6%=VAL(RIGHT$(DECMIL$,1))
10190 DECMLL$=RIGHT$(MILLON$,2):DECMLL%=VAL(DECMLL$):DIG1%=VAL(LEFT$(MILLON$,1)):DIG2%=VAL(LEFT$(DECMLL$,1)):DIG3%=VAL(RIGHT$(DECMLL$,1))
10200 'Tratamiento particular
10210 GOSUB 10270:GOSUB 10350:GOSUB 10430 'Tratamiento millones,miles y pesetas
10220 'FIN RUTINA
10230 IF LEN(LIT1$)<48 THEN LIT1$=LIT1$+STRING$(48-LEN(LIT1$),"*")
10240 IF LEN(LIT2$)<48 THEN LIT2$=LIT2$+STRING$(48-LEN(LIT2$),"*")
10250 IF LEN(LIT3$)<48 THEN LIT3$=LIT3$+STRING$(48-LEN(LIT3$),"*")
10260 RETURN
10270 'Tratamiento millones
10280 IF MILLON%=0 THEN 10340
10290 IF MILLON%=1 THEN LIT1$="UN MILLON ":GOTO 10340
10300 IF MILLON%=100 THEN LIT1$="CIEN MILLONES ":GOTO 10340
10310 DECENA%=DECMLL%:DIGA%=DIG1%:DIGB%=DIG2%:DIGC%=DIG3%:IND90%=1
10320 GOSUB 10590 'Tratamiento comun
10330 IND90%=0:MAN$="MILLONES ":GOSUB 10520 'Salida a literales
10340 RETURN
10350 'Tratamiento miles
10360 IF MILES%=0 THEN 10420
10370 IF MILES%=1 THEN 10410
10380 IF MILES%=100 THEN MAN$="CIEN ":GOSUB 10520:GOTO 10410
10390 DECENA%=DECMIL%:DIGA%=DIG4%:DIGB%=DIG5%:DIGC%=DIG6%
10400 GOSUB 10590 'Tratamiento comun
10410 MAN$="MIL ":GOSUB 10520 'Salida a literales
10420 RETURN
10430 'Tratamiento pesetas
10440 IF PESETA%=0 THEN 10500
10450 'IF PESETA%=1 AND MYM>0 THEN MAN$="Y ":GOSUB 1700 'Salida a literales
10460 IF PESETA%=1 THEN MAN$="UNA ":GOSUB 10520:GOTO 10500
10470 IF PESETA%=100 THEN MAN$="CIEN ":GOSUB 10520:GOTO 10500
10480 DECENA%=DECPTA%:DIGA%=DIG7%:DIGB%=DIG8%:DIGC%=DIG9%
10490 GOSUB 10590 'Tratmiento comun
10500 IF LIT2$="" THEN 10502 ELSE LIT2$=LEFT$(LIT2$,LEN(LIT2$)-1)+" PESETAS.":RETURN
10502 SS%=LEN(LIT1$):IF (48-SS%)<7 THEN LIT2$="PESETAS.":RETURN
10504 LIT1$=LEFT$(LIT1$,LEN(LIT1$)-1)+" PESETAS."
10510 RETURN
10520 'Salida a literales
10530 IF IND33%=2 THEN 10570
10540 IF IND33%=1 THEN 10560
10550 IF (48-LEN(LIT1$))>=LEN(MAN$)THEN LIT1$=LIT1$+MAN$:RETURN ELSE LIT2$=LIT2$+MAN$:IND33%=1:RETURN
10560 IF (48-LEN(LIT2$))>=LEN(MAN$)THEN LIT2$=LIT2$+MAN$:RETURN ELSE LIT3$=LIT3$+MAN$:IND33%=2:RETURN
10570 LIT3$=LIT3$+MAN$
10580 RETURN
10590 'Tratamiento comun
10600 IF DIGA%=O THEN 10640 ELSE MAN$=TC(DIGA%)
10610 IF DIGA%=1 THEN 10630
10620 IF IND90%=1 THEN MAN$=MAN$+"OS " ELSE MAN$=MAN$+"AS "
10630 GOSUB 10520 'Salida a literales
10640 IF DIGB%=0 THEN 10680
10650 IF DECENA%>29 THEN MAN$=TB(DIGB%):GOSUB 10520:GOTO 10680
10660 IF IND90%=1 AND DECENA%=21 THEN MAN$="VEINTIUN ":GOSUB 10520:IND60%=1:GOTO 10680
10670 MAN$=TA(DECENA%):GOSUB 10520:IND60%=1
10680 IF IND60%=1 OR DIGC%=0 THEN 10730
10690 IF DIGB%<>0 THEN MAN$="Y ":GOSUB 10520
10700 IF IND90%=1 AND DIGC%=1 THEN MAN$="UN ":GOSUB 10520:GOTO 10730
10710 IF DIGC%=1 THEN MAN$="UNA ":GOSUB 10520:GOTO 10730
10720 MAN$=TA(DIGC%):GOSUB 10520
10730 IND60%=0
10740 RETURN
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,21 AS CKAKA,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
31500 '********************************
31502 '***APERTURA FICHERO DE FALTAS***
31504 '********************************
31508 '
31510 'APERTURA FICHERO DE DATOS.
31512 OPEN "R",#5,"FACACONT.DAT",70
31514 'REGISTRO DE DATOS.
31516 FIELD #5,1 AS ASTA,1 AS AMAR,2 AS ALUM,20 AS ANOM,20 AS APE1,20 AS APE2,4 AS ACAN,2 AS ACER
31517 'REGISTRO 1
31518 FIELD #5,1 AS AST1,1 AS AMA1,12 AS AETI,2 AS AULT,54 AS ALIB
31520 '
31522 GET #5,1
31524 IF OPC%=1 THEN 31530
31526 ULTI%=CVI(AULT)
31528 RETURN
31530 '
31532 LSET AST1="A":LSET AMA1="A":LSET AETI="FACACONT.DAT"
31534 LSET AULT=MKI$(1):LSET ALIB=""
31536 PUT #5,1
31538 '
31587 RETURN
31588 '
31590 '
32000 '***APERTURA FICHERO DE NOTAS ***
32010 '********************************
32020 FICHE6$="FACANOTA.A"+LEFT$(FAMD$,2)
32030 'DEFSTR N
32040 'APERTURA FICHERO DE DATOS.
32050 OPEN "R",#6,FICHE6$,100
32060 'REGISTRO DE DATOS.
32070 FIELD #6,2 AS NCOE11,2 AS NCOS11,2 AS NCOE12,2 AS NCOS12,2 AS NCOE13,2 AS NCOS13
32080 FIELD #6,12 AS NRE001,2 AS NCOE21,2 AS NCOS21,2 AS NCOE22,2 AS NCOS22,2 AS NCOE23,2 AS NCOS23
32090 FIELD #6,24 AS NRE002,2 AS NCOE31,2 AS NCOS31,2 AS NCOE32,2 AS NCOS32,2 AS NCOE33,2 AS NCOS33
32100 FIELD #6,36 AS NRE003,2 AS NGRE01,2 AS NGRS01,2 AS NGRE02,2 AS NGRS02,2 AS NGRE03,2 AS NGRS03
32110 FIELD #6,48 AS NRE004,2 AS NEXE01,2 AS NEXS01,2 AS NEXE02,2 AS NEXS02,2 AS NEXE03,2 AS NEXS03
32120 FIELD #6,60 AS NRE005,2 AS NLIE01,2 AS NLIS01,2 AS NLIE02,2 AS NLIS02,2 AS NLIE03,2 AS NLIS03
32130 FIELD #6,72 AS NRE006,1 AS NCOMP1,1 AS NCOMP2,1 AS NCOMP3,1 AS NTARE1,1 AS NTARE2,1 AS NTARE3,22 AS NRE007
32140 '
32150 RETURN
32160 '
32500 '*******************************
32502 '***APERTURA FICHERO TURISMO ***
32504 '*******************************
32506 '
32508 ' Apertura del fichero de datos   FACAALUM.DAT
32510 OPEN "R",#4,"FTURALUM.DAT",350
32512 FIELD #4,5 AS GCUENTA,1 AS GSTATUS,20 AS GNOMBRE,20 AS GAPELL1,20 AS GAPELL2,40 AS GDIREC,20 AS GPOBLA,15 AS GPROVI,5 AS GCOPOS,7 AS GTELEFO,7 AS GTELEFA,13 AS GDNI,1 AS GCURSO
32514 FIELD #4,174 AS TORO1$,176 AS TORO2$
32518 FIELD #4,5 AS GCUEN1,1 AS GSTAT,2 AS GULTI,2 AS GULTACT,12 AS GFICHER,1 AS GSWG,1 AS GSWA,2 AS GVIGO,235 AS ROST1$,4 AS GFEFAC,85 AS ROST2$
32520 GET #4,1
32522  IF GFICHER<>"FTURALUM.DAT" THEN 9144
32524  ULTITU%=CVI(GULTI)
32526  ULTATU%=CVI(GULTACT):N.R%=ULTATU%
32528  VIVASTU%=CVI(GVIGO)
32530 RETURN
32532 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="E. I. D. E."
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '*********************************
35002 '*** PANTALLA DE DATOS A METER ***
35004 '*********************************
35005 COLOR 11
35006 LOCATE 6,27:PRINT "Fecha Factura : <        >"
35008 LOCATE 8,27:PRINT "Importe ..... : <          >"
35010 LOCATE 10,27:PRINT "De 75.OOO ..  : <  >"
35014 LOCATE 12,27:PRINT "Precio 1 .... : <      >"
35016 LOCATE 14,27:PRINT "Precio 2 .... : <      >"
35018 LOCATE 16,27:PRINT "Precio 3 .... : <      >"
35020 LOCATE 18,27:PRINT "Precio 4 .... : <      >"
35022 LOCATE 20,27:PRINT "Precio 5 .... : <      >"
35024 '
35026 RETURN
35028 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 CUCUCU$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(XOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(XOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(XPE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(XPE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(XPE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(XPE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(XOM$,LNOMBRE%)+" "+LEFT$(XPE1$,LAPELL1%)+", "+LEFT$(XPE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0     ':N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN GUAPRI$=GUAPRI$+".000":GOTO 64794
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
