10 '                       CREAACAD.BAS
20 '                      --------------
30 '                  GENERA FICHEROS ACADEMIA
40 '             ----------------------------------
50 '
52 ON ERROR GOTO 9100
54 '
56 '
60 '
70 OPTION BASE 1
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
92 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
94 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
100 FECHA$=RIGHT$(FDIA$,2)+"/"+MID$(FDIA$,3,2)+"/"+LEFT$(FDIA$,2)
130 GOSUB 8000
140 '*************************
150 '***PRESENTAMOS EL MENU***
160 '*************************
170 TIT$="- GENERACION FICHEROS DE ACADEMIAS -"
180 X%=80-LEN(TIT$)
190 X%=INT(X%/2)
200 '
210 LOCATE 5,X%:PRINT TIT$
220 LOCATE 6,X%:PRINT STRING$(LEN(TIT$),205)
230 '
240 LOCATE 8,4:PRINT "1.- FICHERO DE GRUPOS."
250 LOCATE 9,4:PRINT "2.- FICHERO DE BANCOS."
260 LOCATE 10,4:PRINT "3.- FICHERO DE ALUMNOS."
270 LOCATE 11,4:PRINT "4.- ESCUELA DE IDIOMAS.  "
280 LOCATE 12,4:PRINT "5.- FICHERO DE AUSENCIAS. "
290 LOCATE 13,4:PRINT "6.- FICHERO DE NOTAS. "
300 LOCATE 14,4:PRINT "7.-FICHERO ALFABETICO ALUMNOS."
310 LOCATE 15,4:PRINT "8.-FICHERO DE EMPRESAS.  "
320 LOCATE 16,4:PRINT "9.-FICHERO ALFABETICO EMPRESAS."
330 LOCATE 17,3:PRINT "10.-                      "
340 LOCATE 18,3:PRINT "11.-                     "
350 '
360 LOCATE 8,43:PRINT "12.-          "
370 LOCATE 9,43:PRINT "13.-                          "
380 LOCATE 10,43:PRINT "14.-                       "
390 LOCATE 11,43:PRINT "15.-                          "
400 LOCATE 12,43:PRINT "16.-                       "
410 LOCATE 13,43:PRINT "17.-                          "
412 LOCATE 14,43:PRINT "18.-                        "
414 LOCATE 15,43:PRINT "19.-                       "
416 LOCATE 16,43:PRINT "20.-"
418 LOCATE 17,43:PRINT "21.-"
420 LOCATE 18,43:PRINT "22.- FIN CREACION DE FICHEROS."
430 '
440 '
450 '
460 '
470 '
480 '
490 '
500 LOCATE 19,33:PRINT "OPCION : "
510 LOCATE 20,26:PRINT "ESTABLECIMIENTO : <        >"
520 '
530 F.C1%=19:C.C1%=43:L.E%=2:BO%=2:ESC%=1:GOSUB 63000
540 IF I.T%<>0 THEN 530
550 OPC$=D.E$
560 IF VAL(OPC$)<1 OR VAL(OPC$)>22 THEN 530
570 '
580 IF OPC$="22" THEN CLOSE:CLS:SYSTEM
590 '
600 F.C1%=20:C.C1%=45:L.E%=8:BO%=8:GOSUB 64000
602 IF I.T%=2 THEN LOCATE 20,45:PRINT ESTA$:GOTO 640
610 IF I.T%<>0 THEN 600
620 ESTA$=D.E$
630 '
640 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1-":GOSUB 8500
650 A$=INPUT$(1)
660 IF ASC(A$)<13 OR ASC(A$)>14 THEN 650
670 IF ASC(A$)=14 THEN 240
680 '
690 '
700 '
710 '********************************************************
720 '***TRATAMIENTO DE CREACION DE LOS DIFERENTES FICHEROS***
730 '********************************************************
740 '
750 ON VAL(OPC$) GOTO 1302,1202,1002,1402,1502,1602,1702,1802,1902,240,240,240,240,240,240,240,240,240,240,240,240
760 '
770 '
780 '
1000 '*****************************
1002 '***APERTURA FICHERO ALUMNOS**
1004 '*****************************
1006 FICHE1$="\"+ESTA$+"\FACAALUM.DAT"
1008 DEFSTR C
1010 'APERTURA FICHERO DE DATOS.
1012 OPEN "R",#7,FICHE1$,250
1014 'REGISTRO 1.
1016 FIELD #7,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,226 AS REST1$
1018 GET #7,1
1020 LSET CULTI=MKI$(1)
1022 LSET CCUEN1="     "
1024 LSET CULTACT=MKI$(1)
1026 LSET CFICHER="FACAALUM.DAT"
1028 LSET REST1$=STRING$(226," ")
1030 LSET CSTAT="A":LSET CSWG="0":LSET CSWA="0"
1032 PUT #7,1
1034 CLOSE
1106 FICHE1$="\"+ESTA$+"\FACAALUM.DIR"
1108 DEFSTR J
1110 'APERTURA FICHERO DE DATOS.
1112 OPEN "R",#8,FICHE1$,25
1116 FIELD #8,7 AS JRESTOS,1 AS JSTATU,12 AS JETIQUETA,2 AS JULTIMO,3 AS JFIL00
1118 GET #8,1
1120 LSET JULTIMO=MKI$(1)
1122 LSET JRESTOS=STRING$(7," ")
1126 LSET JETIQUETA="FACAALUM.DIR"
1128 LSET JFIL00=STRING$(3,0)
1130 LSET JSTATU="A"
1132 PUT #8,1
1134 CLOSE
1136 GOTO 240
1200 '*****************************
1202 '***APERTURA FICHERO BANCOS***
1204 '*****************************
1206 FICHE1$="\"+ESTA$+"\FACABANC.DAT"
1208 DEFSTR B
1210 'APERTURA FICHERO DE DATOS.
1212 OPEN "R",#1,FICHE1$,125
1214 'REGISTRO 1.
1216 FIELD #1,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
1218 GET #1,1
1220 LSET BULTIMO=MKI$(1)
1222 LSET BALTAS=MKI$(0)
1224 LSET BULTACT=MKI$(0)
1226 LSET BETIQUETA="FACABANC.DAT"
1228 LSET BFILLER=STRING$(106,0)
1230 LSET BSTATUS="A"
1232 PUT #1,1
1234 CLOSE
1236 GOTO 240
1300 '*****************************
1302 '***APERTURA FICHERO GRUPOS***
1304 '*****************************
1306 FICHE1$="\"+ESTA$+"\FACAGRUP.DAT"
1308 DEFSTR G
1310 'APERTURA FICHERO DE DATOS.
1312 OPEN "R",#3,FICHE1$,380
1314 'REGISTRO DE DATOS.
1316 FIELD #3,3 AS GLIB1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB3
1318 GET #3,1
1320 LSET GLIB1=STRING$(3," ")
1322 LSET GSTATUS="A"
1330 LSET GETIQUETA="FACAGRUP.DAT":LSET GULTIMO=MKI$(1):LSET GLIB2=STRING$(162," "):LSET GLIB3=STRING$(200," ")
1332 PUT #3,1
1334 CLOSE
1336 GOTO 240
1400 '*****************************
1402 '***APERTURA FICHERO ESCUELA**
1404 '*****************************
1406 FICHE1$="\"+ESTA$+"\FACAESCU.DAT"
1408 DEFSTR E
1410 'APERTURA FICHERO DE DATOS.
1412 OPEN "R",#4,FICHE1$,100
1414 'REGISTRO DE DATOS.
1416 FIELD #4,1 AS ESTAT,12 AS ETIQUETA,2 AS EULTI,2 AS EACTU,83 AS ELIB
1418 GET #4,1
1420 LSET ELIB=STRING$(83," ")
1422 LSET ESTAT="A"
1430 LSET ETIQUETA="FACAESCU.DAT":LSET EULTI=MKI$(1):LSET EACTU=MKI$(1)
1432 PUT #4,1
1434 CLOSE
1436 GOTO 240
1500 '********************************
1502 '***APERTURA FICHERO DE FALTAS***
1504 '********************************
1506 FICHE1$="\"+ESTA$+"\FACAAUSE.DAT"
1508 DEFSTR A,J
1510 'APERTURA FICHERO DE DATOS.
1512 OPEN "R",#5,FICHE1$,48
1514 'REGISTRO DE DATOS.
1515 FIELD #5,2 AS AENE,2 AS AFEB,2 AS AMAR,2 AS AABR,2 AS AMAY,2 AS AJUN,2 AS AJUL,2 AS AAGO,2 AS ASET,2 AS AOCT,2 AS ANOV,2 AS ADIC
1516 FIELD #5,24 AS JTOD,2 AS JENE,2 AS JFEB,2 AS JMAR,2 AS JABR,2 AS JMAY,2 AS JJUN,2 AS JJUL,2 AS JAGO,2 AS JSET,2 AS JOCT,2 AS JNOV,2 AS JDIC
1517 FOR A%=1 TO 9999
1518 GET #5,A%
1519 LSET AENE=MKI$(0):LSET AFEB=MKI$(0):LSET AMAR=MKI$(0):LSET AABR=MKI$(0)
1520 LSET AMAY=MKI$(0):LSET AJUN=MKI$(0):LSET AJUL=MKI$(0):LSET AAGO=MKI$(0)
1522 LSET ASET=MKI$(0):LSET AOCT=MKI$(0):LSET ANOV=MKI$(0):LSET ADIC=MKI$(0)
1526 LSET JENE=MKI$(0):LSET JFEB=MKI$(0):LSET JMAR=MKI$(0):LSET JABR=MKI$(0)
1528 LSET JMAY=MKI$(0):LSET JJUN=MKI$(0):LSET JJUL=MKI$(0):LSET JAGO=MKI$(0)
1530 LSET JSET=MKI$(0):LSET JOCT=MKI$(0):LSET JNOV=MKI$(0):LSET JDIC=MKI$(0)
1532 PUT #5,A%
1533 NEXT A%
1534 CLOSE
1536 GOTO 240
1600 '***APERTURA FICHERO DE NOTAS ***
1601 '********************************
1602 FICHE1$="\"+ESTA$+"\FACANOTA.DAT"
1603 DEFSTR N
1604 'APERTURA FICHERO DE DATOS.
1605 OPEN "R",#6,"FACANOTA.DAT",100
1606 'REGISTRO DE DATOS.
1607 FIELD #6,2 AS NCOE11,2 AS NCOS11,2 AS NCOE12,2 AS NCOS12,2 AS NCOE13,2 AS NCOS13
1608 FIELD #6,12 AS NRE001,2 AS NCOE21,2 AS NCOS21,2 AS NCOE22,2 AS NCOS22,2 AS NCOE23,2 AS NCOS23
1609 FIELD #6,24 AS NRE002,2 AS NCOE31,2 AS NCOS31,2 AS NCOE32,2 AS NCOS32,2 AS NCOE33,2 AS NCOS33
1610 FIELD #6,36 AS NRE003,2 AS NGRE01,2 AS NGRS01,2 AS NGRE02,2 AS NGRS02,2 AS NGRE03,2 AS NGRS03
1611 FIELD #6,48 AS NRE004,2 AS NEXE01,2 AS NEXS01,2 AS NEXE02,2 AS NEXS02,2 AS NEXE03,2 AS NEXS03
1612 FIELD #6,60 AS NRE005,2 AS NLIE01,2 AS NLIS01,2 AS NLIE02,2 AS NLIS02,2 AS NLIE03,2 AS NLIS03
1613 FIELD #6,72 AS NRE006,1 AS NCOMP1,1 AS NCOMP2,1 AS NCOMP3,1 AS NTARE1,1 AS NTARE2,1 AS NTARE3,22 AS NRE007
1614 '
1615 'RETURN
1616 '
1617 FOR A%=1 TO 9999
1618 GET #6,A%
1620 LSET NCOE11=MKI$(0):LSET NCOS11=MKI$(0):LSET NCOE12=MKI$(0):LSET NCOS12=MKI$(0):LSET NCOE13=MKI$(0):LSET NCOS13=MKI$(0)
1621 LSET NCOE21=MKI$(0):LSET NCOS21=MKI$(0):LSET NCOE22=MKI$(0):LSET NCOS22=MKI$(0):LSET NCOE23=MKI$(0):LSET NCOS23=MKI$(0)
1622 LSET NCOE31=MKI$(0):LSET NCOS31=MKI$(0):LSET NCOE32=MKI$(0):LSET NCOS32=MKI$(0):LSET NCOE33=MKI$(0):LSET NCOS33=MKI$(0)
1623 LSET NGRE01=MKI$(0):LSET NGRS01=MKI$(0):LSET NGRE02=MKI$(0):LSET NGRS02=MKI$(0):LSET NGRE03=MKI$(0):LSET NGRS03=MKI$(0)
1624 LSET NEXE01=MKI$(0):LSET NEXS01=MKI$(0):LSET NEXE02=MKI$(0):LSET NEXS02=MKI$(0):LSET NEXE03=MKI$(0):LSET NEXS03=MKI$(0)
1625 LSET NLIE01=MKI$(0):LSET NLIS01=MKI$(0):LSET NLIE02=MKI$(0):LSET NLIS02=MKI$(0):LSET NLIE03=MKI$(0):LSET NLIS03=MKI$(0)
1626 LSET NCOMP1="":LSET NCOMP2="":LSET NCOMP3=""
1627 LSET NTARE1="":LSET NTARE2="":LSET NTARE3=""
1628 LSET NRE007=""
1632 PUT #6,A%
1633 NEXT A%
1634 CLOSE
1636 GOTO 240
1700 '*****************************
1702 '***APERTURA FICHERO ALFAB.***
1704 '*****************************
1706 FICHE1$="\"+ESTA$+"\FACAALUM.ALF"
1708 DEFSTR F
1710 'APERTURA FICHERO DE DATOS.
1712 OPEN "R",#1,FICHE1$,65
1714 'REGISTRO 1.
1716 FIELD #1,2 AS FULTIMO,20 AS FLLAMA,20 AS FAPELL1,20 AS FAPELL2,2 AS FILLER,1 AS FSTAT
1718 GET #1,1
1720 LSET FULTIMO=MKI$(1)
1722 LSET FLLAMA=STRING$(20," ")
1724 LSET FAPELL1=STRING$(20," ")
1726 LSET FAPELL2=STRING$(20," ")
1728 LSET FILLER="  "
1730 LSET FSTAT="A"
1732 PUT #1,1
1734 CLOSE
1736 GOTO 240
1800 '*****************************
1802 '***APERTURA FICHERO EMPRESAS*
1804 '*****************************
1806 FICHE1$="\"+ESTA$+"\FEMPCLIE.DAT"
1808 DEFSTR C
1810 'APERTURA FICHERO DE DATOS.
1812 OPEN "R",#7,FICHE1$,250
1814 'REGISTRO 1.
1816 FIELD #7,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,1 AS CSWA,239 AS REST1$
1818 GET #7,1
1820 LSET CULTI=MKI$(1)
1822 LSET CCUEN1="     "
1824 LSET CULTACT=MKI$(1)
1828 LSET REST1$=STRING$(239," ")
1830 LSET CSTAT="A":LSET CSWA="0"
1832 PUT #7,1
1834 CLOSE
1900 '*****************************
1902 '***APERTURA FICHERO ALFAB.***
1904 '*****************************
1906 FICHE1$="\"+ESTA$+"\FEMPCLIE.ALF"
1908 DEFSTR F
1910 'APERTURA FICHERO DE DATOS.
1912 OPEN "R",#1,FICHE1$,45
1914 'REGISTRO 1.
1916 FIELD #1,2 AS FULTIMO,40 AS FLLAMA,2 AS FILLER,1 AS FSTAT
1918 GET #1,1
1920 LSET FULTIMO=MKI$(1)
1922 LSET FLLAMA=STRING$(40," ")
1928 LSET FILLER="  "
1930 LSET FSTAT="A"
1932 PUT #1,1
1934 CLOSE
1936 GOTO 240
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 0,7,0:LOCATE 2,3:PRINT " CREAR FICHEROS":COLOR 7,0,0:LOCATE 2,26:PRINT "   Aplicacion : PUNTO DE VENTA":LOCATE 2,63:PRINT "FECHA : "FECHA$" ": LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 20 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):LOCATE 22,3:PRINT "  Mensajes: ":LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8525 IF RET%=1 THEN FOR KK%=1 TO 15000:NEXT KK%:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PPUVMEN1"
9050 SYSTEM
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9180 ON ERROR GOTO 0
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63092  IF ASC(D.R$)=15 THEN JMG%=L.E%:I.T%=9:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63212 IF ESP%=1 AND LEN(D.E$)=2 THEN RETURN
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
