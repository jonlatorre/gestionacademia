10 '                       PTURLABA.BAS
20 '                      --------------
30 '          LISTADO NUMERICO DE ALUMNOS DADOS DE BAJA
40 '         -------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR C,G,B,F
150 OPTION BASE 1
200 DIM TABENT$(30)
520 '
522 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 'GOSUB 30010        'Apertura Fichero ALUNMOS
550 'GOSUB 32000         'APERTURA FICHERO DE TARIFAS
570 '
590 '
600 GOTO 4490
3310 '
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 IF LIN%>60 THEN GOSUB 3740   '--HACER CABECERAS--
3532 NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2
3540 GOSUB 40000    'poner apellidos y nombre en una linea.
3550 GUAPRI#=VAL(CCUENTA):GOSUB 64770:LPRINT TAB(6-L.C%) GUAPRI$;
3560 LPRINT TAB(8) LEFT$(NOMBRE$,40);
3570 LPRINT TAB(49) LEFT$(CDIREC,30);
3580 LPRINT TAB(80) LEFT$(CPOBLA,20);
3590 LPRINT TAB(101) CPROVI;
3600 'LPRINT TAB(118) USING"#####";VAL(CCOPOS);
3610 IF VAL(CTELEFO)<>0 THEN LPRINT TAB(118):LPRINT USING "#######";VAL(CTELEFO)
3642 LPRINT:LIN%=LIN%+2
3650 '
3660 RETURN
3670 '
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 IF LIN%= 90 THEN 3760
3757 LPRINT CHR$(12)
3760 PAG%=PAG%+1:LIN%=0
3770 LPRINT TAB(1) EMPRESA$;
3780 IF TIPO$="T" THEN LPRINT TAB(44) "RELACION DE BAJAS DE ALUMNOS DE TURISMO AL " FECHA$ ;:GOTO 3790
3782 IF TIPO$="S" THEN LPRINT TAB(41) "RELACION DE BAJAS DE ALUMNOS DE SECRETARIADO AL " FECHA$ ;:GOTO 3790
3784 LPRINT TAB(49) "RELACION DE BAJAS DE ALUMNOS AL " FECHA$ ;
3790 LPRINT TAB(107) USING "HOJA NUMERO : #,###";PAG%
3800 LPRINT TAB(1) STRING$(LEN(EMPRESA$),"-");
3805 IF TIPO$="T" THEN LPRINT TAB(44) STRING$(51,"-");:GOTO 3820
3810 IF TIPO$="S" THEN LPRINT TAB(41) STRING$(56,"-");:GOTO 3820
3815 LPRINT TAB(49) STRING$(40,"-");
3820 LPRINT TAB(107) STRING$(19,"-")
3840 LPRINT
3850 LPRINT TAB(1) "NUMERO";
3860 LPRINT TAB(11) "N O M B R E   Y   A P E L L I D O S";
3870 LPRINT TAB(55) "D I R E C C I O N";
3872 LPRINT TAB(81) "P O B L A C I O N";
3874 LPRINT TAB(104) "PROVINCIA";
3876 'LPRINT TAB(117) "C.POS.";
3878 LPRINT TAB(118) "TELEFONO"
3900 '
3910 LPRINT TAB(1) STRING$(6,"-");
3912 LPRINT TAB(8) STRING$(40,"-");
3914 LPRINT TAB(49) STRING$(30,"-");
3916 LPRINT TAB(80) STRING$(20,"-");
3918 LPRINT TAB(101) STRING$(15,"-");
3919 'LPRINT TAB(117) STRING$(6,"-");
3920 LPRINT TAB(117) STRING$(9,"-")
3926 'LPRINT
3928 LIN%=LIN%+7
3930 RETURN
3940 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4493 GOSUB 6000
4494 FALUDIR$="FTURALUM.ALF":FALUDAT$="FTURALUM.DAT"
4495 '
4496 GOSUB 8000
4501 '
4511 '
4512 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4513   LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
4514 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4516 COLOR 10
4520 IF TIPO$="T" THEN LOCATE 6,17:PRINT "- LISTADO DE ALUMNOS DE TURISMO DADOS DE BAJA -":LOCATE 7,17:PRINT STRING$(47,205):GOTO 4550
4530 IF TIPO$="S" THEN LOCATE 6,15:PRINT "- LISTADO DE ALUMNOS DE SECRETARIADO DADOS DE BAJA -":LOCATE 7,15:PRINT STRING$(52,205):GOTO 4550
4540 LOCATE 6,20:PRINT "- LISTADO TOTAL DE ALUMNOS DADOS DE BAJA -":LOCATE 7,20:PRINT STRING$(42,205)
4550 COLOR 11:LOCATE 12,12:PRINT "DESDE EL NUMERO : <     >    HASTA EL NUMERO : <     >"
4552 COLOR 15:LOCATE 14,25:PRINT " F10";
4554 COLOR 11:PRINT " Volver al Men£ Anterior"
4560 COLOR 15
4590 'Pedimos numero de Orden DESDE.
4600 F.C1%=12:C.C1%=31:L.E%=5:BO%=5:ESC%=0:GOSUB 63000
4610 IF I.T%=0 THEN LOCATE 12,31:PRINT D.E$:GOTO 4640
4620 IF I.T%=2 THEN CLOSE:GOTO 9000
4630 GOTO 4600
4640 DESDE$=D.E$
4645 '
4650 '
4660 '
4670 '
4680 GOSUB 8500:MEN$="- HASTA=99999, hasta el Fin de Fichero -":GOSUB 8500
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=12:C.C1%=60:L.E%=5:BO%=5:ESC%=0:GOSUB 63000
4710 IF I.T%=0 THEN LOCATE 12,60:PRINT D.E$:GOTO 4740
4720 IF I.T%=2 THEN CLOSE:GOTO 9000
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 HASTA$=D.E$
4780 '
4790 'Comprobamos DESDE con HASTA.
4800 IF DESDE$>HASTA$ THEN BEEP:GOSUB 8500:MEN$="N£mero. -DESDE- mayor que -HASTA-, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4801 PAG%=0:LIN%=90
4802   GOSUB 8500
4803   COLOR 10:LOCATE 22,15:PRINT " Datos Correctos ";
4804   COLOR 15:PRINT " ENTER ";
4805   COLOR 10:PRINT " Datos Erroneos ";
4806   COLOR 15:PRINT " F1 "
4807   COLOR 10
4814 A$=INPUT$(1)
4816 IF ASC(A$)=14 THEN CLOSE:GOTO 4490
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4820 GOSUB 34000
4830 FOR REG%=2 TO ULTIMO%
4840   GET #2,REG%
4842   IF REG%<VAL(DESDE$) THEN 4900     'AL NEXT
4844   IF REG%>VAL(HASTA$) THEN REG%=ULTIMO%:GOTO 4900  'AL NEXT
4846   'N.L%=CVI(DNORDEN$)
4848   'GET #2,N.L%
4850   IF CSTATUS<>"O" THEN 4900 'AL NEXT
4860    IF CMARCA<>"L" THEN 4900
4870  IF TIPO$=" " THEN 4890
4880  IF CESCUE<> TIPO$ THEN 4900
4890   GOSUB 3500   'LINEA DE DETALLE.
4900 NEXT REG%
4901 IF LIN%=90 THEN GOSUB 8500:MEN$="No disponemos de los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4902 LPRINT CHR$(12)
4904 CLOSE
4906 GOTO 4490
4908 '
4914 '
4970 '
6000 '*********************************
6005 '*** SELECIONAR OPCION LISTADO ***
6010 '*********************************
6015 '
6020 GOSUB 8000
6025 COLOR 11:LOCATE 5,23:PRINT "*** LISTADO DE BAJAS DE ALUMNOS ***"
6030 LOCATE 6,23:PRINT STRING$(35,205)
6035 COLOR 15:LOCATE 8,28:PRINT "1.- ";
6040 COLOR 10:PRINT "Escuela de Turismo."
6045 COLOR 15:LOCATE 10,28:PRINT "2.- ";
6047 COLOR 10:PRINT "Escuela de Secretariado."
6050 COLOR 15:LOCATE 12,28:PRINT "3.- ";
6055 COLOR 10:PRINT "Ambas Escuelas."
6060 COLOR 15:LOCATE 14,28:PRINT "4.- ";
6065 COLOR 10:PRINT "No usado.      "
6070 COLOR 15:LOCATE 16,28:PRINT "5.- ";
6075 COLOR 10:PRINT "Final de listados."
6080 COLOR 15:LOCATE 19,26:PRINT "Teclea la opcion deseada : < >"
6085 LOCATE 19,54:A$=INPUT$(1)
6090 IF A$<"1" OR A$>"5" THEN 6020
6095 LOCATE 19,54:PRINT A$
6100 IF A$="5" THEN 9000
6102 IF A$="4" THEN 6000
6105 IF A$="1" THEN TIPO$="T":RETURN
6110 IF A$="2" THEN TIPO$="S":RETURN
6115 IF A$="3" THEN TIPO$=" ":RETURN
6120 RETURN
6125 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,37:PRINT "Aplicaci¢n : ACADEMIAS  ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PTURCLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FTURALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FTURALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,14 AS CKAKA1,10 AS CCUBAN,7 AS CKAKA2,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,1 AS CESCUE,9 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FTURALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30132 '
30140 RETURN
30150 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="*** TURISMO Y SECRETARIADO ***"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*****************************
34010 '***APERTURA FICHERO BANCOS***
34020 '*****************************
34030 FICHE1$="FRESBANC.DAT"
34040 '
34050 'APERTURA FICHERO DE DATOS.
34060 OPEN "R",#3,FICHE1$,125
34070 'REGISTRO 1.
34080 FIELD #3,2 AS BULTIMO,2 AS BALTAS,2 AS BULTACT,12 AS BETIQUETA,106 AS BFILLER,1 AS BSTATUS
34090 'REGISTRO DE DATOS.
34100 FIELD #3,4 AS BANCO,30 AS BNOM,30 AS BDIREC,5 AS BCPOS,30 AS BPOBLA,20 AS BPROV,5 AS BFIL001,1 AS BSTATUS
34110 GET #3,1
34120 IF BETIQUETA="FACABANC.DAT" THEN 34160  'CONTINUAR
34130 GOSUB 8500:MEN$="NO EXISTE FICHERO BANCOS, PULSE UNA TECLA":GOSUB 8500
34140 A$=INPUT$(1)
34150 GOTO 9000
34160 'CONTINUAR.
34170 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
34180 ULTBAN%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
34190 ALTDIR%=CVI(BALTAS)     'NUMERO DE ALTAS EN DIR.
34200 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
34210 N.B%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
34220 '
34230 RETURN
34240 '
34250 '*************************
34260 '***LECTURA AZAR BANCOS***
34270 '*************************
34280 'El argumento de busqueda sobre 'BUSCA$'
34290 'El numero de registros sobre 'N.B%'
34300 R.B%=0           'Donde devuelve el nro. de registro.
34310 R.W%=0           'Donde devuelve el nro. de registro del DIR.
34320 SW1%=0         'SW indicador de la busqueda.
34330 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34340 '
34350 R.I%=INT((N.B%/2)+.5)
34360 R.C%=R.I%
34370 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34490
34380 IF R.I%>N.B% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.B%:GOTO 34490
34390 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
34400 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 34660      'MIRAR STATUS
34410 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34420 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34440
34430 R.I%=R.I%+R.C%
34440 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34370
34450 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34370
34460 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34370
34470 REG2%=R.I%:SW100%=0
34480 GOTO 34370
34490 'REGISTRO NO BUSCADO.
34500 XX%=0
34510 FOR A%=DES% TO HAS%
34520   GET #3,A%
34530   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
34540 NEXT A%
34550 IF XX%=1 THEN 34660
34560 '
34570 '
34580 'Registro NO BUSCADO
34590 XX%=0
34600 IF N.B%=0 THEN N.B%=1
34610 FOR JMG%=N.B% TO ULTBAN%
34620     GET #3,JMG%
34630     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
34640 NEXT JMG%
34650 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
34660 'MIRAMOS STATUS.
34670 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
34680 WSTATUS$=""
34690 RETURN
34700 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
