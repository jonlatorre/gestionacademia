10 '                       PRESLIFA.BAS
20 '                      --------------
30 '         LISTADO DE LAS FACTURAS DEL RESTAURANTE
40 '         ---------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 GOSUB 33000
135 OPEN "I",#4,"FRESCLA.DIA"
136 LINE INPUT #4,TODO$
137 CLAVE$=LEFT$(TODO$,4)
138 CLOSE
140 DEFSTR F,O,C,H
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
174 DATA 31,28,31,30,31,30,31,31,30,31,30,31
176 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
178 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 'GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4496 'WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4498 'LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(18)
4500 GOSUB 8000
4502 COLOR 11:LOCATE 6,18:PRINT "- LISTADOS DE FACTURAS ESCUELA DE HOSTELERIA -"
4504 LOCATE 7,18:PRINT STRING$(46,205)
4506 LOCATE 10,23:PRINT "1.- LISTADO TOTAL DE FACTURAS POR NUMERO"
4508 LOCATE 12,23:PRINT "2.- LISTADO RESUMEN ENTRE DOS FECHAS"
4510 LOCATE 14,23:PRINT "3.- FINAL DE LOS LISTADOS"
4512 LOCATE 18,31:PRINT "Teclear Opci¢n : < >"
4514 LOCATE 18,49:T$=INPUT$(1)
4516 IF T$<"1" OR T$>"3" THEN BEEP:GOTO 4514
4518 COLOR 15:LOCATE 18,49:PRINT T$
4522   GOSUB 8500
4524   LOCATE 22,15:COLOR 10:PRINT "Petici¢n correcta ";
4526   COLOR 15:PRINT "  ENTER  ";
4528   COLOR 10:PRINT " Petici¢n err¢nea ";
4530   COLOR 15:PRINT "  F1 "
4532   A$=INPUT$(1)
4534   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4532
4536   IF ASC(A$)=14 THEN 4500
4538   IF T$="3" THEN 9000
4540   IF T$="1" THEN 7000
4542   LI$="- LISTADO RESUMEN DE FACTURAS ENTRE DOS FECHAS -"
4550   GOSUB 8000
4552   COLOR 10:LOCATE 6,17:PRINT LI$
4554   LOCATE 7,17:PRINT STRING$(48,205)
4556   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
4558   COLOR 15:LOCATE 16,28:PRINT "F7 ==>  ";:COLOR 11:PRINT "Final de la Opci¢n":COLOR 15
4600       '***********************
4602       '***ENTRADA DE FDESDE***
4604       '***********************
4606       F.C1%=12:C.C1%=31:L.E%=6:BO%=8:GOSUB 63000
4608       IF I.T%=0 THEN DESDE$=D.E$:GOTO 4614
4609       IF I.T%=4 THEN 4500
4610       GOTO 4600
4612       '
4614       FECSUB$=DESDE$:GOSUB 60000
4616       IF ERF%<>1 THEN 4622
4618       BEEP:GOSUB 8500:MEN$="Fecha Desde Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,31:PRINT SPC(8)
4620       GOTO 4602
4622       LOCATE 12,31:PRINT FDMA$:FDES$=FAMD$:FLI1$=FDMA$
4700       '***********************
4702       '***ENTRADA DE FHASTA***
4704       '***********************
4706       F.C1%=12:C.C1%=58:L.E%=6:BO%=8:GOSUB 63000
4708       IF I.T%=0 THEN HASTA$=D.E$:GOTO 4714
4709       IF I.T%=4 THEN 4500
4710       GOTO 4700
4712       '
4714       FECSUB$=HASTA$:GOSUB 60000
4716       IF ERF%<>1 THEN 4722
4718       BEEP:GOSUB 8500:MEN$="Fecha Hasta Err¢nea, Pulsar una tecla":GOSUB 8500:A$=INPUT$(1):LOCATE 12,58:PRINT SPC(8)
4720       GOTO 4702
4722       LOCATE 12,58:PRINT FDMA$:FHAS$=FAMD$:FLI2$=FDMA$
4730   GOSUB 8500
4732   LOCATE 22,15:COLOR 10:PRINT "Fechas correctas ";
4734   COLOR 15:PRINT "  ENTER  ";
4736   COLOR 10:PRINT " Fechas err¢neas ";
4738   COLOR 15:PRINT "  F1 "
4740   A$=INPUT$(1)
4742   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4740
4744   IF ASC(A$)=14 THEN 4540
4746   '
4748   IF FHAS$<FDES$ THEN GOSUB 8500:MEN$="Fechas err¢neas, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 4540
4750   '
5500 '********************************
5502 '***LISTADOS GENERALES DE CAJA***
5504 '********************************
5506 '
5508 '
5510 GOSUB 32000
5512 OPEN "O",#5,"FRESOPER.SEL"
5514 FOR NN!=2 TO ULFA!
5516 GET #3,NN!
5518 IF FSTA<>"A" THEN 5560
5520 '
5522 IF FFEC<FDES$ THEN 5560
5524 IF FFEC>FHAS$ THEN 5560
5546 NOPE$=STR$(NN!):CE%=LEN(NOPE$)-1
5548 NOPE$=LEFT$(CERO$,6-CE%)+RIGHT$(NOPE$,CE%)
5550 CANT#=CVD(FIMP):CANT$=STR$(CANT#):CE%=LEN(CANT$)-1
5552 CANT$=LEFT$(CERO$,15-CE%)+RIGHT$(CANT$,CE%)
5554 SALIDA$=NOPE$+CANT$+CLAVE$+FFEC+FMES+FCAM+"****"
5556 PRINT #5,SALIDA$
5560 NEXT NN!
5562 CLOSE
5564 SHELL "RESFACT.BAT"
5566 GOSUB 6000
5568 CLOSE
5570 GOTO 4490
5600 '***************************
5602 '***LISTADO LINEA DETALLE***
5604 '***************************
5606 '
5610 IF LIN%<43 THEN 5618
5612 IF LIN%=90 THEN 5616
5614 LPRINT CHR$(12)
5616 LIN%=0:PAG%=PAG%+1:GOSUB 5700
5618 GUAPRI#=VAL(CNUM):GOSUB 64770
5620 LPRINT TAB(11-L.C%) GUAPRI$;
5622 LPRINT TAB(20) RIGHT$(CFEC,2)"-"MID$(CFEC,3,2)"-"LEFT$(CFEC,2);
5628 LPRINT TAB(37) CMES;
5630 '
5632 LPRINT TAB(53) CCAM;
5634 GUAPRI#=VAL(CIMP):GOSUB 64770
5636 '
5638 LPRINT TAB(75-L.C%) GUAPRI$
5640 LIN%=LIN%+1
5642 '
5644 RETURN
5646 '
5690 '***************************
5692 '***CABECERAS DEL LISTADO***
5694 '***************************
5700 LPRINT TAB(6) "ESCUELA DE HOSTELERIA";
5701 LPRINT TAB(30) "FACTURAS DE HOSTELERIA AL " FECHA$;
5702 LPRINT TAB(66) USING "HOJA : ###";PAG%
5704 LPRINT TAB(6) STRING$(21,205);
5705 LPRINT TAB(30) STRING$(34,205);
5706 LPRINT TAB(66) STRING$(10,205)
5708 LPRINT
5710 LPRINT TAB(19) "DESDE EL DIA " FLI1$ " HASTA EL DIA " FLI2$
5712 LPRINT
5716 LPRINT TAB(6) "NUMERO         FECHA.         MESA        CAMARERO           IMPORTE  "
5718 LPRINT TAB(6) "ÍÍÍÍÍÍ        ÍÍÍÍÍÍÍÍ        ÍÍÍÍ        ÍÍÍÍÍÍÍÍ        ÍÍÍÍÍÍÍÍÍÍÍÍ":LPRINT:LIN%=8
5720 '
5722 RETURN
6000 '*************************
6002 '***INFORME DEL FICHERO***
6004 '*************************
6006 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
6008 WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
6010 LPRINT CHR$(27)+"&l1O"+CHR$(27)+"&a98L";
6012 GOSUB 8000
6014   LI$="- LISTADO RESUMEN DE FACTURAS ENTRE DOS FECHAS -"
6016   COLOR 10:LOCATE 6,17:PRINT LI$
6018   LOCATE 7,17:PRINT STRING$(48,205)
6020   LOCATE 12,15:PRINT "Desde el dia : <        >  hasta el dia : <        >"
6022       LOCATE 12,31:PRINT FLI1$
6024       LOCATE 12,58:PRINT FLI2$
6026   GOSUB 8500:MEN$="Espere por favor........":GOSUB 8500
6028 OPEN "I",#5,"FRESOPER.CLA"
6050 FF$=""
6060 LIN%=90
6070 PAG%=0
6080 '
6140 GOSUB 6600   'LECTURA SECUENCIAL COBROS Y PAGOS PENDIENTES.
6150 '
6160 IF FF$="FF" THEN GOSUB 8500:MEN$="No tenemos los datos pedidos, pulse una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4490
6170 TOOP#=0:TOTA#=0
6200 '
6210 WHILE FF$<>"FF"
6220   FECH$=CFEC
6230   TODIOP#=0:TODIPA#=0
6290   '
6300  WHILE FF$<>"FF" AND FECH$=CFEC
6310    '
6320    IMPORTE#=VAL(CIMP)
6330    TODIOP#=TODIOP#+1
6370    TODIPA#=TODIPA#+IMPORTE#
6380    '
6410    GOSUB 5600   'LINEA DE DETALLE
6420    GOSUB 6600   'HACER LECTURA SECUENCIAL
6430     '
6440  WEND
6450  'ROTURA POR DIA.
6460  GUAPRI#=TODIOP#:GOSUB 64770
6462  LPRINT
6464  LPRINT TAB(7) "Total dia    "RIGHT$(FECH$,2)"-"MID$(FECH$,3,2)"-"LEFT$(FECH$,2);
6466  LPRINT TAB(36-L.C%) GUAPRI$;
6467  LPRINT TAB(38) "Facturas por un importe de ";
6468  GUAPRI#=TODIPA#:GOSUB 64770
6470  LPRINT TAB(75-L.C%) GUAPRI$
6472  TOOP#=TOOP#+TODIOP#:TOTA#=TOTA#+TODIPA#
6474  LPRINT:LIN%=LIN%+3
6480  '
6490 WEND
6500 'ROTURA POR FIN DE FICHERO.
6510  GUAPRI#=TOOP#:GOSUB 64770
6512  LPRINT
6514  LPRINT TAB(7) "Total general ";
6516  LPRINT TAB(36-L.C%) GUAPRI$;
6517  LPRINT TAB(38) "Facturas por un importe de ";
6518  GUAPRI#=TOTA#:GOSUB 64770
6520  LPRINT TAB(75-L.C%) GUAPRI$
6530 LPRINT CHR$(12)
6540 '
6550 CLOSE
6560 GOTO 4490
6570 '
6600 '***************************
6602 '***LEER FICHERO DE DATOS***
6604 '***************************
6606 '
6608 IF EOF(5) THEN FF$="FF":RETURN
6610 LINE INPUT #5,TODO$
6612 CNUM=LEFT$(TODO$,6)
6614 CIMP=MID$(TODO$,7,15)
6616 CCLA=MID$(TODO$,22,4)
6618 CFEC=MID$(TODO$,26,6)
6620 CMES=MID$(TODO$,32,2)
6622 CCAM=MID$(TODO$,34,3)
6624 CTES=MID$(TODO$,37,4)
6626 '
6628 RETURN
6630 '
6632 '
7000   LI$="- LISTADO DE FACTURAS ENTRE DOS NUMEROS -"
7002   GOSUB 8000
7004   COLOR 10:LOCATE 6,20:PRINT LI$
7006   LOCATE 7,20:PRINT STRING$(41,205)
7008   LOCATE 12,16:PRINT "Desde el N§. : <      >    hasta el N§. : <      >"
7010   COLOR 15:LOCATE 16,28:PRINT "F7 ==>  ";:COLOR 11:PRINT "Final de la Opci¢n":COLOR 15
7012       '***********************
7014       '***ENTRADA DE FDESDE***
7016       '***********************
7018       F.C1%=12:C.C1%=32:L.E%=5:BO%=6:GOSUB 63000
7020       IF I.T%=0 THEN DESDE!=VAL(D.E$):GOTO 7028
7022       IF I.T%=4 THEN 4500
7024       GOTO 7012
7026       '
7028       GUAPRI#=DESDE!:GOSUB 64770
7036       LOCATE 12,32:PRINT SPC(6):LOCATE 12,37-L.C%:PRINT GUAPRI$
7038       '***********************
7040       '***ENTRADA DE FHASTA***
7042       '***********************
7044       F.C1%=12:C.C1%=59:L.E%=5:BO%=6:GOSUB 63000
7046       IF I.T%=0 THEN HASTA!=VAL(D.E$):GOTO 7054
7048       IF I.T%=4 THEN 4500
7050       GOTO 7038
7052       '
7054       GUAPRI#=HASTA!:GOSUB 64770
7056       LOCATE 12,59:PRINT SPC(6):LOCATE 12,64-L.C%:PRINT GUAPRI$
7064   GOSUB 8500
7066   LOCATE 22,15:COLOR 10:PRINT "Petici¢n correcta ";
7068   COLOR 15:PRINT "  ENTER  ";
7070   COLOR 10:PRINT " Petici¢n err¢nea ";
7072   COLOR 15:PRINT "  F1 "
7074   A$=INPUT$(1)
7076   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 7074
7078   IF ASC(A$)=14 THEN 7000
7080   '
7082   IF HASTA!=>DESDE! THEN 7084
7083   GOSUB 8500:MEN$="N£meros err¢neos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 7000
7084   '
7086 WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
7088 LPRINT CHR$(27)+"&l1O"+CHR$(27)+"&a98L";
7094 '
7096 GOSUB 32000:GOSUB 32500
7098 IF DESDE!<2 OR DESDE!>ULFA! THEN 7083
7100 IF HASTA!<2 OR HASTA!>ULFA! THEN 7083
7102 FOR NN!=DESDE! TO HASTA!
7104 GET #3,NN!
7106 IF FSTA<>"A" THEN 7204
7110 '
7112 '
7114 LPRINT
7116 LPRINT TAB(7) "ESCUELA DE HOSTELERIA DE SANTURCE S.L."
7118 LPRINT TAB(7) "Genaro Ora , 6 - Tel. 461 95 55 - 48980 SANTURCE"
7120 LPRINT TAB(7) "C.I.F. : B-48-300.073"
7122 LPRINT TAB(7) "Restaurante : Jos‚ Miguel de Barandiaran, 3"
7124 LPRINT TAB(7) "Tel. 462 58 23 - Fax 461 57 23 - 48980 SANTURCE"
7126 LPRINT:LPRINT
7128 LPRINT TAB(7) STRING$(68,196)
7130 LPRINT TAB(7) "FACTURA NUMERO :";
7132 GUAPRI#=NN!:GOSUB 64770
7134 LPRINT TAB(29-L.C%) GUAPRI$;
7136 LPRINT TAB(61) "FECHA " RIGHT$(FFEC,2)"-"MID$(FFEC,3,2)"-"LEFT$(FFEC,2)
7138 LPRINT TAB(7) STRING$(68,196)
7140 LPRINT TAB(7) "NUMERO DE MESA : " FMES;
7142 LPRINT TAB(61) "CAMARERO : " FCAM
7144 LPRINT TAB(7) STRING$(68,196):LPRINT
7146 LPRINT TAB(7) "Cantidad            C O N C E P T O S          Precio  I M P O R T E"
7148 LPRINT TAB(7) "ÄÄÄÄÄÄÄÄ  ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ  ÄÄÄÄÄÄ  ÄÄÄÄÄÄÄÄÄÄÄÄÄ"
7150 TOTAL#=0:TOFAC#=0
7152 FOR A!=CVS(FPRI) TO CVS(FULT)
7154 GET #4,A!
7156 IF CVI(OCAN)=0 THEN 7162
7158 GUAPRI#=CVI(OCAN):GOSUB 64770
7160 LPRINT TAB(12-L.C%) GUAPRI$;
7162 LPRINT TAB(17) LEFT$(OTES,35);
7164 IF CVS(OPRE)=0 THEN 7170
7166 GUAPRI#=CVS(OPRE):GOSUB 64770
7168 LPRINT TAB(59-L.C%) GUAPRI$;
7170 GUAPRI#=CVD(OIMP):GOSUB 64770
7172 LPRINT TAB(74-L.C%) GUAPRI$
7174 TOFAC#=TOFAC#+CVD(OIMP)
7176 NEXT A!
7178 LPRINT TAB(62) STRING$(13,196)
7180 LPRINT TAB(45) "SUMA ...........";
7182 GUAPRI#=TOFAC#:GOSUB 64770
7184 LPRINT TAB(74-L.C%) GUAPRI$
7186 IF FIVA="S" THEN LPRINT TAB(45) "I.V.A. .........      Incluido":GOTO 7196
7188 IVA#=INT(TOFAC#*6/100):GUAPRI#=IVA#:GOSUB 64770
7190 LPRINT TAB(45) "I.V.A. 6 % .....";
7192 LPRINT TAB(74-L.C%) GUAPRI$
7194 LPRINT TAB(62) STRING$(13,196)
7196 TOTAL#=TOFAC#+IVA#:GUAPRI#=TOTAL#:GOSUB 64770
7198 LPRINT TAB(45) "IMPORTE TOTAL ..";
7200 LPRINT TAB(74-L.C%) GUAPRI$
7202 LPRINT CHR$(12)
7204 NEXT NN!
7206 CLOSE
7208 GOTO 4490
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,36:PRINT "Aplicaci¢n : RESTAURANTE":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188)
8070 'FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESFACT"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO DIARIO  ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30090 ' Apertura del fichero de datos   FRESCAJA.DIA
30095 '
30100 OPEN "R",#1,FICHERO$,90
30105 '
30110 FIELD #1,1 AS HSTA,8 AS HINI,8 AS HCOB,8 AS HPAG,8 AS HCTA,8 AS HPTA,8 AS HCPL,8 AS HPPL,8 AS HCOT,8 AS HPOT,8 AS HFIN,9 AS HRES
30114 '
30128 RETURN
30130 '
30140 '
30500 '**********************************
30510 '***APERTURA FICHERO OPERACIONES***
30520 '**********************************
30525 'Comprobamos la existencia del fichero.
30552 '
30590 ' Apertura del fichero de datos   FRESOPER.DAT
30595 '
30600 OPEN "R",#2,"FRESOPER.DAT",80
30605 '
30610 FIELD #2,4 AS CNUM,1 AS CSTA,1 AS CMAR,4 AS CCLA,6 AS CFEC,1 AS CCOP,1 AS CTIP,35 AS CTES,8 AS CIMP,19 AS CRES
30611 FIELD #2,4 AS CULT,1 AS CST1,1 AS CMA1,74 AS CFIL
30614 '
30616 GET #2,1
30617 'LSET CULT=MKS$(1)
30618 'LSET CST1="A":LSET CMA1="A":LSET CFIL=""
30619 'PUT #2,1
30620 'CLOSE
30621 'STOP
30622 '
30624 ULTI!=CVS(CULT)
30628 RETURN
30630 '
30640 '
32000 '****************************************
32010 '***APERTURA FICHERO FACTURAS REDUCIDO***
32020 '****************************************
32025 'Comprobamos la existencia del fichero.
32090 ' Apertura del fichero de datos   FRESFACT.RED
32095 '
32100 OPEN "R",#3,"FRESFACT.RED",30
32105 '
32110 FIELD #3,1 AS FSTA,4 AS FPRI,4 AS FULT,8 AS FIMP,2 AS FMES,3 AS FCAM,1 AS FTIP,1 AS FIVA,6 AS FFEC
32112 FIELD #3,1 AS FST1,4 AS FULTIMO,25 AS FRE1
32114 '
32116 GET #3,1
32118  'LSET FST1="A":LSET FULTIMO=MKS$(1):LSET FRE1=""
32120  'PUT #3,1
32122  'CLOSE:STOP
32124 ULFA!=CVS(FULTIMO)
32126 '
32128 RETURN
32130 '
32140 '
32500 '**********************************
32510 '***APERTURA FICHERO DE FACTURAS***
32520 '**********************************
32590 ' Apertura del fichero de datos   FRESFACT.DAT
32595 '
32600 OPEN "R",#4,"FRESFACT.DAT",80
32605 '
32610 FIELD #4,4 AS OFACT,1 AS OSTA,1 AS OMAR,6 AS OFEC,2 AS OCOD,40 AS OTES,2 AS OCAN,4 AS OPRE,8 AS OIMP,12 AS ORES
32611 FIELD #4,4 AS OULT,1 AS OST1,1 AS OMA1,74 AS OFIL
32614 '
32616 GET #4,1
32617 'LSET OULT=MKS$(1)
32618 'LSET OST1="A":LSET OMA1="A":LSET OFIL=""
32619 'PUT #4,1
32620 'CLOSE
32621 'STOP
32624 OLTI!=CVS(OULT)
32628 RETURN
32630 '
32640 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
