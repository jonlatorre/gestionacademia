10 '                       PACAAUSE.BAS
20 '                      --------------
30 '          TRATAMIENTO DE LAS AUSENCIAS DE ALUMNOS
40 '          ---------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(50,".")
100 '
130 FEDIA$=RIGHT$(DATE$,4)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,5,2)+"-"+LEFT$(FEDIA$,4)
134 '
140 DEFSTR C,A,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),FAL%(12),PERI$(12),JUS%(12),PERA$(12)
170 '
172 '
174 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SETIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
176 FOR I%=1 TO 12:READ PERA$(I%):NEXT I%
178 '
200 DATA "Enero","Febrero","Marzo","Abril","Mayo","Junio","Julio","Agosto","Setiembre","Octubre","Noviembre","Diciembre"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '*********************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
310 '*********************************
315 TIT$="- TRATAMIENTO DEL FICHERO DE AUSENCIAS -"
320 X%=80-LEN(TIT$):X%=INT(X%/2)
325 GOSUB 8000
330 LOCATE 4,X%:PRINT TIT$
335 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
340 LOCATE 7,26:PRINT "1.- INTRODUCIR AUSENCIAS."
345 LOCATE 8,26:PRINT "2.- QUITAR AUSENCIAS."
350 LOCATE 9,26:PRINT "3.- CONSULTAR AUSENCIAS."
355 LOCATE 10,26:PRINT "4.- LISTAR AUSENCIAS."
360 LOCATE 11,26:PRINT "5.- SACAR CARTAS DE AUSENCIAS."
365 LOCATE 12,26:PRINT "6.- No se usa."
370 LOCATE 13,26:PRINT "7.- No se usa."
375 LOCATE 14,26:PRINT "8.- No se usa."
380 LOCATE 15,26:PRINT "9.- No se usa."
385 LOCATE 16,25:PRINT "10.- No se usa."
390 LOCATE 17,25:PRINT "11.- FINAL DE AUSENCIAS."
395 LOCATE 19,24:COLOR 12:PRINT " ESC ";
400 COLOR 15:PRINT " Finalizar ";
405 COLOR 11:PRINT " OPCION : <  > "
410 '
415 'PEDIMOS OPCION.
420 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
425 IF I.T%=4 THEN COLOR 15:GOTO 9000
430 IF I.T%<>0 THEN 420
435 CLOSE:COLOR 15
440 OPC%=VAL(D.E$)
445 IF OPC%<1 OR OPC%>11 THEN 395
450 ON OPC% GOTO 500,500,2500,6000,4000,300,300,300,300,300,9000
455 '
460 '
465 '
500 IF OPC%=1 THEN TIT$="- INTRODUCIR AUSENCIAS -":GOTO 510
505 TIT$="  - QUITAR AUSENCIAS -  "
510 '
511 GOSUB 8000
512 COLOR 10
514 LOCATE 5,28:PRINT TIT$
516 LOCATE 6,28:PRINT STRING$(LEN(TIT$),205)
518 COLOR 11:LOCATE 8,6:PRINT "Teclea Mes : <  >
520       '**********************
522       '***ENTRADA DEL MES ***
524       '**********************
526       COLOR 15:F.C1%=8:C.C1%=20:L.E%=2:BO%=2:GOSUB 63000
528       IF I.T%=0 THEN MES$=D.E$:GOTO 532
530       BEEP:GOTO 526
532       IF MES$<"01" OR MES$>"12" THEN 530
534       MES%=VAL(MES$):LOCATE 8,20:PRINT MES$
536   '*************************************
538   '***PEDIMOS CONFORMIDAD A LOS DATOS***
540   '*************************************
542   GOSUB 8500
544   COLOR 10:LOCATE 22,15:PRINT "Mes Correcto ";
546   COLOR 15:PRINT " ENTER ";
548   COLOR 10:PRINT " Mes Err¢neo ";
550   COLOR 15:PRINT " F1 "
552   COLOR 10
554   '
556   A$=INPUT$(1)
558   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 556
560   '
562   IF ASC(A$)=13 THEN 566
564   LOCATE 8,20:PRINT SPC(2):GOTO 526
566   '
700 '
702 '
704 '
706 '
718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
720 GOSUB 30000:GOSUB 31500
724 COLOR 11:LOCATE 10,6:PRINT "Alumno N§. : <" SPACE$(6) ">"
728 GOSUB 7650
730       '*******************************
732       '***ENTRADA NUMERO DE ALUMNO ***
734       '*******************************
736       COLOR 15
738       F.C1%=10:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 758 'Entrada OK
741       IF I.T%=4 THEN CLOSE:GOTO 300
742       BEEP:LOCATE 10,20:PRINT SPC(6):GOTO 732
756       '
758       REG%=VAL(CUENTA$)
760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 10,25-L.C%:PRINT GUAPRI$
762     ' GET #2,REG%  'BUSCAR REGISTRO
764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
765       GET #2,REG%  'BUSCAR REGISTRO
766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
770       LOCATE 10,28:PRINT CUCUCU$
774       '
776       '
778       '
780       '
878       COLOR 11:LOCATE 12,6:PRINT "N§. Faltas : <" SPACE$(2) ">"
880 COLOR 15
976       '**********************
978       '***ENTRADA NUMERO  ***
980       '**********************
982       F.C1%=12:C.C1%=20:L.E%=2:BO%=2:GOSUB 63000
983       IF I.T%=0 THEN FALTA$=D.E$:GOTO 1000
985       GOTO 982
1000      FALTA%=VAL(FALTA$)
1070      LOCATE 12,20:PRINT FALTA$
1100      COLOR 11:LOCATE 14,6:PRINT "Justifica. : <" SPACE$(2) ">"
1102      COLOR 15
1104      '**********************
1106      '***ENTRADA NUMERO  ***
1108      '**********************
1110      F.C1%=14:C.C1%=20:L.E%=2:BO%=2:GOSUB 63000
1112      IF I.T%=0 THEN JUSTI$=D.E$:GOTO 1116
1114      GOTO 1110
1116      JUSTI%=VAL(JUSTI$)
1118      LOCATE 14,20:PRINT JUSTI$
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2042
2028  'LOCATE 8,2:PRINT SPC(78)
2030   LOCATE 10,2:PRINT SPC(78)
2032   LOCATE 12,2:PRINT SPC(78)
2033   LOCATE 14,2:PRINT SPC(78)
2034   GOTO 724
2042   '
2054   GET #5,REG%
2056   ENE%=CVI(AENE):FEB%=CVI(AFEB):MAR%=CVI(AMAR):ABR%=CVI(AABR)
2057   MAY%=CVI(AMAY):JUN%=CVI(AJUN):JUL%=CVI(AJUL):AGO%=CVI(AAGO)
2059   SET%=CVI(ASET):OCT%=CVI(AOCT):NOV%=CVI(ANOV):DIC%=CVI(ADIC)
2061   JNE%=CVI(AJNE):JEB%=CVI(AJEB):JAR%=CVI(AJAR):JBR%=CVI(AJBR)
2063   JAY%=CVI(AJAY):JJN%=CVI(AAUN):JJL%=CVI(AAUL):JGO%=CVI(AJGO)
2064   JET%=CVI(AJET):JCT%=CVI(AJCT):JOV%=CVI(AJOV):JIC%=CVI(AJIC)
2066   IF OPC%=2 THEN FALTA%=FALTA%*-1:JUSTI%=JUSTI%*-1
2098   '
2100   IF MES%=1 THEN ENE%=ENE%+FALTA%:JNE%=JNE%+JUSTI%:GOTO 2260
2102   IF MES%=2 THEN FEB%=FEB%+FALTA%:JEB%=JEB%+JUSTI%:GOTO 2260
2104   IF MES%=3 THEN MAR%=MAR%+FALTA%:JAR%=JAR%+JUSTI%:GOTO 2260
2106   IF MES%=4 THEN ABR%=ABR%+FALTA%:JBR%=JBR%+JUSTI%:GOTO 2260
2108   IF MES%=5 THEN MAY%=MAY%+FALTA%:JAY%=JAY%+JUSTI%:GOTO 2260
2110   IF MES%=6 THEN JUN%=JUN%+FALTA%:JJN%=JJN%+JUSTI%:GOTO 2260
2112   IF MES%=7 THEN JUL%=JUL%+FALTA%:JJL%=JJL%+JUSTI%:GOTO 2260
2114   IF MES%=8 THEN AGO%=AGO%+FALTA%:JGO%=JGO%+JUSTI%:GOTO 2260
2116   IF MES%=9 THEN SET%=SET%+FALTA%:JET%=JET%+JUSTI%:GOTO 2260
2118   IF MES%=10 THEN OCT%=OCT%+FALTA%:JCT%=JCT%+JUSTI%:GOTO 2260
2120   IF MES%=11 THEN NOV%=NOV%+FALTA%:JOV%=JOV%+JUSTI%:GOTO 2260
2122   DIC%=DIC%+FALTA%:JIC%=JIC%+JUSTI%
2260   '
2270   LSET AENE=MKI$(ENE%):LSET AFEB=MKI$(FEB%):LSET AMAR=MKI$(MAR%)
2272   LSET AJNE=MKI$(JNE%):LSET AJEB=MKI$(JEB%):LSET AJAR=MKI$(JAR%)
2280   LSET AABR=MKI$(ABR%):LSET AMAY=MKI$(MAY%):LSET AJUN=MKI$(JUN%)
2282   LSET AJBR=MKI$(JBR%):LSET AJAY=MKI$(JAY%):LSET AAUN=MKI$(JJN%)
2290   LSET AJUL=MKI$(JUL%):LSET AAGO=MKI$(AGO%):LSET ASET=MKI$(SET%)
2292   LSET AAUL=MKI$(JJL%):LSET AJGO=MKI$(JGO%):LSET AJET=MKI$(JET%)
2295   LSET AOCT=MKI$(OCT%):LSET ANOV=MKI$(NOV%):LSET ADIC=MKI$(DIC%)
2297   LSET AJCT=MKI$(JCT%):LSET AJOV=MKI$(JOV%):LSET AJIC=MKI$(JIC%)
2300   PUT #5,REG%
2302   LOCATE 10,2:PRINT SPC(78)
2304   LOCATE 12,2:PRINT SPC(78)
2305   LOCATE 14,2:PRINT SPC(78)
2306   GOTO 724
2500 '
2505 TIT$="  - CONSULTAS DE AUSENCIAS -  "
2510 '
2511 GOSUB 8000
2512 COLOR 10
2514 LOCATE 5,28:PRINT TIT$
2516 LOCATE 6,28:PRINT STRING$(LEN(TIT$),205)
2718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
2720 GOSUB 30000:GOSUB 31500
2724 COLOR 11:LOCATE 8,6:PRINT "Alumno N§. : <" SPACE$(6) ">"
2728 GOSUB 7650
2730       '*******************************
2732       '***ENTRADA NUMERO DE ALUMNO ***
2734       '*******************************
2736       COLOR 15
2738       F.C1%=8:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2758 'Entrada OK
2741       IF I.T%=4 THEN CLOSE:GOTO 300
2742       BEEP:LOCATE 8,20:PRINT SPC(6):GOTO 2732
2756       '
2758       REG%=VAL(CUENTA$)
2760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 8,25-L.C%:PRINT GUAPRI$
2762     ' GET #2,REG%  'BUSCAR REGISTRO
2764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N£mero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2765       GET #2,REG%  'BUSCAR REGISTRO
2766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
2770       LOCATE 8,28:PRINT CUCUCU$
2774       GET #5,REG%
2780       LOCATE 10,5:PRINT USING "Setiembre ....  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(ASET);CVI(AJET)
2782       LOCATE 11,5:PRINT USING "Octubre ......  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AOCT);CVI(AJCT)
2784       LOCATE 12,5:PRINT USING "Noviembre ....  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(ANOV);CVI(AJOV)
2786       LOCATE 13,5:PRINT USING "Diciembre ....  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(ADIC);CVI(AJIC)
2788       LOCATE 14,5:PRINT USING "Enero ........  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AENE);CVI(AJNE)
2790       LOCATE 15,5:PRINT USING "Febrero ......  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AFEB);CVI(AJEB)
2792       LOCATE 16,5:PRINT USING "Marzo ........  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AMAR);CVI(AJAR)
2794       LOCATE 17,5:PRINT USING "Abril ........  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AABR);CVI(AJBR)
2796       LOCATE 18,5:PRINT USING "Mayo .........  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AMAY);CVI(AJAY)
2798       LOCATE 19,5:PRINT USING "Junio ........  ##  Ausencias de las cuales  ##  han sido Justificadas";CVI(AJUN);CVI(AAUN)
2800       GOSUB 8500:MEN$="Para otra Consulta pulsa una tecla":GOSUB 8500
2805       A$=INPUT$(1):CLOSE:GOTO 2500
4000 '
4005 TIT$="- LISTAR CARTAS DE AUSENCIAS -"
4010 '
4011 GOSUB 8000
4012 COLOR 10
4014 LOCATE 5,26:PRINT TIT$
4016 LOCATE 6,26:PRINT STRING$(LEN(TIT$),205)
4018 COLOR 11:LOCATE 10,28:PRINT "Teclea Mes a listar : <  >
4020       '**********************
4022       '***ENTRADA DEL MES ***
4024       '**********************
4026       COLOR 15:F.C1%=10:C.C1%=51:L.E%=2:BO%=2:GOSUB 63000
4028       IF I.T%=0 THEN MES$=D.E$:GOTO 4032
4030       BEEP:GOTO 4026
4032       IF MES$<"01" OR MES$>"12" THEN 4030
4034       MES%=VAL(MES$):LOCATE 10,51:PRINT MES$
4036      COLOR 11:LOCATE 12,28:PRINT "N§. tope ausencias  : <" SPACE$(2) ">"
4038      COLOR 15
4040      '**********************
4042      '***ENTRADA NUMERO  ***
4044      '**********************
4046      F.C1%=12:C.C1%=51:L.E%=2:BO%=2:GOSUB 63000
4048      IF I.T%=0 THEN FALTA$=D.E$:GOTO 4052
4050      GOTO 4046
4052      FALTA%=VAL(FALTA$)
4054      LOCATE 12,51:PRINT FALTA$
4056   '*************************************
4058   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4060   '*************************************
4062   GOSUB 8500
4064   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4066   COLOR 15:PRINT " ENTER ";
4068   COLOR 10:PRINT " Datos Erroneos ";
4070   COLOR 15:PRINT " F1 "
4072   COLOR 10
4074   '
4076   A$=INPUT$(1)
4078   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4076
4080   '
4082   IF ASC(A$)=13 THEN 4090
4084   LOCATE 10,2:PRINT SPC(78)
4086   LOCATE 12,2:PRINT SPC(78)
4088   GOTO 4000
4090   '
4095 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
4100   GOSUB 30000:GOSUB 31500
4102   WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4103   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B";
4105   FOR NN%=2 TO ULTIMO%
4110   GET #2,NN%
4112   IF CMARCA="L" THEN 4990
4115   FE1#=VAL(FEDIA$):FE2#=VAL(CFENA):DIF#=FE1#-FE2#
4120   IF DIF#>199999! THEN 4990
4125  GET #5,NN%
4130  FAL%(1)=CVI(AENE):FAL%(2)=CVI(AFEB):FAL%(3)=CVI(AMAR):FAL%(4)=CVI(AABR)
4132  JUS%(1)=CVI(AJNE):JUS%(2)=CVI(AJEB):JUS%(3)=CVI(AJAR):JUS%(4)=CVI(AJBR)
4135  FAL%(5)=CVI(AMAY):FAL%(6)=CVI(AJUN):FAL%(7)=CVI(AJUL):FAL%(8)=CVI(AAGO)
4137  JUS%(5)=CVI(AJAY):JUS%(6)=CVI(AAUN):JUS%(7)=CVI(AAUL):JUS%(8)=CVI(AJGO)
4140  FAL%(9)=CVI(ASET):FAL%(10)=CVI(AOCT):FAL%(11)=CVI(ANOV):FAL%(12)=CVI(ADIC)
4142  JUS%(9)=CVI(AJET):JUS%(10)=CVI(AJCT):JUS%(11)=CVI(AJOV):JUS%(12)=CVI(ADIC)
4145  IF FAL%(MES%)<FALTA% THEN 4990
4150  NOM$="Sres.":APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4152  DESTI$=CUCUCU$
4155  NOM$=CPOBLA:APE1$="-":APE2$=CPROVI:GOSUB 40000
4157 PUEBLO$=CUCUCU$
4160  NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4165  ALUMNO$=CUCUCU$:LC%=LEN(ALUMNO$)
4170  IF LC%<50 THEN ALUMNO$=ALUMNO$+LEFT$(PUNTO$,50-LC%)
4175  FOR A%=1 TO 10:LPRINT:NEXT A%
4180  LPRINT TAB(11) "SCHOOL OF ENGLISH";:LPRINT TAB(45) LEFT$(DESTI$,35)
4185  LPRINT TAB(11) "GENARO ORAA N§ 6";:LPRINT TAB(45) LEFT$(CDIREC,35)
4190  LPRINT TAB(11) "SANTURCE - VIZCAYA";:LPRINT TAB(45) LEFT$(PUEBLO$,35)
4195  LPRINT TAB(11) "Tel‚fono 4.93.70.05"
4200  FOR A%=1 TO 8:LPRINT:NEXT A%
4205  LPRINT TAB(16) "Estimados Padres :":LPRINT
4210  LPRINT TAB(19) "                Les  comunicamos  que su hijo(a):"  ':LPRINT
4215  LPRINT TAB(16) ALUMNO$
4216  LPRINT TAB(16) USING "ha faltado a clase ## dias en el mes de ";FAL%(MES%);
4217 LPRINT TAB(56) PERI$(MES%) ","
4225  LPRINT TAB(16) USING "de las cuales ## han sido justificadas.";JUS%(MES%)
4226 '
4227 IF FAL%(MES%)<6 THEN 4239
4228 LPRINT
4229  LPRINT TAB(16) "                   Este elevado n£mero de faltas im-"
4230  LPRINT TAB(16) "pide un seguimiento  progresivo  y continuado de las"
4231  LPRINT TAB(16) "clases; as¡ como de las tareas asignadas; por lo cu-"
4232  LPRINT TAB(16) "al su aprendizaje y  aprovechamiento  se ve reducido"
4233  LPRINT TAB(16) "en gran medida."
4239 LPRINT:LPRINT
4240  LPRINT TAB(16) "                   Les rogamos se pongan en contacto"
4245  LPRINT TAB(16) "con nosotros para justificar dichas faltas."
4250  LPRINT TAB(16) "     "
4252  LPRINT TAB(16) "     "
4253  LPRINT TAB(16) "     "
4255 LPRINT TAB(27) "Atentamente,"
4260  LPRINT TAB(16) "     "
4262  LPRINT TAB(16) "     "
4263  LPRINT TAB(16) "     "
4265 LPRINT TAB(27) "En Santurce a " LEFT$(FECHA$,2) " de " PERI$(VAL(MID$(FECHA$,4,2))) " de " MID$(FECHA$,7,1) "." RIGHT$(FECHA$,3)
4270 FOR A%=1 TO 15:LPRINT TAB(16) STRING$(50," "):NEXT A%
4985 LPRINT CHR$(12);
4990  NEXT NN%
5000  '
5005  'CLOSE
5010  GOTO 300
6000 '
6005 TIT$="  - LISTAR AUSENCIAS -  "
6010 '
6015 GOSUB 8000
6020 COLOR 10
6025 LOCATE 5,28:PRINT TIT$
6030 LOCATE 6,28:PRINT STRING$(LEN(TIT$),205)
6035 COLOR 11:LOCATE 10,28:PRINT "Teclea Mes a listar : <  >
6040       '**********************
6045       '***ENTRADA DEL MES ***
6050       '**********************
6055       COLOR 15:F.C1%=10:C.C1%=51:L.E%=2:BO%=2:GOSUB 63000
6060       IF I.T%=0 THEN MES$=D.E$:GOTO 6070
6065       BEEP:GOTO 6055
6070       IF MES$<"01" OR MES$>"12" THEN 6065
6075       MES%=VAL(MES$):LOCATE 10,51:PRINT MES$
6080      COLOR 11:LOCATE 12,28:PRINT "N§. tope ausencias  : <" SPACE$(2) ">"
6085      COLOR 15
6090      '**********************
6095      '***ENTRADA NUMERO  ***
6100      '**********************
6105      F.C1%=12:C.C1%=51:L.E%=2:BO%=2:GOSUB 63000
6110      IF I.T%=0 THEN FALTA$=D.E$:GOTO 6120
6115      GOTO 6105
6120      FALTA%=VAL(FALTA$)
6125      LOCATE 12,51:PRINT FALTA$
6130   '*************************************
6135   '***PEDIMOS CONFORMIDAD A LOS DATOS***
6140   '*************************************
6145   GOSUB 8500
6150   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
6155   COLOR 15:PRINT " ENTER ";
6160   COLOR 10:PRINT " Datos Erroneos ";
6165   COLOR 15:PRINT " F1 "
6170   COLOR 10
6175   '
6180   A$=INPUT$(1)
6185   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 6180
6190   '
6195   IF ASC(A$)=13 THEN 6215
6200   LOCATE 10,2:PRINT SPC(78)
6205   LOCATE 12,2:PRINT SPC(78)
6210   GOTO 6000
6215   '
6220 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
6225   GOSUB 30000:GOSUB 31500
6230   WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
6235   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h0B";
6237  'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
6240   PAG%=0:LIN%=90
6245   FOR NN%=2 TO ULTIMO%
6250   GET #2,NN%
6255   IF CMARCA="L" THEN 6475
6260  GET #5,NN%
6265  FAL%(1)=CVI(AENE):FAL%(2)=CVI(AFEB):FAL%(3)=CVI(AMAR):FAL%(4)=CVI(AABR)
6270  JUS%(1)=CVI(AJNE):JUS%(2)=CVI(AJEB):JUS%(3)=CVI(AJAR):JUS%(4)=CVI(AJBR)
6275  FAL%(5)=CVI(AMAY):FAL%(6)=CVI(AJUN):FAL%(7)=CVI(AJUL):FAL%(8)=CVI(AAGO)
6280  JUS%(5)=CVI(AJAY):JUS%(6)=CVI(AAUN):JUS%(7)=CVI(AAUL):JUS%(8)=CVI(AJGO)
6285  FAL%(9)=CVI(ASET):FAL%(10)=CVI(AOCT):FAL%(11)=CVI(ANOV):FAL%(12)=CVI(ADIC)
6290  JUS%(9)=CVI(AJET):JUS%(10)=CVI(AJCT):JUS%(11)=CVI(AJOV):JUS%(12)=CVI(ADIC)
6295  IF FAL%(MES%)<FALTA% THEN 6475
6297 'IF (FAL%(MES%)-JUS%(MES%))<FALTA% THEN 6475
6320  NOM$=CAPELL1:APE1$=CAPELL2:APE2$=CNOMBRE:GOSUB 40000
6325  ALUMNO$=CUCUCU$:LC%=LEN(ALUMNO$)
6330  '
6335  IF LIN%<60 THEN 6400
6340  IF LIN%=90 THEN 6350
6345  LPRINT CHR$(12)
6350  PAG%=PAG%+1:LIN%=0
6355  LPRINT TAB(1) "SCHOOL OF ENGLISH";
6360  LPRINT TAB(25) "AUSENCIAS DEL MES DE "PERA$(MES%)" DEL "RIGHT$(FECHA$,2);
6365  LPRINT TAB(68) USING "HOJA: ##";PAG%
6370 'LPRINT TAB(1) "-----------------    ------------------------------------------------   --------"
6375  LPRINT
6380  LPRINT TAB(6) "NUMERO         APELLIDOS Y NOMBRE DEL ALUMNO          FALTAS    JUSTI."
6385  LPRINT TAB(6) "------    ----------------------------------------    ------    ------"
6390  LPRINT
6395 LIN%=6
6400 '
6405 GUAPRI#=NN%:GOSUB 64770
6410 LPRINT TAB(11-L.C%) GUAPRI$;
6415 LPRINT TAB(16) LEFT$(ALUMNO$,40);
6420 LPRINT TAB(61) USING "#####";FAL%(MES%);
6425 LPRINT TAB(71) USING "#####";JUS%(MES%)
6430 'LPRINT
6435 LIN%=LIN%+1
6475  NEXT NN%
6477 IF LIN%=90 THEN 6485
6480 LPRINT CHR$(12)
6482  '
6485  CLOSE
6490  GOTO 300
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7672 'COLOR 10:PRINT " F1 =>";
7674 'COLOR 15:PRINT " Ayuda ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin de la Opci¢n."
7720 'COLOR 10:PRINT " F8 =>";
7730 'COLOR 15:PRINT " Campo Atr s ";
7740 'COLOR 10:PRINT " F10 =>";
7750 'COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,33:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,61:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACAINIC"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058 ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,6 AS CFEFAC,3 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
31500 '********************************
31502 '***APERTURA FICHERO DE FALTAS***
31504 '********************************
31506 'FICHE1$="\"+ESTA$+"\FACAAUSE.DAT"
31508 '
31510 'APERTURA FICHERO DE DATOS.
31512 OPEN "R",#5,"FACAAUSE.DAT",48
31514 'REGISTRO DE DATOS.
31516 FIELD #5,2 AS AENE,2 AS AFEB,2 AS AMAR,2 AS AABR,2 AS AMAY,2 AS AJUN,2 AS AJUL,2 AS AAGO,2 AS ASET,2 AS AOCT,2 AS ANOV,2 AS ADIC
31517 '
31518 FIELD #5,24 AS AJOD,2 AS AJNE,2 AS AJEB,2 AS AJAR,2 AS AJBR,2 AS AJAY,2 AS AAUN,2 AS AAUL,2 AS AJGO,2 AS AJET,2 AS AJCT,2 AS AJOV,2 AS AJIC
31520 '
31587 RETURN
31588 '
31590 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,4):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1900  OR ANO% > 2099 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
