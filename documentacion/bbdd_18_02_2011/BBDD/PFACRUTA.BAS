10 '                         PFACRUTA.BAS
20 '                       ---------------
30 '                   GESTION DE RUTAS-CLIENTE
40 '        -----------------------------------------------
42 '         ---PREPARADO PARA HNOS. FERNANDEZ S.A. ---
44 '        -----------------------------------------------
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"/"+MID$(FDIA$,3,2)+"/"+LEFT$(FDIA$,2)
94 '
96 DEFSTR D,P,R,E,X
98 '
100 OPTION BASE 1
110 DIM DIAS%(12)
120 DATA 31,28,31,30,31,30,31,31,30,31,30,31
130 FOR I%=1 TO 12
140   READ DIAS%(I%)
150 NEXT I%
152 'APERTURA DE IMPRESORA.
160 'WIDTH "LPT1:",145:OPEN "LPT1:" FOR OUTPUT AS #9
162 'LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
164 '
490 '*********************************
500 '***PRESENTAMOS EL MENU GENERAL***
510 '*********************************
520 TIT$="- GESTION DE RUTAS DE LA FLOTA -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 4,X%:PRINT TIT$
560 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
570 LOCATE 7,22:PRINT "1.- INTRODUCIR RUTAS."
580 LOCATE 8,22:PRINT "2.- BAJAS DE RUTAS."
590 LOCATE 9,22:PRINT "3.- MODIFICACION DE RUTAS."
600 LOCATE 10,22:PRINT "4.- CONSULTAS DE RUTAS."
610 LOCATE 11,22:PRINT "5.- LISTADO NUMERICO DE RUTAS."
620 LOCATE 12,22:PRINT "6.- LISTADO ALFABETICO DE RUTAS."
630 LOCATE 13,22:PRINT "7.- No se usa."
640 LOCATE 14,22:PRINT "8.- No se usa."
650 LOCATE 15,22:PRINT "9.- No se usa."
660 LOCATE 16,21:PRINT "10.- No se usa."
662 LOCATE 17,21:PRINT "11.- No se usa."
670 LOCATE 19,24:COLOR 12:PRINT " ESC ";
680 COLOR 15:PRINT " Finalizar ";
690 COLOR 11:PRINT " OPCION : <  > "
700 '
710 'PEDIMOS OPCION.
720 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
730 IF I.T%=4 THEN COLOR 7,0,0:GOTO 9000
740 IF I.T%<>0 THEN 720
750 COLOR 7,0,0
760 OPC%=VAL(D.E$)
770 IF OPC%<1 OR OPC%>11 THEN 670
780 ON OPC% GOTO 1010,1010,1010,1010,5202,6200,500,500,500,500,500
790 '
800 '
810 '
1000 '**************************************************
1010 '****PROCESO DE ACTUALIZACION FICHERO DE PEDIDOS***
1020 '**************************************************
1030 IF OPC%=1 THEN TITA$="- ALTAS DE RUTAS -":GOTO 1080
1040 IF OPC%=2 THEN TITA$="- BAJAS DE RUTAS -":GOTO 1080
1050 IF OPC%=3 THEN TITA$="- MODIFICACION DE RUTAS -":GOTO 1080
1060 IF OPC%=4 THEN TITA$="- CONSULTAS DE RUTAS -":GOTO 1080
1070 GOTO 500
1080 WREG!=0
1090 GOSUB 31000      'CARATULA DE PANTALLA
1110 GOSUB 30202      'APERTURA FICHERO DE MERCANCIAS
1120 GOSUB 30002      'APERTURA PLAN CONTABLE
1140 '
1150 '
1160 '
1170 IF OPC%<>1 THEN 4850     'PEDIR NRO DE ORDEN.
1180 GOSUB 7650
1190 '
1200 '
1210                    '************************************
1220                    '***PROCESO DE ALTA Y MODIFICACION***
1230                    '************************************
1240 '
2880 '*******************************
2890 '***ENTRADA LINEAS DE DETALLE***
2900 '*******************************
2910 '
2920 IF OPC%=3 OR RETRO%=1 THEN RETRO%=0:GOTO 3060  'NO PRESENTAR NRO. DE ORDEN
2930 '
2940 FOR F%=9 TO 18
2942  '
2950 '*****************************
2952 '***PEDIMOS NUMERO DE BANCO***
2954 '*****************************
2956 F.C1%=F%:C.C1%=2:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
2958 IF I.T%=0 THEN NURU$=D.E$:GOTO 2972
2960 IF I.T%=1 THEN 2956
2962 '
2964 IF I.T%=2 THEN 2956
2966 IF I.T%=3 THEN 2956
2968 IF I.T%=4 THEN 6800   'FIN DE ENTRADA
2970 GOTO 2956
2972 'HACER LECTURA DEL FICHERO.
2974 LOCATE F%,2:PRINT NURU$
2976 JESUS$=NURU$
2978 GOSUB 30410    'LECTURA AL AZAR.
2980 COLOR 15
2982 '
2984 IF SW1%=1 AND OPC%=1 THEN 2992   'CONTINUAR ALTA Y CLAVE NO BUSCADA.
2986 IF SW1%=0 AND OPC%=1 THEN GOSUB 8500:MEN$="-ESTA RUTA YA EXISTE, PULSE UNA TECLA-":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 2952
2988 IF SW1%=1 AND OPC%<>1 THEN GOSUB 8500:MEN$="-ESTA RUTA NO EXISTE, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 7500:GOTO 2952
2990 '
2992 NORDEN%=ULTRUTA%+1
2994 GET #3,NORDEN%
3050  '***********************************
3060  '***ENTRADA DESCRIPCION MERCANCIA***
3070  '***********************************
3080  F.C1%=F%:C.C1%=9:L.E%=30:BO%=30:GOSUB 64000
3090  IF I.T%=0 THEN RUTA$=D.E$:GOTO 3170
3095  IF I.T%=1 AND OPC%=1 THEN 3050
3100  IF I.T%=1 AND OPC%=3 THEN RUTA$=RNOAR:GOTO 3170
3110  IF I.T%=2 AND OPC%=1 THEN 3170
3120  IF I.T%=2 THEN 3050
3130  IF I.T%=3 THEN LOCATE F%,9:PRINT SPC(30):GOTO 3050
3140  '
3150  IF I.T%=4 THEN 6800
3152  '
3160  GOTO 3080
3170  LOCATE F%,9:PRINT RUTA$
3268  '
3270  '
3310  '**************************
3312  '***ENTRADA CLAVE RUTA  ***
3314  '**************************
3316  F.C1%=F%:C.C1%=42:L.E%=3:BO%=3:GOSUB 64000
3318  IF I.T%=0 THEN PELI$=D.E$:GOTO 3338
3320  IF I.T%=1 AND OPC%=1 THEN 3316
3322  IF I.T%=1 THEN PELI$=RCLAV:GOTO 3338
3324  IF I.T%=2 AND OPC%=1 THEN 3338
3326  IF I.T%=2 THEN 3316
3328  IF I.T%=3 THEN LOCATE F%,42:PRINT SPC(3):GOTO 3060
3332  IF I.T%=4 THEN 6800
3336  GOTO 3316
3338  '
3340  LOCATE F%,42:PRINT PELI$
3342  '
3344  '
3350 '*********************
3360 '***PEDIMOS CLIENTE***
3370 '*********************
3380 F.C1%=F%:C.C1%=48:L.E%=5:L.D%=0:NEG%=0:BO%=7:GOSUB 63000
3390 IF I.T%=0 THEN CLI$="43"+D.E$:GOTO 3490
3400 IF I.T%=1 AND OPC%=3 THEN CLI$=RCLIE:GOTO 3490
3410 IF I.T%=1 THEN 3380
3420 IF I.T%=2 AND OPC%=1 THEN 3490
3430 IF I.T%=2 THEN 3380
3440 IF I.T%=3 THEN LOCATE F%,48:PRINT SPC(7):GOTO 3310
3470 IF I.T%=4 THEN 6800
3480 GOTO 3380
3490 IF RIGHT$(CLI$,2)=LEFT$(NURU$,2) THEN 3510
3492 BEEP:BEEP:GOSUB 8500:LOCATE 22,15:COLOR 11:PRINT "Cliente no se corresponde con Ruta, pulsa una tecla"
3494 A$=INPUT$(1)
3496 '
3498 IF OPC%=1 THEN GOSUB 7650
3500 IF OPC%=3 THEN GOSUB 7500
3502 IF ASC(A$)=13 THEN 3610
3504 LOCATE F%,48:PRINT SPC(7):GOTO 3350
3510 LOCATE F%,48:PRINT CLI$
3520 BUSCA$=CLI$:GOSUB 30084
3530 IF SW1%=1 THEN NOMCLI$="*** ESTE CLIENTE NO TENEMOS ***":GOTO 3570
3540 GET #2,R.B%
3550 NOMCLI$=PNOMBRE
3570 LOCATE 20,21:COLOR 12:PRINT NOMCLI$
3580 GOSUB 8500:LOCATE 22,15:COLOR 11:PRINT "Cliente correcto pulsa ";
3582 COLOR 15:PRINT " ENTER, ";
3584 COLOR 11:PRINT "Cliente errขneo ";
3586 COLOR 15:PRINT " F1":COLOR 15
3588 A$=INPUT$(1)
3590 IF ASC(A$)<13 OR ASC(A$)>14 THEN 3588
3592 LOCATE 20,21:PRINT SPC(40)
3594 IF OPC%=1 THEN GOSUB 7650
3596 IF OPC%=3 THEN GOSUB 7500
3600 IF ASC(A$)=13 THEN 3610
3602 LOCATE F%,48:PRINT SPC(7):GOTO 3350
3610 '
3710  '**************************
3712  '***ENTRADA PRECIO T.M. ***
3714  '**************************
3716  F.C1%=F%:C.C1%=57:L.E%=9:BO%=11:L.D%=0:NEG%=0:GOSUB 63000
3718  IF I.T%=0 THEN TONE#=VAL(D.E$):GOTO 3738
3720  IF I.T%=1 AND OPC%=1 THEN 3716
3722  IF I.T%=1 THEN TONE#=CVD(RTONE):GOTO 3738
3724  IF I.T%=2 AND OPC%=1 THEN 3738
3726  IF I.T%=2 THEN 3716
3728  IF I.T%=3 THEN LOCATE F%,57:PRINT SPC(11):GOTO 3350
3732  IF I.T%=4 THEN 6800
3736  GOTO 3716
3738  '
3740  GUAPRI#=TONE#:GOSUB 44444
3742  LOCATE F%,57:PRINT SPC(11):LOCATE F%,67-L.C%:PRINT GUAPRI$
3744  '
3810  '**************************
3812  '***ENTRADA PRECIO VIAJE***
3814  '**************************
3816  F.C1%=F%:C.C1%=70:L.E%=9:BO%=11:L.D%=0:NEG%=0:GOSUB 63000
3818  IF I.T%=0 THEN VIAJ#=VAL(D.E$):GOTO 3838
3820  IF I.T%=1 AND OPC%=1 THEN 3816
3822  IF I.T%=1 THEN VIAJ#=CVD(RVIAJ):GOTO 3838
3824  IF I.T%=2 AND OPC%=1 THEN 3838
3826  IF I.T%=2 THEN 3816
3828  IF I.T%=3 THEN LOCATE F%,70:PRINT SPC(11):GOTO 3712
3832  IF I.T%=4 THEN 6800
3836  GOTO 3816
3838  '
3840  GUAPRI#=VIAJ#:GOSUB 44444
3842  LOCATE F%,70:PRINT SPC(11):LOCATE F%,80-L.C%:PRINT GUAPRI$
3844  '
4200  '*************************************
4210  '***PEDIMOS CONFORMIDAD A LOS DATOS***
4220  '*************************************
4222  GOSUB 8500:LOCATE 22,15
4224  COLOR 11:PRINT "Datos Correctos pulsa ";
4226  COLOR 15:PRINT " ENTER , ";
4228  COLOR 11:PRINT "Datos Errขneos pulsa ";
4230  COLOR 15:PRINT " F1"
4240  A$=INPUT$(1)
4250  IF ASC(A$)<13 OR ASC(A$)>14 THEN 4240
4260  IF ASC(A$)=14 THEN RETRO%=1:LOCATE F%,9:PRINT SPC(72):GOSUB 7650:GOTO 2900
4270  RETRO%=0
4280  'GRABAR DATOS.
4290  '
4320  LSET RNUAR=NURU$
4330  LSET RNOAR=RUTA$
4340  LSET RCLAV=PELI$
4342  LSET RCLIE=CLI$
4345  LSET RTONE=MKD$(TONE#)
4347  LSET RVIAJ=MKD$(VIAJ#)
4350  LSET RLIBR=""
4360  LSET RMARCA="A"
4370  LSET RSTAT="O"
4500  '
4670  '
4680  PUT #3,NORDEN%
4690  IF OPC%=3 THEN 5162   'TERMINAR MODIFICACION
4700  'REGRABAMOS REGISTRO 1.
4710  GET #3,1
4720  LSET RULTI=MKI$(NORDEN%)
4722  '
4730  PUT #3,1
4740  '
4750  CLOSE
4760  GOSUB 30202  'APERTURA FICHERO DE MERCANCIAS
4765  GOSUB 30002  'APERTURA PLAN CONTABLE
4770  '
4780  GOSUB 7650  'PRESENTAR MENSAJE TECLAS DE FUNCION.
4790 NEXT F%
4800 '
4810 GOTO 2940
4820 '
4830 '
4840 '*****************************
4842 '***ENTRADA NUMERO DE ORDEN***
4844 '*****************************
4850 FOR F%=9 TO 18
4852 GOSUB 6510
4854 GOSUB 8500:GOSUB 7500
4856 F.C1%=F%:C.C1%=2:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
4858 IF I.T%=0 THEN NURU$=D.E$:GOTO 4870
4860 IF I.T%=1 THEN 4856
4862 IF I.T%=2 THEN 4856
4864 IF I.T%=3 THEN 4856
4866 IF I.T%=4 THEN 6800   'FIN DE ENTRADA
4868 GOTO 4856
4870 'HACER LECTURA DEL FICHERO.
4872 LOCATE F%,2:PRINT NURU$
4874 JESUS$=NURU$
4876 GOSUB 30410    'LECTURA AL AZAR.
4878 NORDEN%=R.B%:COLOR 15
4880 '
4882 IF SW1%=1 AND OPC%=1 THEN 4888   'CONTINUAR ALTA Y CLAVE NO BUSCADA.
4884 IF SW1%=0 AND OPC%=1 THEN GOSUB 8500:MEN$="-ESTA RUTA YA EXISTE, PULSE UNA TECLA-":GOSUB 8500:A$=INPUT$(1):GOSUB 7650:GOTO 4852
4886 IF SW1%=1 AND OPC%<>1 THEN GOSUB 8500:MEN$="-ESTA RUTA NO EXISTE, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 7500:GOTO 4852
4888 '
5000   LOCATE F%,9:PRINT RNOAR
5001   LOCATE F%,42:PRINT RCLAV
5002   LOCATE F%,48:PRINT RCLIE
5004   GUAPRI#=CVD(RTONE):GOSUB 44444
5006   LOCATE F%,67-L.C%:PRINT GUAPRI$
5008   GUAPRI#=CVD(RVIAJ):GOSUB 44444
5010   LOCATE F%,80-L.C%:PRINT GUAPRI$
5112   '
5114   IF OPC%=2 THEN 5136   'TRATAR BAJA
5116   IF OPC%=3 THEN 1180   'TRATAR MODIFICACION
5118   '
5120   '*********************
5122   '***TRATAR CONSULTA***
5124   '*********************
5126   GOSUB 8500:MEN$="PULSE UNA TECLA PARA NUEVA CONSULTA":GOSUB 8500
5128   A$=INPUT$(1)
5130   GOTO 5162   'TERMINAR
5132   '
5134   '*****************
5136   '***TRATAR BAJA***
5138   '*****************
5140   GOSUB 8500:MEN$="Para dar de Baja pulsa ENTER , no dar de Baja F1":GOSUB 8500
5142   A$=INPUT$(1)
5144   IF ASC(A$)<13 OR ASC(A$)>14 THEN 5142
5146   IF ASC(A$)=14 THEN 5162  'TERMINAR
5148   'DAR DE BAJA
5150   LSET RMARCA="L"
5152   PUT #3,NORDEN%
5154   '
5156   '
5158   '
5160   '
5162   'TERMINAR.
5164   CLOSE
5166   GOSUB 30202   'APERTURA FICHERO DE PEDIDOS.
5168   GOSUB 30002   'APERTURA FICHERO CLIENTES
5170   '
5172 NEXT F%
5174 '
5176 GOTO 4850
5178 '
5200 '********************************************
5202 '***LISTADO DE RUTAS   POR NUMERO DE ORDEN***
5204 '********************************************
5206 TIT$="- LISTADO DE RUTAS POR NUMERO DE ORDEN -"
5208 X%=80-LEN(TIT$)
5210 X%=INT(X%/2)
5212 GOSUB 8000
5214 LOCATE 9,X%:PRINT TIT$
5216 LOCATE 10,X%:PRINT STRING$(LEN(TIT$),205)
5218 LOCATE 12,14:PRINT "DESDE EL NUMERO : <    >   HASTA EL NUMERO : <    >"
5220 COLOR 12
5222 LOCATE 14,32:PRINT " ESC ";:COLOR 15
5224 PRINT " Finalizar"
5226 'PEDIMOS DESDE.
5228 F.C1%=12:C.C1%=33:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
5230 IF I.T%=4 THEN 6800
5232 IF I.T%<>0 THEN 5228
5234 DESDE%=VAL(D.E$):LOCATE 12,33:PRINT USING "####";DESDE%
5236 'PEDIMOS HASTA.
5238 F.C1%=12:C.C1%=60:L.E%=4:L.D%=0:NEG%=0:BO%=4:GOSUB 63000
5240 IF I.T%=4 THEN 6800
5242 IF I.T%<>0 THEN 5236
5244 HASTA%=VAL(D.E$):LOCATE 12,60:PRINT USING "####";HASTA%
5246 IF DESDE%>HASTA% THEN 5226
5248 'ABRIR FICHEROS.
5249 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" ,DATOS ERRONEOS -F1-":GOSUB 8500
5250 A$=INPUT$(1):IF ASC(A$)<13 OR ASC(A$)>14 THEN 5248
5251 IF ASC(A$)=14 THEN GOSUB 8500:GOTO 5218
5252 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
5254 '
5255 WIDTH "LPT1:",145:OPEN "LPT1:" FOR OUTPUT AS #9
5256 LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
5257 GOSUB 30202   'APERTURA FICHERO MERCANCIAS
5258 GOSUB 30002   'APERTURA FICHERO PLAN CONTABLE
5259 '
5260 IF DESDE%<2 THEN DESDE%=2
5262 IF DESDE%>ULTRUTA% THEN DESDE%=ULTRUTA%+1
5264 IF HASTA%>ULTRUTA% THEN HASTA%=ULTRUTA%
5265 DESDE%=DESDE%-1
5266 FF$="":LIN%=90:PAG%=0
5268 GOSUB 5510   'LECTURA SECUENCIAL DE MERCANCIAS
5270 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:IF OPC%=6 THEN 6800 ELSE 5200
5272 WHILE FF$<>"FF"
5274   IF LIN%>55 THEN GOSUB 5362    'HACER CABECERAS.
5276  'GUAPRI#=CVI(RNUAR):GOSUB 44444
5278   LPRINT TAB(2) RNUAR;
5280   LPRINT TAB(12) RNOAR;
5281   LPRINT TAB(48) RCLAV;
5282   BUSCA$=RCLIE:GOSUB 30084
5284   IF SW1%=1 THEN NOMCLI$="*** ESTE CLIENTE NO TENEMOS ***":GOTO 5290
5286   GET #2,R.B%
5288   NOMCLI$=PNOMBRE
5290   LPRINT TAB(57) NOMCLI$;
5292   GUAPRI#=CVD(RTONE):GOSUB 44444
5294   LPRINT TAB(111-L.C%) GUAPRI$;
5296   GUAPRI#=CVD(RVIAJ):GOSUB 44444
5298   LPRINT TAB(127-L.C%) GUAPRI$
5320   '
5342   LIN%=LIN%+1
5344   '
5346   GOSUB 5510   'LECTURA SECUENCIAL FICHERO DE PEDIDOS.
5348   '
5350 WEND
5352 LPRINT CHR$(12)
5354 '
5355 CLOSE
5356 GOTO 5200
5358 '
5360 '***************************
5362 '***CABECERAS DEL LISTADO***
5364 '***************************
5366 IF LIN%<>90 THEN LPRINT CHR$(12)
5368 LIN%=0:PAG%=PAG%+1
5370 LPRINT TAB(1) EMPRESA$;
5371 LPRINT TAB(49) "LISTADO DE RUTAS POR NUMERO DE ORDEN AL " FECHA$;
5372 LPRINT TAB(116):LPRINT USING "PAGINA : ###";PAG%
5373 LPRINT TAB(1) STRING$(LEN(EMPRESA$),205);
5374 LPRINT TAB(49) STRING$(48,205);
5378 LPRINT TAB(116) STRING$(12,205)
5380 '
5382 LPRINT
5384 '
5386 LPRINT TAB(1) "N.RUTA         DESCRIPCION DE LA RUTA         CLAVE                NOMBRE DEL CLIENTE                PRECIO TM.     PREC. VIAJE"
5388 LPRINT TAB(1) "ออออออ     ออออออออออออออออออออออออออออออ     อออออ     ออออออออออออออออออออออออออออออออออออออออ     ออออออออออ     อออออออออออ"
5444 '
5496 LPRINT
5498 '
5500 LIN%=LIN%+6
5502 '
5504 RETURN
5506 '
5508 '*******************************************
5510 '***LECTURA SECUENCIAL FICHERO DE PEDIDOS***
5511 '*******************************************
5512 TECLA$=INKEY$:IF TECLA$="" THEN 5514
5513 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
5514 DESDE%=DESDE%+1
5516 IF DESDE%>HASTA% THEN FF$="FF":RETURN
5518 GET #3,DESDE%
5519 IF RMARCA="L" THEN 5514
5540 '
5550 RETURN
5560 '
6200 '********************************************
6202 '***LISTADO DE PEDIDOS POR NUMERO DE ORDEN***
6204 '********************************************
6206 TIT$="- LISTADO DE RUTAS POR ORDEN ALFABETICO -"
6208 X%=80-LEN(TIT$)
6210 X%=INT(X%/2)
6212 GOSUB 8000
6214 LOCATE 9,X%:PRINT TIT$
6216 LOCATE 10,X%:PRINT STRING$(LEN(TIT$),205)
6218 SHELL "AFARUTA.BAT"
6219 TIT$="- LISTADO DE RUTAS POR ORDEN ALFABETICO -"
6220 X%=80-LEN(TIT$)
6222 X%=INT(X%/2)
6224 GOSUB 8000
6226 LOCATE 9,X%:PRINT TIT$
6228 LOCATE 10,X%:PRINT STRING$(LEN(TIT$),205)
6230 COLOR 12
6232 LOCATE 14,32:PRINT " ESC ";:COLOR 15
6234 PRINT " Finalizar"
6236 'PEDIMOS HASTA.
6238  WIDTH "LPT1:",145:OPEN "LPT1:" FOR OUTPUT AS #9
6240  LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
6244 'HASTA%=ULTRUTA%:DESDE%=2
6256 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
6258 '
6260 GOSUB 30002   'APERTURA FICHERO PLAN CONTABLE
6262 GOSUB 30202   'APERTURA FICHERO MERCANCIAS
6263 HASTA%=ULTRUTA%:DESDE%=2
6264 '
6266 IF DESDE%<2 THEN DESDE%=2
6268 IF DESDE%>ULTRUTA% THEN DESDE%=ULTRUTA%+1
6270 IF HASTA%>ULTRUTA% THEN HASTA%=ULTRUTA%
6272 DESDE%=DESDE%-1
6274 FF$="":LIN%=90:PAG%=0
6276 GOSUB 5510   'LECTURA SECUENCIAL DE MERCANCIAS
6278 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:IF OPC%=6 THEN 6800 ELSE 6200
6280 WHILE FF$<>"FF"
6282   IF LIN%>55 THEN GOSUB 6334    'HACER CABECERAS.
6284   LPRINT TAB(1) RNOAR;
6286  'GUAPRI#=CVI(RNUAR):GOSUB 44444
6288   LPRINT TAB(37) RNUAR;
6290   LPRINT TAB(48) RCLAV;
6292   BUSCA$=RCLIE:GOSUB 30084
6294   IF SW1%=1 THEN NOMCLI$="*** ESTE CLIENTE NO TENEMOS ***":GOTO 6300
6296   GET #2,R.B%
6298   NOMCLI$=PNOMBRE
6300   LPRINT TAB(57) NOMCLI$;
6302   GUAPRI#=CVD(RTONE):GOSUB 44444
6304   LPRINT TAB(111-L.C%) GUAPRI$;
6306   GUAPRI#=CVD(RVIAJ):GOSUB 44444
6308   LPRINT TAB(127-L.C%) GUAPRI$
6310   '
6312   LIN%=LIN%+1
6314   '
6316   GOSUB 5510   'LECTURA SECUENCIAL FICHERO DE PEDIDOS.
6318   '
6320 WEND
6322 LPRINT CHR$(12)
6324 '
6326 CLOSE
6328 GOTO 6800
6330 '
6332 '***************************
6334 '***CABECERAS DEL LISTADO***
6336 '***************************
6338 IF LIN%<>90 THEN LPRINT CHR$(12)
6340 LIN%=0:PAG%=PAG%+1
6342 LPRINT TAB(1) EMPRESA$;
6344 LPRINT TAB(49) "LISTADO DE RUTAS POR ORDEN ALFABETICO AL " FECHA$;
6346 LPRINT TAB(116):LPRINT USING "PAGINA : ###";PAG%
6348 LPRINT TAB(1) STRING$(LEN(EMPRESA$),205);
6350 LPRINT TAB(49) STRING$(49,205);
6352 LPRINT TAB(116) STRING$(12,205)
6354 '
6356 LPRINT
6358 '
6360 LPRINT TAB(1) "    DESCRIPCION DE LA RUTA         N.ORD.     CLAVE                NOMBRE DEL CLIENTE                PRECIO TM.     PREC. VIAJE"
6362 LPRINT TAB(1) "ออออออออออออออออออออออออออออออ     ออออออ     อออออ     ออออออออออออออออออออออออออออออออออออออออ     ออออออออออ     อออออออออออ"
6364 '
6366 LPRINT
6368 '
6370 LIN%=LIN%+6
6372 '
6374 RETURN
6376 '
6500 '*****************************
6510 '***BORRAR LINEA DE DETALLE***
6520 '*****************************
6530 LOCATE F%,2:PRINT SPC(78)
6610 '
6620 '
6630 '
6640 '
6650 RETURN
6660 '
6800 '**************************
6810 '***FIN ENTRADA DE DATOS***
6820 '**************************
6830 CLOSE
6840 COLOR 15
6850 GOTO 500
6860 '
7500 GOSUB 8500:COLOR 15:LOCATE 22,15:PRINT " F7 =>";
7510 COLOR 11:PRINT " Fin de Opciขn"
7520 COLOR 15
7540 '
7550 RETURN
7560 '**************************************************
7570 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin de Altas ";
7712 COLOR 15:PRINT " ENTER =>";
7715 COLOR 11:PRINT " Saltar ";
7720 COLOR 15:PRINT " F8 =>";
7730 COLOR 11:PRINT " Retro ";
7732 COLOR 15:PRINT " F10 =>";
7734 COLOR 11:PRINT " Dup. "
7736 COLOR 15
7738 'COLOR 7,0,0:PRINT " Otro Albarn "
7740 '
7750 RETURN
7770 '
8000 '**************************
8010 '***CARATULA DE PANTALLA***
8020 '**************************
8030 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8040 LOCATE 2,1:PRINT CHR$(186)
8050 COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10
8060 LOCATE 2,34:PRINT "Aplicaciขn : FACTURACION":COLOR 11
8070 LOCATE 2,63:PRINT "FECHA : " FECHA$
8080 COLOR 10:LOCATE 2,80:PRINT CHR$(186)
8090 LOCATE 3,1:PRINT CHR$(200) STRING$(78,205) CHR$(188)
8140 LOCATE 21,1:PRINT CHR$(201) STRING$(78,205) CHR$(187)
8150 LOCATE 22,1:PRINT CHR$(186)
8160 LOCATE 22,3:PRINT "Mensajes :"
8170 LOCATE 22,80:PRINT CHR$(186)
8180 LOCATE 23,1:PRINT CHR$(200) STRING$(78,205) CHR$(188);
8190 RETURN
8200 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9032 GOSUB 30200
9034 IF ULTRUTA%=ULTACTU% THEN CLOSE:GOTO 9080
9036 LSET RACTU=MKI$(ULTRUTA%)
9040 PUT #3,1
9042 CLOSE
9044 SHELL "AFARUTNU.BAT"
9046 '
9060 '
9070 '
9080 RUN "PFACMENU"
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=34 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '***************************************
30002 '***APERTURA FICHERO DE PLAN CONTABLE***
30004 '***************************************
30006 FICHE1$="\"+DIRE$+"\FCONPLAC.DIR"
30008 FICHE2$="\"+DIRE$+"\FCONPLAC.DAT"
30010 'APERTURA FICHERO DE INDICES.
30012 OPEN "R",#1,FICHE1$,19
30014 'REGISTRO 1.
30016 FIELD #1,2 AS DULTIMO,2 AS DALTAS,2 AS DULTACT,12 AS DETIQUETA,1 AS DSTATUS
30018 'REGISTRO DE DATOS.
30020 FIELD #1,7 AS DCUENTA,2 AS DNREG,9 AS DFILLER,1 AS DSTATUS
30022 GET #1,1
30024 IF DETIQUETA="FCONPLAC.DIR" THEN 30032  'CONTINUAR
30026 GOSUB 8500:MEN$="NO EXISTE FICHERO INDICES PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30028 A$=INPUT$(1)
30030 GOTO 9000
30032 'CONTINUAR.
30034 ULTDIR%=CVI(DULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30036 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30038 ALTDIR%=CVI(DALTAS)     'NUMERO DE ALTAS EN DIR.
30040 ACTDIR%=CVI(DULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30042 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30044 '
30046 'APERTURA FICHERO DE DATOS.
30048 OPEN "R",#2,FICHE2$,390
30050 'REGISTRO 1.
30052 FIELD #2,2 AS PULTIMO,2 AS PALTAS,12 AS PETIQUETA,200 AS PFIL1,133 AS PFIL2,1 AS PSTATUS,40 AS PFIL1000
30054 'REGISTRO DE DATOS CLIENTES.
30056 FIELD #2,7 AS PCUENTA,40 AS PNOMBRE,40 AS PDIREC,5 AS PCPOS,30 AS PPOBLA,40 AS PDIPAG,5 AS PCPPAG,30 AS PPOPAG,14 AS PNIF,11 AS PTELE1,11 AS PTELE2,8 AS PBANCO,15 AS PCTA,11 AS PPROVE,1 AS PACEPTA,6 AS PRIESGO,4 AS PGFI,2 AS PIVA,110 AS PRESTO
30058 FIELD #2,250 AS PANTE1,30 AS PANTE2,2 AS PRECARGO,1 AS PFPOFI,4 AS PDCTOFI,1 AS PFPBLA,4 AS PDCTBLA,2 AS PDIA1,2 AS PDIA2,2 AS PDIA3,7 AS PR1,7 AS PR2,3 AS PZONA,1 AS PAGRUPA,4 AS PCOMIS,28 AS PFIL3,1 AS PACTIVO,1 AS PSTATUS,40 AS PCONTACTO
30060 GET #2,1
30062 IF PETIQUETA="FCONPLAC.DAT" THEN 30070 'CONTINUAR.
30064 GOSUB 8500:MEN$="NO EXISTE EL FICHERO DE PLAN CONTABLE, PULSE UNA TECLA":GOSUB 8500
30066 A$=INPUT$(1)
30068 GOTO 9000
30070 'CONTINUAR.
30072 ULTDAT%=CVI(PULTIMO)    'ULTIMO DE DAT.
30074 ALTDAT%=CVI(PALTAS)     'ALTAS DE DAT.
30076 '
30078 RETURN
30080 '
30082 '************************************
30084 '***LECTURA AZAR DEL PLAN CONTABLE***
30086 '************************************
30088 'El argumento de busqueda sobre 'BUSCA$'
30090 'El numero de registros sobre 'N.R%'
30092 R.B%=0           'Donde devuelve el nro. de registro.
30094 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30096 SW1%=0         'SW indicador de la busqueda.
30098 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30100 WSTATUS$=""
30102 R.I%=INT((N.R%/2)+.5)
30104 R.C%=R.I%
30106 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30130
30108 IF R.I%>N.R% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 30130
30110 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
30112 IF DCUENTA=BUSCA$ THEN R.W%=R.I%:R.B%=CVI(DNREG):GOTO 30164      'MIRAR STATUS
30114 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30116 IF DCUENTA>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 30120
30118 R.I%=R.I%+R.C%
30120 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30106
30122 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30106
30124 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30106
30126 REG2%=R.I%:SW100%=0
30128 GOTO 30106
30130 'REGISTRO NO BUSCADO.
30132 XX%=0
30134 FOR A%=DES% TO HAS%
30136   GET #1,A%
30138   IF DCUENTA=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=CVI(DNREG):XX%=1:A%=HAS%
30140 NEXT A%
30142 IF XX%=1 THEN 30164
30144 '
30146 '
30148 'Registro NO BUSCADO
30150 XX%=0
30152 IF N.R%=0 THEN N.R%=1
30154 FOR JMG%=N.R% TO ULTIMO%
30156     GET #1,JMG%
30158     IF DCUENTA=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=CVI(DNREG):XX%=1:JMG%=32000
30160 NEXT JMG%
30162 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30164 'MIRAMOS STATUS.
30166 IF DSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30168 WSTATUS$=""
30170 RETURN
30172 '
30200 '***********************************
30202 '***APERTURA FICHERO DE RUTAS    ***
30204 '***********************************
30206 IF OPC%=6 THEN FICHE3$="\"+DIRE$+"\FFACRUTA.ALF":GOTO 30210
30208 FICHE3$="\"+DIRE$+"\FFACRUTA.DAT"
30210 'APERTURA FICHERO DE INDICES.
30212 OPEN "R",#3,FICHE3$,70
30214 'REGISTRO 1.
30216 FIELD #3,2 AS RULTI,12 AS RETIQ,2 AS RACTU,52 AS RLIBRE,1 AS RMARC1,1 AS RSTA1
30218 'REGISTRO DE DATOS.
30220 FIELD #3,4 AS RNUAR,30 AS RNOAR,3 AS RCLAV,7 AS RCLIE,8 AS RTONE,8 AS RVIAJ,8 AS RLIBR,1 AS RMARCA,1 AS RSTAT
30222 GET #3,1
30224  IF RETIQ="FFACRUTA.DAT" THEN 30232  'CONTINUAR
30226  GOSUB 8500:MEN$="NO EXISTE FICHERO DE RUTAS, PULSE UNA TECLA":GOSUB 8500
30228  A$=INPUT$(1)
30230  GOTO 9000
30232 'CONTINUAR.
30234  ULTRUTA%=CVI(RULTI):ULTACTU%=CVI(RACTU)
30244  N.O%=ULTACTU%
30246 'LSET RULTI=MKI$(1):LSET RETIQ="FFACRUTA.DAT"
30248 'LSET RLIBRE="":LSET RMARC1="A":LSET RSTA1="A"
30250 'PUT #3,1
30252 'CLOSE:STOP
30254  '
30256  RETURN
30400 '*************************
30410 '***LECTURA AZAR BANCOS***
30420 '*************************
30430 'El argumento de busqueda sobre 'BUSCA$'
30440 'El numero de registros sobre 'N.O%'
30450 R.B%=0           'Donde devuelve el nro. de registro.
30452 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30460 SW1%=0         'SW indicador de la busqueda.
30470 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30480 '
30490 R.I%=INT((N.O%/2)+.5)
30500 R.C%=R.I%
30510 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30630
30520 IF R.I%>N.O% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.O%:GOTO 30630
30530 IF R.I%=1 THEN R.I%=2:GET #3,R.I% ELSE GET #3,R.I%
30540 IF RNUAR=JESUS$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 30800      'MIRAR STATUS
30550 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30560 IF RNUAR>JESUS$ THEN R.I%=R.I%-R.C%:GOTO 30580
30570 R.I%=R.I%+R.C%
30580 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30510
30590 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30510
30600 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30510
30610 REG2%=R.I%:SW100%=0
30620 GOTO 30510
30630 'REGISTRO NO BUSCADO.
30640 XX%=0
30650 FOR A%=DES% TO HAS%
30660   GET #3,A%
30670   IF RNUAR=JESUS$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
30680 NEXT A%
30690 IF XX%=1 THEN 30800
30700 '
30710 '
30720 'Registro NO BUSCADO
30730 XX%=0
30740 IF N.O%=0 THEN N.O%=1
30750 FOR JMG%=N.O% TO ULTRUTA%
30760     GET #3,JMG%
30770     IF RNUAR=JESUS$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
30780 NEXT JMG%
30790 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30800 'MIRAMOS STATUS.
30810 IF RSTAT<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30820 WSTATUS$=""
30830 RETURN
30840 '
31000 '********************************
31010 '***PRESENTAR ENTRADA DE DATOS***
31020 '********************************
31030 X%=80-LEN(TITA$):X%=INT(X%/2)
31040 GOSUB 8000
31042 '
31050 LOCATE 4,X%:COLOR 10:PRINT TITA$:LOCATE 5,X%:PRINT STRING$(LEN(TITA$),205)
31060 COLOR 11
31140 LOCATE 7,1:PRINT "N.RUTA      DESCRIPCION DE LA RUTA      CLAVE  CLIENTE  PRECIO T.M.  PREC. VIAJE"
31150 LOCATE 8,1:PRINT "ออออออ  ออออออออออออออออออออออออออออออ  อออออ  อออออออ  อออออออออออ  อออออออออออ"
31155 COLOR 15
31160 '
31170 RETURN
31180 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
44444 '********************************************
44454 '***RUTINA PARA EDITAR CAMPOS EN CRISTIANO***
44464 '********************************************
44494 GUAPRI$=STR$(GUAPRI#)
44504 SIGNO$=LEFT$(GUAPRI$,1)
44514 GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
44524 'BUSCAMOS SI TIENE DECIMALES.
44534 PUNTO%=0
44544 FOR CRIS%=LEN(GUAPRI$) TO 1 STEP -1
44554  IF MID$(GUAPRI$,CRIS%,1)="." THEN PUNTO%=CRIS%:CRIS%=0
44564 NEXT CRIS%
44574 'DIVIDIMOS EN ENTEROS Y DECIMALES.
44584 IF PUNTO%=0 THEN ENTERO$=GUAPRI$:L.EN%=LEN(ENTERO$):DECIMAL$="":L.DE%=0:GOTO 44624
44594 ENTERO$=LEFT$(GUAPRI$,PUNTO%-1):L.EN%=LEN(ENTERO$):ENTERO#=VAL(ENTERO$)
44604 DECIMAL$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-PUNTO%):L.DE%=LEN(DECIMAL$)
44614 IF LEN(DECIMAL$)<2 THEN DECIMAL$=DECIMAL$+"0":L.DE%=LEN(DECIMAL$)
44624 'REDONDEAMOS LOS DECIMALES
44634 IF DECIMAL$="" THEN 44764  'CONTINUAR
44644 DECA$=LEFT$(DECIMAL$,2)+"."+RIGHT$(DECIMAL$,L.DE%-2)
44654 DECA#=VAL(DECA$)
44664 DECA#=INT(DECA#+.5)
44674 IF DECA#>99 THEN 44734   'SUMAR 1 A ENTERO Y DECIMALES = '00'
44684 DECA$=STR$(DECA#):DECA$=RIGHT$(DECA$,2)
44694 IF LEN(DECA$)<>2 THEN DECA$="0"+DECA$
44704 IF LEFT$(DECA$,1)=" " THEN DECA$="0"+RIGHT$(DECA$,1)
44714 DECIMAL$=DECA$
44724 GOTO 44764  'CONTINUAR
44734 ENTERO#=ENTERO#+1
44744 ENTERO$=STR$(ENTERO#):ENTERO$=RIGHT$(ENTERO$,LEN(ENTERO$)-1):L.EN%=LEN(ENTERO$)
44754 DECIMAL$="00":L.DE%=2
44764 'CONTINUAMOS
44774 CAMPO$="":X%=0
44784 FOR CRIS%=L.EN% TO 1 STEP -1
44794  X%=X%+1
44804  IF X%=4 THEN CAMPO$=CAMPO$+".":X%=1
44814  CAMPO$=CAMPO$+MID$(ENTERO$,CRIS%,1)
44824 NEXT CRIS%
44834 GUAPRI$=""
44844 FOR CRIS%=LEN(CAMPO$) TO 1 STEP -1
44854  GUAPRI$=GUAPRI$+MID$(CAMPO$,CRIS%,1)
44864 NEXT CRIS%
44874 IF GUAPRI$="" THEN GUAPRI$="0"
44876 IF SIGNO$=" " THEN SIGNO$=""
44884 GUAPRI$=SIGNO$+GUAPRI$
44894 IF DECIMAL$<>"" THEN GUAPRI$=GUAPRI$+","+LEFT$(DECIMAL$,2)
44904 L.C%=LEN(GUAPRI$)-1
44906 RETURN
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 'LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63152  IF ASC(D.R$)=14 THEN I.T%=5:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
