10 '                       PRESASIG.BAS
20 '                      --------------
30 '           GESTION DE ASIGNATURAS DE HOSTELERIA
40 '           ------------------------------------
50 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
90 CERO$=STRING$(15,"0")
110 '
120 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR G,A,C,F
150 OPTION BASE 1
160 DIM TABRUT$(6),DIAS%(12),TABENT$(30),CICU$(8)
170 '
180 DATA "- ALTAS DE ASIGNATURAS DE HOSTELERIA -"
190 DATA "- BAJAS DE ASIGNATURAS DE HOSTELERIA -"
200 DATA "- MODIFICACION DE ASIGNATURAS DE HOSTELERIA -"
210 DATA "- CONSULTA DE ASIGNATURAS DE HOSTELERIA -"
220 DATA "- INFORMES DE ASIGNATURAS DE HOSTELERIA -"
222 DATA "- FIN DEL PROGRAMA -"
224 FOR I%=1 TO 6:READ TABRUT$(I%):NEXT I%
250 '
252 'DATA "11 11 ","12 12A","12 12B","13 13A","13 13B","13 13C","21 21 ","22 22 "
253 DATA "11 11 ","12 12A","999999","13 13A","999999","13 13C","999999","999999"
254 FOR I%=1 TO 8:READ CICU$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12
296    READ DIAS%(I%)
298 NEXT I%
300 '*********************************
302 '***PRESENTAMOS EL MENU GENERAL***
304 '*********************************
306 TIT$="- GESTION DE ASIGNATURAS DE HOSTELERIA -"
308 X%=80-LEN(TIT$):X%=INT(X%/2)
310 GOSUB 8000
312 LOCATE 4,X%:PRINT TIT$
314 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
316 LOCATE 7,26:PRINT "1.- ALTAS DE ASIGNATURAS."
318 LOCATE 9,26:PRINT "2.- BAJAS DE ASIGNATURAS."
320 LOCATE 11,26:PRINT "3.- MODIFICAR ASIGNATURAS."
322 LOCATE 13,26:PRINT "4.- CONSULTAR ASIGNATURAS."
324 LOCATE 15,26:PRINT "5.- INFORMES DE ASIGNATURAS."
326 LOCATE 17,26:PRINT "6.- FINAL GESTION ASIGNATURAS."
338 LOCATE 20,31:COLOR 11:PRINT "Teclear Opci¢n : < >"
340 '
344 '
346 'PEDIMOS OPCION.
348 F.C1%=20:C.C1%=49:L.E%=1:L.D%=0:NEG%=0:BO%=1:GOSUB 63000
350 IF I.T%=4 THEN COLOR 15:GOTO 9000
352 IF I.T%<>0 THEN 348
354 COLOR 15
356 I.A%=VAL(D.E$)
358 IF I.A%<1 OR I.A%>6 THEN 338
360 ON I.A% GOTO 510,2660,2660,2660,4080,9000
362 '
364 '
366 '
500 '*************************
510 '***TRATAMIENTO DE ALTA***
520 '*************************
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 GOSUB 32000        'Apertura Fichero de GRUPOS
550 TIT$=TABRUT$(I.A%)
560 GOSUB 8000
570 '
580 GOSUB 31000
590 '
600 '
610 '
615 GOSUB 7650
620 '
630       '*************************************
640       '***ENTRADA NUMERO DE ASIGNATURA   ***
650       '*************************************
660       COLOR 15        'Presentamos Mensaje Teclas de Funcion.
670       GET #6,1:N.ORDEN%=ULTGRU%+1
680       GRUPO$=STR$(N.ORDEN%):LN%=LEN(GRUPO$)-1
690       GRUPO$=LEFT$(CERO$,3-LN%)+RIGHT$(GRUPO$,LN%)
700       IF N.ORDEN%<900 THEN 720
710       GOSUB 8500:MEN$="TENEMOS MUCHOS GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1)
720       LOCATE 6,20:PRINT USING"###";N.ORDEN%:LSET GGRUPO=GRUPO$
730       '
750       '***********************
752       '***ENTRADA CICLO    ***
754       '***********************
756       F.C1%=6:C.C1%=42:L.E%=1:BO%=1:GOSUB 63000
758       IF I.T%=0 THEN CICLO$=D.E$:LSET GCICLO=CICLO$:GOTO 772
760       IF I.T%=1 AND I.A%=1 THEN 756
762       IF I.T%=1 AND I.A%=3 THEN CICLO$=GCICLO:GOTO 772
764       IF I.T%=3 THEN 750
766       IF I.T%=4 THEN 4000               'Fin Entrada.
768       IF I.T%=2 AND I.A%=1 THEN LSET GCICLO=CICLO$:GOTO 772
770       IF I.T%=2 THEN 756
772       '
774       IF CICLO$="1" OR CICLO$="2" THEN 778
775       GOTO 750
776       '
778       LOCATE 6,42:PRINT CICLO$
780       IF CICLO$="1" THEN LOCATE 6,45:PRINT "PRIMERO":GOTO 784
782       LOCATE 6,45:PRINT "SEGUNDO"
784       '
850       '***********************
852       '***ENTRADA CURSO    ***
854       '***********************
856       F.C1%=6:C.C1%=68:L.E%=2:BO%=2:GOSUB 64000
858       IF I.T%=0 THEN CURSO$=D.E$:LSET GCURSO=CURSO$:GOTO 872
860       IF I.T%=1 AND I.A%=1 THEN 856
862       IF I.T%=1 AND I.A%=3 THEN CURSO$=GCURSO:GOTO 872
863       IF I.T%=3 AND I.A%=1 THEN LOCATE 6,42:PRINT SPC(1):LOCATE 6,45:PRINT SPC(7):GOTO 750
864       IF I.T%=3 THEN 750
866       IF I.T%=4 THEN 4000               'Fin Entrada.
868       IF I.T%=2 AND I.A%=1 THEN LSET GCURSO=CURSO$:GOTO 872
870       IF I.T%=2 THEN 856
872       IF LEFT$(CURSO$,1)="1" OR LEFT$(CURSO$,1)="2" OR LEFT$(CURSO$,1)="3" THEN 874
873       GOTO 850
874       LOCATE 6,68:PRINT CURSO$
875       IF CURSO$="1 " THEN LOCATE 6,72:PRINT "PRIMERO":GOTO 888
876       IF CURSO$="2 " THEN LOCATE 6,72:PRINT "SEGUNDO":GOTO 888
877       IF CURSO$="2A" THEN LOCATE 6,72:PRINT "SEGUNDO":GOTO 888
878       IF CURSO$="2B" THEN LOCATE 6,72:PRINT "SEGUNDO":GOTO 888
879       IF CURSO$="3 " THEN LOCATE 6,72:PRINT "TERCERO":GOTO 888
880       IF CURSO$="3A" THEN LOCATE 6,72:PRINT "TERCERO":GOTO 888
881       IF CURSO$="3B" THEN LOCATE 6,72:PRINT "TERCERO":GOTO 888
882       IF CURSO$="3C" THEN LOCATE 6,72:PRINT "TERCERO":GOTO 888
883       GOTO 850
888       '***********************
890       '***ENTRADA TEXTO    ***
900       '***********************
910       F.C1%=8:C.C1%=30:L.E%=49:BO%=49:GOSUB 64000
920       IF I.T%=0 THEN TEXTO$=D.E$:LSET GTEXTO=TEXTO$:GOTO 1000
930       IF I.T%=1 AND I.A%=1 THEN TEXTO$="":LSET GTEXTO=TEXTO$:GOTO 1000
940       IF I.T%=1 AND I.A%=3 THEN TEXTO$=GTEXTO:GOTO 1000
945       IF I.T%=3 AND I.A%=1 THEN LOCATE 6,68:PRINT SPC(2):LOCATE 6,72:PRINT SPC(7):GOTO 850
950       IF I.T%=3 THEN 850
960       IF I.T%=4 THEN 4000               'Fin Entrada.
970       IF I.T%=2 AND I.A%=1 THEN LSET GTEXTO=TEXTO$:GOTO 1000
980       IF I.T%=2 THEN 890
990       '
1000       LOCATE 8,30:PRINT TEXTO$
1010       '
2500       '*************************************
2501       '***PEDIMOS CONFORMIDAD A LOS DATOS***
2502       '*************************************
2505       '
2510       GOSUB 8500:COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2515       COLOR 15:PRINT " ENTER ";
2520       COLOR 10:PRINT " Datos Erroneos ";
2525       COLOR 15:PRINT " F1 "
2530       COLOR 10
2535       A$=INPUT$(1)
2540       IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2535
2545       IF ASC(A$)=14 THEN 550
2550       'Grabar Datos.
2555       '
2557       '
2558       '
2560       IF I.A%=3 THEN 2585
2561       LSET GSTATUS="O":LSET GALTAS=MKI$(0):LSET GMARCA="A"
2562       LSET GLIBRE=""
2564       PUT #6,N.ORDEN%
2566       GET #6,1
2568       LSET GULTIMO=MKI$(N.ORDEN%)
2570       PUT #6,1
2572       CLOSE
2574       GOTO 530
2585       PUT #6,N.ORDEN%
2590       CLOSE
2610 '
2618 '
2650 '************************************************
2660 '***TRATAMIENTO COMUN DE BAJA,MODI, Y CONSULTA***
2670 '************************************************
2690 TIT$=TABRUT$(I.A%)
2710 GOSUB 32000
2720 GOSUB 8000:GOSUB 31000
2732 IF I.A%=3 THEN GOSUB 8500:GOSUB 7500
2734 IF I.A%=2 THEN GOSUB 8500:LOCATE 22,14:COLOR 10:PRINT " F7 => ";:COLOR 15:PRINT " F¡n de Bajas"
2736 IF I.A%=4 THEN GOSUB 8500:LOCATE 22,14:COLOR 10:PRINT " F7 => ";:COLOR 15:PRINT " F¡n de Consultas"
2740    '*****************************
2750    '***PEDIMOS NUMERO DE GRUPO **
2760    '*****************************
2770 COLOR 15
2780      F.C1%=6:C.C1%=20:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
2785      IF I.T%=0 THEN CLA$=D.E$:GOTO 2800            'Entrada OK
2790      IF I.T%=4 THEN 4000
2795      GOTO 2780                                     ' -ERROR-
2800      IF LEN(CLA$)<>3 THEN BEEP:GOTO 2780 'Se ha duplicado y no hay Cta.
2810      N.ORDEN%=VAL(CLA$)
2812      IF N.ORDEN%> ULTGRU% OR N.ORDEN%<2 THEN 2840
2814      GET #6,N.ORDEN%
2820     '
2830      IF GMARCA="A" THEN  2850
2835      GOSUB 8500:MEN$="Esta Asignatura est  dada de Baja, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2732
2840      GOSUB 8500:MEN$="Esta Asignatura no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2732
2850      ' LOCATE 4,77:PRINT GESCUE
2855      LOCATE 6,20:PRINT USING"###";VAL(CLA$)
2860      LOCATE 6,42:PRINT GCICLO
2870      IF GCICLO="1" THEN LOCATE 6,45:PRINT "PRIMERO":GOTO 2874
2872      LOCATE 6,45:PRINT "SEGUNDO"
2874      '
2880      LOCATE 6,68:PRINT GCURSO
2910      IF LEFT$(GCURSO,1)="1" THEN LOCATE 6,72:PRINT "PRIMERO":GOTO 2920
2912      IF LEFT$(GCURSO,1)="2" THEN LOCATE 6,72:PRINT "SEGUNDO":GOTO 2920
2914      LOCATE 6,72:PRINT "TERCERO"
2920      '
2930      LOCATE 8,30:PRINT LEFT$(GTEXTO,49)
2935      '
2937      '
2998     COLOR 15
3000     IF I.A%<>2 THEN 3190     'T. de Modi y consulta.
3010     '
3020     '*************************
3030     '***TRATAMIENTO DE BAJA***
3040     '*************************
3042     IF VAL(GALTAS)=0 THEN 3050
3044     GOSUB 8500:MEN$="NO PODEMOS DAR DE BAJA POR TENER ALUMNOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 2720
3050     GOSUB 8500
3060     COLOR 10:LOCATE 22,15:PRINT "Dar de baja ";
3070     COLOR 15:PRINT " ENTER ";
3080     COLOR 10:PRINT " No dar de baja ";
3090     COLOR 15:PRINT " F1 ":COLOR 10
3100     A$=INPUT$(1)
3110     IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 3100
3120     IF ASC(A$)=14 THEN 2720
3140     LSET GMARCA="L":PUT #6,N.ORDEN%
3150     '
3160     '
3170     GOTO 2720    'A BUSCAR OTRO
3180     '
3190     IF I.A%=3 THEN 3300    'T. DE MODIFICACION.
3200     '
3210     '*****************************
3220     '***TRATAMIENTO DE CONSULTA***
3230     '*****************************
3240     GOSUB 8500:MEN$="   Pulse una tecla para nueva consulta   ":GOSUB 8500
3250     A$=INPUT$(1)
3260     GOTO 2720
3270     '
3280     '
3290     '*********************************
3300     '***TRATAMIENTO DE MODIFICACION***
3310     '*********************************
3330     GOSUB 7510   'Presentar mensajes teclas de Funcion Modi.
3340     GOTO 750     '===>PASAMOS POR EL  ALTA
3350     '
3360     '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE
4050 GOTO 302
4060 '
4080 '**********************************
4110 '***LISTADO NUMERICO DEL FICHERO***
4140 '**********************************
4145 WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4150 LPRINT CHR$(27) "C" CHR$(66);:LPRINT CHR$(18)
4170 GOSUB 8000
4200 COLOR 10:LOCATE 9,19:PRINT "- INFORME NUMERICO DE ASIGNATURAS EN VIGOR -"
4230 LOCATE 10,19:PRINT STRING$(44,"-")
4240 LOCATE 15,28:PRINT "Final del listado ";
4245 COLOR 15:PRINT " F10 ":COLOR 10
4250 GOSUB 8500:LOCATE 22,15:PRINT "Espere por favor.........."
4252 FOR A%=1 TO 8
4260 GOSUB 5010
4265 NEXT A%
4270 GOTO 300
5010 LIN%=90:PAG%=0
5130 GOSUB 32000
5140 FOR NUME%=2 TO ULTGRU%
5150 GET #6,NUME%
5155 IF GSTATUS<>"O" THEN 5170
5157 IF GMARCA="L" THEN 5170
5160 CAMPO$=GCICLO+GCURSO
5161 IF LEFT$(CICU$(A%),3)=CAMPO$ THEN 5165
5162 IF RIGHT$(CICU$(A%),3)=CAMPO$ THEN 5165
5163 GOTO 5170
5165 GOSUB 5460
5170 NEXT NUME%
5190 IF LIN%=90 THEN 5370      'AL NEXT
5340 LPRINT CHR$(12)
5370 CLOSE:RETURN      'Volver al menu de Listado.
5400 '
5430 '*******************************************
5460 '***LINEA DE DETALLE DEL INFORME NUMERICO***
5490 '*******************************************
5520 '
5580 IF LIN%>66 THEN GOSUB 5910    'Hacer Cabeceras.
5581 LPRINT TAB(6) GGRUPO;
5582 LPRINT TAB(15) GTEXTO;
5611 GUAPRI#=CVI(GALTAS):GOSUB 64770:LPRINT TAB(76-L.C%) GUAPRI$
5612 '
5654 LPRINT
5720 '
5820 RETURN
5850 '
5880 '***************************
5910 '***CABECERAS DEL LISTADO***
5940 '***************************
5950 IF LIN%=90 THEN 5970
5960 LPRINT CHR$(12)
5970 PAG%=PAG%+1
6000 LPRINT TAB(26) "*** ESCUELA DE HOSTELERIA ***"
6005 LPRINT TAB(26) STRING$(29,"-"):LPRINT
6010 LPRINT TAB(17) "ASIGNATURAS POR CICLOS Y CURSOS AL DIA "+FECHA$
6060 LPRINT TAB(17) STRING$(47,"-")
6065 LPRINT
6090 '
6100 IF LEFT$(CICU$(A%),1)="1" THEN LPRINT TAB(23) "CICLO ...... : PRIMERO":GOTO 6110
6105 LPRINT TAB(23) "CICLO ...... : SEGUNDO"
6110 LPRINT
6115 IF MID$(CICU$(A%),2,1)="1" THEN LPRINT TAB(23) "CURSO ...... : PRIMERO":GOTO 6130
6120 IF MID$(CICU$(A%),2,1)="2" THEN LPRINT TAB(23) "CURSO ...... : SEGUNDO":GOTO 6130
6125 LPRINT TAB(23) "CURSO ...... : TERCERO"
6130 LPRINT
6135 IF RIGHT$(CICU$(A%),1)=" " THEN 6290
6140 IF RIGHT$(CICU$(A%),3)="12A" THEN LPRINT TAB(23) "ESPECIALIDAD : COCINA / PASTELERIA":GOTO 6280
6145 IF RIGHT$(CICU$(A%),3)="12B" THEN LPRINT TAB(23) "ESPECIALIDAD : MAITRE / SOMMELIER":GOTO 6280
6150 IF RIGHT$(CICU$(A%),3)="13A" THEN LPRINT TAB(23) "ESPECIALIDAD : TECNICO EN COCINA":GOTO 6280
6155 IF RIGHT$(CICU$(A%),3)="13B" THEN LPRINT TAB(23) "ESPECIALIDAD : TECNICO EN PASTELERIA":GOTO 6280
6160 LPRINT TAB(23) "ESPECIALIDAD : TECNICO EN RESTAURANTE":GOTO 6280
6280 LPRINT
6290 LPRINT TAB(4) "NUMERO                  NONBRE DE LA ASIGNATURA                   ALUMNOS"
6300 LPRINT TAB(4) "------     --------------------------------------------------     -------"
6330 LPRINT
6360 '
6390 LIN%=12
6420 RETURN
6450 '
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 22,14
7540 COLOR 10:PRINT " ENTER =>";
7550 COLOR 15:PRINT " Saltar ";
7560 COLOR 10:PRINT " F7 =>";
7570 COLOR 15:PRINT " Fin Modificaciones ";
7580 COLOR 10:PRINT " F8 =>";
7590 COLOR 15:PRINT " Campo Atr s"
7600 '
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,14
7680 COLOR 10:PRINT " ENTER =>";
7690 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Altas ";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup. ";
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT EMPRESA$:COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : HOSTELERIA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15
8120 RETURN
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 CLS:RUN "PRESINIC"
9050 GOTO 302
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA--":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9132 IF ERL=30220 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero de TARIFAS inesixtente":RET%=1:GOSUB 8500:GOTO 9000
9134 GOTO 9170
9142 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero de Alumnos inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,65
30054 FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,350
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,1 AS CCURSO
30112 FIELD #2,174 AS TODO1$,6 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,14 AS CKAKA,10 AS CCUBAN,7 AS CKAK1,6 AS CFALTA,1 AS CMARCA,50 AS COBSE1,50 AS COBSE2,4 AS CFACT
30113 FIELD #2,250 AS TODO3$,82 AS TODO4$,8 AS CIMPOR,10 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,85 AS REST2$
30115 GET #2,1
30116  IF CFICHER<>"FRESALUM.DAT" THEN 9144
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120  VIVAS%=CVI(CVIGO)
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1):LSET CULTACT=MKI$(1)
30124 'LSET CFICHER="FRESALUM.DAT":LSET CSWG="0":LSET CSWA="0"
30126 'LSET CVIGO=MKI$(0):LSET REST1$="":LSET CFEFAC="0000":LSET REST2$=""
30128 'PUT #2,1
30130 'CLOSE
30132 'STOP
30140 RETURN
30150 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 10:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 4,X%:PRINT TIT$
31042 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
31045 '
31050 COLOR 11:LOCATE 6,3:PRINT "Asignatura N§ : <   >         Ciclo : < >               Curso : <  >"
31052 LOCATE 8,3:PRINT "T¡tulo de la Asignatura : <                                                 >"
31160 COLOR 15:RETURN
31170 '
32000 '**********************************
32010 '***APERTURA FICHERO ASIGNATURAS***
32020 '**********************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FRESASIG.DAT",70
32060 FIELD #6,1 AS GSTATUS,1 AS GMARCA,3 AS GGRUPO,1 AS GCICLO,2 AS GCURSO,50 AS GTEXTO,2 AS GALTAS,10 AS GLIBRE
32070 FIELD #6,1 AS GSTAT,1 AS GMAR,2 AS GULTIMO,12 AS GETIQUETA,54 AS GLIB1
32110 GET #6,1
32120  IF GETIQUETA="FRESASIG.DAT" THEN 32140
32130  GOSUB 8500:MEN$="- NO TENEMOS FICHERO DE ASIGNATURAS, PULSAR UNA TECLA -":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140  ULTGRU%=CVI(GULTIMO)
32141 'LSET GLIB1="":LSET GSTAT="A":LSET GETIQUETA="FRESASIG.DAT"
32142 'LSET GULTIMO=MKI$(1):LSET GMAR="A"
32143 'PUT #6,1
32144 'CLOSE:STOP
32150  RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$="** ESCUELA DE HOSTELERIA **"
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 15:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 10:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 10:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 10
50300 RETURN
50340 '
50510 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF ASC(D.R$)=130 OR (ASC(D.R$)>159 AND ASC(D.R$)<166) THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
