10 '                       PACATRI2.BAS
20 '                      --------------
30 '        TRATAMIENTO DE LAS CALIFICAIONES TRIMESTRE 2
40 '        --------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," "):PUNTO$=STRING$(100,"")
100 '
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,A,F,J,N,G
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30),JAL%(12),FAL%(12),PERI$(12)
170 '
172 '
174 '
176 '
178 '
200 DATA "Enero","Febrero","Marzo","Abril","Mayo","Junio","Julio","Agosto","Setiembre","Octubre","Noviembre","Diciembre"
202 FOR I%=1 TO 12:READ PERI$(I%):NEXT I%
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
300 '*********************************
302 '***TRATAMIENTO DEL PROGRAMA***
304 '******************************
310 '*********************************
315 TIT$="- TRATAMIENTO CALIFICACIONES SEGUNDO TRIMESTRE -"
320 X%=80-LEN(TIT$):X%=INT(X%/2)
325 GOSUB 8000
330 LOCATE 4,X%:PRINT TIT$
335 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
340 LOCATE 7,26:PRINT "1.- INTRODUCIR CALIFICACIONES."
345 LOCATE 8,26:PRINT "2.- QUITAR CALIFICACIONES."
350 LOCATE 9,26:PRINT "3.- MODIFICAR CALIFICACIONES."
355 LOCATE 10,26:PRINT "4.- CONSULTAR CALIFICACIONES."
360 LOCATE 11,26:PRINT "5.- LISTAR CALIFICACIONES."
365 LOCATE 12,26:PRINT "6.- No se usa."
370 LOCATE 13,26:PRINT "7.- No se usa."
375 LOCATE 14,26:PRINT "8.- No se usa."
380 LOCATE 15,26:PRINT "9.- No se usa."
385 LOCATE 16,25:PRINT "10.- No se usa."
390 LOCATE 17,25:PRINT "11.- FINAL DE CALIFICACIONES."
395 LOCATE 19,33:COLOR 11:PRINT "TECLEAR OPCION : <  > "
400 COLOR 15
405 '
410 '
415 'PEDIMOS OPCION.
420 F.C1%=19:C.C1%=51:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
425 IF I.T%=4 THEN COLOR 15:GOTO 9000
430 IF I.T%<>0 THEN 420
435 COLOR 15
440 I.A%=VAL(D.E$)
445 IF I.A%<1 OR I.A%>11 THEN 395
450 ON I.A% GOTO 500,300,2500,2500,4010,300,300,300,300,300,9000
455 '
460 '
465 '
500 TIT$="- INTRODUCIR CALIFICACIONES SEGUNDO TRIMESTRE -":GOTO 510
505 '
510 '
511 GOSUB 8000
512 COLOR 10
514 LOCATE 5,18:PRINT TIT$
516 LOCATE 6,18:PRINT STRING$(LEN(TIT$),205)
700 '
702 '
704 '
706 '
718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
720 GOSUB 30000:GOSUB 32000
724 GOSUB 35000
728 GOSUB 7650
730       '*******************************
732       '***ENTRADA NUMERO DE ALUMNO ***
734       '*******************************
736       COLOR 15
738       F.C1%=8:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 758 'Entrada OK
741       IF I.T%=4 THEN CLOSE:GOTO 300
742       BEEP:LOCATE 8,20:PRINT SPC(6):GOTO 732
756       '
758       REG%=VAL(CUENTA$)
760       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 8,25-L.C%:PRINT GUAPRI$
762     ' GET #2,REG%  'BUSCAR REGISTRO
764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N즡ero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
765       GET #2,REG%  'BUSCAR REGISTRO
766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 724
768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
770       LOCATE 8,28:PRINT CUCUCU$
774       '
776       '
778       '
780       '
878       '
880       '
1000       '******************************
1002       '***ENTRADA PUNTOS CONTROL 1***
1004       '******************************
1006       F.C1%=10:C.C1%=18:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1008       IF I.T%=0 THEN CONO1%=VAL(D.E$):GOTO 1026
1010       '
1012       IF I.T%=1 AND I.A%=1 THEN CONO1%=0:GOTO 1026
1014       IF I.T%=1 AND I.A%=3 THEN CONO1%=CVI(NCOE21):GOTO 1026
1016       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,18:PRINT SPC(3):GOTO 1006
1018       IF I.T%=3 THEN 1006
1020       IF I.T%=4 THEN 4000               'Fin Entrada.
1022       IF I.T%=2 AND I.A%=1 THEN 1026
1024       IF I.T%=2 THEN 1006
1026       '
1028       LOCATE 10,18:PRINT USING "###";CONO1%
1029       '
1030       '******************************
1032       '***ENTRADA SOBRE  CONTROL 1***
1034       '******************************
1036       F.C1%=10:C.C1%=22:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1038       IF I.T%=0 THEN CONS1%=VAL(D.E$):GOTO 1056
1040       '
1042       IF I.T%=1 AND I.A%=1 THEN CONS1%=0:GOTO 1056
1044       IF I.T%=1 AND I.A%=3 THEN CONS1%=CVI(NCOS21):GOTO 1056
1046       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,18:PRINT SPC(3):GOTO 1006
1048       IF I.T%=3 THEN 1006
1050       IF I.T%=4 THEN 4000               'Fin Entrada.
1052       IF I.T%=2 AND I.A%=1 THEN 1056
1054       IF I.T%=2 THEN 1036
1056       '
1058       LOCATE 10,22:PRINT USING "###";CONS1%
1059       '
1060       '******************************
1062       '***ENTRADA PUNTOS CONTROL 2***
1064       '******************************
1066       F.C1%=10:C.C1%=43:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1068       IF I.T%=0 THEN CONO2%=VAL(D.E$):GOTO 1086
1070       '
1072       IF I.T%=1 AND I.A%=1 THEN CONO2%=0:GOTO 1086
1074       IF I.T%=1 AND I.A%=3 THEN CONO2%=CVI(NCOE22):GOTO 1086
1076       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,22:PRINT SPC(3):GOTO 1036
1078       IF I.T%=3 THEN 1036
1080       IF I.T%=4 THEN 4000               'Fin Entrada.
1082       IF I.T%=2 AND I.A%=1 THEN 1086
1084       IF I.T%=2 THEN 1066
1086       '
1088       LOCATE 10,43:PRINT USING "###";CONO2%
1089       '
1090       '******************************
1092       '***ENTRADA SOBRE  CONTROL 2***
1094       '******************************
1096       F.C1%=10:C.C1%=47:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1098       IF I.T%=0 THEN CONS2%=VAL(D.E$):GOTO 1116
1100       '
1102       IF I.T%=1 AND I.A%=1 THEN CONS2%=0:GOTO 1116
1104       IF I.T%=1 AND I.A%=3 THEN CONS2%=CVI(NCOS22):GOTO 1116
1106       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,43:PRINT SPC(3):GOTO 1066
1108       IF I.T%=3 THEN 1066
1110       IF I.T%=4 THEN 4000               'Fin Entrada.
1112       IF I.T%=2 AND I.A%=1 THEN 1116
1114       IF I.T%=2 THEN 1096
1116       '
1118       LOCATE 10,47:PRINT USING "###";CONS2%
1119       '
1120       '******************************
1122       '***ENTRADA PUNTOS CONTROL 3***
1124       '******************************
1126       F.C1%=10:C.C1%=68:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1128       IF I.T%=0 THEN CONO3%=VAL(D.E$):GOTO 1146
1130       '
1132       IF I.T%=1 AND I.A%=1 THEN CONO3%=0:GOTO 1146
1134       IF I.T%=1 AND I.A%=3 THEN CONO3%=CVI(NCOE23):GOTO 1146
1136       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,47:PRINT SPC(3):GOTO 1096
1138       IF I.T%=3 THEN 1096
1140       IF I.T%=4 THEN 4000               'Fin Entrada.
1142       IF I.T%=2 AND I.A%=1 THEN 1146
1144       IF I.T%=2 THEN 1126
1146       '
1148       LOCATE 10,68:PRINT USING "###";CONO3%
1149       '
1150       '******************************
1152       '***ENTRADA SOBRE  CONTROL 3***
1154       '******************************
1156       F.C1%=10:C.C1%=72:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1158       IF I.T%=0 THEN CONS3%=VAL(D.E$):GOTO 1176
1160       '
1162       IF I.T%=1 AND I.A%=1 THEN CONS3%=0:GOTO 1176
1164       IF I.T%=1 AND I.A%=3 THEN CONS3%=CVI(NCOS23):GOTO 1176
1166       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,68:PRINT SPC(3):GOTO 1126
1168       IF I.T%=3 THEN 1126
1170       IF I.T%=4 THEN 4000               'Fin Entrada.
1172       IF I.T%=2 AND I.A%=1 THEN 1176
1174       IF I.T%=2 THEN 1156
1176       '
1178       LOCATE 10,72:PRINT USING "###";CONS3%
1179       '
1180       '******************************
1182       '***ENTRADA COMPORTAMIENTO  ***
1184       '******************************
1186       F.C1%=12:C.C1%=30:L.E%=1:BO%=1:GOSUB 64000
1188       IF I.T%=0 THEN COMPO$=D.E$:GOTO 1206
1190       '
1192       IF I.T%=1 AND I.A%=1 THEN COMPO$=" ":GOTO 1206
1194       IF I.T%=1 AND I.A%=3 THEN COMPO$=NCOMP2:GOTO 1206
1196       IF I.T%=3 AND I.A%=1 THEN LOCATE 10,72:PRINT SPC(3):GOTO 1156
1198       IF I.T%=3 THEN 1156
1200       IF I.T%=4 THEN 4000               'Fin Entrada.
1202       IF I.T%=2 AND I.A%=1 THEN 1206
1204       IF I.T%=2 THEN 1186
1206       IF COMPO$="M" OR COMPO$="R" OR COMPO$="B" OR COMPO$="E" THEN 1208
1207       GOTO 1186
1208       LOCATE 12,30:PRINT COMPO$
1209       '
1210       '******************************
1212       '***ENTRADA TAREAS          ***
1214       '******************************
1216       F.C1%=12:C.C1%=67:L.E%=1:BO%=1:GOSUB 64000
1218       IF I.T%=0 THEN TAREA$=D.E$:GOTO 1236
1220       '
1222       IF I.T%=1 AND I.A%=1 THEN TAREA$=" ":GOTO 1236
1224       IF I.T%=1 AND I.A%=3 THEN TAREA$=NTARE2:GOTO 1236
1226       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,30:PRINT SPC(1):GOTO 1186
1228       IF I.T%=3 THEN 1186
1230       IF I.T%=4 THEN 4000               'Fin Entrada.
1232       IF I.T%=2 AND I.A%=1 THEN 1236
1234       IF I.T%=2 THEN 1216
1236       IF TAREA$="S" OR TAREA$="A" OR TAREA$="N" THEN 1238
1237       GOTO 1216
1238       LOCATE 12,67:PRINT TAREA$
1239       '
1240       '******************************
1242       '***ENTRADA PUNTOS GRAMATICA***
1244       '******************************
1246       F.C1%=14:C.C1%=53:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1248       IF I.T%=0 THEN GRAME%=VAL(D.E$):GOTO 1266
1250       '
1252       IF I.T%=1 AND I.A%=1 THEN GRAME%=0:GOTO 1266
1254       IF I.T%=1 AND I.A%=3 THEN GRAME%=CVI(NGRE02):GOTO 1266
1256       IF I.T%=3 AND I.A%=1 THEN LOCATE 12,67:PRINT SPC(1):GOTO 1216
1258       IF I.T%=3 THEN 1216
1260       IF I.T%=4 THEN 4000               'Fin Entrada.
1262       IF I.T%=2 AND I.A%=1 THEN 1266
1264       IF I.T%=2 THEN 1246
1266       '
1268       LOCATE 14,53:PRINT USING "###";GRAME%
1269       '
1270       '******************************
1272       '***ENTRADA PUNTOS GRAMATICA***
1274       '******************************
1276       F.C1%=14:C.C1%=57:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1278       IF I.T%=0 THEN GRAMS%=VAL(D.E$):GOTO 1296
1280       '
1282       IF I.T%=1 AND I.A%=1 THEN GRAMS%=0:GOTO 1296
1284       IF I.T%=1 AND I.A%=3 THEN GRAMS%=CVI(NGRS02):GOTO 1296
1286       IF I.T%=3 AND I.A%=1 THEN LOCATE 14,53:PRINT SPC(3):GOTO 1246
1288       IF I.T%=3 THEN 1246
1290       IF I.T%=4 THEN 4000               'Fin Entrada.
1292       IF I.T%=2 AND I.A%=1 THEN 1296
1294       IF I.T%=2 THEN 1276
1296       '
1298       LOCATE 14,57:PRINT USING "###";GRAMS%
1299       '
1300       '******************************
1302       '***ENTRADA PUNTOS COMPRENSI***
1304       '******************************
1306       F.C1%=16:C.C1%=53:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1308       IF I.T%=0 THEN COMPR%=VAL(D.E$):GOTO 1326
1310       '
1312       IF I.T%=1 AND I.A%=1 THEN COMPR%=0:GOTO 1326
1314       IF I.T%=1 AND I.A%=3 THEN COMPR%=CVI(NEXE02):GOTO 1326
1316       IF I.T%=3 AND I.A%=1 THEN LOCATE 14,57:PRINT SPC(3):GOTO 1276
1318       IF I.T%=3 THEN 1276
1320       IF I.T%=4 THEN 4000               'Fin Entrada.
1322       IF I.T%=2 AND I.A%=1 THEN 1326
1324       IF I.T%=2 THEN 1306
1326       '
1328       LOCATE 16,53:PRINT USING "###";COMPR%
1329       '
1330       '******************************
1332       '***ENTRADA PUNTOS COMPRENSI***
1334       '******************************
1336       F.C1%=16:C.C1%=57:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1338       IF I.T%=0 THEN COMPS%=VAL(D.E$):GOTO 1356
1340       '
1342       IF I.T%=1 AND I.A%=1 THEN COMPS%=0:GOTO 1356
1344       IF I.T%=1 AND I.A%=3 THEN COMPS%=CVI(NEXS02):GOTO 1356
1346       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,53:PRINT SPC(3):GOTO 1306
1348       IF I.T%=3 THEN 1306
1350       IF I.T%=4 THEN 4000               'Fin Entrada.
1352       IF I.T%=2 AND I.A%=1 THEN 1356
1354       IF I.T%=2 THEN 1336
1356       '
1358       LOCATE 16,57:PRINT USING "###";COMPS%
1359       '
1360       '******************************
1362       '***ENTRADA PUNTOS LITERATUR***
1364       '******************************
1366       F.C1%=18:C.C1%=53:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1368       IF I.T%=0 THEN LITER%=VAL(D.E$):GOTO 1386
1370       '
1372       IF I.T%=1 AND I.A%=1 THEN LITER%=0:GOTO 1386
1374       IF I.T%=1 AND I.A%=3 THEN LITER%=CVI(NLIE02):GOTO 1386
1376       IF I.T%=3 AND I.A%=1 THEN LOCATE 16,57:PRINT SPC(3):GOTO 1336
1378       IF I.T%=3 THEN 1336
1380       IF I.T%=4 THEN 4000               'Fin Entrada.
1382       IF I.T%=2 AND I.A%=1 THEN 1386
1384       IF I.T%=2 THEN 1366
1386       '
1388       LOCATE 18,53:PRINT USING "###";LITER%
1389       '
1390       '******************************
1392       '***ENTRADA PUNTOS LITERATUR***
1394       '******************************
1396       F.C1%=18:C.C1%=57:L.E%=3:BO%=3:ESC%=1:GOSUB 63000
1398       IF I.T%=0 THEN LITES%=VAL(D.E$):GOTO 1416
1400       '
1402       IF I.T%=1 AND I.A%=1 THEN LITES%=0:GOTO 1416
1404       IF I.T%=1 AND I.A%=3 THEN LITES%=CVI(NLIS02):GOTO 1416
1406       IF I.T%=3 AND I.A%=1 THEN LOCATE 18,53:PRINT SPC(3):GOTO 1366
1408       IF I.T%=3 THEN 1366
1410       IF I.T%=4 THEN 4000               'Fin Entrada.
1412       IF I.T%=2 AND I.A%=1 THEN 1416
1414       IF I.T%=2 THEN 1396
1416       '
1418       LOCATE 18,57:PRINT USING "###";LITES%
1419       '
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2042
2028   '
2030   '
2032   '
2034   GOTO 724
2042   '
2054   GET #6,REG%
2056   LSET NCOE21=MKI$(CONO1%):LSET NCOS21=MKI$(CONS1%)
2057   LSET NCOE22=MKI$(CONO2%):LSET NCOS22=MKI$(CONS2%)
2059   LSET NCOE23=MKI$(CONO3%):LSET NCOS23=MKI$(CONS3%)
2061   LSET NCOMP2=COMPO$:LSET NTARE2=TAREA$
2064   LSET NGRE02=MKI$(GRAME%):LSET NGRS02=MKI$(GRAMS%)
2065   LSET NEXE02=MKI$(COMPR%):LSET NEXS02=MKI$(COMPS%)
2066   LSET NLIE02=MKI$(LITER%):LSET NLIS02=MKI$(LITES%)
2098   '
2300   PUT #6,REG%
2302   IF I.A%=3 THEN CLOSE:GOTO 2511
2304   '
2306   GOTO 724
2500 '
2505 IF I.A%=3 THEN TIT$="- MODIFICAR NOTAS SEGUNDO TRIMESTRE -":GOTO 2511
2510 TIT$="- CONSULTAR NOTAS SEGUNDO TRIMESTRE -":GOTO 2511
2511 GOSUB 8000
2512 COLOR 10
2514 LOCATE 5,23:PRINT TIT$
2516 LOCATE 6,23:PRINT STRING$(LEN(TIT$),205)
2718 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
2720 GOSUB 30000:GOSUB 32000
2724 COLOR 11:GOSUB 35000
2728 GOSUB 7650
2730       '*******************************
2732       '***ENTRADA NUMERO DE ALUMNO ***
2734       '*******************************
2736       COLOR 15
2738       F.C1%=8:C.C1%=20:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
2740       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 2758 'Entrada OK
2741       IF I.T%=4 THEN CLOSE:GOTO 300
2742       BEEP:LOCATE 8,20:PRINT SPC(6):GOTO 2732
2756       '
2758       REG%=VAL(CUENTA$)
2760      GUAPRI#=VAL(CUENTA$):N.D%=0:GOSUB 64770:LOCATE 8,25-L.C%:PRINT GUAPRI$
2762     ' GET #2,REG%  'BUSCAR REGISTRO
2764       IF REG%<2 OR REG%>ULTIMO% THEN GOSUB 8500:MEN$="Este N즡ero de Alumno no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2765       GET #2,REG%  'BUSCAR REGISTRO
2766       IF CMARCA="L" THEN GOSUB 8500:MEN$="Alumno dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 2724
2768       NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
2770       LOCATE 8,28:PRINT CUCUCU$
2774       GET #6,REG%
2780       LOCATE 10,18:PRINT USING "###/###";CVI(NCOE21);CVI(NCOS21)
2782       LOCATE 10,43:PRINT USING "###/###";CVI(NCOE22);CVI(NCOS22)
2784       LOCATE 10,68:PRINT USING "###/###";CVI(NCOE23);CVI(NCOS23)
2786       LOCATE 12,30:PRINT NCOMP2
2788       LOCATE 12,67:PRINT NTARE2
2790       LOCATE 14,53:PRINT USING "###/###";CVI(NGRE02);CVI(NGRS02)
2792       LOCATE 16,53:PRINT USING "###/###";CVI(NEXE02);CVI(NEXS02)
2794       LOCATE 18,53:PRINT USING "###/###";CVI(NLIE02);CVI(NLIS02)
2796       IF I.A%=3 THEN 1000
2800       GOSUB 8500:MEN$="Para otra Consulta pulsa una tecla":GOSUB 8500
2805       A$=INPUT$(1):CLOSE:GOTO 2500
4000 CLOSE:GOTO 300
4010 TIT$="- LISTAR CALIFICACIONES DEL SEGUNDO TIMESTRE -"
4020 GOSUB 32500
4030 GOSUB 8000
4040 COLOR 10
4050 LOCATE 5,18:PRINT TIT$
4060 LOCATE 6,18:PRINT STRING$(LEN(TIT$),205)
4070 COLOR 11:LOCATE 10,16:PRINT "Teclea Grupo a listar : <   >
4080      GOSUB 8500:MEN$=" Teclear 999 para finalizar el listado.":GOSUB 8500
4090       '**********************
4100       '***ENTRADA DEL GRUPO**
4110       '**********************
4120       COLOR 15:F.C1%=10:C.C1%=41:L.E%=3:BO%=3:GOSUB 63000
4130       IF I.T%=0 THEN GRU%=VAL(D.E$):GOTO 4150
4140       BEEP:GOTO 4120
4150       IF GRU%=999 THEN CLOSE:GOTO 300
4160       GET #4,GRU%:CURSO$=GCURSO:ESCUE$=GESCUE:NALU%=CVI(GALTAS)
4170       LOCATE 10,41:PRINT D.E$:LOCATE 10,46:PRINT CURSO$:DESDE%=-4
4180      COLOR 11:LOCATE 12,16:PRINT "Profesores del Curso .. <" SPACE$(30) ">"
4190      COLOR 15
4200      '**********************
4210      '***ENTRADA PROFES  ***
4220      '**********************
4230      F.C1%=12:C.C1%=41:L.E%=30:BO%=30:GOSUB 64000
4240      IF I.T%=0 THEN PROFE$=D.E$:GOTO 4260
4250      GOTO 4230
4260      '
4270      LOCATE 12,41:PRINT PROFE$
4280   '*************************************
4290   '***PEDIMOS CONFORMIDAD A LOS DATOS***
4300   '*************************************
4310   GOSUB 8500
4320   COLOR 10:LOCATE 22,15:PRINT "Datos Correctos ";
4330   COLOR 15:PRINT " ENTER ";
4340   COLOR 10:PRINT " Datos Erroneos ";
4350   COLOR 15:PRINT " F1 "
4360   COLOR 10
4370   '
4380   A$=INPUT$(1)
4390   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 4380
4400   '
4410   IF ASC(A$)=13 THEN 4450
4420   LOCATE 10,2:PRINT SPC(78)
4430   LOCATE 12,2:PRINT SPC(78)
4440   CLOSE:GOTO 4000
4450   '
4460 FALUDIR$="FACAALUM.ALF":FALUDAT$="FACAALUM.DAT"
4470   GOSUB 30000:GOSUB 31500:GOSUB 32000
4480   WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4490   LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(18)
4500   FOR NN%=1 TO NALU%
4510   DESDE%=DESDE%+5
4520   GET #5,VAL(MID$(GALUS,DESDE%,5))
4530  'GET #5,245
4540  FA2%=CVI(AENE)+CVI(AFEB)+CVI(AMAR)
4550  JA2%=CVI(JENE)+CVI(JFEB)+CVI(JMAR)
4560  FAL%=CVI(AOCT)+CVI(ANOV)+CVI(ADIC)
4570  JAL%=CVI(JOCT)+CVI(JNOV)+CVI(JDIC)
4580  GET #2,VAL(MID$(GALUS,DESDE%,5))
4590  GET #6,VAL(MID$(GALUS,DESDE%,5))
4600  NOM$="Sres.":APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4610  DESTI$=CUCUCU$
4620  NOM$=CPOBLA:APE1$="-":APE2$=CPROVI:GOSUB 40000
4630 PUEBLO$=CUCUCU$
4640  NOM$=CNOMBRE:APE1$=CAPELL1:APE2$=CAPELL2:GOSUB 40000
4650  ALUMNO$=CUCUCU$:LC%=LEN(ALUMNO$)
4660  FOR A%=1 TO 5:LPRINT:NEXT A%
4670  LPRINT TAB(11) "SCHOOL OF ENGLISH";:LPRINT TAB(45) LEFT$(DESTI$,35)
4680  LPRINT TAB(11) "GENARO ORAA N 6";:LPRINT TAB(45) LEFT$(CDIREC,35)
4690  LPRINT TAB(11) "SANTURCE - VIZCAYA";:LPRINT TAB(45) LEFT$(PUEBLO$,35)
4700  LPRINT TAB(11) "Tel괽ono 4.61.95.55"
4710  FOR A%=1 TO 6:LPRINT:NEXT A%
4720  LPRINT TAB(23) "N O T A S    T R I M E S T R A L E S"
4730  LPRINT TAB(23) STRING$(36,205):LPRINT
4740  LPRINT TAB(7) "Nombre del Alumno : " ALUMNO$
4750  LPRINT TAB(7) "Curso ........... : " CURSO$
4760  LPRINT TAB(7) "Profesores ...... : " PROFE$:LPRINT
4770  LPRINT TAB(7) "                                   PRIMER.      SEGUNDO      TERCER. "
4780  LPRINT TAB(7) "      C O N C E P T O S           TRIMESTRE    TRIMESTRE    TRIMESTRE"
4790  LPRINT TAB(7) "------------------------------    ---------    ---------    ---------":LPRINT
4800  LPRINT TAB(7) "Faltas de asistencia .........";
4810  LPRINT TAB(46) USING "##";FAL%;
4820  LPRINT TAB(59) USING "##";FA2%
4830  LPRINT TAB(7) "Faltas justificadas ..........";
4840  LPRINT TAB(46) USING "##";JAL%;
4850  LPRINT TAB(59) USING "##";JA2%
4860  LPRINT
4870  LPRINT TAB(7) "Control n즡ero 1 .............";
4880  IF CVI(NCOS11)=0 THEN 4910
4885  IF CVI(NCOE11)=999 AND CVI(NCOS11)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 4910
4890  GUAPRI#=CVI(NCOE11)*100/CVI(NCOS11):N.D%=2:GOSUB 64770
4900  LPRINT TAB(47-L.C%) GUAPRI$ " %";
4910  IF CVI(NCOS21)=0 THEN LPRINT:GOTO 4970
4915  IF CVI(NCOE21)=999 AND CVI(NCOS21)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 4970
4920  GUAPRI#=CVI(NCOE21)*100/CVI(NCOS21):N.D%=2:GOSUB 64770
4930  LPRINT TAB(60-L.C%) GUAPRI$ " %"
4940 'IF CVI(NCOS31)=0 THEN 4320
4950 'GUAPRI#=CVI(NCOE31)*100/CVI(NCOS31):N.D%=2:GOSUB 64770
4960 'LPRINT TAB(73-L.C%) GUAPRI$ " %"
4970  LPRINT TAB(7) "Control n즡ero 2 .............";
4980  IF CVI(NCOS12)=0 THEN 5010
4985  IF CVI(NCOE12)=999 AND CVI(NCOS12)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5010
4990  GUAPRI#=CVI(NCOE12)*100/CVI(NCOS12):N.D%=2:GOSUB 64770
5000  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5010  IF CVI(NCOS22)=0 THEN LPRINT:GOTO 5070
5015  IF CVI(NCOE22)=999 AND CVI(NCOS22)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 5070
5020  GUAPRI#=CVI(NCOE22)*100/CVI(NCOS22):N.D%=2:GOSUB 64770
5030  LPRINT TAB(60-L.C%) GUAPRI$ " %"
5040 'IF CVI(NCOS32)=0 THEN 4350
5050 'GUAPRI#=CVI(NCOE32)*100/CVI(NCOS32):N.D%=2:GOSUB 64770
5060 'LPRINT TAB(73-L.C%) GUAPRI$ " %"
5070  IF ESCUE$="S" THEN LPRINT TAB(7) "Control n즡ero 3 .............";:GOTO 5090
5080  LPRINT TAB(7) "EXAMEN FIN DE TRIMESTRE ......";
5090  IF CVI(NCOS13)=0 THEN 5120
5095  IF CVI(NCOE13)=999 AND CVI(NCOS13)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5120
5100  GUAPRI#=CVI(NCOE13)*100/CVI(NCOS13):N.D%=2:GOSUB 64770
5110  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5120  IF CVI(NCOS23)=0 THEN LPRINT:GOTO 5180
5125  IF CVI(NCOE23)=999 AND CVI(NCOS23)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 5180
5130  GUAPRI#=CVI(NCOE23)*100/CVI(NCOS23):N.D%=2:GOSUB 64770
5140  LPRINT TAB(60-L.C%) GUAPRI$ " %"
5150 'IF CVI(NCOS33)=0 THEN 4380
5160 'GUAPRI#=CVI(NCOE33)*100/CVI(NCOS33):N.D%=2:GOSUB 64770
5170 'LPRINT TAB(73-L.C%) GUAPRI$ " %";
5180  LPRINT:LPRINT:LPRINT TAB(7) "Comportamiento ...............";
5190  IF NCOMP1="M" THEN LPRINT TAB(42) "Malo";:GOTO 5240
5200  IF NCOMP1="R" THEN LPRINT TAB(42) "Regular";:GOTO 5240
5210  IF NCOMP1="B" THEN LPRINT TAB(42) "Bueno";:GOTO 5240
5220  IF NCOMP1="E" THEN LPRINT TAB(42) "Muy Bueno";:GOTO 5240
5230  LPRINT TAB(42) "         ";
5240  IF NCOMP2="M" THEN LPRINT TAB(55) "Malo":GOTO 5280
5250  IF NCOMP2="R" THEN LPRINT TAB(55) "Regular":GOTO 5280
5260  IF NCOMP2="B" THEN LPRINT TAB(55) "Bueno":GOTO 5280
5270  IF NCOMP2="E" THEN LPRINT TAB(55) "Muy Bueno":GOTO 5280
5271  LPRINT TAB(55) "         "
5280  IF ESCUE$="N" THEN 5760
5290  LPRINT TAB(7) "Realizaci줻 de Tareas ........";
5300  IF NTARE1="S" THEN LPRINT TAB(42) "Siempre";:GOTO 5332
5310  IF NTARE1="A" THEN LPRINT TAB(42) "A veces";:GOTO 5332
5320  IF NTARE1="N" THEN LPRINT TAB(42) "Nunca";:GOTO 5332
5330  LPRINT TAB(42) "     ";
5332  IF NTARE2="S" THEN LPRINT TAB(55) "Siempre":GOTO 5430
5334  IF NTARE2="A" THEN LPRINT TAB(55) "A veces":GOTO 5430
5336  IF NTARE2="N" THEN LPRINT TAB(55) "Nunca":GOTO 5430
5338  LPRINT TAB(55) "     ":GOTO 5430
5340  '
5350  LPRINT
5360  LPRINT TAB(7) "De  0,00 %  a  49,99 %  컴>  Suspenso     "
5370  LPRINT TAB(7) "De 50,00 %  a  59,99 %  컴>  Apto         "
5380  LPRINT TAB(7) "De 60,00 %  a  69,99 %  컴>  Bien         "
5390  LPRINT TAB(7) "De 70,00 %  a  89,99 %  컴>  Notable      "
5400  LPRINT TAB(7) "De 90,00 %  a 100,00 %  컴>  Sobresaliente"
5410  LPRINT
5420  GOTO 5760
5430  LPRINT
5440  LPRINT TAB(7) "EXAMEN FIN DE TRIMESTRE"
5450  LPRINT TAB(7) "-----------------------"
5460  LPRINT TAB(7) "Gram쟴ica y vocabulario ......";
5470  IF CVI(NGRS01)=0 THEN 5500
5475  IF CVI(NGRE01)=999 AND CVI(NGRS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5500
5480  GUAPRI#=CVI(NGRE01)*100/CVI(NGRS01):N.D%=2:GOSUB 64770
5490  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5500  IF CVI(NGRS02)=0 THEN LPRINT:GOTO 5560
5505  IF CVI(NGRE02)=999 AND CVI(NGRS02)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 5560
5510  GUAPRI#=CVI(NGRE02)*100/CVI(NGRS02):N.D%=2:GOSUB 64770
5520  LPRINT TAB(60-L.C%) GUAPRI$ " %"
5530 'IF CVI(NGRS03)=0 THEN 4450
5540 'GUAPRI#=CVI(NGRE03)*100/CVI(NGRS03):N.D%=2:GOSUB 64770
5550 'LPRINT TAB(73-L.C%) GUAPRI$ " %"
5560  LPRINT TAB(7) "Comprensi줻 y expresi죓n oral.";
5570  IF CVI(NEXS01)=0 THEN 5600
5575  IF CVI(NEXE01)=999 AND CVI(NEXS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5600
5580  GUAPRI#=CVI(NEXE01)*100/CVI(NEXS01):N.D%=2:GOSUB 64770
5590  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5600  IF CVI(NEXS02)=0 THEN LPRINT:GOTO 5660
5605  IF CVI(NEXE02)=999 AND CVI(NEXS02)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 5660
5610  GUAPRI#=CVI(NEXE02)*100/CVI(NEXS02):N.D%=2:GOSUB 64770
5620  LPRINT TAB(60-L.C%) GUAPRI$ " %"
5630 'IF CVI(NEXS03)=0 THEN 4480
5640 'GUAPRI#=CVI(NEXE03)*100/CVI(NEXS03):N.D%=2:GOSUB 64770
5650 'LPRINT TAB(73-L.C%) GUAPRI$ " %"
5660  LPRINT TAB(7) "Literatura y composici줻 .....";
5670  IF CVI(NLIS01)=0 THEN 5700
5675  IF CVI(NLIE01)=999 AND CVI(NLIS01)=999 THEN LPRINT TAB(41) "NO PRESEN";:GOTO 5700
5680  GUAPRI#=CVI(NLIE01)*100/CVI(NLIS01):N.D%=2:GOSUB 64770
5690  LPRINT TAB(47-L.C%) GUAPRI$ " %";
5700  IF CVI(NLIS02)=0 THEN LPRINT:GOTO 5760
5705  IF CVI(NLIE02)=999 AND CVI(NLIS02)=999 THEN LPRINT TAB(54) "NO PRESEN":GOTO 5760
5710  GUAPRI#=CVI(NLIE02)*100/CVI(NLIS02):N.D%=2:GOSUB 64770
5720  LPRINT TAB(60-L.C%) GUAPRI$ " %"
5730 'IF CVI(NLIS03)=0 THEN 4500
5740 'GUAPRI#=CVI(NLIE03)*100/CVI(NLIS03):N.D%=2:GOSUB 64770
5750 'LPRINT TAB(73-L.C%) GUAPRI$ " %"
5760  LPRINT
5770  LPRINT:LPRINT TAB(7) "Observaciones generales :":LPRINT
5780  LPRINT TAB(7) STRING$(69,196)
5790  LPRINT TAB(7) STRING$(69,196)
5800  LPRINT TAB(7) STRING$(69,196)
5810  LPRINT TAB(7) STRING$(69,196)
5820  LPRINT TAB(7) STRING$(69,196)
5830  LPRINT
5840  LPRINT TAB(51) "Firma de los padres"
5850  FOR A%=1 TO 5:LPRINT:NEXT A%
5860  LPRINT TAB(7) "Comentarios de los padres :":LPRINT
5870  LPRINT TAB(7) STRING$(69,196)
5880  LPRINT TAB(7) STRING$(69,196)
5890  LPRINT TAB(7) STRING$(69,196):LPRINT
5900  IF ESCUE$="N" THEN 5920
5910  LPRINT TAB(7) "Este centro considera que el nivel requerido para aprobar es un 70 %":GOTO 5970
5920  LPRINT TAB(7) "De  0,00 %  a  49,99 %  컴>  Suspenso     "
5930  LPRINT TAB(7) "De 50,00 %  a  59,99 %  컴>  Apto         "
5940  LPRINT TAB(7) "De 60,00 %  a  69,99 %  컴>  Bien         "
5950  LPRINT TAB(7) "De 70,00 %  a  89,99 %  컴>  Notable      "
5960  LPRINT TAB(7) "De 90,00 %  a 100,00 %  컴>  Sobresaliente"
5970  LPRINT CHR$(12)
5980  NEXT NN%
5990 CLOSE
6000 GOTO 4000
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,13
7672 COLOR 10:PRINT "ENTER =>";
7674 COLOR 15:PRINT " Saltar ";
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Opci줻.";
7720 COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr쟳 ";
7740 COLOR 10:PRINT " F10 =>";
7750 COLOR 15:PRINT " Dup."
7760 '
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci줻 : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACAFANO"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 'OPEN "R",#1,FALUDIR$,65
30054 'FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 'GET #1,1
30058 'ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,20 AS CSUCUR,17 AS CCUBAN,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,237 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 '
30128 RETURN
30130 '
30140 '
31500 '********************************
31502 '***APERTURA FICHERO DE FALTAS***
31504 '********************************
31506 'FICHE1$="\"+ESTA$+"\FACAAUSE.DAT"
31508 '
31510 'APERTURA FICHERO DE DATOS.
31512 OPEN "R",#5,"FACAAUSE.DAT",48
31514 'REGISTRO DE DATOS.
31516 FIELD #5,2 AS AENE,2 AS AFEB,2 AS AMAR,2 AS AABR,2 AS AMAY,2 AS AJUN,2 AS AJUL,2 AS AAGO,2 AS ASET,2 AS AOCT,2 AS ANOV,2 AS ADIC
31517 '
31518 FIELD #5,24 AS JTOD,2 AS JENE,2 AS JFEB,2 AS JMAR,2 AS JABR,2 AS JMAY,2 AS JJUN,2 AS JJUL,2 AS JAGO,2 AS JSET,2 AS JOCT,2 AS JNOV,2 AS JDIC
31520 '
31587 RETURN
31588 '
31590 '
32000 '***APERTURA FICHERO DE NOTAS ***
32010 '********************************
32020 'FICHE1$="\"+ESTA$+"\FACANOTA.DAT"
32030 'DEFSTR N
32040 'APERTURA FICHERO DE DATOS.
32050 OPEN "R",#6,"FACANOTA.DAT",100
32060 'REGISTRO DE DATOS.
32070 FIELD #6,2 AS NCOE11,2 AS NCOS11,2 AS NCOE12,2 AS NCOS12,2 AS NCOE13,2 AS NCOS13
32080 FIELD #6,12 AS NRE001,2 AS NCOE21,2 AS NCOS21,2 AS NCOE22,2 AS NCOS22,2 AS NCOE23,2 AS NCOS23
32090 FIELD #6,24 AS NRE002,2 AS NCOE31,2 AS NCOS31,2 AS NCOE32,2 AS NCOS32,2 AS NCOE33,2 AS NCOS33
32100 FIELD #6,36 AS NRE003,2 AS NGRE01,2 AS NGRS01,2 AS NGRE02,2 AS NGRS02,2 AS NGRE03,2 AS NGRS03
32110 FIELD #6,48 AS NRE004,2 AS NEXE01,2 AS NEXS01,2 AS NEXE02,2 AS NEXS02,2 AS NEXE03,2 AS NEXS03
32120 FIELD #6,60 AS NRE005,2 AS NLIE01,2 AS NLIS01,2 AS NLIE02,2 AS NLIS02,2 AS NLIE03,2 AS NLIS03
32130 FIELD #6,72 AS NRE006,1 AS NCOMP1,1 AS NCOMP2,1 AS NCOMP3,1 AS NTARE1,1 AS NTARE2,1 AS NTARE3,22 AS NRE007
32140 '
32150 RETURN
32160 '
32500 '*******************************
32510 '***APERTURA FICHERO GRUPOS  ***
32520 '*******************************
32530 'Comprobamos la existencia del fichero.
32540 ' Apertura del fichero de datos   FACAGRUP.DAT
32550 OPEN "R",#4,"FACAGRUP.DAT",380
32560 FIELD #4,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32570 FIELD #4,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32580 FIELD #4,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB
32590 FIELD #4,150 AS GPRI2,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,1 AS GSB,1 AS GESCUE,14 AS GLIBR,200 AS GALUS
32600 FIELD #4,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32610 FIELD #4,180 AS GTODO,200 AS GALUS
32620 GET #4,1
32630 IF GETIQUETA="FACAGRUP.DAT" THEN 32650
32640 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32650 ULTGRU%=CVI(GULTIMO)
32660 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
35000 '*********************************
35002 '*** PANTALLA DE DATOS A METER ***
35004 '*********************************
35006 COLOR 11:LOCATE 8,5:PRINT "Alumno N.  : <      >" STRING$(50," ")
35008 LOCATE 10,5:PRINT "Control 1 : <   /   >"
35010 LOCATE 10,30:PRINT "Control 2 : <   /   >"
35012 LOCATE 10,55:PRINT "Control 3 : <   /   >"
35014 LOCATE 12,12:PRINT "Comportamiento : < >"
35016 LOCATE 12,42:PRINT "Realizacion de Tareas : < >"
35018 LOCATE 14,21:PRINT "Gram쟴ica y vocabulario .... : <   /   >"
35020 LOCATE 16,21:PRINT "Comprensi줻 y expresi줻 oral : <   /   >"
35022 LOCATE 18,21:PRINT "Literatura y composici줻 ... : <   /   >"
35024 '
35026 RETURN
35028 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 CUCUCU$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 CUCUCU$=LEFT$(CUCUCU$,50)
40210 RETURN
40220 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="" OR D.R$="" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0     ':N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN GUAPRI$=GUAPRI$+".000":GOTO 64794
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64835 N.D%=0
64836 RETURN
64838 '
