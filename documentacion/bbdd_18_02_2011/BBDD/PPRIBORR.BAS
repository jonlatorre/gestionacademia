10 '                       PACABORR.BAS
20 '                      --------------
30 '       BORRADO DE CONFIRMACION DE ASISTENCIA A TODOS
40 '       ---------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
140 DEFSTR C,F
150 OPTION BASE 1
4490 '*************************
4491 '***CREAR FICHERO FACT.***
4492 '*************************
4494 FALUDAT$="FACAALUM.DAT"
4496 'GOSUB 30000
4498 'IF ULTACT%=ULTIMO% THEN CLOSE:GOTO 4501
4499 'GET #2,1:LSET CULTACT=CULTI:PUT #2,1:CLOSE
4500 'SHELL "ACANUME.BAT"
4501 GOSUB 8000
4504 '
4520 COLOR 10:LOCATE 6,13:PRINT "- BORRAR CONFIRMACION DE ASISTENCIA A TODOS LOSALUMNOS -"
4530 LOCATE 7,13:PRINT STRING$(56,205)
4550 COLOR 11:LOCATE 10,15:PRINT "¨ ESTAS DE ACUERDO EN QUITAR LA CONFIRMACION A TODOS ?"
4552 COLOR 11:LOCATE 12,29:PRINT "Teclear -S- o -N-   < >"
4554 COLOR 15:LOCATE 12,50:A$=INPUT$(1)
4556 IF A$="N" OR A$="n" OR A$="S" OR A$="s" THEN 4560
4558 BEEP:GOTO 4552
4560 LOCATE 12,50:PRINT A$
4562 IF A$="N" OR A$="n" THEN CLOSE:GOTO 9040
4564 '
4566 COLOR 11:LOCATE 14,21:PRINT "¨ ESTAS TOTALMANTE SEGURA DE QUITARLAS ?"
4568 COLOR 11:LOCATE 16,29:PRINT "Teclear -S- o -N-   < >"
4570 COLOR 15:LOCATE 16,50:A$=INPUT$(1)
4572 IF A$="N" OR A$="n" OR A$="S" OR A$="s" THEN 4700
4690 BEEP:GOTO 4568
4700 LOCATE 16,50:PRINT A$
4710 IF A$="N" OR A$="n" THEN CLOSE:GOTO 9040
4720 '
4801   LIN%=90:PAG%=0
4802   GOSUB 8500:MEN$=" Espere por favor ........":GOSUB 8500
4803  'GOSUB 35000
4818 GOSUB 30000
4830 FOR REG%=2 TO ULTIMO%
4840   GET #2,REG%
4842   IF CSTATUS<>"O" THEN 4900 'AL NEXT
4844   'IF CMARCA="L" THEN 4900
4846   LSET CCONFI="N"
4848   PUT #2,REG%
4900 NEXT REG%
5370     '
5380     GOTO 9000
5390     '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACACLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FVIDCLIE.DIR inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FVIDCLIE.DAT inexistente":RET%=1:GOSUB 8500:CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 'OPEN "R",#1,FALUDIR$,65
30054 'FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 'GET #1,1
30058 'ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,6 AS CFENA,4 AS CBANCO,20 AS CSUCUR,17 AS CCUBAN,6 AS CFALTA,6 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,4 AS CFACT,4 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,237 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 '
30128 RETURN
30130 '
30140 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
