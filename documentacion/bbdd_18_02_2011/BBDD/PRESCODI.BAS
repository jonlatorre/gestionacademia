10 '                       PRESCODI.BAS
20 '                      --------------
30 '    GESTION FICHERO DE CODIGOS Y PRECIOS DE HOSTELERIA
40 '   ----------------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 'WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
75 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):T$=STRING$(4,205):B$="    ":F$=STRING$(4,178)
100 '
132 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 GOSUB 33000
140 DEFSTR H,C
150 OPTION BASE 1
160 DIM TABRUT$(7),DIAS%(12),TABENT$(30)
174 DATA 31,28,31,30,31,30,31,31,30,31,30,31
176 FOR A%=1 TO 12:READ DIAS%(A%):NEXT A%
178 '
200 DATA "      ALTAS DE PRECIOS       "
202 DATA "      BAJAS DE PRECIOS       "
210 DATA "   MODIFICACION DE PRECIOS   "
215 DATA "     CONSULTA DE PRECIOS     "
220 DATA "     LISTADO DE PRECIOS      "
225 DATA "   LISTAR TABLAS DE CODIGOS  "
230 DATA "       FIN DE PROGRAMA       "
232 FOR I%=1 TO 7
234    READ TABRUT$(I%)
236 NEXT I%
299 '
300 '******************************
302 '***TRATAMIENTO DEL PROGRAMA***
310 '******************************
320 'Presentamos el Menu del Programa.
330 GOSUB 8000
332 TIT$="- GESTION FICHERO DE PRECIOS DEL RESTAURANTE -"
334 X%=80-LEN(TIT$):X%=INT(X%/2)
340 COLOR 11:LOCATE 7,X%:PRINT TIT$
350 LOCATE 8,X%:PRINT STRING$(LEN(TIT$),205)
360 FOR I%=10 TO 16
370    LOCATE I%,24:PRINT TABRUT$(I%-9)
380 NEXT I%
390 '
400 GOSUB 8500:COLOR 11
410 LOCATE 23,15:PRINT "Seleccione Opciขn con las Flechas y Pulsando la Tecla ";
420 COLOR 15:PRINT " ENTER "
430 'COLOR 7,0,0
440 '
450 C.F%=10:C%=24:L.F%=16:N.L%=7
460 GOSUB 50000
470 '
480 ON I.A% GOTO 510,510,510,510,4490,7000,9000
490 '
500 '
510 '
520 '
522 GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 GOSUB 30010        'Apertura Fichero HIBRIDO
550 '
570 '
590 '
610 MARGEN$="I"
618 GOSUB 8000:GOSUB 31000
619 '
620 FOR F%=9 TO 20
630 IF I.A%=1 THEN GOSUB 7500:GOTO 1100
640 GOSUB 7650
1100       '*******************************
1105       '***ENTRADA NUMERO ARTICULO  ***
1110       '*******************************
1115       COLOR 15
1120       F.C1%=F%:C.C1%=5:L.E%=4:BO%=4:ESC%=0:GOSUB 63000
1125       IF I.T%=0 THEN COD$=D.E$:GOTO 1170 'Entrada OK
1135       IF I.T%=1 THEN COD$=CODI:GOTO 1170
1145       IF I.T%=3 THEN 1120               'Retroceder Campo.
1150       IF I.T%=4 THEN 4000               'Fin Entrada.
1155       IF I.T%=2 THEN 1120
1165       '
1170       REG%=VAL(COD$)
1175       GET #2,REG%
1180       LOCATE F%,5:PRINT COD$
1185       IF I.A%=1 AND CSTA="P" THEN 1800
1195       IF I.A%=1 THEN GOSUB 8500:MEN$="Este cขdigo ya existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 630
1200       IF CSTA<>"O" THEN GOSUB 8500:MEN$="Este cขdigo no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 630
1210       LOCATE F%,15:PRINT CDES
1220       GUAPRI#=CVS(CPRE):GOSUB 64770
1221       LOCATE F%,75-L.C%:PRINT GUAPRI$
1222       IF REG%>830 AND REG%<851 THEN 1232
1224       IF REG%>100 AND REG%<651 THEN 1232
1230       LOCATE F%,75-L.C%:PRINT GUAPRI$
1231       GOTO 1240
1232       COLOR 11:LOCATE 21,11:PRINT "Precio de la Botella : <      >      Nง de Centilitros : <   >":COLOR 15
1233       GUAPRI#=CVS(CBOT):GOSUB 64770
1234       LOCATE 21,35:PRINT SPC(6)
1235       LOCATE 21,40-L.C%:PRINT GUAPRI$
1237       GUAPRI#=CVI(CCEN):GOSUB 64770
1238       LOCATE 21,69:PRINT SPC(3)
1239       LOCATE 21,71-L.C%:PRINT GUAPRI$
1240       IF I.A%=2 THEN 3000
1250       IF I.A%=4 THEN 3240
1251       '
1800       '***********************
1802       '***ENTRADA PRODUCTO ***
1804       '***********************
1806       F.C1%=F%:C.C1%=15:L.E%=50:BO%=50:GOSUB 64000
1808       IF I.T%=0 THEN DES$=D.E$:GOTO 1828
1810       IF I.T%=5 THEN 1806
1812       IF I.T%=1 AND I.A%=1 THEN 1806
1814       IF I.T%=1 AND I.A%=3 THEN DES$=CDES:GOTO 1828
1816       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,15:PRINT SPC(58):GOTO 1806
1818       IF I.T%=3 THEN 1806
1820       IF I.T%=4 THEN 4000               'Fin Entrada.
1822       IF I.T%=2 AND I.A%=1 THEN 1828
1824       IF I.T%=2 THEN 1802
1826       '
1828       LOCATE F%,15:PRINT DES$
1829       IF REG%>830 AND REG%<851 THEN 6000
1830       IF REG%>100 AND REG%<651 THEN 6000
1831       PRECI!=0:CENTI%=0
1832       '***************************
1834       '***ENTRADA IMPOR CUOTA  ***
1836       '***************************
1838       F.C1%=F%:C.C1%=70:L.E%=5:BO%=6:ESC%=0:GOSUB 63000
1840       IF I.T%=0 THEN IMPOR!=VAL(D.E$):GOTO 1858
1842       IF I.T%=5 THEN 1838
1844       IF I.T%=1 AND I.A%=1 THEN 1838
1846       IF I.T%=1 AND I.A%=3 THEN IMPOR!=CVS(CPRE):GOTO 1860
1848       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,15:PRINT SPC(50):GOTO 1806
1850       IF I.T%=3 THEN 1806
1852       IF I.T%=4 THEN 4000               'Fin Entrada.
1854       IF I.T%=2 AND I.A%=1 THEN 1858
1856       IF I.T%=2 THEN 1834
1858       GUAPRI#=IMPOR!:GOSUB 64770
1859       LOCATE F%,70:PRINT SPC(6)
1860       LOCATE F%,75-L.C%:PRINT GUAPRI$
1862       'GOTO 1300
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   LOCATE 23,15:COLOR 10:PRINT "Datos Correctos ";
2010   COLOR 15:PRINT "  ENTER  ";
2012   COLOR 10:PRINT " Datos Erroneos ";
2014   COLOR 15:PRINT "  F1 "
2016   '
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2026   IF ASC(A$)=13 THEN 2030
2027   LOCATE F%,2:PRINT SPC(78):F%=F%-1:LOCATE 21,2:PRINT SPC(78):GOTO 2052
2030  LOCATE 21,2:PRINT SPC(78)
2032  LSET CODI=COD$:LSET CDES=DES$:LSET CPRE=MKS$(IMPOR!):LSET CLIB=""
2034  LSET CSTA="O":LSET CBOT=MKS$(PRECI!):LSET CCEN=MKI$(CENTI%)
2042   '
2043   PUT #2,REG%
2050  LOCATE 21,2:PRINT SPC(78)
2052   NEXT F%
2053   '
2054   '
2058   '
2060   '
2064   FOR F%=9 TO 20:LOCATE F%,2:PRINT SPC(78):NEXT F%
2065   '
2066   GOTO 619
2500 '
3000 '*****************************
3040 '***TRATAMIENTO DE BAJAS   ***
3050 '*****************************
3060 GOSUB 8500:MEN$="Para dar de Baja  ENTER, no dar de Baja  F1":GOSUB 8500
3062 A$=INPUT$(1)
3064 IF ASC(A$)<13 OR ASC(A$)>14 THEN 3062
3066 IF ASC(A$)=13 THEN 3075
3068 GOTO 630
3070 '
3075 LSET CSTA="P"
3080 GOTO 2043
3090 '
3100 '
3230 '*****************************
3240 '***TRATAMIENTO DE CONSULTA***
3250 '*****************************
3260 GOSUB 8500:MEN$="Pulsar una tecla para nueva consulta ":GOSUB 8500
3270 A$=INPUT$(1)
3280 GOTO 2050
3290 '
3300 '
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 302      'Volvemos al Menu.
4060 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 GOSUB 8500:MEN$="Espere por Favor...":GOSUB 8500
4496 WIDTH "LPT1:",80:OPEN "LPT1:" FOR OUTPUT AS #9
4498 LPRINT CHR$(27)+"&l1O"+CHR$(27)+"&a98L";
4500 '
4502 '
4504 '
4510 GOSUB 30052
4515 LIN%=90:PAG%=0
4520 FOR A%=1 TO 9999
4525 GET #2,A%
4530 IF CSTA="P" THEN 5000
4535 IF LIN%<43 THEN 4610
4540 IF LIN%=90 THEN 4550
4545 LPRINT CHR$(12)
4550 LIN%=0:PAG%=PAG%+1
4555 LPRINT:LPRINT TAB(6) "ESCUELA DE HOSTELERIA";
4560 LPRINT TAB(31) "RELACION DE PRECIOS AL " FECHA$;
4565 LPRINT TAB(66) USING "HOJA : ###";PAG%
4570 LPRINT TAB(6) STRING$(21,205);
4575 LPRINT TAB(31) STRING$(31,205);
4580 LPRINT TAB(66) STRING$(10,205)
4585 LPRINT
4590 LPRINT TAB(6) "CODIGO                C  O  N  C  E  P  T  O                  PRECIO  "
4595 LPRINT TAB(6) "ออออออ  ออออออออออออออออออออออออออออออออออออออออออออออออออ  ออออออออออ"
4600 LPRINT
4605 LIN%=7
4610 LPRINT TAB(7) CODI;
4615 LPRINT TAB(14) CDES;
4620 GUAPRI#=CVS(CPRE):GOSUB 64770
4625 LPRINT TAB(75-L.C%) GUAPRI$
4630 LIN%=LIN%+1
5000 NEXT A%
5010 LPRINT CHR$(12)
5020 CLOSE
5030 GOTO 300
6000 '**************************
6002 '***PEDIR COPAS Y PRECIO***
6004 '**************************
6006 IF I.A%=3 THEN 6032
6008 COLOR 11
6010 LOCATE 21,11:PRINT "Precio de la Botella : <      >      Nง de Centilitros : <   >":COLOR 15
6032       '***************************
6034       '***ENTRADA IMPOR BOTELLA***
6036       '***************************
6038       F.C1%=21:C.C1%=35:L.E%=5:BO%=6:ESC%=0:GOSUB 63000
6040       IF I.T%=0 THEN PRECI!=VAL(D.E$):GOTO 6058
6042       IF I.T%=5 THEN 6038
6044       IF I.T%=1 AND I.A%=1 THEN 6038
6046       IF I.T%=1 AND I.A%=3 THEN PRECI!=CVS(CBOT):GOTO 6058
6048       IF I.T%=3 AND I.A%=1 THEN LOCATE F%,15:PRINT SPC(50):GOTO 1806
6050       IF I.T%=3 THEN 1806
6052       IF I.T%=4 THEN 4000               'Fin Entrada.
6054       IF I.T%=2 AND I.A%=1 THEN 6058
6056       IF I.T%=2 THEN 6034
6058       GUAPRI#=PRECI!:GOSUB 64770
6059       LOCATE 21,35:PRINT SPC(6)
6060       LOCATE 21,40-L.C%:PRINT GUAPRI$
6062       '
6132       '***************************
6134       '***ENTRADA CENTRILITOS  ***
6136       '***************************
6138       F.C1%=21:C.C1%=69:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
6140       IF I.T%=0 THEN CENTI%=VAL(D.E$):GOTO 6158
6142       IF I.T%=5 THEN 6138
6144       IF I.T%=1 AND I.A%=1 THEN 6138
6146       IF I.T%=1 AND I.A%=3 THEN CENTI%=CVI(CCEN):GOTO 6158
6148       IF I.T%=3 AND I.A%=1 THEN LOCATE 21,35:PRINT SPC(6):GOTO 6034
6150       IF I.T%=3 THEN 6034
6152       IF I.T%=4 THEN 4000               'Fin Entrada.
6154       IF I.T%=2 AND I.A%=1 THEN 6158
6156       IF I.T%=2 THEN 6134
6158       GUAPRI#=CENTI%:GOSUB 64770
6159       LOCATE 21,69:PRINT SPC(3)
6160       LOCATE 21,71-L.C%:PRINT GUAPRI$
6162       '
6170 IF CENTI%=70 THEN COPAS%=7:GOTO 6176
6172 IF CENTI%=75 THEN COPAS%=7:GOTO 6176
6174 IF CENTI%=100 THEN COPAS%=11:GOTO 6176
6175 LOCATE 21,69:PRINT SPC(3):GOTO 6138
6176 '
6178 CAMPO!=(PRECI!/COPAS%)*1.5
6180 RESTO!=PRECI!/100
6182 CAMPO!=CAMPO!+RESTO!:CAMPO!=INT(CAMPO!)+5
6184 CAMPO$=STR$(CAMPO!):LCA%=LEN(CAMPO$)
6185 CAMPO$=LEFT$(CAMPO$,LCA%-1)+"0"
6187 IMPOR!=VAL(CAMPO$)
6188 GOTO 1858
6190 '
7000 RUN "PRESLICO"
7500 '***********************************************************
7510 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION MODIFICACIONES***
7520 '***********************************************************
7530 GOSUB 8500:LOCATE 23,15
7540 COLOR 15:PRINT " ENTER =>";
7550 COLOR 11:PRINT " Salto  ";
7560 COLOR 15:PRINT " F7 =>";
7570 COLOR 11:PRINT " Fin  ";
7580 COLOR 15:PRINT " F8 =>";
7590 COLOR 11:PRINT " M.Izda. "
7600 COLOR 15
7610 '
7620 '
7630 RETURN
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 23,15
7680 '
7690 '
7700 COLOR 15:PRINT " F7 =>";
7710 COLOR 11:PRINT " Fin de la Opciขn."
7720 'COLOR 0,7,0:PRINT " F8 =>";
7730 'COLOR 7,0,0:PRINT " M.Izda. ";
7740 'COLOR 0,7,0:PRINT " F10 =>";
7750 'COLOR 7,0,0:PRINT " Dup. "
7760 COLOR 15
7770 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,36:PRINT "Aplicaciขn : RESTAURANTE":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 22,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 23,1:PRINT CHR$(186):COLOR 11:LOCATE 23,3:PRINT "Mensajes: ":COLOR 10:LOCATE 23,80:PRINT CHR$(186)
8100 LOCATE 24,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 L.L$=""+MEN$+"":LOCATE 23,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR H%=1 TO 1500:NEXT H%:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PRESINIC"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 IF ERL=30052 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DIR inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9150 IF ERL=30100 AND ERR=53 THEN GOSUB 8500:MEN$="Fichero FACTCALE.DAT inexistente, pulsa una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:RESUME 9000
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO HIBRIDO ***
30020 '*******************************
30025 'Comprobamos la existencia del fichero.
30040 '
30050 '
30052 '
30090 ' Apertura del fichero de datos   FRESCODI.DAT
30095 '
30100 OPEN "R",#2,"FRESCODI.DAT",80
30105 '
30110 FIELD #2,4 AS CODI,50 AS CDES,4 AS CPRE,4 AS CBOT,2 AS CCEN,15 AS CLIB,1 AS CSTA
30114 '
30116 'FOR A%=1 TO 9999
30117 'GET #2,A%
30118 'LSET CODI="":LSET CDES="":LSET CPRE=MKS$(0):LSET CBOT=MKS$(0):LSET CCEN=MKI$(0):LSET CLIB="":LSET CSTA="P"
30119 'PUT #2,A%
30120 'NEXT A%
30121 'STOP
30122 '
30128 RETURN
30130 '
30140 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 11
31040 LOCATE 4,27:PRINT TABRUT$(I.A%)
31042 LOCATE 5,27:PRINT STRING$(28,205)
31050 LOCATE 7,4:PRINT "CODIGO                D  E  S  C  I  P  C  I  O  N                PRECIO"
31054 LOCATE 8,4:PRINT "------     --------------------------------------------------     ------"
31084 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
40000 '
40010 '   +-------------------------------------------+
40020 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
40030 '   +-------------------------------------------+
40040 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
40050 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
40060 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
40070 '-------------------------------------------------------------------------
40080 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
40090 ANOMAX%=VAL(RIGHT$(DATE$,2))
40100 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
40110 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 40150
40120 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
40130 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 40150
40140 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
40150 ERF%=1:RETURN  'Fecha Incorrecta
40160 '-------------------------------------------------------------------------
40170 '
45010 'RINT CHR$(27)+"&l0O"+CHR$(27)+"(s10h3B";
50000 '************************
50010 '***RUTINA VIDEO INVERSO*
50020 '************************
50030 '
50035            ' Devuelve la opcion sobre I.A%
50050 R$=CHR$(0):F%=C.F%
50060 WHILE ASC(R$)<>13
50065 F.I%=C.F%-1
50070 FOR I.A%=1 TO N.L%
50080 F.I%=F.I%+1:IF F%=F.I% THEN 50100
50090 NEXT I.A%
50100 'Tengo el valor de I.A%
50110 COLOR 12:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50120 R$=INKEY$:IF R$="" THEN 50120
50121 IF ASC(R$)=13 OR ASC(R$)=30 OR ASC(R$)=31 THEN 50130
50122 IF R$=CHR$(0)+CHR$(80) THEN R$=CHR$(31)
50123 IF R$=CHR$(0)+CHR$(72) THEN R$=CHR$(30)
50124 IF R$="2" THEN R$=CHR$(31)
50125 IF R$="8" THEN R$=CHR$(30)
50130 IF ASC(R$)=13 THEN 50190
50140 IF F%=C.F% AND ASC(R$)<>31 THEN 50120
50145 IF F%=L.F% AND ASC(R$)=31 AND NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1):F%=C.F%:GOTO 50050
50150 IF F%=L.F% AND ASC(R$)=31 THEN COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%):F%=C.F%:GOTO 50050
50160 IF NOMENU%=1 THEN LOCATE F%,C%:PRINT SPC(1) ELSE COLOR 11:LOCATE F%,C%:PRINT TABRUT$(I.A%)
50170 IF ASC(R$)=30 THEN F%=F%-1
50180 IF ASC(R$)=31 THEN F%=F%+1
50190 WEND
50200 COLOR 15
50300 RETURN
50340 '
50510 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,7
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 IF ESC%=1 THEN LOCATE F.C1%,C.C1%:PRINT D.E$
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,7
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="ฅ" OR D.R$="ค" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
