10 '                         PACANOAL.BAS
20 '                       ---------------
30 '           GESTION NOTAS ALEMAN  DE ESCUELA-IDIOMAS
40 '           ----------------------------------------
50 ON ERROR GOTO 9100
60 '
70 CLS
80 GOSUB 33000    'COGER DATOS DE LA EMPRESA.
82 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
84 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
86 CERO$=STRING$(15,"0"):GUION$=STRING$(15,"-")
88 ENTER$=CHR$(17)+CHR$(196)+CHR$(217)
90 FDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
92 FECHA$=RIGHT$(FDIA$,2)+"-"+MID$(FDIA$,3,2)+"-"+LEFT$(FDIA$,2)
94 '
96 DEFSTR B
98 '
100 '
110 '
120 '
130 '
140 '
306 '**********************
307 '***LISTADO POR NRO.***
308 '**********************
309 'MIRAMOS SI ESTA EL FICHERO ACTUALIZADO.
310 GOSUB 30010   'APERTURA FICHERO DE PLAN CONTABLE.
311 CLOSE
312 IF ULTDIR%=ACTDIR% THEN 500   'CONTINUAR CON EL PROGRAMA.
313 '
314 FICBAT$="ACAESALE.BAT;"+DIRE$+";"+PROGRAMA$
315 '
316 CLS
317 SHELL FICBAT$
318 CLS
319 CLOSE
320 GOSUB 30010
321 GET #1,1
324 LSET BULTACT=MKI$(ULTIMO%)
325 PUT #1,1
326 CLOSE
327 '
490 '*********************************
500 '***PRESENTAMOS EL MENU GENERAL***
510 '*********************************
520 TIT$="- MENU DE CALIFICACIONES DE LA ESCUELA DE IDIOMAS (ALEMAN) -"
530 X%=80-LEN(TIT$):X%=INT(X%/2)
540 GOSUB 8000
550 LOCATE 4,X%:PRINT TIT$
560 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
570 LOCATE 7,14:PRINT "1.- INTRODUCIR CALIFICACIONES DE JUNIO A TODO UN CURSO."
575 LOCATE 8,14:PRINT "2.- INTRODUCIR CALIFICACIONES DE SEPTIEMBRE A TODO UN CURSO."
580 LOCATE 9,14:PRINT "3.- MODIFICAR CALIFICACIONES JUNIO UNO A UNO."
585 LOCATE 10,14:PRINT "4.- MODIFICAR CALIFICACIONES SEPTIEMBRE UNO A UNO."
590 LOCATE 11,14:PRINT "5.- LISTADO DE CALIFICACIONES JUNIO."
600 LOCATE 12,14:PRINT "6.- LISTADO DE CALIFICACIONES SEPTIEMBRE."
630 LOCATE 13,14:PRINT "7.- No se usa."
640 LOCATE 14,14:PRINT "8.- No se usa."
650 LOCATE 15,14:PRINT "9.- No se usa."
660 LOCATE 16,13:PRINT "10.- No se usa."
662 LOCATE 17,13:PRINT "11.- No se usa."
670 LOCATE 19,12:COLOR 12:PRINT " ESC ";
680 COLOR 15:PRINT " Finalizar ";
690 COLOR 11:PRINT " OPCION : <  > "
700 '
710 'PEDIMOS OPCION.
720 F.C1%=19:C.C1%=39:L.E%=2:L.D%=0:NEG%=0:BO%=2:GOSUB 63000
730 IF I.T%=4 THEN COLOR 15:GOTO 9000
740 IF I.T%<>0 THEN 720
750 COLOR 15
760 OPC%=VAL(D.E$)
770 IF OPC%<1 OR OPC%>11 THEN 670
780 ON OPC% GOTO 1010,1010,1010,1010,5500,5500,500,500,500,500,500
790 '
800 '
810 '
1000 '*****************************************************
1010 '***TRATAMIENTO COMUN DE ALTA,BAJA.MODI. Y CONSULTA***
1020 '*****************************************************
1030 IF OPC%=1 THEN TITA$="- INTRODUCIR CALIFICACIONES JUNIO (ALEMAN) -":GOTO 1080
1040 IF OPC%=2 THEN TITA$="- INTRODUCIR CALIFICACIONES SEPTIEMBRE (ALEMAN) -":GOTO 1080
1050 IF OPC%=3 THEN TITA$="- MODIFICAR CALIFICACIONES JUNIO (ALEMAN) -":GOTO 1080
1060 IF OPC%=4 THEN TITA$="- MODIFICAR CALIFICACIONES SEPTIEMBRE (ALEMAN) -":GOTO 1080
1070 '
1080 '
1090 '
1100 '
1110 '
1112 GOSUB 30010  'APERTURA FICHERO DE EXPEDIENTES
1114 '
1116 GOSUB 31000
1118 '
1680 '******************************
1690 '***ENTRADA NUMERO CURSO    ***
1700 '******************************
1710 F.C1%=7:C.C1%=37:L.E%=1:BO%=1:L.D%=0:NEG%=0:GOSUB 63000
1720 IF I.T%=0 THEN CUR$=D.E$:GOTO 1740
1730 GOTO 1700
1740 GOSUB 8500:MEN$="Si Curso correcto pulsa ENTER, Si no pulsa F1":GOSUB 8500
1750 A$=INPUT$(1)
1755 GOSUB 8500
1760 IF ASC(A$)<13 OR ASC(A$)>14 THEN 1740
1770 IF ASC(A$)=14 THEN LOCATE 7,37:PRINT "  ":GOTO 1710
1775 IF OPC%=3 OR OPC%=4 THEN 2000
1780 FOR REG%=2 TO ULTIMO%
1785 GET #1,REG%
1790 IF BSTATUS<>"O" THEN 1880
1792 IF BMARCA="B" THEN 1880
1795 IF BCUR1=CUR$ THEN 1805
1796 IF BCUR2=CUR$ THEN 1805
1797 IF BCUR3=CUR$ THEN 1805
1798 IF BCUR4=CUR$ THEN 1805
1799 IF BCUR5=CUR$ THEN 1805
1800 GOTO 1880
1805 '
1820 LOCATE 9,37:PRINT BANCO
1825 LOCATE 11,14:PRINT BNOM
1827 LOCATE 15,37:PRINT " >                "
1830 '*****************************
1832 '***ENTRADA CALIFICACION   ***
1834 '*****************************
1836 F.C1%=15:C.C1%=37:L.E%=1:BO%=1:GOSUB 64000
1838 IF I.T%=0 THEN CALI$=D.E$:GOTO 1844
1840 IF I.T%=1 THEN CALI$=" ":GOTO 1844
1842 GOTO 1836
1844 IF CALI$="A" OR CALI$="N" OR CALI$=" " THEN 1848
1846 BEEP:GOTO 1836
1848 LOCATE 15,37:PRINT CALI$
1850 IF CALI$="A" THEN LOCATE 15,40:PRINT "APTO":GOTO 1853
1851 IF CALI$="N" THEN LOCATE 15,40:PRINT "NO APTO":GOTO 1853
1852 LOCATE 15,40:PRINT "NO PRESENTADO":GOTO 1853
1853 '*************************************
1854 '***PEDIMOS CONFORMIDAD A LOS DATOS***
1855 '*************************************
1856 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" ,DATOS ERRONEOS -F1-":GOSUB 8500
1858 A$=INPUT$(1)
1860 IF ASC(A$)<13 OR ASC(A$)>14 THEN 1858
1862 IF ASC(A$)=14 THEN LOCATE 15,37:PRINT " >               ":GOTO 1836
1864 IF OPC%=1 THEN 1872
1866 IF CUR$="1" THEN LSET BSEP1=CALI$
1867 IF CUR$="2" THEN LSET BSEP2=CALI$
1868 IF CUR$="3" THEN LSET BSEP3=CALI$
1869 IF CUR$="4" THEN LSET BSEP4=CALI$
1870 IF CUR$="5" THEN LSET BSEP5=CALI$
1871 GOTO 1877
1872 IF CUR$="1" THEN LSET BJUN1=CALI$
1873 IF CUR$="2" THEN LSET BJUN2=CALI$
1874 IF CUR$="3" THEN LSET BJUN3=CALI$
1875 IF CUR$="4" THEN LSET BJUN4=CALI$
1876 IF CUR$="5" THEN LSET BJUN5=CALI$
1877 PUT #1,REG%
1880 NEXT REG%
1882 CLOSE
1884 GOTO 500
2000 '
2010 GOSUB 7650
2120 '*****************************
2130 '***PEDIMOS NUMERO EXPEDIENTE*
2140 '*****************************
2150 F.C1%=9:C.C1%=37:L.E%=6:L.D%=0:NEG%=0:BO%=6:GOSUB 63000
2160 IF I.T%=0 THEN NBAN$=D.E$:GOTO 2240
2170 IF I.T%=1 THEN 2150
2180 '
2190 IF I.T%=2 THEN 2150
2210 IF I.T%=3 THEN 2150
2220 IF I.T%=4 THEN 6800   'FIN DE ENTRADA
2230 GOTO 2150
2240 'HACER LECTURA DEL FICHERO.
2250 LOCATE 9,37:PRINT NBAN$
2260 BUSCA$=NBAN$
2270 GOSUB 30410    'LECTURA AL AZAR.
2271 COLOR 15
2272 '
2292 IF SW1%=1 THEN GOSUB 8500:MEN$="- ESTE EXPEDIENTE NO EXISTE, PULSE UNA TECLA -":GOSUB 8500:A$=INPUT$(1):LOCATE 9,37:PRINT SPC(6):GOTO 2130
2294 IF BMARCA="B" THEN GOSUB 8500:MEN$="- EXPEDIENTE DADO DE BAJA, PULSE UNA TECLA -":GOSUB 8500:A$=INPUT$(1):LOCATE 9,37:PRINT SPC(6):GOTO 2130
2300 '
2310 'PRESENTAMOS EL REGISTRO EN PANTALLA.
2324 LOCATE 11,14:PRINT BNOM
2330 '
2350 IF CUR$="1" AND OPC%=3 THEN NOTA$=BJUN1
2352 IF CUR$="1" THEN NOTA$=BSEP1
2354 IF CUR$="2" AND OPC%=3 THEN NOTA$=BJUN2
2356 IF CUR$="2" THEN NOTA$=BSEP2
2358 IF CUR$="3" AND OPC%=3 THEN NOTA$=BJUN3
2360 IF CUR$="3" THEN NOTA$=BSEP3
2362 IF CUR$="4" AND OPC%=3 THEN NOTA$=BJUN4
2364 IF CUR$="4" THEN NOTA$=BSEP4
2366 IF CUR$="5" AND OPC%=3 THEN NOTA$=BJUN5
2368 IF CUR$="5" THEN NOTA$=BSEP5
2370 LOCATE 15,37:PRINT NOTA$
2372 IF NOTA$="A" THEN LOCATE 15,40:PRINT "APTO         "
2374 IF NOTA$="N" THEN LOCATE 15,40:PRINT "NO APTO      "
2376 LOCATE 15,40:PRINT "NO PRESENTADO"
2830 '*****************************
2832 '***ENTRADA CALIFICACION   ***
2834 '*****************************
2836 F.C1%=15:C.C1%=37:L.E%=1:BO%=1:GOSUB 64000
2838 IF I.T%=0 THEN CALI$=D.E$:GOTO 2844
2840 IF I.T%=1 THEN CALI$=" ":GOTO 2844
2842 GOTO 2836
2844 IF CALI$="A" OR CALI$="N" OR CALI$=" " THEN 2848
2846 BEEP:GOTO 2836
2848 LOCATE 15,37:PRINT CALI$
2850 IF CALI$="A" THEN LOCATE 15,40:PRINT "APTO         ":GOTO 2853
2851 IF CALI$="N" THEN LOCATE 15,40:PRINT "NO APTO      ":GOTO 2853
2852 LOCATE 15,40:PRINT "NO PRESENTADO"
2853 '*************************************
2854 '***PEDIMOS CONFORMIDAD A LOS DATOS***
2855 '*************************************
2856 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" ,DATOS ERRONEOS -F1-":GOSUB 8500
2858 A$=INPUT$(1)
2860 IF ASC(A$)<13 OR ASC(A$)>14 THEN 2858
2862 IF ASC(A$)=14 THEN LOCATE 15,37:PRINT " >               ":GOTO 2836
2864 IF OPC%=3 THEN 2872
2866 IF CUR$="1" THEN LSET BSEP1=CALI$
2867 IF CUR$="2" THEN LSET BSEP2=CALI$
2868 IF CUR$="3" THEN LSET BSEP3=CALI$
2869 IF CUR$="4" THEN LSET BSEP4=CALI$
2870 IF CUR$="5" THEN LSET BSEP5=CALI$
2871 GOTO 2877
2872 IF CUR$="1" THEN LSET BJUN1=CALI$
2873 IF CUR$="2" THEN LSET BJUN2=CALI$
2874 IF CUR$="3" THEN LSET BJUN3=CALI$
2875 IF CUR$="4" THEN LSET BJUN4=CALI$
2876 IF CUR$="5" THEN LSET BJUN5=CALI$
2877 PUT #1,R.B%
2880 '
2882 '
2884 GOTO 2000
3000 '
5500 '***********************************************************
5502 '***LISTADO NUMERICO FICHERO DE CALIFICACIONES E.IDIOMAS ***
5504 '***********************************************************
5506 TIT$="- LISTADO EXPEDIENTES APTOS ESCUELA IDIOMAS (ALEMAN) -"
5508 '
5510 '
5512 X%=80-LEN(TIT$)
5514 X%=INT(X%/2)
5516 GOSUB 8000
5518 LOCATE 6,X%:PRINT TIT$
5520 LOCATE 7,X%:PRINT STRING$(LEN(TIT$),205)
5522 LOCATE 11,12:PRINT "TECLLEA EL NUMERO DE CURSO A LISTAR ...... : < >"
5524 LOCATE 14,29:COLOR 0,7,0:PRINT " ESC ";:COLOR 7,0,0:PRINT " Terminar Listado"
5530 '
5540 'PEDIMOS HASTA.
5542 F.C1%=11:C.C1%=58:L.E%=1:L.D%=0:NEG%=0:BO%=1:GOSUB 63000
5544 IF I.T%=0 THEN HASTA$=D.E$:GOTO 5550
5546 IF I.T%=4 THEN CLOSE:GOTO 500
5548 GOTO 5552
5550 LOCATE 11,58:PRINT HASTA$
5552 IF HASTA$>"5" OR HASTA$<"1" THEN PLAY "A":GOTO 5530
5554 GOSUB 8500:MEN$="DATOS CORRECTOS "+ENTER$+" , DATOS ERRONEOS -F1-":GOSUB 8500
5556 A$=INPUT$(1)
5558 IF ASC(A$)<13 OR ASC(A$)>14 THEN 5556
5560 IF ASC(A$)=14 THEN GOSUB 8500:GOTO 5530
5562 '
5590 'ABRIR FICHEROS.
5592 WIDTH "LPT1:",140:OPEN "LPT1:" FOR OUTPUT AS #9
5594   LPRINT CHR$(27)+"&10O"+CHR$(27)+"(s16.66h0B";
5595 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
5596 '
5600 GOSUB 30010  'ABRIR INDICE Y DATOS.
5602 ULTIMO%=ACTDIR%
5604 '
5614 '
5616 '
5618 REG%=1:PAG%=0:LIN%=90:FF$="":NCLI%=0
5620 '
5622 GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5624 '
5626 IF FF$="FF" THEN GOSUB 8500:MEN$="NO HAY DATOS, PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 5502
5628 '
5630 WHILE FF$<>"FF"
5634   'LINEA DE DETALLE
5636   IF LIN%>65 THEN GOSUB 5746  'HACER CABECERAS.
5638   LPRINT TAB(7) LEFT$(BANCO,3)"."MID$(BANCO,4,3);
5640   LPRINT TAB(21) BNOM;
5645   LPRINT TAB(101) "A  P  T  O"
5650   LPRINT TAB(6) STRING$(120,"-")
5666   LIN%=LIN%+2:NCLI%=NCLI%+1
5716   GOSUB 5896              'HACER LECTURA DE INDICES Y DATOS
5718   '
5720  WEND
5722  LPRINT TAB(5):LPRINT USING "TOTAL ALUMNOS APTOS  : #####";NCLI%
5724  LIN%=88:NCLI%=0
5726  LPRINT CHR$(12)
5730 '
5732 'LPRINT
5734 'LPRINT TAB(5):LPRINT USING "TOTAL BANCOS LISTADOS : #####";NCLI%
5736 'LPRINT CHR$(12)
5738 CLOSE
5740 GOTO 5500
5742 '
5744 '***************************
5746 '***CABECERAS DEL LISTADO***
5748 '***************************
5749 IF OPC%=5 THEN TIT2$="  ALUMNOS APTOS ESCUELA IDIOMAS CONVOCATORIA JUNIO DEL "+RIGHT$(FECHA$,2)+"   ":GOTO 5751
5750 TIT2$="ALUMNOS APTOS ESCUELA IDIOMAS CONVOCATORIA SEPTIEMBRE DEL "+RIGHT$(FECHA$,2)
5751 IF LIN%<>90 THEN LPRINT CHR$(12)
5752 PAG%=PAG%+1
5754 LPRINT TAB(6) EMPRESA$;
5756 LPRINT TAB(45) TIT2$;
5758 LPRINT TAB(113):LPRINT USING "PAGINA : ####";PAG%
5760 LPRINT TAB(6) STRING$(LEN(EMPRESA$),"=");
5762 LPRINT TAB(45) STRING$(LEN(TIT2$),"=");
5764 LPRINT TAB(113) STRING$(13,"=")
5765 '
5766 LPRINT
5768 LPRINT TAB(56) USING "CURSO ## DE ALEMAN";VAL(HASTA$)
5770 LPRINT TAB(56) STRING$(18,"=")
5772 LPRINT
5780 LPRINT TAB(6) "EXPEDIENTE";
5782 LPRINT TAB(33) "A P E L L I D O S   Y   N O M B R E";
5784 LPRINT TAB(94) "C A L I F I C A C I O N"
5790 '
5792 LPRINT TAB(6) STRING$(10,"=");
5794 LPRINT TAB(21) STRING$(60,"=");
5796 LPRINT TAB(86) STRING$(40,"=")
5802 'LPRINT
5804 '
5890 '
5892 LIN%=8
5893 RETURN
5894 '********************************
5896 '***LECTURA DE INDICES Y DATOS***
5898 '********************************
5900 REG%=REG%+1
5901 TECLA$=INKEY$:IF TECLA$="" THEN 5903
5902 IF ASC(TECLA$)=27 THEN FF$="FF":RETURN
5903 IF REG%>ULTIMO% THEN FF$="FF":RETURN
5904 GET #1,REG%
5916 IF BSTATUS<>"O" THEN 5900
5918 IF BMARCA="B" THEN 5900
5919 IF OPC%=6 THEN 5940
5920 IF BCUR1=HASTA$ AND BJUN1="A" THEN RETURN
5922 IF BCUR2=HASTA$ AND BJUN2="A" THEN RETURN
5924 IF BCUR3=HASTA$ AND BJUN3="A" THEN RETURN
5926 IF BCUR4=HASTA$ AND BJUN4="A" THEN RETURN
5928 IF BCUR5=HASTA$ AND BJUN5="A" THEN RETURN
5930 GOTO 5900
5940 IF BCUR1=HASTA$ AND BSEP1="A" THEN RETURN
5942 IF BCUR2=HASTA$ AND BSEP2="A" THEN RETURN
5944 IF BCUR3=HASTA$ AND BSEP3="A" THEN RETURN
5946 IF BCUR4=HASTA$ AND BSEP4="A" THEN RETURN
5948 IF BCUR5=HASTA$ AND BSEP5="A" THEN RETURN
5950 GOTO 5900
6000 '
6800 '**************************
6810 '***FIN ENTRADA DE DATOS***
6820 '**************************
6830 CLOSE:COLOR 15
6840 GOTO 500
6850 '
7640 '**************************************************
7650 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7660 '**************************************************
7670 GOSUB 8500:LOCATE 22,15
7700 COLOR 10:PRINT " F7 =>";
7710 COLOR 15:PRINT " Fin Modificaciones:"
7720 RETURN   'COLOR 10:PRINT " F8 =>";
7730 COLOR 15:PRINT " Campo Atr s. ";
7732 COLOR 10:PRINT " F10 =>";
7734 COLOR 15:PRINT " Duplicar Campo. "
7740 '
7750 RETURN
7770 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8500 '***************************
8510 '***IMPRESION DE MENSAJES***
8520 '***************************
8530 L.L$=""+MEN$+"":LOCATE 22,15:PRINT L.L$;
8550 MEN$=SPACE$(65)
8560 RETURN
8570 '
9000 '*********************
9010 '***FIN DE PROGRAMA***
9020 '*********************
9030 CLOSE
9040 CLS
9050 RUN "PACAESAL"
9060 '
9070 '
9080 END
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOSUB 8500:RESUME
9140 '
9150 '
9160 '
9170 '
9180 '
9190 ON ERROR GOTO 0
9200 '
30000 '*****************************
30010 '***APERTURA FICHERO BANCOS***
30020 '*****************************
30022 FICHE1$="\"+DIRE$+"\FACAESAL.DAT"
30024 '
30030 'APERTURA FICHERO DE DATOS.
30040 OPEN "R",#1,FICHE1$,100
30042 'REGISTRO 1.
30050 FIELD #1,1 AS BSTATUS,12 AS BETIQUETA,2 AS BULTIMO,2 AS BULTACT,83 AS BFILLER
30052 'REGISTRO DE DATOS.
30054 FIELD #1,1 AS BSTATUS,6 AS BANCO,60 AS BNOM,1 AS BCUR1,1 AS BJUN1,1 AS BSEP1,1 AS BCUR2,1 AS BJUN2,1 AS BSEP2,1 AS BCUR3,1 AS BJUN3,1 AS BSEP3,1 AS BCUR4,1 AS BJUN4,1 AS BSEP4,1 AS BCUR5,1 AS BJUN5,1 AS BSEP5,1 AS BMARCA,17 AS BFIL001
30060 GET #1,1
30070 IF BETIQUETA="FACAESAL.DAT" THEN 30110  'CONTINUAR
30080 GOSUB 8500:MEN$="NO EXISTE FICHERO ESCUELA IDIOMAS, PULSE UNA TECLA":GOSUB 8500
30090 A$=INPUT$(1)
30100 GOTO 9000
30110 'CONTINUAR.
30120 ULTDIR%=CVI(BULTIMO)    'ULTIMO INTRODUCIDO EN DIR.
30122 ULTIMO%=ULTDIR%         'ULTIMO PARA LA BUSQUEDA.
30140 ACTDIR%=CVI(BULTACT)    'ULTIMO NUMERO ACTUALIZADO EN DIR.
30142 N.R%=ACTDIR%            'NUMEO DE REGSITROS EN EL DIR.
30150 '
30160 RETURN
30170 '
30400 '*************************
30410 '***LECTURA AZAR BANCOS***
30420 '*************************
30430 'El argumento de busqueda sobre 'BUSCA$'
30440 'El numero de registros sobre 'N.R%'
30450 R.B%=0           'Donde devuelve el nro. de registro.
30452 R.W%=0           'Donde devuelve el nro. de registro del DIR.
30460 SW1%=0         'SW indicador de la busqueda.
30470 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
30480 '
30490 R.I%=INT((N.R%/2)+.5)
30500 R.C%=R.I%
30510 IF R.I%<1 THEN SW1%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 30630
30520 IF R.I%>N.R% THEN SW1%=1:DES%=R.I%-R.C%:HAS%=N.R%:GOTO 30630
30530 IF R.I%=1 THEN R.I%=2:GET #1,R.I% ELSE GET #1,R.I%
30540 IF BANCO=BUSCA$ THEN R.W%=R.I%:R.B%=R.I%:GOTO 30800      'MIRAR STATUS
30550 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
30560 IF BANCO>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 30580
30570 R.I%=R.I%+R.C%
30580 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 30510
30590 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 30510
30600 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 30510
30610 REG2%=R.I%:SW100%=0
30620 GOTO 30510
30630 'REGISTRO NO BUSCADO.
30640 XX%=0
30650 FOR A%=DES% TO HAS%
30660   GET #1,A%
30670   IF BANCO=BUSCA$ THEN SW1%=0:R.W%=A%:R.B%=A%:XX%=1:A%=HAS%
30680 NEXT A%
30690 IF XX%=1 THEN 30800
30700 '
30710 '
30720 'Registro NO BUSCADO
30730 XX%=0
30740 IF N.R%=0 THEN N.R%=1
30750 FOR JMG%=N.R% TO ULTIMO%
30760     GET #1,JMG%
30770     IF BANCO=BUSCA$ THEN SW1%=0:R.W%=JMG%:R.B%=JMG%:XX%=1:JMG%=32000
30780 NEXT JMG%
30790 IF XX%=0 THEN SW1%=1:WSTATUS$="":RETURN
30800 'MIRAMOS STATUS.
30810 IF BSTATUS<>"O" THEN WSTATUS$="L":SW1%=1:RETURN
30820 WSTATUS$=""
30830 RETURN
30840 '
31000 '********************************
31010 '***PRESENTAR ENTRADA DE DATOS***
31020 '********************************
31030 X%=80-LEN(TITA$):X%=X%/2
31040 GOSUB 8000
31042 LOCATE 4,X%:PRINT TITA$
31044 LOCATE 5,X%:PRINT STRING$(LEN(TITA$),205)
31046 LOCATE 7,18:PRINT "CURSO           : < >"
31048 LOCATE 9,18:PRINT "NRO. EXPEDIENTE : <" SPACE$(6) ">"
31050 LOCATE 11,4:PRINT "ALUMNO : <" SPACE$(60) ">"
31060 LOCATE 15,18:PRINT "CALIFICACION    : <" SPACE$(1) ">"
31070 RETURN
31080 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 W.LE%=L.E%
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%:PRINT SPC(BO%)
63060 LOCATE F.C1%,C.C1%,1,0,6
63070 L.E%=W.LE%+NEG%
63080 FOR JMG%=1 TO L.E%
63090  D.R$=INPUT$(1)
63100  IF ASC(D.R$)=13 AND JMG%=1 THEN I.T%=1:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63110  IF ASC(D.R$)=13 AND L.D%<>0 THEN JMG%=L.E%:D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270'AL NEXT
63120  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63270  'AL NEXT
63130  IF ASC(D.R$)=10 THEN I.T%=2:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270 'AL NEXT
63140  IF ASC(D.R$)=9 THEN I.T%=3:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63150  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270  'AL NEXT
63152  IF ASC(D.R$)=14 THEN I.T%=5:JMG%=L.E%:L.D%=0:D.E$="":GOTO 63270
63160  IF D.R$<"0" OR D.R$>"9" THEN 63200 'MIRAR EL POSIBLE ERROR.
63170  IF JMG%=1 AND NEG%=1 THEN D.E$=D.E$+" ":PRINT " ";:JMG%=2
63180  D.E$=D.E$+D.R$:PRINT D.R$;
63190  GOTO 63270 'AL NEXT
63200  'MIRAR POSIBLE ERROR.
63210  IF D.R$="-" AND NEG%=1 AND JMG%=1 THEN D.E$=D.E$+D.R$:PRINT D.R$;:GOTO 63270 'AL NEXT
63220  IF ASC(D.R$)=46 OR ASC(D.R$)=44 AND L.D%=0 THEN D.R$=".":D.E$=D.E$+D.R$:PRINT D.R$;:JMG%=L.E%:GOTO 63270 'AL NEXT
63230  IF ASC(D.R$)<>8 THEN 63090
63240  IF JMG%=1 THEN 63090
63250  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-2
63260  '
63270 NEXT JMG%
63280 IF L.D%=0 THEN 63500
63290 IF RIGHT$(D.E$,1)="." THEN 63310 'CONTINUAR
63300 D.E$=D.E$+".":PRINT ".";
63310 LOCATE F.C1%,C.C1%+LEN(D.E$)
63320 WJMG%=JMG%
63330 FOR JMG2%=1 TO L.D%
63340  D.R$=INPUT$(1)
63350  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=1 THEN I.T%=1:JMG2%=L.D%:GOTO 63490
63360  IF ASC(D.R$)=13 AND JMG2%=1 AND I.T%=0 THEN I.T%=0:JMG2%=L.D%:GOTO 63490
63370  IF ASC(D.R$)=13 THEN JMG2%=L.D%:GOTO 63490  'AL NEXT
63380  IF ASC(D.R$)=10 THEN I.T%=2:JMG2%=L.D%:D.E$="":GOTO 63490 'AL NEXT
63390  IF ASC(D.R$)=9 THEN I.T%=3:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63400  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN I.T%=4:JMG2%=L.D%:D.E$="":GOTO 63490  'AL NEXT
63410  IF D.R$<"0" OR D.R$>"9" THEN 63440 'MIRAR EL POSIBLE ERROR.
63420  D.E$=D.E$+D.R$:PRINT D.R$;
63430  GOTO 63490 'AL NEXT
63440  'MIRAR POSIBLE ERROR
63450  IF ASC(D.R$)<>8 THEN 63340
63460  IF JMG2%=1 THEN 63040 'AL PRINCIPIO
63470  LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG2%=JMG2%-2
63480  '
63490 NEXT JMG2%
63500 IF I.T%<>0 THEN RETURN
63510 IF LEFT$(D.E$,1)="-" THEN RETURN
63520 IF L.D%<>0 THEN RETURN
63530 '
63540 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63550 RETURN
63560 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 OR ASC(D.R$)=27 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64122  IF ASC(D.R$)=14 THEN JMG%=L.E%:I.T%=5:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¤" OR D.R$="¥" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
