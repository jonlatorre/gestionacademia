10 '                       PBIBQUIE.BAS
20 '                      --------------
30 '        PROGRAMA PARA VER QUIEN HA TENIDO LOS LIBROS
40 '        --------------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 GOSUB 33000
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0"):BLANCO$=STRING$(40," ")
100 EMPRESA$="*** ESCUELA DE IDIOMAS ***
130 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
132 FECHA$=RIGHT$(FEDIA$,2)+"-"+MID$(FEDIA$,3,2)+"-"+LEFT$(FEDIA$,2)
134 '
140 DEFSTR C,B,D,F
150 OPTION BASE 1
160 DIM DIAS%(12),TABENT$(30)
170 '
172 '
174 '
176 '
178 '
290 '
292 DATA 31,28,31,30,31,30,31,31,30,31,30,31
294 FOR I%=1 TO 12:READ DIAS%(I%):NEXT I%
296 '
299 '
500 '**********************************************
505 '***PARTE COMUN BAJA,MODIFICACION Y CONSULTA***
510 '**********************************************
515 '
520 TIT$="- จ QUIEN HA TENIDO ESTE LIBRO ? -"
525 '
530 'GOSUB 8500:MEN$="Espere por favor......":GOSUB 8500
535 FALUDIR$="FBIBLIOT.ALF":FALUDAT$="FBIBLIOT.DAT"
540 GOSUB 30052
545 GOSUB 8000:GOSUB 31000
550 GOSUB 7405
555       '*******************************
560       '***ENTRADA NUMERO DE ALUMNO ***
565       '*******************************
570       COLOR 15
575       F.C1%=6:C.C1%=24:L.E%=5:BO%=6:ESC%=1:GOSUB 63000
580       IF I.T%=0 THEN CUENTA$=D.E$:GOTO 625 'Entrada OK
585       IF I.T%=5 THEN 575
590       IF I.T%=1 AND I.A%=1 THEN 575
595       IF I.T%=1 THEN 560
600       IF I.T%=3 THEN 560                 'Retroceder Campo.
605       IF I.T%=4 THEN 4000               'Fin Entrada.
610       IF I.T%=2 AND I.A%=1 THEN 625
615       IF I.T%=2 THEN 560
620       '
625       '
630       GUAPRI#=VAL(CUENTA$):GOSUB 64770:LOCATE 6,29-L.C%:PRINT GUAPRI$:BUSCA$=CUENTA$
635       N.L%=VAL(CUENTA$)
640       IF N.L%<2 OR N.L%>ULTIMO% THEN GOSUB 8500:MEN$="Este Nฃmero de Libro no existe, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 550
645       GET #2,N.L%
650       IF CMARCA="L" THEN GOSUB 8500:MEN$="Libro dado de BAJA, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 550
660       LOCATE 6,33:PRINT CNOMBRE
2000   '*************************************
2002   '***PEDIMOS CONFORMIDAD A LOS DATOS***
2004   '*************************************
2006   GOSUB 8500
2008   COLOR 10:LOCATE 22,15:PRINT "Datos correctos pulsar ";
2010   COLOR 15:PRINT " ENTER ";
2012   COLOR 10:PRINT " Errขneos pulsar ";
2014   COLOR 15:PRINT " F1 "
2016   COLOR 10
2018   '
2020   A$=INPUT$(1)
2022   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2020
2024   '
2028   IF ASC(A$)=14 THEN 545
2030   '
2032   SIHAY%=0:LINEA%=9
2034   FOR AA%=2 TO DULTI%
2036   GET #4,AA%
2038   IF VAL(DCUENTA)<>N.L% THEN 2066
2040   LINEA%=LINEA%+1
2042   IF LINEA%<20 THEN 2056
2044   GOSUB 8500:MEN$="Para ver si hay ms pulsar -ENTER-, si no -F1-":GOSUB 8500
2046   A$=INPUT$(1)
2048   IF ASC(A$)<13 OR ASC(A$)>14 THEN BEEP:GOTO 2046
2050   IF ASC(A$)=14 THEN 545
2052   FOR X%=10 TO 19:LOCATE X%,2:PRINT SPC(78):NEXT X%
2054   LINEA%=10
2056   LOCATE LINEA%,6:PRINT DNOMBRE
2058   LOCATE LINEA%,48:PRINT DTELEFO
2060   LOCATE LINEA%,57:PRINT RIGHT$(DDESDE,2)"-"MID$(DDESDE,3,2)"-"LEFT$(DDESDE,2)
2062   LOCATE LINEA%,67:PRINT RIGHT$(DHASTA,2)"-"MID$(DHASTA,3,2)"-"LEFT$(DHASTA,2)
2064   SIHAY%=1
2066   NEXT AA%
2068   IF SIHAY%=0 THEN GOSUB 8500:MEN$="No hay datos de este Libro, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):GOTO 545
2070   GOSUB 8500:MEN$="Pulsa una tecla para nueva consulta":GOSUB 8500:A$=INPUT$(1):GOTO 545
4000 '**************************
4010 '***FIN ENTRADA DE DATOS***
4020 '**************************
4030 CLS
4040 CLOSE   'Cierre de los Ficheros.
4050 GOTO 9000     'Volvemos al Menu.
4060 '
7405 '**************************************************
7415 '***PRESENTAMOS MENSAJES TECLAS DE FUNCION ALTAS***
7425 '**************************************************
7435 GOSUB 8500:LOCATE 22,14
7445 'COLOR 10:PRINT " ENTER =>";
7455 'COLOR 15:PRINT " Saltar ";
7465 COLOR 15:PRINT " F7 =>";
7475 COLOR 10:PRINT " Final de las consultas de quien ha tenido el Libro."
7525 '
7535 RETURN
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaciขn : BIBLIOTECA":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 15:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 10:L.L$="Mensajes: "+MEN$+"":LOCATE 22,3:PRINT L.L$;
8525 IF RET%=1 THEN FOR H=1 TO 7500:NEXT H:RET%=0
8530 MEN$=SPACE$(66)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PBIBLIOT"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 OPEN "R",#1,FALUDIR$,50
30054 FIELD #1,2 AS FULTI,3 AS FTEMA,40 AS FLLAMA,4 AS FILER,1 AS FSTA
30056 GET #1,1
30058  ULTIAL%=CVI(FULTI)
30060 'LSET FULTI=MKI$(1):LSET FTEMA="":LSET FLLAMA=""
30061 'LSET FILER="":LSET FSTA="A"
30062 'PUT #1,1
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,40 AS CNOMBRE,40 AS CAUTOR,40 AS CEDITOR,3 AS CTEMAS,6 AS CFEAL,7 AS CTELEFO,40 AS CALUMNO,6 AS CFESA,6 AS CFEDE
30112 FIELD #2,194 AS TODO1$,1 AS CSITUA,6 AS CFEBA,4 AS CVECES,1 AS CMARCA,40 AS COBSER,24 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,1 AS CSWA,250 AS REST1$,9 AS REST2$
30115 GET #2,1
30117  ULTIMO%=CVI(CULTI)
30118  ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30122 'LSET CCUEN1="":LSET CSTAT="A":LSET CULTI=MKI$(1)
30124 'LSET CULTACT=MKI$(1):LSET CSWA="0":LSET REST1$="":LSET REST2$=""
30126 'PUT #2,1
30128 '
30138 '
30150 '
30500 OPEN "R",#4,"FBIBQUIE.DAT",70
30510 FIELD #4,5 AS DCUENTA,1 AS DSTATUS,40 AS DNOMBRE,7 AS DTELEFO,6 AS DDESDE,6 AS DHASTA,5 AS DLIBRE
30514 FIELD #4,5 AS DCUEN1,1 AS DSTAT,2 AS DULTI,62 AS DRESTO
30515 GET #4,1
30517  DULTI%=CVI(DULTI)
30522 'LSET DCUEN1="":LSET DSTAT="A":LSET DULTI=MKI$(1)
30524 'LSET DRESTO=""
30526 'PUT #4,1
30528 'CLOSE
30538 'STOP
30540 RETURN
30550 '
31000 '**********************************************
31010 '***PRESENTAMOS PANTALLA DE ENTRADA DE DATOS***
31020 '**********************************************
31030 COLOR 11:X%=80-LEN(TIT$):X%=INT(X%/2)
31040 LOCATE 4,X%:PRINT TIT$
31042 LOCATE 5,X%:PRINT STRING$(LEN(TIT$),205)
31050 COLOR 10:LOCATE 6,11:PRINT "Libro Nง. : <" SPACE$(6) ">":COLOR 11
31052 LOCATE 8,6:PRINT "            N  O  M  B  R  E              TELEFO.   DESDE.    HASTA."
31058 LOCATE 9,6:PRINT "ออออออออออออออออออออออออออออออออออออออออ  อออออออ  ออออออออ  ออออออออ"
31260 COLOR 15:RETURN
31270 '
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
38760 '
60000 '   +-------------------------------------------+
60010 '   +   SUBRUTINA de CONTROL DE FECHA LOGICA    +
60020 '   +-------------------------------------------+
60030 '   -  Recibe la fecha en formato DDMMAA sobre FECSUB$
60040 '   -  Devuelve sobre FEF$ la fecha en formato DD-MM-AA
60050 '   -  Utiliza ERF como Codigo de Retorno.0=fecha correcta,1=fecha erronea
60060 '-------------------------------------------------------------------------
60070 AA$=RIGHT$(FECSUB$,2):MM$=MID$(FECSUB$,3,2):DD$=LEFT$(FECSUB$,2)
60080 ANOMAX%=VAL(RIGHT$(DATE$,2))
60090 ANO%=VAL(AA$):MES%=VAL(MM$):DIA%=VAL(DD$):ERF%=0
60100 IF  (ANO% < 1  OR ANO% > 99 OR MES% < 1 OR MES% > 12) THEN 60140
60110 IF (ANO% MOD 4)= 0 THEN DIAS%(2)=29 ELSE DIAS%(2)=28
60120 IF (DIA% < 1 OR DIA% > DIAS%(MES%)) THEN 60140
60130 FDMA$=DD$+"-"+MM$+"-"+AA$:FAMD$=AA$+MM$+DD$:ERF%=0:RETURN 'Fecha Correcta
60140 ERF%=1:RETURN  'Fecha Incorrecta
60150 '-------------------------------------------------------------------------
60160 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 63200  '===>AL NEXT
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64075  IF ASC(D.R$)=14 AND JMG%=1 THEN JMG%=L.E%:I.T%=5:GOTO 64200  '===>AL NEXT
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="ฅ" OR D.R$="ค" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'D.E$=D.E$+LEFT$(BLANCO$,L.E%-LEN(D.E$))
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
