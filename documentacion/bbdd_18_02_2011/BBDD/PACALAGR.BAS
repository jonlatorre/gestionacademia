10 '                       PACALAGR.BAS
20 '                      --------------
30 '           LISTADO NUMERICO DE ALUMNOS POR GRUPOS
40 '           --------------------------------------
52 '
54 '
56 '
60 ON ERROR GOTO 9100
70 '
80 KEY 1,CHR$(14):KEY 7,CHR$(7):KEY 8,CHR$(9):KEY 10,CHR$(10)
82 KEY 2,CHR$(15):KEY 3,CHR$(16):KEY 4,CHR$(17)
90 CERO$=STRING$(15,"0")
100 '
132 GOSUB 33000
133 FECHA$=MID$(DATE$,4,3)+LEFT$(DATE$,3)+RIGHT$(DATE$,2)
134 FEDIA$=RIGHT$(DATE$,2)+LEFT$(DATE$,2)+MID$(DATE$,4,2)
140 DEFSTR C,G,H,F
150 OPTION BASE 1
200 DIM TABENT$(30),MES$(12)
210 DATA "ENERO","FEBRERO","MARZO","ABRIL","MAYO","JUNIO","JULIO","AGOSTO","SEPTIEMBRE","OCTUBRE","NOVIEMBRE","DICIEMBRE"
220 FOR A%=1 TO 12:READ MES$(A%):NEXT A%
520 GOTO 4490
522 'GOSUB 8500:MEN$="Espere Por Favor...":GOSUB 8500
530 'GOSUB 30010        'Apertura Fichero ALUNMOS
550 'GOSUB 34000         'APERTURA FICHERO DE CALENDARIO
552 IF LEFT$(GLUNESA,1)<>" " OR LEFT$(GLUNESB,1)<>" " THEN DI1$="LU"
554 IF LEFT$(GMARTEA,1)<>" " OR LEFT$(GMARTEB,1)<>" " THEN DI2$="MA"
556 IF LEFT$(GMIERCA,1)<>" " OR LEFT$(GMIERCB,1)<>" " THEN DI3$="MI"
558 IF LEFT$(GJUEVEA,1)<>" " OR LEFT$(GJUEVEB,1)<>" " THEN DI4$="JU"
560 IF LEFT$(GVIERNA,1)<>" " OR LEFT$(GVIERNB,1)<>" " THEN DI5$="VI"
570 '
590 '
600 BUSCA$=FEDIA$+"01":GOSUB 34170
605 N.L%=N.L%-1
610 L1$=CHR$(218):L2$=CHR$(179):L3$=CHR$(179):L4$=CHR$(197):L5$=CHR$(179):L6$=CHR$(193)
625 N.L%=N.L%+1
630 GET #4,N.L%
632 IF MID$(BUSCA$,5,2)<>MID$(HDIA,5,2) THEN 800
635 IF HSBIL="F" THEN 625
636 IF LEFT$(HDES,2)=DI1$ THEN 644
637 IF LEFT$(HDES,2)=DI2$ THEN 644
638 IF LEFT$(HDES,2)=DI3$ THEN 644
639 IF LEFT$(HDES,2)=DI4$ THEN 644
640 IF LEFT$(HDES,2)=DI5$ THEN 644
642 GOTO 625
644 L1$=L1$+CHR$(196)+CHR$(196)+CHR$(194)
645 L2$=L2$+LEFT$(HDES,2)+CHR$(179)
650 L3$=L3$+RIGHT$(HDIA,2)+CHR$(179)
655 L4$=L4$+CHR$(196)+CHR$(196)+CHR$(197)
660 L5$=L5$+"  "+CHR$(179)
661 L6$=L6$+CHR$(196)+CHR$(196)+CHR$(193)
665 GOTO 625
800 L1$=LEFT$(L1$,LEN(L1$)-1)+CHR$(191)
801 L4$=LEFT$(L4$,LEN(L4$)-1)+CHR$(180)
802 L6$=LEFT$(L6$,LEN(L6$)-1)+CHR$(217)
804 DI1$="  ":DI2$="  ":DI3$="  ":DI4$="  ":DI5$="  "
3310 RETURN
3490 '***************************************
3500 '***LINEA DE DETALLE LISTADO NUMERICO***
3510 '***************************************
3520 '
3530 'IF CGRUPO<> ANTEG$ THEN GOSUB 3740   '--HACER CABECERAS--
3532 NOM$=CAPELL1:APE1$=CAPELL2:APE2$=CNOMBRE
3540 GOSUB 40000:NOMBRE$=LEFT$(NOMBRE$,30) 'e en una linea.
3542 FENA$=RIGHT$(CFENA,2)+"-"+MID$(CFENA,5,2)+"-"+MID$(CFENA,3,2)
3544 IF CCONFI<>"S" THEN CONF$="N" ELSE CONF$=CCONFI
3550 GUAPRI#=VAL(CCUENTA):GOSUB 64770
3568 LPRINT TAB(1) CHR$(179);:LPRINT TAB(8-L.C%) GUAPRI$;
3570 LPRINT TAB(10) CHR$(179)+" "+FENA$;
3572 LPRINT TAB(21) CHR$(179)+" "+CONF$;
3574 LPRINT TAB(25) CHR$(179)+" "+NOMBRE$;
3576 LPRINT TAB(58) L5$
3582 LPRINT TAB(1) CHR$(195)+STRING$(8,196)+CHR$(197);
3584 LPRINT TAB(11) STRING$(10,196)+CHR$(197);
3586 LPRINT TAB(22) STRING$(3,196)+CHR$(197);
3588 LPRINT TAB(26) STRING$(32,196);
3590 LPRINT TAB(58) L4$
3592 'LPRINT TAB(84) STRING$(14,196)+CHR$(197);
3594 'LPRINT TAB(99) STRING$(14,196)+CHR$(197);
3596 'LPRINT TAB(114) STRING$(15,196)+CHR$(180)
3642 LIN%=LIN%+2
3650 '
3660 RETURN
3670 '
3730 '************************************
3740 '***CABECERAS DEL LISTADO NUMERICO***
3750 '************************************
3755 IF LIN%= 90 THEN 3769
3756 LPRINT TAB(1) CHR$(192)+STRING$(8,196)+CHR$(193);
3757 LPRINT TAB(11) STRING$(10,196)+CHR$(193);
3758 LPRINT TAB(22) STRING$(3,196)+CHR$(193);
3759 LPRINT TAB(26) STRING$(32,196);
3760 LPRINT TAB(58) L6$
3761 'LPRINT TAB(84) STRING$(14,196)+CHR$(193);
3762 'LPRINT TAB(99) STRING$(14,196)+CHR$(193);
3763 'LPRINT TAB(114) STRING$(15,196)+CHR$(217)
3768 LPRINT CHR$(12)
3769 PAG%=PAG%+1:LIN%=0
3770 GOSUB 550
3772 LPRINT TAB(1) EMPRESA$;
3775 IF LEFT$(FEDIA,2)="19" THEN TITE$="RELACION DE ALUMNOS POR GRUPOS PARA EL MES DE "+MES$(VAL(RIGHT$(FEDIA$,2)))+" DE 1.9"+MID$(FEDIA$,3,2):XX%=INT(LEN(TITE$)/2):GOTO 3780
3777 TITE$="RELACION DE ALUMNOS POR GRUPOS PARA EL MES DE "+MES$(VAL(RIGHT$(FEDIA$,2)))+" DEL 20"+MID$(FEDIA$,3,2):XX%=INT(LEN(TITE$)/2)
3780 LPRINT TAB(70-XX%) TITE$;
3790 LPRINT TAB(111) USING "HOJA NUMERO : #,###";PAG%
3800 LPRINT TAB(1) STRING$(LEN(EMPRESA$),"-");
3810 LPRINT TAB(70-XX%) STRING$(LEN(TITE$),"-");
3820 LPRINT TAB(111) STRING$(19,"-")
3840 LPRINT:LPRINT
3845 'GET #6,VAL(CGRUPO)
3850 LPRINT TAB(1) "Grupo ...... "GGRUPO;
3852 LPRINT TAB(27) "Curso ...... "GCURSO;
3854 LPRINT TAB(66) "Texto ...... "GTEXTO;
3856 LPRINT TAB(105) USING"N£mero de Alumnos .... ##";CVI(GALTAS):LPRINT
3858 LPRINT TAB(45) "            H O R A R I O S"
3860 LPRINT TAB(45) STRING$(39,"-")
3862 IF GSB="0" THEN 3868
3864 LPRINT TAB(45) "          S E M A N A   - A -          "
3866 LPRINT TAB(45) STRING$(39,"-")
3868 LPRINT TAB(45) "    D I A      DESDE  HASTA  PISO  AULA"
3870 LPRINT TAB(45) "-------------  -----  -----  ----  ----"
3872 IF LEFT$(GLUNESA,1)=" " THEN 3876
3874 LPRINT TAB(45) "Lunes .......  "LEFT$(GLUNESA,2)"-"MID$(GLUNESA,3,2)"  "MID$(GLUNESA,5,2)"-"RIGHT$(GLUNESA,2)"   "GPILUA"    "GAULUA
3876 '
3878 IF LEFT$(GMARTEA,1)=" " THEN 3882
3880 LPRINT TAB(45) "Martes ......  "LEFT$(GMARTEA,2)"-"MID$(GMARTEA,3,2)"  "MID$(GMARTEA,5,2)"-"RIGHT$(GMARTEA,2)"   "GPIMAA"    "GAUMAA
3882 '
3884 IF LEFT$(GMIERCA,1)=" " THEN 3888
3886 LPRINT TAB(45) "Miercoles ...  "LEFT$(GMIERCA,2)"-"MID$(GMIERCA,3,2)"  "MID$(GMIERCA,5,2)"-"RIGHT$(GMIERCA,2)"   "GPIMIA"    "GAUMIA
3888 '
3890 IF LEFT$(GJUEVEA,1)=" " THEN 3894
3892 LPRINT TAB(45) "Jueves .. ...  "LEFT$(GJUEVEA,2)"-"MID$(GJUEVEA,3,2)"  "MID$(GJUEVEA,5,2)"-"RIGHT$(GJUEVEA,2)"   "GPIJUA"    "GAUJUA
3894 '
3896 IF LEFT$(GVIERNA,1)=" " THEN 3900
3898 LPRINT TAB(45) "Viernes . ...  "LEFT$(GVIERNA,2)"-"MID$(GVIERNA,3,2)"  "MID$(GVIERNA,5,2)"-"RIGHT$(GVIERNA,2)"   "GPIVIA"    "GAUVIA
3900 LPRINT
3902 IF GSB="0" THEN 3950
3904 LPRINT TAB(45) "          S E M A N A   - B -          "
3906 LPRINT TAB(45) STRING$(39,"-")
3908 LPRINT TAB(45) "    D I A      DESDE  HASTA  PISO  AULA"
3910 LPRINT TAB(45) "-------------  -----  -----  ----  ----"
3912 IF LEFT$(GLUNESB,1)=" " THEN 3916
3914 LPRINT TAB(45) "Lunes .......  "LEFT$(GLUNESB,2)"-"MID$(GLUNESB,3,2)"  "MID$(GLUNESB,5,2)"-"RIGHT$(GLUNESB,2)"   "GPILUB"    "GAULUB
3916 IF LEFT$(GMARTEB,1)=" " THEN 3920
3918 LPRINT TAB(45) "Martes ......  "LEFT$(GMARTEB,2)"-"MID$(GMARTEB,3,2)"  "MID$(GMARTEB,5,2)"-"RIGHT$(GMARTEB,2)"   "GPIMAB"    "GAUMAB
3920 IF LEFT$(GMIERCB,1)=" " THEN 3924
3922 LPRINT TAB(45) "Miercoles....  "LEFT$(GMIERCB,2)"-"MID$(GMIERCB,3,2)"  "MID$(GMIERCB,5,2)"-"RIGHT$(GMIERCB,2)"   "GPIMIB"    "GAUMIB
3924 IF LEFT$(GJUEVEB,1)=" " THEN 3928
3926 LPRINT TAB(45) "Jueves ......  "LEFT$(GJUEVEB,2)"-"MID$(GJUEVEB,3,2)"  "MID$(GJUEVEB,5,2)"-"RIGHT$(GJUEVEB,2)"   "GPIJUB"    "GAUJUB
3928 IF LEFT$(GVIERNB,1)=" " THEN 3950
3930 LPRINT TAB(45) "Viernes .....  "LEFT$(GVIERNB,2)"-"MID$(GVIERNB,3,2)"  "MID$(GVIERNB,5,2)"-"RIGHT$(GVIERNB,2)"   "GPIVIB"    "GAUVIB
3950 LPRINT:LPRINT
4000 LPRINT TAB(58) L1$
4002 LPRINT TAB(58) L2$
4050 LPRINT TAB(3) "NUMERO";
4055 LPRINT TAB(12) "F.NACIM.";
4057 LPRINT TAB(22) "CNF";
4060 LPRINT TAB(33) "NOMBRE Y APELLIDOS";
4062 LPRINT TAB(58) L3$
4100 LPRINT TAB(1) CHR$(218)+STRING$(8,196)+CHR$(194);
4102 LPRINT TAB(11) STRING$(10,196)+CHR$(194);
4104 LPRINT TAB(22) STRING$(3,196)+CHR$(194);
4106 LPRINT TAB(26) STRING$(32,196);
4108 LPRINT TAB(58) L4$
4110 'LPRINT TAB(84) STRING$(14,196)+CHR$(194);
4112 'LPRINT TAB(99) STRING$(14,196)+CHR$(194);
4114 'LPRINT TAB(114) STRING$(15,196)+CHR$(191)
4128 LIN%=LIN%+7
4129 ANTEG$=CGRUPO
4130 RETURN
4140 '
4480 '*************************
4490 '***INFORME DEL FICHERO***
4492 '*************************
4494 'FALUDAT$="FACAALUM.DAT"
4496 'GOSUB 30000
4497 'IF ULTACT%=ULTIMO%  THEN CLOSE:GOTO 4501
4498 'GET #2,1:LSET CULTACT=CULTI:PUT #2,1:CLOSE
4499 'SHELL "ACAGRUP.BAT"
4500 'SHELL "ACANUME.BAT"
4501 GOSUB 8000
4502 WIDTH "LPT1:",132:OPEN "LPT1:" FOR OUTPUT AS #9
4503   LPRINT CHR$(27)+"&l0O"+CHR$(27)+"(s16.66h0B"+CHR$(27)  '+"&l11D";
4504 'LPRINT CHR$(27) "C" CHR$(72);:LPRINT CHR$(15)
4506 '
4520 COLOR 10:LOCATE 6,20:PRINT "- LISTADO NUMERICO DE ALUMNOS POR GRUPOS -"
4525 LOCATE 7,20:PRINT STRING$(42,205)
4526 COLOR 11:LOCATE 12,12:PRINT "TECLEA MES Y A¥O PARA EL CUAL QUIERES EL LISTADO: <      >"
4528 COLOR 15:LOCATE 16,25:PRINT " F10";
4530 COLOR 11:PRINT " Volver al Men£ Anterior"
4532 COLOR 15
4533 F.C1%=12:C.C1%=63:L.E%=6:BO%=6:ESC%=0:GOSUB 63000
4534 IF I.T%=0 THEN LOCATE 12,63:PRINT D.E$:GOTO 4537
4535 IF I.T%=2 THEN CLOSE:GOTO 9000
4536 CLOSE:BEEP:LOCATE 12,63:PRINT SPC(6):GOTO 4533
4537 FEDIA$=RIGHT$(D.E$,4)+LEFT$(D.E$,2)
4538 IF VAL(LEFT$(FEDIA$,4))<1989 OR VAL(LEFT$(FEDIA$,4))>2086 THEN 4536
4539 IF VAL(RIGHT$(FEDIA$,2))<1 OR VAL(RIGHT$(FEDIA$,2))>12 THEN 4536
4540 'GOSUB 550
4550 COLOR 11:LOCATE 14,15:PRINT "DESDE EL GRUPO  : <   >      HASTA EL GRUPO  : <   >"
4552 COLOR 15:LOCATE 16,25:PRINT " F10";
4554 COLOR 11:PRINT " Volver al Men£ Anterior"
4560 COLOR 15
4590 'Pedimos numero de Orden DESDE.
4600 F.C1%=14:C.C1%=34:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
4610 IF I.T%=0 THEN LOCATE 14,34:PRINT D.E$:GOTO 4640
4620 IF I.T%=2 THEN CLOSE:GOTO 9000
4630 GOTO 4600
4640 DESDE$=D.E$
4645 '
4650 '
4660 '
4670 '
4680 GOSUB 8500:MEN$="- HASTA=999, hasta el Fin de Fichero -":GOSUB 8500
4690 COLOR 15    'Pedimos numero de Orden HASTA.
4700 F.C1%=14:C.C1%=63:L.E%=3:BO%=3:ESC%=0:GOSUB 63000
4710 IF I.T%=0 THEN LOCATE 14,63:PRINT D.E$:GOTO 4740
4720 IF I.T%=2 THEN CLOSE:GOTO 9000
4730 GOTO 4700
4740 '
4750 '
4760 '
4770 HASTA$=D.E$
4780 '
4790 'Comprobamos DESDE con HASTA.
4800 IF DESDE$>HASTA$ THEN BEEP:GOSUB 8500:MEN$="N£mero. -DESDE- mayor que -HASTA-, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4801 PAG%=0:LIN%=90
4802   GOSUB 8500
4803   COLOR 10:LOCATE 22,15:PRINT " Datos Correctos ";
4804   COLOR 15:PRINT " ENTER ";
4805   COLOR 10:PRINT " Datos Erroneos ";
4806   COLOR 15:PRINT " F1 "
4807   COLOR 10
4814 A$=INPUT$(1)
4816 IF ASC(A$)=14 THEN 4550
4817 FALUDAT$="FACAALUM.DAT"
4818 GOSUB 30000:GOSUB 8500:MEN$=" Espere por Favor........":GOSUB 8500
4820 GOSUB 32000:GOSUB 34000
4830 FOR REG%=2 TO ULTGRU%
4831     GET #6,REG%
4832     '
4833     IF GGRUPO<DESDE$ THEN 4900    'AL NEXT
4834     IF GGRUPO>HASTA$ THEN REG%=ULTGRU%:GOTO 4900     'AL NEXT
4836     IF CVI(GALTAS)=0 THEN 4900    'AL NEXT
4837     '
4838     GOSUB 3740
4840     FOR ALU%=1 TO CVI(GALUMNOS)*5 STEP 5
4842         IF MID$(GALUS,ALU%,1)=" " THEN 4890
4844         N.L%=VAL(MID$(GALUS,ALU%,5)):GET #2,N.L%
4846         GOSUB 3500    'LINEA DEL ALUMNO
4890     NEXT ALU%
4900 NEXT REG%
4901 IF LIN%=90 THEN GOSUB 8500:MEN$="No disponemos de los datos pedidos, pulsar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 4501
4902 IF LIN%=90 THEN 9030
4904 LPRINT TAB(1) CHR$(192)+STRING$(8,196)+CHR$(193);
4906 LPRINT TAB(11) STRING$(10,196)+CHR$(193);
4908 LPRINT TAB(22) STRING$(3,196)+CHR$(193);
4910 LPRINT TAB(26) STRING$(32,196);
4912 LPRINT TAB(58) L6$
4914 'LPRINT TAB(84) STRING$(14,196)+CHR$(193);
4916 'LPRINT TAB(99) STRING$(14,196)+CHR$(193);
4918 'LPRINT TAB(114) STRING$(15,196)+CHR$(217)
4962 LPRINT CHR$(12)
4963 ANTEG$=" ":LIN%=90
4964 CLOSE
4966 GOTO 4501
4968 '
4969 '
4970 '
8000 '***********************************************
8010 '***  RUTINA PARA SACAR CARATULA EN PANTALLA ***
8020 '***********************************************
8030 '
8040 CLS:COLOR 10:LOCATE 1,1:PRINT CHR$(201)+STRING$(78,205)+CHR$(187)
8050 LOCATE 2,1:PRINT CHR$(186):COLOR 11:LOCATE 2,3:PRINT" " EMPRESA$" ":COLOR 10:LOCATE 2,34:PRINT "Aplicaci¢n : ACADEMIAS ":COLOR 11:LOCATE 2,63:PRINT "FECHA : "FECHA$" ":COLOR 10 : LOCATE 2,80 :PRINT CHR$(186)
8060 LOCATE 3,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8070 FOR J%=4 TO 21 :LOCATE J%,1:PRINT CHR$(186):LOCATE J%,80 :PRINT CHR$(186):NEXT
8080 LOCATE 21,1:PRINT CHR$(204)+STRING$(78,205)+CHR$(185)
8090 LOCATE 22,1:PRINT CHR$(186):COLOR 11:LOCATE 22,3:PRINT "Mensajes: ":COLOR 10:LOCATE 22,80:PRINT CHR$(186)
8100 LOCATE 23,1:PRINT CHR$(200)+STRING$(78,205)+CHR$(188);
8110 COLOR 7,0,0:RETURN
8120 '
8250 '
8490 '*************************
8500 '***IMPRESION DE MENSAJES*
8510 '*************************
8515 '
8520 COLOR 11:L.L$=""+MEN$+"":LOCATE 22,14:PRINT L.L$;
8525 IF RET%=1 THEN FOR HH%=1 TO 7500:NEXT HH%:RET%=0
8530 MEN$=SPACE$(64)
8540 RETURN
8550 '
9000 '**********************
9010 '***FIN DEL PROGRAMA***
9020 '**********************
9030 CLOSE
9040 RUN "PACACLIE"
9050 END
9060 '
9070 '
9080 '
9090 '
9100 '*******************************
9110 '***RUTINA CONTROL DE ERRORES***
9120 '*******************************
9130 IF ERR=24 OR ERR=25 OR ERR=27 THEN GOSUB 8500:MEN$="--COMPRUEBE LA IMPRESORA Y PULSE UNA TECLA---":GOSUB 8500:A$=INPUT$(1):RESUME
9140 GOTO 9150
9142 GOSUB 8500:MEN$="Fichero FACAALUM.DIR inexistente, pusar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9144 GOSUB 8500:MEN$="Fichero FACAALUM.DAT inexistente, pusar una tecla":GOSUB 8500:A$=INPUT$(1):CLOSE:GOTO 9000
9150 '
9170 ON ERROR GOTO 0
9180 '
30000 '*******************************
30010 '***APERTURA FICHERO ALUMNOS ***
30020 '*******************************
30025 '
30052 'OPEN "R",#1,FALUDIR$,65
30054 'FIELD #1,2 AS FULTI,20 AS FLLAMA,20 AS FAPE1,20 AS FAPE2,2 AS FILER,1 AS FSTA
30056 'GET #1,1
30058 'ULTIAL%=CVI(FULTI)
30090 ' Apertura del fichero de datos   FACAALUM.DAT
30100 OPEN "R",#2,FALUDAT$,270
30110 FIELD #2,5 AS CCUENTA,1 AS CSTATUS,20 AS CNOMBRE,20 AS CAPELL1,20 AS CAPELL2,40 AS CDIREC,20 AS CPOBLA,15 AS CPROVI,5 AS CCOPOS,7 AS CTELEFO,7 AS CTELEFA,13 AS CDNI,3 AS CGRUPO
30112 FIELD #2,176 AS TODO1$,8 AS CFENA,4 AS CBANCO,4 AS CSUCUR,2 AS CDIGI,10 AS CCUBAN,8 AS CFALTA,8 AS CFBAJA,1 AS CCUESP,4 AS CIMPOR,1 AS CMARCA,1 AS CCONFI,20 AS COBSER,6 AS CFACT,17 AS CFILL
30114 FIELD #2,5 AS CCUEN1,1 AS CSTAT,2 AS CULTI,2 AS CULTACT,12 AS CFICHER,1 AS CSWG,1 AS CSWA,2 AS CVIGO,235 AS REST1$,4 AS CFEFAC,5 AS REST2$
30115 GET #2,1
30116 IF CFICHER<>"FACAALUM.DAT" THEN 9144
30117 ULTIMO%=CVI(CULTI)
30118 ULTACT%=CVI(CULTACT):N.R%=ULTACT%
30120 VIVAS%=CVI(CVIGO)
30140 RETURN
30150 '
32000 '*******************************
32010 '***APERTURA FICHERO GRUPOS  ***
32020 '*******************************
32030 'Comprobamos la existencia del fichero.
32040 ' Apertura del fichero de datos   FACAGRUP.DAT
32050 OPEN "R",#6,"FACAGRUP.DAT",380
32060 FIELD #6,3 AS GGRUPO,1 AS GSTATUS,15 AS GCURSO,15 AS GTEXTO,2 AS GALUMNOS,2 AS GALTAS,2 AS GCONFIR,4 AS GCUOTA,8 AS GLUNESA,2 AS GPILUA,2 AS GAULUA,8 AS GMARTEA,2 AS GPIMAA,2 AS GAUMAA
32070 FIELD #6,68 AS GPRIP,8 AS GMIERCA,2 AS GPIMIA,2 AS GAUMIA,8 AS GJUEVEA,2 AS GPIJUA,2 AS GAUJUA,8 AS GVIERNA,2 AS GPIVIA,2 AS GAUVIA
32080 FIELD #6,104 AS GPRI1,8 AS GLUNESB,2 AS GPILUB,2 AS GAULUB,8 AS GMARTEB,2 AS GPIMAB,2 AS GAUMAB,8 AS GMIERCB,2 AS GPIMIB,2 AS GAUMIB,8 AS GJUEVEB,2 AS GPIJUB,2 AS GAUJUB,8 AS GVIERNB,2 AS GPIVIB,2 AS GAUVIB,1 AS GSB,15 AS GLIBR,200 AS GALUS
32090 FIELD #6,3 AS GLIBR1,1 AS GSTATUS,12 AS GETIQUETA,2 AS GULTIMO,162 AS GLIB2,200 AS GLIB2
32100 FIELD #6,180 AS GTODO,200 AS GALUS
32110 GET #6,1
32120 IF GETIQUETA="FACAGRUP.DAT" THEN 32140
32130 GOSUB 8500:MEN$="NO TENEMOS FICHERO DE GRUPOS, PULSAR UNA TECLA":GOSUB 8500:A$=INPUT$(1):GOTO 9000
32140 ULTGRU%=CVI(GULTIMO)
32150 RETURN
33000 '**********************************
33010 '***APERTURA FICHERO DATOS FIJOS***
33020 '**********************************
33030 OPEN "R",#1,"FDATFIJ.DAT",100
33040 FIELD #1,2 AS NRO$,8 AS NDIRE$,30 AS NEMPRESA$,15 AS NNIF$,8 AS NPROG$,37 AS NFILLER$
33050 GET #1,1
33052 NROEM$=NRO$
33060 DIRE$=NDIRE$
33070 EMPRESA$=NEMPRESA$
33080 NIF$=NNIF$
33082 PROGRAMA$=NPROG$
33090 CLOSE
33100 '
33110 '
33120 RETURN
33130 '
34000 '*******************************
34010 '***APERTURA FICHERO HIBRIDO ***
34020 '*******************************
34030 '
34040 ' Apertura del fichero de datos   FACTCALE.DAT
34050 '
34060 OPEN "R",#4,"FACACALE.DAT",20
34070 FIELD #4,8 AS HDIA,9 AS HDES,1 AS HSTAT,1 AS HSBIL,1 AS HFIL
34080 FIELD #4,2 AS HULTI,2 AS HULTACT,16 AS RESTO$
34090 GET #4,1
34100 HIBIMO%=CVI(HULTI):N.E%=HIBIMO%
34110 HIBACT%=HIBIMO%
34120 '
34130 RETURN
34140 '
34150 '
34160 '*************************
34170 '***BUSQUEDA DICOTOMICA***
34180 '*************************
34190 'El argumento de busqueda sobre 'BUSCA$'
34200 'El numero de registros sobre 'N.E%'
34210 'El REGISTRO ENCONTRADO SOBRE  N.L%
34220 N.L%=0:DES%=0:HAS%=0
34230 SWBUSCA%=0         'SW indicador de la busqueda.
34240 REG1%=0:REG2%=0:SW100%=0:DES%=0:HAS%=0
34250 '
34260 R.I%=INT((N.E%/2)+.5)
34270 R.C%=R.I%
34280 IF R.I%<1 THEN SWBUSCA%=1:DES%=2:HAS%=R.I%+R.C%:GOTO 34400
34290 IF R.I%>N.E% THEN SWBUSCA%=1:DES%=R.I%-R.C%:HAS%=N.E%:GOTO 34400
34300 IF R.I%=1 THEN R.I%=2:GET #4,R.I% ELSE GET #4,R.I%
34310 IF HDIA=BUSCA$ THEN N.L%=R.I%:GET #4,N.L%:RETURN     'Registro Buscado.
34320 R.C%=INT((R.C%/2)+.5):IF R.C%=R.I% THEN R.C%=R.I%-1
34330 IF HDIA>BUSCA$ THEN R.I%=R.I%-R.C%:GOTO 34350
34340 R.I%=R.I%+R.C%
34350 IF SW100%=0 AND REG1%=R.I% THEN R.I%=0:GOTO 34280
34360 IF SW100%=1 AND REG2%=R.I% THEN R.I%=0:GOTO 34280
34370 IF SW100%=0 THEN REG1%=R.I%:SW100%=1:GOTO 34280
34380 REG2%=R.I%:SW100%=0
34390 GOTO 34280
34400 'BUSQUEDA DE LOS ULTIMOS METIDOS.
34410 'BUSQUEDA DE LOS ULTIMOS METIDOS.
34420 SWBUSCA%=1
34430 FOR A% = DES% TO HAS%
34440 GET #4,A%
34450 IF HDIA=BUSCA$ THEN SWBUSCA%=0:N.L%=A%:GET #4,N.L%:RETURN     'Registro Buscado.
34460 NEXT A%
34470 FOR N.L%=HIBACT%+1 TO HIBIMO%
34480    GET #4,N.L%
34490    IF BUSCA$=HDIA THEN SWBUSCA%=0:WN.L%=N.L%:N.L%=HIBIMO%:GOTO 34500
34500 NEXT N.L%
34510 N.L%=WN.L%
34520 RETURN
34530 '
40000 'Ponemos el Nombre y los dos Apellidos en una linea.
40010 NOMBRE$=""
40020 'Bucle del Nombre.
40030 LONG%=LEN(NOM$)
40040 FOR I%=LONG% TO 1 STEP -1
40050    IF MID$(NOM$,I%,1)<>" " THEN LNOMBRE%=I%:I%=0
40060 NEXT I%
40070 '
40080 'Bucle del Apellido 1.
40090 LONG%=LEN(APE1$)
40100 FOR I%=LONG% TO 1 STEP -1
40110    IF MID$(APE1$,I%,1)<>" " THEN LAPELL1%=I%:I%=0
40120 NEXT I%
40130 '
40140 'Bucle del Apellido 2.
40150 LONG%=LEN(APE2$)
40160 FOR I%=LONG% TO 1 STEP -1
40170    IF MID$(APE2$,I%,1)<>" " THEN LAPELL2%=I%:I%=0
40180 NEXT I%
40190 NOMBRE$=LEFT$(NOM$,LNOMBRE%)+" "+LEFT$(APE1$,LAPELL1%)+" "+LEFT$(APE2$,LAPELL2%)
40200 NOMBRE$=LEFT$(NOMBRE$,50)
40210 RETURN
40220 '
63000 '********************************
63010 '***RUTINA DE ENTRADA NUMERICA***
63020 '********************************
63030 '
63040 D.E$="":I.T%=0
63050 LOCATE F.C1%,C.C1%,1,0,6
63060 FOR JMG%=1 TO L.E%
63070  D.R$=INPUT$(1)
63080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 63200  '===>AL NEXT
63090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 63200  '===>AL NEXT
63100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 63200  '===>AL NEXT
63110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 63200   '===>AL NEXT
63120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 63200   '===>AL NEXT
63130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%,"-"):LOCATE F.C1%,C.C1%
63140  IF D.R$<"0" OR D.R$>"9" THEN 63170               'Error
63150  D.E$=D.E$+D.R$:PRINT D.R$;
63160  GOTO 63200 'AL NEXT
63170  'Error.
63175  IF ASC(D.R$)=8 AND JMG%=1 THEN 63190
63180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT "-":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
63190  JMG%=JMG%-1
63200 NEXT JMG%
63210 IF I.T%<>0 THEN 63230
63220 IF LEN(D.E$)<>L.E% THEN D.E$=LEFT$(CERO$,L.E%-LEN(D.E$))+D.E$
63225 LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," ")
63230 RETURN
63240 '
64000 '*************************************
64010 '***RUTINA DE ENTRADA ALFA/NUMERICA***
64020 '*************************************
64030 '
64040 D.E$="":I.T%=0
64050 LOCATE F.C1%,C.C1%,1,0,6
64060 FOR JMG%=1 TO L.E%
64070  D.R$=INPUT$(1)
64080  IF ASC(D.R$)=13 AND JMG%=1 THEN JMG%=L.E%:I.T%=1:GOTO 64200  '===>AL NEXT
64090  IF ASC(D.R$)=13 THEN JMG%=L.E%:GOTO 64200  '===>AL NEXT
64100  IF ASC(D.R$)=10 THEN JMG%=L.E%:I.T%=2:GOTO 64200  '===>AL NEXT
64110  IF ASC(D.R$)=9 THEN JMG%=L.E%:I.T%=3:GOTO 64200   '===>AL NEXT
64120  IF ASC(D.R$)=7 THEN JMG%=L.E%:I.T%=4:GOTO 64200   '===>AL NEXT
64130  IF JMG%=1 THEN LOCATE F.C1%,C.C1%:PRINT STRING$(BO%," "):LOCATE F.C1%,C.C1%
64135  IF ASC(D.R$)=34 THEN 64170
64140  IF ASC(D.R$)<32 OR ASC(D.R$)>126 THEN 64170               'Error
64150  D.E$=D.E$+D.R$:PRINT D.R$;
64160  GOTO 64200 'AL NEXT
64170  'Error.
64172  IF D.R$="¥" OR D.R$="¤" THEN 64150
64175  IF ASC(D.R$)=8 AND JMG%=1 THEN 64190
64180  IF ASC(D.R$)=8 THEN LOCATE F.C1%,C.C1%+LEN(D.E$)-1:PRINT " ":LOCATE F.C1%,C.C1%+LEN(D.E$)-1:D.E$=LEFT$(D.E$,LEN(D.E$)-1):JMG%=JMG%-1
64190  JMG%=JMG%-1
64200 NEXT JMG%
64210 IF I.T%<>0 THEN 64230
64220 'LOCATE F.C1%,C.C1%:PRINT SPC(L.E%)
64225 'LOCATE F.C1%,C.C1%:PRINT D.E$
64230 RETURN
64240 '
64760 '========================================================================
64762 ' RUTINA PARA EDITAR IMPORTES EN CRISTIANO X.XXX.XXX,XX                 =
64764 '                                                                       =
64766 ' SE ENTRARA A ESTA RUTINA DE LA FORMA  SIGUIENTE:                      =
64768 '                                                                       =
64770 ' GOSUB 64760 MANDANDO EN UN CAMPO LLAMADO GUAPRI#,                     =
64772 '                                                                       =
64773 '             LA CANTIDAD O NUMERO QUE DESEEMOS EDITAR.                 =
64774 '                                                                       =
64775 '             Y EN UN CAMPO LLAMADO N.D% EL NUMERO DE DECIMALES         =
64776 '                                                                       =
64778 ' DEVOLVERA EL CAMPO EDITADO EN GUAPRI$ Y LA LONGITUD EN L.C%           =
64780 '                                                                       =
64782 '========================================================================
64784 '
64786 '
64788 GUAPRI$=STR$(GUAPRI#):L.P%=0:N.D%=0
64790 FOR I%=1 TO 30:TABENT$(I%)="":NEXT I%
64792 SIGNO$=LEFT$(GUAPRI$,1):GUAPRI$=RIGHT$(GUAPRI$,LEN(GUAPRI$)-1)
64794 FOR I%=1 TO LEN(GUAPRI$)
64796   IF MID$(GUAPRI$,I%,1)="." THEN L.P%=I%:I%=LEN(GUAPRI$)
64798 NEXT I%
64800 IF L.P%=0 THEN DEC$="":ENT$=GUAPRI$:GOTO 64812
64802 IF N.D%=0 THEN DEC$="":GOTO 64808
64804 DEC$=","+MID$(GUAPRI$,L.P%+1,N.D%)
64806 IF LEN(DEC$)<>N.D%+1 THEN DEC$=DEC$+LEFT$(CERO$,N.D%+1-LEN(DEC$))
64808 ENT$=LEFT$(GUAPRI$,L.P%-1)
64810 IF LEN(ENT$)=0 THEN GUAPRI$="0":GOTO 64834
64812 X%=31
64814 FOR I%=LEN(ENT$) TO 1 STEP -1
64816   X%=X%-1
64818   IF X%=27 OR X%=23 OR X%=19 OR X%=15 OR X%=12 THEN TABENT$(X%)=".":X%=X%-1
64820   TABENT$(X%)=MID$(ENT$,I%,1)
64822 NEXT I%
64824 GUAPRI$=""
64826 FOR I%=1 TO 30
64828   IF TABENT$(I%)="" THEN 64832
64830   GUAPRI$=GUAPRI$+TABENT$(I%)
64832 NEXT I%
64833 IF SIGNO$=" " THEN GUAPRI$=GUAPRI$+DEC$:L.C%=LEN(GUAPRI$)-1:RETURN
64834 GUAPRI$=GUAPRI$+DEC$+SIGNO$:L.C%=LEN(GUAPRI$)-1
64836 RETURN
64838 '
